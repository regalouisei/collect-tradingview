// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Tiagorocha1989
//                              ████████╗ ████████╗  ████████╗  ███████╗   ██████╗        ██████╗   ██████╗   ██████╗ ██╗   ██╗ ████████╗
//                              ╚══██╔══╝ ╚══██╔══╝  ██╔═══██║  ██╔════╝  ██╔═══██╗       ██╔══██╗ ██╔═══██╗ ██╔════╝ ██║   ██║ ██╔═══██║
//                                 ██║       ██║     ████████║  ██║ ███║  ██║   ██║       ██████╔╝ ██║   ██║ ██║      ████████║ ████████║
//                                 ██║       ██║     ██╔═══██║  ██║  ██║  ██║   ██║       ██╔══██╗ ██║   ██║ ██║      ██║   ██║ ██╔═══██║
//                                 ██║    ████████╗  ██║   ██║  ███████║  ╚██████╔╝       ██║  ██║ ╚██████╔╝ ╚██████╗ ██║   ██║ ██║   ██║
//@version=6
indicator('Momentum - MOM', 'MOM →', false)
//╔════════════════════════╗
//║ Choose Moving Averages ║
//╚════════════════════════╝
ma(source, length, type) =>
     type == 'EMA' ? ta.ema(source, length) :
     type == 'SMA' ? ta.sma(source, length) :
     type == 'RMA' ? ta.rma(source, length) :
     type == 'WMA' ? ta.wma(source, length) :
     type == 'VWMA' ? ta.vwma(source, length) :
     type == 'HMA' ? ta.hma(source, length) :
     na
//╔════════════════════════╗
//║     INPUT SETTINGS     ║
//╚════════════════════════╝
Color_Mode =        input.string('Classic', 'Color Choice', group = 'Color', options = ['Classic', 'Modern', 'Robust', 'Accented', 'Monochrome'])
Length_MOM =        input.int(14, title = 'Length MOM:', group='MOM SETTING', inline = 'MOM')
Source_MOM =        input(close, title = 'Source MOM:', group='MOM SETTING', inline = 'MOM')
Length_MA_MOM =     input.int(365, title = 'Length MA', group='MOM MA SETTING', inline = 'MA')
Source_MA_MOM =     input.string('EMA', '', group='MOM MA SETTING', inline = 'MA', options=['EMA', 'SMA', 'RMA', 'WMA', 'VWMA', 'HMA'],tooltip = 'Moving Averages')
//╔════════════════════════╗
//║      Calculation       ║
//╚════════════════════════╝
MOM =     ta.mom(Source_MOM,Length_MOM)
MOM_MA =  ma(MOM, Length_MA_MOM, Source_MA_MOM)
isBull_MOM = MOM > MOM_MA
isBear_MOM = MOM < MOM_MA
//╔════════════════════════╗
//║         Color          ║
//╚════════════════════════╝
[UpC, DnC] = switch Color_Mode
    'Classic' =>    [#008000, #FF0000]
    'Modern' =>     [#5ffae0, #c22ed0]
    'Robust' =>     [#ffbb00, #770737]
    'Accented' =>   [#9618f7, #ff0078]
    'Monochrome' => [#dee2e6, #495057]
//╔════════════════════════╗
//║          Plot          ║
//╚════════════════════════╝
Plot_MOM =     plot(MOM, color = MOM > MOM_MA ? color.new(UpC, 50):color.new(DnC, 50), linewidth = 3)
Plot_MOM_MA =  plot(MOM_MA,color = color.yellow)
midLinePlot = plot(50, color = color.new(color.white,50), display =display.pane)
fill(Plot_MOM, Plot_MOM_MA, 15000, MOM_MA, top_color = color.new(UpC, 0),    bottom_color = color.new(UpC, 100), title = 'Overbought Fill')
fill(Plot_MOM, Plot_MOM_MA, MOM_MA, -15000, top_color = color.new(DnC, 100), bottom_color = color.new(DnC, 0), title = 'Oversold Fill')
//╔════════════════════════╗
//║          VIEW          ║
//╚════════════════════════╝
var table Table_MOM = table.new(position.middle_right, 1, 1, border_width = 1)
table.cell(Table_MOM, 0, 0, 
     text = isBull_MOM ? '⬆️ Bullish' : '⬇️ Bearish', 
     text_color = isBull_MOM ? UpC : DnC,
     text_size = size.huge)
//╔════════════════════════╗
//║         Alerts         ║
//╚════════════════════════╝
alertcondition(isBull_MOM, title= 'LONG', message= '{{ticker}} LONG')
alertcondition(isBear_MOM, title= 'SHORT', message= '{{ticker}} SHORT')

// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © refiktuzakli

//@version=6
indicator("True Range Percentage MACD","TR% MACD", timeframe = "", timeframe_gaps = true)

// Inputs
float  sourceInput  = input.source(hl2, "Source")
int    fastLenInput = input.int(12, "Fast length",   1)
int    slowLenInput = input.int(26, "Slow length",   1)
int    sigLenInput  = input.int(9,  "Signal length", 1)
int    trLenInput  = input.int(14,  "TR smoothing length", minval=1)
bool   trGaps = input.bool(true, "TR: handle gaps")
bool   showTRP = input.bool(false, "Show TR%")
string oscTypeInput = input.string("EMA", "Oscillator MA type", ["EMA","SMA","RMA","VWMA","WMA","ALMA","SWMA","HMA"], display = display.data_window)
string sigTypeInput = input.string("EMA", "Signal MA type",     ["EMA","SMA","RMA","VWMA","WMA","ALMA","SWMA","HMA"], display = display.data_window)
string trTypeInput = input.string("NONE", "True Range MA type", ["EMA","SMA","RMA","VWMA","WMA","ALMA","SWMA","HMA","NONE"], display = display.data_window)

// @function    Calculates an EMA or SMA of a `source` series.
ma(float source, int length, simple string maType) =>
    switch maType
        "NONE" => source
        "EMA"  => ta.ema(source, length)
        "SMA"  => ta.sma(source, length)
        "RMA"  => ta.rma(source, length)
        "VWMA" => ta.vwma(source, length)
        "WMA"  => ta.wma(source, length)
        "ALMA" => ta.alma(source, length, 0.85, 6)
        "SWMA" => ta.swma(source)
        "HMA"  => ta.hma(source, length)

// Calculate input
float denom = math.max(math.abs(sourceInput[1]), syminfo.mintick)

// float trpRaw = ta.tr(input.bool(true,"timeframe")) * 100.0 / denom
float trpRaw = ta.tr(trGaps) * 100.0 / denom
float atrp   = ma(trpRaw, trLenInput, trTypeInput)   // (name could be trpSmoothed)

// Calculate MACD, Signal and Histogram values
float maFast = ma(atrp, fastLenInput, oscTypeInput)
float maSlow = ma(atrp, slowLenInput, oscTypeInput)
float macd   = maFast - maSlow
float signal = ma(macd, sigLenInput, sigTypeInput)
float hist   = macd - signal

// Histogram coloring
color hColor = hist >= 0 ? hist > hist[1] ? #26a69a : #b2dfdb : hist > hist[1] ? #ffcdd2 : #ff5252

// Plot
hline(0, "Zero", #787b8680)
plot(hist, "Histogram", hColor, style = plot.style_columns)
plot(macd, "MACD")
plot(signal, "Signal line", #ff6d00)
plot(showTRP ? atrp : na, "TR% (smoothed)", color=color.gray)

// Create alert conditions.
alertcondition(hist[1] >= 0 and hist < 0, "Rising to falling", "MACD histogram switched from a rising to falling state")
alertcondition(hist[1] <= 0 and hist > 0, "Falling to rising", "MACD histogram switched from a falling to rising state")
alertcondition(ta.crossover(macd, signal), "MACD cross up", "MACD crossed above Signal")
alertcondition(ta.crossunder(macd, signal), "MACD cross down", "MACD crossed below Signal")
alertcondition(ta.crossover(macd, 0), "MACD above zero", "MACD crossed above zero")
alertcondition(ta.crossunder(macd, 0), "MACD below zero", "MACD crossed below zero")

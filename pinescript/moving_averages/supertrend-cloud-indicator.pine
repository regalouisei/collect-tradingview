// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © stanislavbenov04
// Last Updated: 23/02/2026 <-- Irish Date Format
//@version=6
indicator("SuperTrend Cloud Indicator", shorttitle = "STCIMA's", overlay = true)

// User Input
atrPeriod = input.int(10, title = "ATR Period")
factor = input.float(3, title = "Factor")
srcInput = input.source(close, "Source Price")

// Smoothing Inputs
useSmooth = input.bool(true, "Enable Smoothing")
smoothLen = input.int(10, "Smoothing Length", minval = 1)
maType = input.string("HMA", "Smoothing MA Type?", options = ["SMA", "HMA", "EMA", "RMA", "WMA"])

// Smoothing Logic
get_ma(type, source, len) =>
    switch type
        "SMA" => ta.sma(source, len)
        "EMA" => ta.ema(source, len)
        "HMA" => ta.hma(source, len)
        "RMA" => ta.rma(source, len)
        "WMA" => ta.wma(source, len)
        => source

// Applying smoothing to source type
smoothedSrc = useSmooth ? get_ma(maType, srcInput, smoothLen) : srcInput

// SuperTrend calculation
atrValue = ta.atr(atrPeriod)
upband = smoothedSrc - (factor * atrValue)
dnband = smoothedSrc + (factor * atrValue)

// Variables to Hold Current Trend
var float trailStop = 0.0
var int _direction = 1

// Logic to Switch Trends
if _direction == 1
    trailStop := math.max(upband, nz(trailStop[1], upband))
    if close < trailStop
        _direction := -1
        trailStop := dnband
else
    trailStop := math.min(dnband, nz(trailStop[1], dnband))
    if close > trailStop
        _direction := 1
        trailStop := upband


// Plotting Visuals
lineColor = _direction == 1 ? color.new(color.green, 0) : color.new(color.red, 0)
plot(trailStop, title = "SuperTrend Stop", color = lineColor, linewidth = 2)

// Cloud Plotting Visuals
p1 = plot(smoothedSrc, title = "Smoothed Price", display = display.none)
p2 = plot(trailStop, title = "Trail Stop Line", display = display.none)
fill(p1, p2, color = color.new(lineColor, 85), title = "Trend Cloud")

// Signal Shapes Plotting
plotshape(_direction == 1 and _direction == -1,  title = "Buy Signal", style = shape.triangleup, location = location.belowbar, color = color.green, size = size.small)
plotshape(_direction == -1 and _direction == 1, title = "Sell Signal", style = shape.triangledown, location = location.abovebar, color = color.red, size = size.small)

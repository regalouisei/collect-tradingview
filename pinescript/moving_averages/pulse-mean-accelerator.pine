// This Pine ScriptÂ® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© MisinkoMaster

//@version=6
indicator("Pulse Mean Accelerator", "PMA | MisinkoMaster", overlay = true, behind_chart = false)
/////////////////////////////////////////////////////////////////////////////////////////////////
//Import Libraries
import TradingView/ta/12
/////////////////////////////////////////////////////////////////////////////////////////////////
//Get User Defined Inputs
src = input.source(close, "Source",
     group = "Pulse Mean Accelerator | MisinkoMaster")

matype = input.string("RMA", "Moving Average Type",
     ["RMA", "SMA", "EMA", "WMA", "DEMA", "TEMA", "HMA"],
     tooltip = "Changes the Moving Average Type Used in the indicator.",
     group = "Pulse Mean Accelerator | MisinkoMaster")
len = input.int(20, "Moving Average Length", step = 1, minval = 2,
     tooltip = "Changes the lookback period of the Moving Average",
     group = "Pulse Mean Accelerator | MisinkoMaster")

palen = input.int(32, "Pulse Acceleration Lookback", step = 1, minval = 2,
     tooltip = "Changes the lookback for acceleration of the Moving Average.",
     group = "Pulse Mean Accelerator | MisinkoMaster")
max_ = input.float(0.2, "Maximum Acceleration", step = 0.01, minval = 0.01,
     tooltip = "Changes the maximum acceleration and how fast it accelerates.",
     group = "Pulse Mean Accelerator | MisinkoMaster")
vtype = input.string("Standard Deviation", "Volatility Type",
     ["ATR", "Standard Deviation", "MAD"],
     "Changes the type of volatility calculation used",
     group = "Pulse Mean Accelerator | MisinkoMaster")
     
smooth = input.string("Double Moving Average", "Smoothing",
     ["NONE", "Exponential", "Extra Moving Average", "Double Moving Average"],
     tooltip = "Changes how the indicator is smoothed",
     group = "Pulse Mean Accelerator | MisinkoMaster")
con = input.bool(true, "Use confirmation?",
     group = "Pulse Mean Accelerator | MisinkoMaster")
/////////////////////////////////////////////////////////////////////////////////////////////////
//Calculations
F_MA(source, length, MovingAverageType) =>
    ma = switch MovingAverageType
        "RMA" => ta.rma(source, length)
        "SMA" => ta.sma(source, length)
        "EMA" => ta.ema(source, length)
        "WMA" => ta.wma(source, length)
        "DEMA" => ta.dema(source, length)
        "TEMA" => ta.tema(source, length)
        "HMA" => ta.hma(source, length)

    [ma]

[ma] = F_MA(src, len, matype)

roc_ma = ma-ma[1]
roc_src = src-src[1]

step = max_/palen
acc = 0.0

for i = 0 to palen-1
    if math.abs(roc_src) > math.abs(roc_ma)
        acc += step
    if math.abs(roc_ma) > math.abs(roc_src)
        acc += -step

[atr] = F_MA(ta.tr(true), palen, matype)
[mad] = F_MA(math.abs(ma-src), palen, matype)
sd = ta.stdev(ma, palen)

vol = switch vtype
    "ATR" => atr
    "Standard Deviation" => sd
    "MAD" => mad

[avgr] = F_MA(roc_ma, palen, matype)
absr = math.abs(avgr)

direction = absr != 0.0 ? avgr / absr : 0.0


pma_ = ma+(acc*vol*direction)

sqrt = math.round(math.sqrt(len))
[pma_ma] = F_MA(pma_, sqrt, matype)

a = 2/(1+len)
pma_e = pma_*(1-a)+ma*a

[pma_d] = F_MA(pma_ma, sqrt, matype)
pma = switch smooth
    "NONE" => pma_
    "Exponential" => pma_e
    "Extra Moving Average" => pma_ma
    "Double Moving Average" => pma_d

CL = con ? (pma-pma[1])+(ma-ma[1])>0 : true
CS = con ? (pma-pma[1])+(ma-ma[1])<0 : true
/////////////////////////////////////////////////////////////////////////////////////////////////
//Trend Logic
var trend = 0

L = src > pma and CL 
S = src < pma and CS

if L
    trend := 1
if S
    trend := -1
/////////////////////////////////////////////////////////////////////////////////////////////////
//Plotting
var col = color.rgb(58, 58, 58)
var colM = color.rgb(58, 58, 58, 20)
var colT = color.rgb(58, 58, 58, 60)
var colMT = color.rgb(58, 58, 58, 40)

if trend == 1
    col := color.rgb(0, 255, 187)
    colM := color.rgb(0, 255, 187, 20)
    colMT := color.rgb(0, 255, 187, 40)
    colT := color.rgb(0, 255, 187, 60)
if trend == -1
    col := color.rgb(255, 0, 157)
    colM := color.rgb(255, 0, 157, 20)
    colMT := color.rgb(255, 0, 157, 40)
    colT := color.rgb(255, 0, 157, 60)

plotcandle(open, high, low, close, bordercolor = col, color = col, wickcolor = col, display = display.pane, force_overlay = true)
plotshape(trend == 1 and trend[1] != 1 ? src : na, style = shape.labelup, location = location.belowbar, size = size.normal, color = color.rgb(0, 255, 187), display = display.pane, force_overlay = true, text = "ð“›ð“¸ð“·ð“°", textcolor = color.rgb(7, 114, 86))
plotshape(trend == -1 and trend[1] != -1 ? src : na, style = shape.labeldown,location = location.abovebar, size = size.normal,color = color.rgb(255, 0, 157), display = display.pane, force_overlay = true, text = "ð“¢ð“±ð“¸ð“»ð“½", textcolor = color.rgb(95, 11, 63))

map = plot(ma, "Moving Average", colM, linewidth = 2)
pmap = plot(pma, "Pulse Mean Accelerator", col, linewidth = 2)
s = plot(close, display= display.none, editable= false)

fill(map, pmap, colMT)
fill(pmap, s, colT, fillgaps = true)

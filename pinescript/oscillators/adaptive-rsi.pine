// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © MisinkoMaster

//@version=6
indicator("Adaptive RSI", "ARSI | MisinkoMaster", overlay = false)
////////////////////////////////////////////////////////////////////////////////////////////////////
//Import Libraries
import TradingView/ta/12
////////////////////////////////////////////////////////////////////////////////////////////////////
//Get User Defined Inputs
len = input.int(37, "RSI Length", step = 1, minval = 2,
     tooltip = "Changes the RSI length",
     group = "Adaptive RSI | MisinkoMaster", inline = "RSI")
src_ = input.source(high, "Source",
     tooltip = "Changes the source of calculation",
     group = "Adaptive RSI | MisinkoMaster", inline = "RSI")
smooth = input.int(8, "Smoothing Length", step = 1, minval = 2,
     tooltip = "Changes the Smoothing length",
     group = "Adaptive RSI | MisinkoMaster")

l = input.int(55, "Long", step = 1, minval = 0, maxval = 100,
      tooltip = "Changes the Long Treshhold of the RSI",
      group = "Adaptive RSI | MisinkoMaster", inline = "T")
s = input.int(45, "Short", step = 1, minval = 0, maxval = 100,
      tooltip = "Changes the Short Treshhold of the RSI",
      group = "Adaptive RSI | MisinkoMaster", inline = "T")

ob = input.int(85, "Overbought", step = 1, minval = 0, maxval = 100,
      tooltip = "Changes the Short Treshhold of the RSI",
      group = "Adaptive RSI | MisinkoMaster", inline = "TO")
os = input.int(20, "Oversold", step = 1, minval = 0, maxval = 100,
      tooltip = "Changes the Oversold Treshhold of the RSI",
      group = "Adaptive RSI | MisinkoMaster", inline = "TO")

speed_tier = input.string("Medium", "Speed",
     options = ["Slow", "Medium", "Fast"],
     tooltip = "Changes the type of speed and noise the RSI will have.",
     group = "Adaptive RSI | MisinkoMaster", inline = "Adapt")

priority = input.string("TVR", "Priority",
      options = ["TVR", "TRV", "VTR", "VRT", "RTV", "RVT"],
      tooltip = "Changes the order of checking the signals,
      V = Volume, R = Rate of Change, T = True Range,
      the order of the letters is the order of the priority",
      group = "Adaptive RSI | MisinkoMaster", inline = "Adapt")

src_U = input.bool(false, "Use Special Source?",
     tooltip = "If true, the RSI will use a special weighted source",
     group = "Special Settings")
////////////////////////////////////////////////////////////////////////////////////////////////////
//Calculations

roc_open = math.abs(open-open[1])
roc_close = math.abs(close-close[1])
roc_high = math.abs(high-high[1])
roc_low = math.abs(low-low[1])

sqrtlen = math.abs(math.round(math.sqrt(smooth)))
special_src_ = open*roc_open+close*roc_close+high*roc_high+low*roc_low/(roc_open+roc_close+roc_high+roc_low)
special_src = ta.ema(special_src_, sqrtlen)

src = src_U ? special_src : src_

tr = ta.tr(true)
vol = volume
roc_ = ta.roc(src, 1)
roc = math.abs(roc_)

change = src - src[1]

gain = math.max(change, 0)
loss = math.abs(math.min(change, 0))


[avg_gain_s, avg_gain_m, avg_gain_f, avg_gain_ff]  = switch speed_tier
    "Slow" => [ta.rma(gain, len), ta.sma(gain, len), ta.ema(gain, len), ta.wma(gain, len)]
    "Medium" => [ta.sma(gain, len), ta.ema(gain, len), ta.wma(gain, len), ta.dema(gain, len)]
    "Fast" => [ta.ema(gain, len), ta.wma(gain, len), ta.dema(gain,len), ta.hma(gain, len)]

[avg_loss_s, avg_loss_m, avg_loss_f, avg_loss_ff]  = switch speed_tier
    "Slow" => [ta.rma(loss, len), ta.sma(loss, len), ta.ema(loss, len), ta.wma(loss, len)]
    "Medium" => [ta.sma(loss, len), ta.ema(loss, len), ta.wma(loss, len), ta.dema(loss, len)]
    "Fast" => [ta.ema(loss, len), ta.wma(loss, len), ta.dema(loss, len), ta.hma(loss, len)]

C01 = switch priority
    "TVR" => tr > tr[1]
    "TRV" => tr > tr[1]
    "VTR" => vol > vol[1]
    "VRT" => vol > vol[1]
    "RTV" => roc > roc[1]
    "RVT" => roc > roc[1]

C02 = switch priority
    "TVR" => vol > vol[1]
    "TRV" => roc > roc[1]
    "VTR" => tr > tr[1]
    "VRT" => roc > roc[1]
    "RTV" => tr > tr[1]
    "RVT" => vol > vol[1]

C03 = switch priority
    "TRV" => vol > vol[1]
    "TVR" => roc > roc[1]
    "VRT" => tr > tr[1]
    "VTR" => roc > roc[1]
    "RVT" => tr > tr[1]
    "RTV" => vol > vol[1]

avg_gain = avg_gain_s
avg_loss = avg_loss_s

if C01
    avg_gain := avg_gain_m
    avg_loss := avg_loss_m

if C01 and C02
    avg_gain := avg_gain_f
    avg_loss := avg_loss_f

if C01 and C02 and C03
    avg_gain := avg_gain_ff
    avg_loss := avg_loss_ff

rs = avg_gain/avg_loss
rsi = 100 - 100/(1+rs)


[rsi_s, rsi_m, rsi_f, rsi_ff]  = switch speed_tier
    "Slow" => [ta.rma(rsi, smooth), ta.sma(rsi, smooth), ta.ema(rsi, smooth), ta.wma(rsi, smooth)]
    "Medium" => [ta.sma(rsi, smooth), ta.ema(rsi, smooth), ta.wma(rsi, smooth), ta.dema(rsi, smooth)]
    "Fast" => [ta.ema(rsi, smooth), ta.wma(rsi, smooth), ta.dema(rsi, smooth), ta.hma(rsi, smooth)]

arsi = rsi_s
if C01
    arsi := rsi_m
if C01 and C02
    arsi := rsi_f
if C01 and C02 and C03
    arsi := rsi_ff

divergence = (rsi - arsi) + 50
////////////////////////////////////////////////////////////////////////////////////////////////////
//Trend Logic
var trend = 0

L = arsi > l
S = arsi < s

if L
    trend := 1
if S
    trend := -1
////////////////////////////////////////////////////////////////////////////////////////////////////
//Plotting
var col = color.rgb(0, 255, 187)
var colT = color.rgb(0, 255, 187, 50)
if trend == 1
    col := color.rgb(0, 255, 187)
    colT := color.rgb(0, 255, 187, 50)
if trend == -1
    col := color.rgb(255, 0, 157)
    colT := color.rgb(255, 0, 157, 50)

var colD = color.rgb(0, 255, 187, 70)
if divergence > 50
    colD := color.rgb(0, 255, 187, 70)
if divergence < 50
    colD := color.rgb(255, 0, 157, 70)

bc = arsi > ob and arsi == ta.highest(arsi, smooth)
sc = arsi < os and arsi == ta.lowest(arsi, smooth) 

plotshape(sc ? arsi : na, style = shape.diamond, location = location.absolute, size = size.normal, color = color.rgb(0, 255, 187, 50), display = display.pane)
plotshape(bc ? arsi : na, style = shape.diamond,location = location.absolute, size = size.normal,color = color.rgb(255, 0, 157, 50), display = display.pane)
plotshape(sc ? arsi : na, style = shape.diamond, location = location.absolute, size = size.small, color = color.rgb(0, 255, 187, 25), display = display.pane)
plotshape(bc ? arsi : na, style = shape.diamond,location = location.absolute, size = size.small,color = color.rgb(255, 0, 157, 25), display = display.pane)
plotshape(sc ? arsi : na, style = shape.diamond, location = location.absolute, size = size.tiny, color = color.rgb(0, 255, 187), display = display.pane)
plotshape(bc ? arsi : na, style = shape.diamond,location = location.absolute, size = size.tiny,color = color.rgb(255, 0, 157), display = display.pane)

bgcolor(arsi > ob ? color.rgb(255, 0, 157, 70) : color.rgb(0, 0, 0, 100), force_overlay = true)
bgcolor(arsi > ob ? color.rgb(255, 0, 157, 70) : color.rgb(0, 0, 0, 100), force_overlay = false)

bgcolor(arsi < os ? color.rgb(0, 255, 187, 70) : color.rgb(0, 0, 0, 100), force_overlay = true)
bgcolor(arsi < os ? color.rgb(0, 255, 187, 70) : color.rgb(0, 0, 0, 100), force_overlay = false)

lp = plot(l, title = "Long Treshhold", color = color.rgb(71, 70, 70), display = display.pane, linewidth = 3)
sp = plot(s, title = "Short Treshhold", color = color.rgb(71, 70, 70), display = display.pane, linewidth = 3)
obp = plot(ob, title = "OB Treshhold", color = color.rgb(0, 255, 187), display = display.pane, linewidth = 3)
osp = plot(os, title = "OS Treshhold", color = color.rgb(255, 0, 157), display = display.pane, linewidth = 3)

fill(sp, osp, color.rgb(255, 0, 157, 85))
fill(lp, obp, color.rgb(0, 255, 187, 85))
fill(lp, sp, color.rgb(71, 70, 70, 75))

plotcandle(open, high, low, close, bordercolor = col, color = col, force_overlay = true, display = display.pane)

plot(divergence, "Divergence", color = colD, style = plot.style_histogram, histbase = 50)
plot(arsi, "ARSI | MisinkoMaster", color = col, linewidth = 2)
plot(arsi, "ARSI | MisinkoMaster", color = colT, linewidth = 3)

//@version=5
// © RoyalNeuron
// This script is an original work by RoyalNeuron
// Licensed under Creative Commons Attribution-NonCommercial-ShareAlike 4.0
// https://creativecommons.org/licenses/by-nc-sa/4.0/
// © RoyalNeuron – [Royal Precision | Neural Power]

indicator("[RoyalNeuron] RSI-SMA [WidowMaker v2.0]", shorttitle="[RoyalNeuron] WidowMaker v2.0", overlay=false, max_lines_count=500, max_labels_count=500, max_boxes_count=50)


RN_src        = input.source(close,       title="RN Source")
RN_rsiLen     = input.int(14, minval=1,   title="RN RSI Length")
RN_smoothLen  = input.int(9,  minval=1,   title="RN Smooth Length")
RN_maType     = input.string("SMA", options=["SMA", "EMA"], title="RN MA Type")
RN_obLevel    = input.int(70,             title="RN Overbought Level")
RN_osLevel    = input.int(30,             title="RN Oversold Level")
RN_histLen    = input.int(5,  minval=1,   title="RN Histogram Smooth")

RN_pivotLen     = input.int(5,  minval=2,   title="RN Pivot Length")
RN_pivotEnabled = input.bool(true,          title="RN Show Price Pivots")


upLineColor    = input.color(color.new(color.green, 0),  title="Rising RSI Line Color")
downLineColor  = input.color(color.new(color.red,   0),  title="Falling RSI Line Color")
upHistColor    = input.color(color.new(color.green, 30), title="Positive Momentum Histogram")
downHistColor  = input.color(color.new(color.red,   30), title="Negative Momentum Histogram")
obBgColor      = input.color(color.new(color.red,   90), title="Overbought Background Shade")
osBgColor      = input.color(color.new(color.green, 90), title="Oversold Background Shade")
midlineColor   = input.color(color.new(color.gray,  70), title="Midline Color")
obLineColor    = input.color(color.new(color.red,   60), title="Overbought Line")
osLineColor    = input.color(color.new(color.green, 60), title="Oversold Line")

RN_rsiRaw     = ta.rsi(RN_src, RN_rsiLen)
RN_rsiSma     = ta.sma(RN_rsiRaw, RN_smoothLen)
RN_rsiEma     = ta.ema(RN_rsiRaw, RN_smoothLen)
RN_rsiSmth    = RN_maType == "SMA" ? RN_rsiSma : RN_rsiEma

RN_histBase   = ta.sma(RN_rsiSmth, RN_histLen)
RN_histLine   = RN_rsiSmth - RN_histBase

RN_oscCol     = RN_rsiSmth > RN_rsiSmth[1] ? upLineColor : downLineColor
RN_histCol    = RN_histLine >= 0 ? upHistColor : downHistColor

RN_buyCond    = ta.crossover(RN_rsiSmth, RN_osLevel)
RN_sellCond   = ta.crossunder(RN_rsiSmth, RN_obLevel)


plot(RN_rsiSmth, title="RN Smoothed RSI", color=RN_oscCol, linewidth=3)

hline(50,         title="RN Midline",     color=midlineColor,          linestyle=hline.style_dashed)
hline(RN_obLevel, title="RN Overbought",  color=obLineColor,           linestyle=hline.style_dotted)
hline(RN_osLevel, title="RN Oversold",    color=osLineColor,           linestyle=hline.style_dotted)

plot(RN_histLine * 2, title="RN Trend Strength", style=plot.style_histogram, color=RN_histCol)

bgcolor(RN_rsiSmth > RN_obLevel ? obBgColor : na)
bgcolor(RN_rsiSmth < RN_osLevel ? osBgColor : na)

// Pivot detection for price
RN_pricePivotHigh = ta.pivothigh(high, RN_pivotLen, RN_pivotLen)
RN_pricePivotLow  = ta.pivotlow(low, RN_pivotLen, RN_pivotLen)

// Plot pivot high markers (only when RSI > 65)
if RN_pivotEnabled and not na(RN_pricePivotHigh)
    currentBar = bar_index - RN_pivotLen
    currentRsi = RN_rsiSmth[RN_pivotLen]
    if currentRsi > 65
        label.new(currentBar, currentRsi, "H",
                  color=color.new(color.red, 0), textcolor=color.white,
                  style=label.style_label_down, size=size.small,
                  tooltip="Pivot High: " + str.tostring(RN_pricePivotHigh))

// Plot pivot low markers (only when RSI < 35)
if RN_pivotEnabled and not na(RN_pricePivotLow)
    currentBar = bar_index - RN_pivotLen
    currentRsi = RN_rsiSmth[RN_pivotLen]
    if currentRsi < 35
        label.new(currentBar, currentRsi, "L",
                  color=color.new(color.green, 0), textcolor=color.white,
                  style=label.style_label_up, size=size.small,
                  tooltip="Pivot Low: " + str.tostring(RN_pricePivotLow))


var table RN_watermark = table.new(position.bottom_right, 1, 1, bgcolor=color.rgb(10,15,30), border_width=1)

if barstate.islast
    table.cell(RN_watermark, 0, 0,
         text          = "[WidowMaker v2.0] - RSI-SMA-PIVOT\n© Royal Neuron 2025",
         text_color    = color.rgb(239, 191, 4),
         text_size     = size.small,
         text_halign   = text.align_center)

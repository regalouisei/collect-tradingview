// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © MisinkoMaster

//@version=6
indicator("RS Filtered RSI", "RSF RSI | MisinkoMaster", overlay = false)
/////////////////////////////////////////////////////////////////////////////////////////////////////////////
//Import Libraries
import TradingView/ta/12
/////////////////////////////////////////////////////////////////////////////////////////////////////////////
//Get User Defined Inputs
len = input.int(21, "Length", step = 1, minval = 2,
     tooltip = "Changes the length of the RSI",
     group = "RS Filtered RSI | MisinkoMaster")
src = input.source(close, "Source",
     tooltip = "Changes the source of the calculation.",
     group = "RS Filtered RSI | MisinkoMaster")
rslen = input.int(12, "Filter Length", step = 1, minval = 2,
     tooltip = "Changes the length of the filter",
     group = "RS Filtered RSI | MisinkoMaster")

N = input.int(2, "Fourier Length", step = 1, minval=2,
     group = "RS Filtered RSI | MisinkoMaster")

ut = input.int(55, "Upper Treshhold", step = 1,
     group = "RS Filtered RSI | MisinkoMaster", inline = "T")
lt = input.int(45, "Lower Treshhold", step = 1,
     group = "RS Filtered RSI | MisinkoMaster", inline = "T")

ob = input.int(85, "Overbought", step = 1, minval = 0, maxval = 100,
      tooltip = "Changes the Short Treshhold of the RSI",
      group = "RS Filtered RSI | MisinkoMaster", inline = "TO")
os = input.int(20, "Oversold", step = 1, minval = 0, maxval = 100,
      tooltip = "Changes the Oversold Treshhold of the RSI",
      group = "RS Filtered RSI | MisinkoMaster", inline = "TO")

/////////////////////////////////////////////////////////////////////////////////////////////////////////////
//Calculations
F_RSI(src, len) =>
    change = src-src[1]

    gain = math.max(change, 0)
    loss = math.abs(math.min(change, 0))

    ag = ta.rma(gain, len)
    al = ta.rma(loss, len)

    rsi = 100 - 100/(1+ag/al)
    [rsi]

[rsi_b] = F_RSI(src, rslen)
rsi_a = math.max(rsi_b, 100-rsi_b) 

DFT(x, y, Nx, _dir) =>
    float _arg = 0.0
    float _cos = 0.0
    float _sin = 0.0
    float xArr_i = 0.0
    float yArr_i = 0.0
    xArr = array.new_float(array.size(x))
    yArr = array.new_float(array.size(y))

    for i = 0 to Nx - 1 by 1
        xArr_i := 0.0
        yArr_i := 0.0
        kx = float(i) / float(Nx)
        _arg := -_dir * 2 * math.pi * kx
        for k = 0 to Nx - 1 by 1
            _cos := math.cos(k * _arg)
            _sin := math.sin(k * _arg)
            xArr_i += array.get(x, k) * _cos - array.get(y, k) * _sin
            yArr_i += array.get(x, k) * _sin + array.get(y, k) * _cos
            yArr_i
        array.set(xArr, i, xArr_i)
        array.set(yArr, i, yArr_i)

    if _dir == 1
        for i = 0 to Nx - 1 by 1
            array.set(x, i, array.get(xArr, i) / float(Nx))
            array.set(y, i, array.get(yArr, i) / float(Nx))
    else
        for i = 0 to Nx - 1 by 1
            array.set(x, i, array.get(xArr, i))
            array.set(y, i, array.get(yArr, i))

x = array.new_float(N, 0.0)
y = array.new_float(N, 0.0)
for i = 0 to N - 1
    array.set(x, i, src[i])
    array.set(y, i, 0.0)

DFT(x, y, N, 1)

mag = array.new_float(N, 0.0)
for i = 0 to N - 1
    mag_i = math.sqrt(math.pow(array.get(x, i), 2) + math.pow(array.get(y, i), 2))
    array.set(mag, i, mag_i)

subject = array.get(mag,0)

filter = 0.0
sum = 0.0
for i = 0 to rslen-1
    filter += subject*rsi_a
    sum += rsi_a

filter := filter/sum

[rsi_] = F_RSI(filter, len)

divergence = (rsi_ - rsi_[1]) + 50

rsi = rsi_+divergence-50
/////////////////////////////////////////////////////////////////////////////////////////////////////////////
//Trend Logic
var trend = 0

L = rsi > ut
S = rsi < lt

if L
    trend := 1
if S
    trend := -1
/////////////////////////////////////////////////////////////////////////////////////////////////////////////
//Plotting
var col = color.rgb(0, 255, 187)
var colT = color.rgb(0, 255, 187, 50)
if trend == 1
    col := color.rgb(0, 255, 187)
    colT := color.rgb(0, 255, 187, 50)
if trend == -1
    col := color.rgb(255, 0, 157)
    colT := color.rgb(255, 0, 157, 50)

var colD = color.rgb(0, 255, 187, 70)
if divergence > 50
    colD := color.rgb(0, 255, 187, 70)
if divergence < 50
    colD := color.rgb(255, 0, 157, 70)

lp = plot(ut, title = "Long Treshhold", color = color.rgb(71, 70, 70), display = display.pane, linewidth = 3)
sp = plot(lt, title = "Short Treshhold", color = color.rgb(71, 70, 70), display = display.pane, linewidth = 3)
obp = plot(ob, title = "OB Treshhold", color = color.rgb(0, 255, 187), display = display.pane, linewidth = 3)
osp = plot(os, title = "OS Treshhold", color = color.rgb(255, 0, 157), display = display.pane, linewidth = 3)

fill(sp, osp, color.rgb(255, 0, 157, 85))
fill(lp, obp, color.rgb(0, 255, 187, 85))
fill(lp, sp, color.rgb(71, 70, 70, 75))

obr0 = rsi > ob ? rsi : na
osr0 = rsi < os ? rsi : na

obr = plot(obr0, display = display.none, editable = false)
osr = plot(osr0, display = display.none, editable = false)

fill(obp, obr, color = colT)
fill(osp, osr, color = colT)

plotcandle(open, high, low, close, bordercolor = col, color = col, force_overlay = true, display = display.pane)

plot(divergence, "Change", color = colD, style = plot.style_histogram, histbase = 50)
plot(rsi, "RS Filtered RSI | MisinkoMaster", color = col, linewidth = 2)
plot(rsi, "RS Filtered RSI | MisinkoMaster", color = colT, linewidth = 3)

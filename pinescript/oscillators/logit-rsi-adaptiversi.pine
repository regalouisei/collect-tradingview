// This Pine Script™ code is subject to the terms of the Creative Commons
// Attribution-NonCommercial-ShareAlike 4.0 International License:
// https://creativecommons.org/licenses/by-nc-sa/4.0/
//
// This license requires that reusers give credit to the creator.
// It allows reusers to distribute, remix, adapt, and build upon the material
// in any medium or format, for non-commercial purposes only.
// If others modify or adapt the material, they must license the modified
// material under identical terms.
//
// CC BY-NC-SA 4.0:
// BY: Credit must be given to the creator.
// NC: Only non-commercial use of this work is permitted. “Non-commercial” means
//     not primarily intended for or directed towards commercial advantage
//     or monetary compensation.
// SA: Adaptations must be shared under the same terms.
//
// © AdaptiveRSI

//@version=6
indicator(title = 'Logit RSI [AdaptiveRSI]',
          shorttitle = 'Logit RSI',
          format = format.price,
          precision = 2,
          timeframe = '',
          timeframe_gaps = true)

// ─── RSI Settings ────────────────────────────────────────────
Length  = input.int(14, minval = 2, title = 'Length', group = 'RSI Settings')
RSIType = input.string('logit', 'RSI Plot:', options = ['logit', 'regular'], inline = 'RSI', group = 'RSI Settings')

// ─── Bollinger Bands Settings ────────────────────────────────
show_BB       = input.bool(true, 'Plot Bollinger Bands', group = 'Bollinger Bands Settings')
maLengthInput = input.int(50, 'Length', group = 'Bollinger Bands Settings', display = display.data_window)
bbMultInput   = input.float(2.0, 'BB StdDev', minval = 0, maxval = 5, step = 0.1, group = 'Bollinger Bands Settings', display = display.data_window)

// ─── Logit Transform Function ────────────────────────────────
logit_transform(x, eps) =>
    // Clamp RSI to avoid log(∞) at 0 or 100
    xc = math.min(math.max(x, eps), 100 - eps)
    math.log(xc / (100 - xc))

// ─── Main RSI Calculation ───────────────────────────────────
rsi_raw   = ta.rsi(close, Length)
eps       = 0.000001
logit_RSI = logit_transform(rsi_raw, eps)

// Switch between regular and logit RSI
plot_RSI = RSIType == 'logit' ? logit_RSI : rsi_raw

// ─── Plots ──────────────────────────────────────────────────
plot(plot_RSI, 'RSI', color.new(color.gray, 25), linewidth = 2)

// Bollinger Bands on the chosen RSI series
smoothingMA    = ta.sma(plot_RSI, maLengthInput)
smoothingStDev = ta.stdev(plot_RSI, maLengthInput) * bbMultInput

plot(smoothingMA, 'RSI-based MA', color = #F2BD1D, display = show_BB ? display.all : display.none, linewidth = 1, editable = true)
bbUpperBand = plot(smoothingMA + smoothingStDev, title = 'Upper Bollinger Band', color = #33E643, display = show_BB ? display.all : display.none, editable = true)
bbLowerBand = plot(smoothingMA - smoothingStDev, title = 'Lower Bollinger Band', color = #33E643, display = show_BB ? display.all : display.none, editable = true)
fill(bbUpperBand, bbLowerBand, color = show_BB ? color.new(#33A645, 90) : na, title = 'Bollinger Bands Background Fill', display = show_BB ? display.all : display.none, editable = true)

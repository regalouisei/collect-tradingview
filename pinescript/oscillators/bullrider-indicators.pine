//@version=5
indicator(title="BULLRIDER INDICATORS", shorttitle="BULLRIDER INDICATORS", overlay=false, scale=scale.right)

//BULLRIDER (Yusuf Benli) YT: https://www.youtube.com/@yusufbenliyb
///BULLRIDER (Yusuf Benli) X: https://x.com/yusufbenli_yb
///BULLRIDER (Yusuf Benli) TikTok: https://www.tiktok.com/@yusufbenliyb


// ==========================================
// SECTION 1: MOVING AVERAGES (HAREKETLİ ORTALAMALAR)
// ==========================================
grp_ma = "─── HAREKETLİ ORTALAMALAR ───"
show_ma = input.bool(true, "MA'ları Göster", group=grp_ma)

ma_type = input.string(defval="Özel", options=["7-21", "11-22", "50-200", "Özel"], title="MA Ayarı", group=grp_ma)
ma_c1 = input.int(9, "Özel MA 1", group=grp_ma)
ma_c2 = input.int(22, "Özel MA 2", group=grp_ma)
ma_src = input.source(close, "Kaynak", group=grp_ma)

int len1 = 0
int len2 = 0

if ma_type == "7-21"
    len1 := 7
    len2 := 21
else if ma_type == "11-22"
    len1 := 11
    len2 := 22
else if ma_type == "50-200"
    len1 := 50
    len2 := 200
else
    len1 := ma_c1
    len2 := ma_c2

out1 = ta.sma(ma_src, len1)
out2 = ta.sma(ma_src, len2)

plot(show_ma ? out1 : na, color=color.rgb(4, 0, 245), title="MA 1", linewidth=2, force_overlay=true)
plot(show_ma ? out2 : na, color=color.rgb(255, 0, 0), title="MA 2", linewidth=2, force_overlay=true)


// ==========================================
// SECTION 2: RSI
// ==========================================
grp_rsi = "─── 1. PENCERE: RSI ───"
rsi_scale = input.float(1.0, "Scale", step=0.1, group=grp_rsi)
rsi_shift = input.int(20, "Shift", step=10, group=grp_rsi)
rsiLen = input.int(14, "RSI Uzunluğu", group=grp_rsi)
rsiSrc = input.source(close, "RSI Kaynak", group=grp_rsi)
show_div = input.bool(false, "Div Göster", group=grp_rsi)
lbl_offset = input.int(3, "Etiket Mesafesi (Sağa Doğru)", minval=1, group=grp_rsi)

// Calculations
rsi_change = ta.change(rsiSrc)
rsi_up = ta.rma(math.max(rsi_change, 0), rsiLen)
rsi_down = ta.rma(-math.min(rsi_change, 0), rsiLen)
rsi_raw = rsi_down == 0 ? 100 : rsi_up == 0 ? 0 : 100 - (100 / (1 + rsi_up / rsi_down))

// visual location
rsi_final = (rsi_raw * rsi_scale) + rsi_shift

// Background
rsi_top = (100 * rsi_scale) + rsi_shift
rsi_bot = (0 * rsi_scale) + rsi_shift
fill(plot(rsi_top, display=display.none), plot(rsi_bot, display=display.none), color=color.rgb(126, 87, 194, 95))

// Drawing
plot(rsi_final, "RSI Görsel", color=#7E57C2, linewidth=2, display=display.pane)
plot(rsi_raw, "RSI Gerçek", color=#7E57C2, display=display.data_window + display.status_line)

// references
p70 = plot((70 * rsi_scale) + rsi_shift, "RSI 70", color=#787B86, display=display.pane)
p30 = plot((30 * rsi_scale) + rsi_shift, "RSI 30", color=#787B86, display=display.pane)
fill(p70, p30, color=color.new(#7E57C2, 90))

// label (RSI)
var label lbl_rsi = na
if barstate.islast
    label.delete(lbl_rsi)
    lbl_rsi := label.new(bar_index + lbl_offset, rsi_final, text=str.tostring(rsi_raw, "#.##"), color=#7E57C2, style=label.style_label_left, textcolor=color.white)

// Divergence
pl = ta.pivotlow(rsi_raw, 5, 5)
ph = ta.pivothigh(rsi_raw, 5, 5)
plotshape(show_div and not na(pl) ? (pl * rsi_scale) + rsi_shift : na, style=shape.labelup, location=location.absolute, color=color.green, text="B", offset=-5, textcolor=color.white)
plotshape(show_div and not na(ph) ? (ph * rsi_scale) + rsi_shift : na, style=shape.labeldown, location=location.absolute, color=color.red, text="S", offset=-5, textcolor=color.white)

hline(20, "line between indicators-1 (indikatörler arası çizgi-1)", color=color.white, linewidth=1)


// ==========================================
// SECTION 3: MACD SLOW
// ==========================================
grp_macd1 = "─── 2. WINOW: MACD SLOW───"
scale_m1 = input.float(1200, "Scale", step=50, group=grp_macd1)
shift_m1 = input.int(-60, "Shift", step=10, group=grp_macd1)
f_len1 = input.int(12, "Fast", group=grp_macd1)
s_len1 = input.int(31, "Slow", group=grp_macd1)
sig_len1 = input.int(14, "Signal", group=grp_macd1)

[mac1, sig1, hist1] = ta.macd(close, f_len1, s_len1, sig_len1)

// visual data
norm_hist1 = (hist1 / close) * scale_m1 + shift_m1
norm_mac1  = (mac1 / close) * scale_m1 + shift_m1
norm_sig1  = (sig1 / close) * scale_m1 + shift_m1
col_hist1 = hist1 >= 0 ? (hist1[1] < hist1 ? #26A69A : #B2DFDB) : (hist1[1] < hist1 ? #FFCDD2 : #FF5252)

// Background
fill(plot(shift_m1 + 40, display=display.none), plot(shift_m1 - 40, display=display.none), color=color.rgb(33, 149, 243, 95))

// Drawing
plot(norm_hist1, "MACD SLOW Hist", style=plot.style_columns, color=col_hist1, histbase=shift_m1, display=display.pane)
plot(norm_mac1, "MACD SLOW Line", color=#2962FF, linewidth=1, display=display.pane)
plot(norm_sig1, "MACD SLOW Signal", color=#FF6D00, linewidth=1, display=display.pane)
plot(shift_m1, "Sıfır", color=color.new(color.white, 50), style=plot.style_circles, display=display.pane)

// true values
plot(hist1, "Real Hist", color=col_hist1, display=display.data_window)
plot(mac1, "Real MACD", color=#2962FF, display=display.data_window)
plot(sig1, "Real Signal", color=#FF6D00, display=display.data_window)

// label (MACD SLOW)
var label lbl_mac1 = na
var label lbl_sig1 = na

if barstate.islast
    label.delete(lbl_mac1)
    label.delete(lbl_sig1)
    lbl_mac1 := label.new(bar_index + lbl_offset, norm_mac1, text=str.tostring(mac1, "#.##"), color=#2962FF, style=label.style_label_left, textcolor=color.white)
    lbl_sig1 := label.new(bar_index + lbl_offset, norm_sig1, text=str.tostring(sig1, "#.##"), color=#FF6D00, style=label.style_label_left, textcolor=color.white)

hline(-140, "line between indicators-2 (indikatörler arası çizgi-2)", color=color.white, linewidth=1)


// ==========================================
// SECTION 4: MACD FAST
// ==========================================
grp_macd2 = "─── 3. WINDOW: MACD FAST ───"
scale_m2 = input.float(1200, "Scale", step=50, group=grp_macd2)
shift_m2 = input.int(-220, "Shift", step=10, group=grp_macd2)
f_len2 = input.int(4, "Fast", group=grp_macd2)
s_len2 = input.int(9, "Slow", group=grp_macd2)
sig_len2 = input.int(14, "Signal", group=grp_macd2)

[mac2, sig2, hist2] = ta.macd(close, f_len2, s_len2, sig_len2)

// visual data
norm_hist2 = (hist2 / close) * scale_m2 + shift_m2
norm_mac2  = (mac2 / close) * scale_m2 + shift_m2
norm_sig2  = (sig2 / close) * scale_m2 + shift_m2
col_hist2 = hist2 >= 0 ? (hist2[1] < hist2 ? #26A69A : #B2DFDB) : (hist2[1] < hist2 ? #FFCDD2 : #FF5252)

// Background
fill(plot(shift_m2 + 40, display=display.none), plot(shift_m2 - 40, display=display.none), color=color.rgb(155, 39, 176, 95))

// Drawing
plot(norm_hist2, "MACD FAST Hist", style=plot.style_columns, color=col_hist2, histbase=shift_m2, display=display.pane)
plot(norm_mac2, "MACD FAST Line", color=color.purple, linewidth=1, display=display.pane)
plot(norm_sig2, "MACD FAST Signal", color=color.yellow, linewidth=1, display=display.pane)
plot(shift_m2, "Sıfır", color=color.new(color.white, 50), style=plot.style_circles, display=display.pane)

// True Values
plot(hist2, "Real Hist", color=col_hist2, display=display.data_window)
plot(mac2, "Real MACD", color=color.purple, display=display.data_window)
plot(sig2, "Real Signal", color=color.yellow, display=display.data_window)

// label (MACD FAST)
var label lbl_mac2 = na
var label lbl_sig2 = na

if barstate.islast
    label.delete(lbl_mac2)
    label.delete(lbl_sig2)
    lbl_mac2 := label.new(bar_index + lbl_offset, norm_mac2, text=str.tostring(mac2, "#.##"), color=color.purple, style=label.style_label_left, textcolor=color.white)
    lbl_sig2 := label.new(bar_index + lbl_offset, norm_sig2, text=str.tostring(sig2, "#.##"), color=color.yellow, style=label.style_label_left, textcolor=color.black)

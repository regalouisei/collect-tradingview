// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// ©ebecihalil

//@version=6
indicator("Institution Radar", overlay = false)

// === INPUTS === //
rsiLength = input.int(14, title="RSI Length")
src = input.source(close, title="RSI Source")
rsiTF = input.timeframe(title="RSI Timeframe (affects both lines)", defval = "")

sensitivityOpt = input.string( title="Signal Sensitivity",defval="5",options=["3", "4", "5", "6", "7", "8", "9", "10","11", "12", "13", "14", "15", "16", "17","18", "19", "20+"],tooltip="Lower = more signals, higher = fewer but stronger signals")

// === Convert dropdown to numeric value === //
signalSensitivity = switch sensitivityOpt
    "3"  => 3.0
    "4"  => 4.0
    "5"  => 5.0
    "6"  => 6.0
    "7"  => 7.0
    "8"  => 8.0
    "9"  => 9.0
    "10" => 10.0
    "11" => 11.0
    "12" => 12.0
    "13" => 13.0
    "14" => 14.0
    "15" => 15.0
    "16" => 16.0
    "17" => 17.0
    "18" => 18.0
    "19" => 19.0
    => 20.0   // "20+"

// === COLORS === //
rsiColor    = input.color(color.purple, title="RSI Line Color")
volumeColor = input.color(color.orange, title="Volume RSI Line Color")

// === RSI & CVD CALC === //
rsiPrice  = request.security(syminfo.tickerid, rsiTF, ta.rsi(src, rsiLength))
delta     = request.security(syminfo.tickerid, rsiTF, close > close[1] ? volume : close < close[1] ? -volume : 0)
cvd       = ta.cum(delta)
rsiVolume = ta.rsi(cvd, rsiLength)

// === VOLUME AVERAGE === //
avgVol = ta.sma(volume, 20)

// === DELTAS === //
dP        = rsiPrice - rsiPrice[1]
dV        = rsiVolume - rsiVolume[1]
deltaDiff = math.abs(dV - dP)
normalizedDeltaDiff = math.abs(rsiVolume - rsiPrice)   // equalized start-point diff

// === CONDITIONS === //
condRed1 = rsiPrice > 50 and rsiPrice[1] > 50 and rsiVolume > rsiPrice and rsiVolume[1] >= rsiPrice[1] and rsiVolume > rsiVolume[1] and dV > 0 and dV > dP and deltaDiff > signalSensitivity and volume > avgVol
condGreen1 = rsiPrice < 50 and rsiVolume < rsiPrice and rsiPrice[1] < 50 and rsiVolume[1] <= rsiPrice[1] and rsiVolume < rsiVolume[1] and dV < 0 and dV < dP and deltaDiff > signalSensitivity and volume > avgVol

condRed2 = rsiVolume < rsiPrice and rsiVolume[1] <= rsiPrice[1] and rsiPrice[1] > 50 and rsiPrice > 60 and rsiPrice > rsiPrice[1] and dV < dP and deltaDiff > signalSensitivity and volume < avgVol
condGreen2 = rsiVolume > rsiPrice and rsiVolume[1] >= rsiPrice[1] and rsiPrice < 40 and rsiPrice < rsiPrice[1] and rsiPrice[1] < 50 and dP < dV and deltaDiff > signalSensitivity and volume < avgVol

// === EXTRA CONDITIONS (start-point equalized) === //
condRed1_alt   = rsiPrice > 50 and rsiPrice[1] > 50 and rsiVolume > rsiPrice and rsiVolume > rsiVolume[1] and rsiVolume[1]  < rsiPrice[1] and dV > 0 and dV > dP and normalizedDeltaDiff > signalSensitivity and volume > avgVol
condGreen1_alt = rsiPrice < 50 and rsiVolume < rsiPrice and rsiPrice[1] < 50 and rsiVolume < rsiVolume[1] and rsiVolume[1]  > rsiPrice[1] and dV < 0 and dV < dP and normalizedDeltaDiff > signalSensitivity and volume > avgVol

condRed2_alt   = rsiVolume < rsiPrice and rsiPrice[1] > 50 and rsiPrice > 60 and rsiVolume[1]  > rsiPrice[1]  and dV < dP and normalizedDeltaDiff > signalSensitivity and volume < avgVol
condGreen2_alt = rsiVolume > rsiPrice and rsiPrice < 40 and rsiVolume[1]  < rsiPrice[1] and rsiPrice[1] < 50 and dP < dV and normalizedDeltaDiff > signalSensitivity and volume < avgVol

// === COMBINED CONDITIONS === //
condRed   = condRed1 or condRed2 or condRed1_alt or condRed2_alt
condGreen = condGreen1 or condGreen2 or condGreen1_alt or condGreen2_alt

// === PLOTS === //
rsiPlot = plot(rsiPrice, title="Price RSI", color=rsiColor, linewidth=2)
volPlot = plot(rsiVolume, title="Volume Delta RSI", color=volumeColor, linewidth=2)

hline(70, "Overbought", color=color.gray, linestyle=hline.style_dotted)
hline(30, "Oversold", color=color.gray, linestyle=hline.style_dotted)
hline(50, "Middle", color=color.gray, linestyle=hline.style_dashed)

upperLine = hline(70, "", editable=false)
lowerLine = hline(30, "", editable=false)
fill(upperLine, lowerLine, color=color.new(#549dd8, 85))

// === CONDITIONAL FILLS === //
greenColor = color.new(color.green, 0)
redColor   = color.new(color.red, 0)
fill(rsiPlot, volPlot, color = condGreen ? greenColor : na)
fill(rsiPlot, volPlot, color = condRed ? redColor : na)

// === LABELS === //
if condRed1
    label.new(bar_index, math.max(rsiPrice, rsiVolume) + 10, text="Absorption", style=label.style_label_down, color=color.red, textcolor=color.white)
if condRed1_alt
    label.new(bar_index, math.max(rsiPrice, rsiVolume) + 10, text="Absorption*", style=label.style_label_down, color=color.red, textcolor=color.white)
if condRed2
    label.new(bar_index, math.max(rsiPrice, rsiVolume) + 10, text="Exhaustion", style=label.style_label_down, color=color.red, textcolor=color.white)
if condRed2_alt
    label.new(bar_index, math.max(rsiPrice, rsiVolume) + 10, text="Exhaustion*", style=label.style_label_down, color=color.red, textcolor=color.white)

if condGreen1
    label.new(bar_index, math.min(rsiPrice, rsiVolume) - 10, text="Absorption", style=label.style_label_up, color=color.green, textcolor=color.white)
if condGreen1_alt
    label.new(bar_index, math.min(rsiPrice, rsiVolume) - 10, text="Absorption*", style=label.style_label_up, color=color.green, textcolor=color.white)
if condGreen2
    label.new(bar_index, math.min(rsiPrice, rsiVolume) - 10, text="Exhaustion", style=label.style_label_up, color=color.green, textcolor=color.white)
if condGreen2_alt
    label.new(bar_index, math.min(rsiPrice, rsiVolume) - 10, text="Exhaustion*", style=label.style_label_up, color=color.green, textcolor=color.white)

// === LEGEND LABELS === //
var label rsiLabel = label.new(x=bar_index, y=0, text=" ⬤ RSI", style=label.style_label_left, textcolor=rsiColor, size=size.large, color=color.new(color.gray, 100))
var label volLabel = label.new(x=bar_index, y=0, text=" ⬤ Volume Delta RSI", style=label.style_label_left, textcolor=volumeColor, size=size.large, color=color.new(color.gray, 100))

rsiY = rsiPrice > rsiVolume ? 53 : 47
volY = rsiPrice > rsiVolume ? 47 : 53
label.set_xy(rsiLabel, bar_index, rsiY)
label.set_textcolor(rsiLabel, rsiColor)
label.set_xy(volLabel, bar_index, volY)
label.set_textcolor(volLabel, volumeColor)

// === ALERT CONDITION (on bar close only) === //
signalTriggered = (condRed or condGreen) and (deltaDiff > signalSensitivity or normalizedDeltaDiff > signalSensitivity)
alertcondition(signalTriggered and barstate.isconfirmed,
      title="Institution Signal Triggered",
      message="Institution Radar: Red or Green signal triggered with sensitivity condition met.")

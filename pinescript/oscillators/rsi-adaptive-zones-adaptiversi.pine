// This Pine Script™ code is subject to the terms of the Creative Commons
// Attribution-NonCommercial-ShareAlike 4.0 International License:
// https://creativecommons.org/licenses/by-nc-sa/4.0/
//
// This license requires that reusers give credit to the creator.
// It allows reusers to distribute, remix, adapt, and build upon the material
// in any medium or format, for non-commercial purposes only.
// If others modify or adapt the material, they must license the modified
// material under identical terms.
//
// CC BY-NC-SA 4.0:
// BY: Credit must be given to the creator.
// NC: Only non-commercial use of this work is permitted. “Non-commercial” means
//     not primarily intended for or directed towards commercial advantage
//     or monetary compensation.
// SA: Adaptations must be shared under the same terms.
//
// © AdaptiveRSI

//@version=6
indicator(title = 'RSI adaptive zones [AdaptiveRSI]',
          shorttitle = 'AdaptiveRSI · RSI zones',
          format = format.price,
          precision = 2,
          timeframe = '',
          timeframe_gaps = true)

// ─── RSI Settings ─────────────────────────────────────────────
Length          = input.int(14, minval = 2, title = 'Length', group = 'RSI Settings')
RSIType         = input.string('candle', 'Plot RSI As:', options = ['line', 'bar', 'candle'], inline = 'RSI', group = 'RSI Settings')
color color_RSI = input(color.gray, '', inline = 'RSI', group = 'RSI Settings')

isLine          = RSIType == 'line'
isBar           = RSIType == 'bar'
isCandle        = RSIType == 'candle'

// ─── Visibility & Colors ─────────────────────────────────────
SupRes          = input.bool(true, 'Support / Resistance Zones', inline = 'SupRes', group = 'Visibility and Colors')
color_SR        = input.color(color.new(color.gray, 50), '', inline = 'SupRes', group = 'Visibility and Colors')

OO              = input.bool(true, 'Oversold / Overbought Zones', inline = 'zones_coloring', group = 'Visibility and Colors')
color_R         = input.color(#D92323, '', inline = 'zones_coloring', group = 'Visibility and Colors')
color_G         = input.color(#33A645, '', inline = 'zones_coloring', group = 'Visibility and Colors')

OO_coloring     = input.bool(true, 'Oversold / Overbought Coloring', inline = 'OO_coloring', group = 'Visibility and Colors')
color_GI        = input.color(#33E643, '', inline = 'OO_coloring', group = 'Visibility and Colors')
color_RI        = input.color(#F22738, '', inline = 'OO_coloring', group = 'Visibility and Colors')

Zones_Values    = input.bool(false, 'Show values on scale', group = 'Visibility and Colors')

// ─── Smoothing Settings ──────────────────────────────────────
smoothing_group = 'RSI Smoothing'
TT_MA = 'All smoothing is performed in logit-transformed RSI space and then converted back to the 0-100 RSI scale. Refer to script documentation for more details.'
TT_BB = 'Only applies when “SMA + Bollinger Bands” is selected. Bands are computed in logit RSI space so they never exceed the 0-100 range. Refer to script documentation for more details.'


rsiSourceInput = input.string('Close', 'RSI input',
                              options = ['Close', '(H + L)/2', '(H + L + C)/3', '(H + L + C + C)/4'],
                              group = smoothing_group, display = display.data_window)

maTypeInput    = input.string('None', 'MA Type',
                              options = ['None', 'SMA', 'SMA + Bollinger Bands', 'EMA', 'SMMA (RMA)', 'WMA', 'VWMA'],
                              tooltip = TT_MA, group = smoothing_group, display = display.data_window)

isBB           = maTypeInput == 'SMA + Bollinger Bands'
enableMA       = maTypeInput != 'None'

maLengthInput  = input.int(21, 'MA Length', inline = 'LMA', group = smoothing_group,
                           display = display.data_window, active = maTypeInput != 'None')
color_LMA      = input.color(#F2BD1D, '', inline = 'LMA', group = smoothing_group)
bbMultInput    = input.float(2.0, 'BB StdDev', minval = 0, maxval = 10, step = 0.1,
                             tooltip = TT_BB, inline = 'LBB', group = smoothing_group,
                             display = display.data_window, active = isBB)
color_LBB      = input.color(#F27405, '', inline = 'LBB', group = smoothing_group)

MA_Values      = input.bool(false, 'Show values on scale', group = smoothing_group)
//


// ─── Common Factors ───────────────────────────────────────────
SF             = 1.0 / Length
power_exponent = 0.5   // Kept as a variable for potential future research; current best fit is √(Length−1)
inv_sqrt_lenm1 = 1.0 / math.pow(Length - 1.0, power_exponent)
half_range     = 50.0
eps            = 1e-10  // Safe denominator floor

// ─── RSI Components (Close, and Intrabar O/H/L) ──────────────
middle   = ta.rma(close, Length)
middle_O = (1.0 - SF) * middle[1] + SF * open
middle_H = (1.0 - SF) * middle[1] + SF * high
middle_L = (1.0 - SF) * middle[1] + SF * low

CC_vol   = ta.rma(math.abs(close - close[1]), Length)
CC_vol_O = (1.0 - SF) * CC_vol[1] + SF * math.abs(open - close[1])
CC_vol_H = (1.0 - SF) * CC_vol[1] + SF * math.abs(high - close[1])
CC_vol_L = (1.0 - SF) * CC_vol[1] + SF * math.abs(low - close[1])

den_C = math.max(CC_vol,   eps) * (Length - 1.0)
den_O = math.max(CC_vol_O, eps) * (Length - 1.0)
den_H = math.max(CC_vol_H, eps) * (Length - 1.0)
den_L = math.max(CC_vol_L, eps) * (Length - 1.0)

myRSI = half_range + half_range * ((close - middle)   / den_C)
RSI_O = half_range + half_range * ((open  - middle_O) / den_O)
RSI_H = half_range + half_range * ((high  - middle_H) / den_H)
RSI_L = half_range + half_range * ((low   - middle_L) / den_L)


// ─── Logit Transform Function ────────────────────────────────
logit_transform(x, eps_local) =>
    // Clamp RSI to avoid log(∞) at 0 or 100
    xc = math.min(math.max(x, eps_local), 100 - eps_local)
    math.log(xc / (100 - xc))

// ─── Logit RSI Calculation ───────────────────────────────────
logit_RSI_C = logit_transform(myRSI, eps)
logit_RSI_O = logit_transform(RSI_O, eps)
logit_RSI_H = logit_transform(RSI_H, eps)
logit_RSI_L = logit_transform(RSI_L, eps)

// ─── Logit-Based Smoothing Source ────────────────────────────
float source_RSI = switch rsiSourceInput
    'Close'             => logit_RSI_C
    '(H + L)/2'         => (logit_RSI_H + logit_RSI_L) / 2.0
    '(H + L + C)/3'     => (logit_RSI_H + logit_RSI_L + logit_RSI_C) / 3.0
    '(H + L + C + C)/4' => (logit_RSI_H + logit_RSI_L + logit_RSI_C + logit_RSI_C) / 4.0

// ─── Smoothing MA Calculation ────────────────────────────────
// The part of the code below is identical as in the default RSI script
ma(source, length, MAtype) =>
    switch MAtype
        'SMA'                   => ta.sma(source, length)
        'SMA + Bollinger Bands' => ta.sma(source, length)
        'EMA'                   => ta.ema(source, length)
        'SMMA (RMA)'            => ta.rma(source, length)
        'WMA'                   => ta.wma(source, length)
        'VWMA'                  => ta.vwma(source, length)

// ─── MA and Bollinger Bands on the Logit RSI Series ──────────
LMA        = enableMA ? ma(source_RSI, maLengthInput, maTypeInput) : na
LRSI_StDev = isBB ? ta.stdev(source_RSI, maLengthInput) : na

smoothingLMA  = 100 * (math.exp(LMA) / (1 + math.exp(LMA)))
LMA_UpperBB   = 100 * (math.exp(LMA + bbMultInput * LRSI_StDev) / (1 + math.exp(LMA + bbMultInput * LRSI_StDev)))
LMA_LowerBB   = 100 * (math.exp(LMA - bbMultInput * LRSI_StDev) / (1 + math.exp(LMA - bbMultInput * LRSI_StDev)))


// ─── Zones Thresholds Logic ──────────────────────────────────
// Body threshold (~50% mass): ≈ 0.658σ
body_threshold     = math.sqrt((5.0 - math.sqrt(17.0)) / 2.0)
// Tail threshold (~1.6% each tail): ≈ 2.135σ
tail_threshold     = math.sqrt((5.0 + math.sqrt(17.0)) / 2.0)
// Breakout points (2nd derivative extrema): ±1.000σ
breakout_threshold = 1.0
// Reversal points (3rd derivative extrema): ±√3σ
reversal_threshold = math.sqrt(3.0)

// ─── Helpers: custom TANH + z→RSI distance mapping ───────────
// Pine lacks tanh; implement via exponentials:
// tanh(x) = (e^(2x) - 1) / (e^(2x) + 1)
TANH(x) =>
    float ex = math.exp(2.0 * x)
    (ex - 1.0) / (ex + 1.0)

// Map z-score to RSI distance from 50: 50 * tanh(z / sqrt(Length - 1))
RSI_distance_from50(z) =>
    half_range * TANH(z * inv_sqrt_lenm1)

// Support/Resistance zone (body ↔ breakout)
Z_ins = RSI_distance_from50(body_threshold)
Z_out = RSI_distance_from50(breakout_threshold)

// Overbought/Oversold zone (reversal ↔ tail)
OO_ins = RSI_distance_from50(reversal_threshold)
OO_out = RSI_distance_from50(tail_threshold)

// ─── Colors & Opacity ────────────────────────────────────────
color_1    = SupRes ? color_SR : color.new(color_RSI, 100)
color_RM   = OO ? color.new(color_R, 15) : color.new(color.red, 100)
color_GM   = OO ? color.new(color_G, 15) : color.new(color.green, 100)
opacity_OO = OO ? 90 : 100

// Up and down candles
isCandleUp     = myRSI > RSI_O
opacity_candle = isCandleUp ? 90 : 10

// Overbought and oversold states
isOB_line  = myRSI > (50 + OO_ins)
isOS_line  = myRSI < (50 - OO_ins)
isOB_OHLC  = bar_index >= Length and RSI_H > 50 + OO_ins
isOS_OHLC  = bar_index >= Length and RSI_L < 50 - OO_ins

BC_color =
     not OO_coloring ? color.new(color_RSI, opacity_candle) :
     isOB_OHLC       ? color.new(color_RI, opacity_candle) :
     isOS_OHLC       ? color.new(color_GI, opacity_candle) :
                       color.new(color_RSI, opacity_candle)

border_color =
     not OO_coloring ? color.new(color_RSI, 0) :
     isOB_OHLC       ? color_RI :
     isOS_OHLC       ? color_GI :
                       color.new(color_RSI, 0)
//

// ─── RSI Type Plots ──────────────────────────────────────────
plot(RSIType == 'line' ? myRSI : na,
     'RSI Line', color.new(color_RSI, 25), linewidth = 2, editable = isLine)

plotcandle(RSIType == 'candle' ? RSI_O : na,
           RSIType == 'candle' ? RSI_H : na,
           RSIType == 'candle' ? RSI_L : na,
           RSIType == 'candle' ? myRSI : na,
           'RSI Candle', color = BC_color, wickcolor = border_color, bordercolor = border_color, editable = isCandle)
//

plotbar(RSIType == 'bar' ? RSI_O : na,
        RSIType == 'bar' ? RSI_H : na,
        RSIType == 'bar' ? RSI_L : na,
        RSIType == 'bar' ? myRSI : na,
        'RSI Bar', color = border_color, editable = isBar)
//

// Dots on overbought/oversold when in line mode
plot(OO_coloring and RSIType == 'line' and (isOB_line or isOS_line) ? myRSI : na,
     'Dots', color = border_color, linewidth = 2, style = plot.style_circles, editable = (OO_coloring and isLine))

// ─── Middle Line ─────────────────────────────────────────────
rsiMiddle = plot(50, 'Middle', color.new(color_RSI, 50), style = plot.style_linebr, editable = true, display = Zones_Values ? display.all : display.all - display.price_scale)

// ─── Overbought Zone Fill ────────────────────────────────────
overbought  = plot(50 + OO_out, 'Top of Overbought',    color = color_GM,                       editable = (Zones_Values and OO) ? true : false, display = (Zones_Values and OO) ? display.price_scale : display.none)
overbought1 = plot(50 + OO_ins, 'Bottom of Overbought', color = color.new(color_G, opacity_OO), editable = (Zones_Values and OO) ? true : false, display = (Zones_Values and OO) ? display.price_scale : display.none)


fill(overbought, overbought1,
     50 + OO_out, 50 + OO_ins,
     top_color = color_GM, bottom_color = color.new(color_G, opacity_OO),
     title = 'Overbought Zone')
//

// ─── Oversold Zone Fill ──────────────────────────────────────
oversold  = plot(50 - OO_out, 'Bottom of Oversold', color = color_RM,                       editable = (Zones_Values and OO) ? true : false, display = (Zones_Values and OO) ? display.price_scale : display.none)
oversold1 = plot(50 - OO_ins, 'Top of Oversold',    color = color.new(color_R, opacity_OO), editable = (Zones_Values and OO) ? true : false, display = (Zones_Values and OO) ? display.price_scale : display.none)

fill(oversold1, oversold,
     50 - OO_ins, 50 - OO_out,
     top_color = color.new(color_R, opacity_OO), bottom_color = color_RM,
     title = 'Oversold Zone')
//

// ─── Support / Resistance Fills ──────────────────────────────
R1 = plot(50 + Z_out, 'Top of Resistance',    color_1, editable = (Zones_Values and SupRes) ? true : false, display = (Zones_Values and OO) ? display.price_scale : display.none)
R2 = plot(50 + Z_ins, 'Bottom of Resistance', color_1, editable = (Zones_Values and SupRes) ? true : false, display = (Zones_Values and OO) ? display.price_scale : display.none)
fill(R1, R2, color_1, title = 'Resistance Zone')

S1 = plot(50 - Z_ins, 'Top of Support',    color_1, editable = (Zones_Values and SupRes) ? true : false, display = (Zones_Values and OO) ? display.price_scale : display.none)
S2 = plot(50 - Z_out, 'Bottom of Support', color_1, editable = (Zones_Values and SupRes) ? true : false, display = (Zones_Values and OO) ? display.price_scale : display.none)
fill(S2, S1, color_1, title = 'Support Zone')

// ─── Smoothing MAs and BBs (RSI Scale) ──────────────────────
plot(smoothingLMA,  'Logit_RSI-based MA',   color_LMA, display = enableMA       ? (MA_Values ? display.all : display.all - display.price_scale) : display.none,     editable = enableMA)
plot(LMA_UpperBB,   'Upper Bollinger Band', color_LBB, display = isBB           ? (MA_Values ? display.all : display.all - display.price_scale) : display.none,     editable = isBB)
plot(LMA_LowerBB,   'Lower Bollinger Band', color_LBB, display = isBB           ? (MA_Values ? display.all : display.all - display.price_scale) : display.none,     editable = isBB)

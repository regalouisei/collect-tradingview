//@version=6
indicator("Filtered TEMA Crossover", overlay=true)

// --- Input degli Utenti ---
// Lunghezza della TEMA veloce (default 10)
lenFast = input.int(10, title="Length", minval=1,inline="veloce",group="TEMA Fast")
lenFastcolor=input.color(defval=color.rgb(255, 0, 100),title="",inline="veloce",group="TEMA Fast")
// Lunghezza della TEMA lenta (default 20)
lenSlow = input.int(20, title="Length", minval=1,inline="lenta",group="TEMA Slow")
lenSlowcolor=input.color(defval=color.rgb(0, 150, 255),title="",inline="lenta",group="TEMA Slow")
// Sorgente del prezzo (default chiusura)
src = input.source(close, title="Source")
//colora sfondo
colorasfondo=input.bool(defval=true,title="Paint Background",inline="x")
sfondobuy=input.color(defval=color.new(color.green,80),title="",inline="x")
sfondosell=input.color(defval=color.new(color.red,80),title="",inline="x")
//per Mercato Laterale (filtro segnali)
Mercatononlaterale=true
//Indice Chop
Attivachop=input.bool(defval=true,title="Active index chop filter",group="Index Chop Filter",tooltip = "The Chop Index Filter (Chop) is designed to measure the market's choppiness, or lack of trend. It's an oscillator that ranges from 0 to 100.High Chop values (close to 100): Indicate that the market is sideways, consolidating.Low Chop values (close to 0): Indicate that the market is trending strongly")
filtrochop=input.int(defval=50,title="Chop Index Threshold",group="Index Chop Filter",step=1)
length = input.int(14, minval=1,group="Index Chop Filter")
Chop = 100 * math.log10(math.sum(ta.atr(1), length) / (ta.highest(length) - ta.lowest(length))) / math.log10(length)
if Chop>filtrochop and Attivachop
    Mercatononlaterale:=false
//AdX
AttivaAdx=input.bool(defval=true,title="Active ADX Filter",group="ADX Filter",tooltip="The Average Directional Index (ADX) is a technical indicator used to measure the strength of a trend. It does not indicate the direction of the trend (up or down), only how strong it is. The ADX ranges from 0 to 100.ADX below 20-25: Indicates a weak or non-existent trend.ADX above 25: Indicates a strong, well-defined trend is in progress")
filtroadx=input.int(defval=25,title="ADX Minimum Threshold",group="ADX Filter",step=1)
adxlen = input(14, title="ADX Smoothing",group="ADX Filter")
dilen = input(14, title="DI Length",group="ADX Filter")
dirmov(len) =>
    up = ta.change(high)
    down = -ta.change(low)
    plusDM = na(up) ? na : (up > down and up > 0 ? up : 0)
    minusDM = na(down) ? na : (down > up and down > 0 ? down : 0)
    truerange = ta.rma(ta.tr, len)
    plus = fixnan(100 * ta.rma(plusDM, len) / truerange)
    minus = fixnan(100 * ta.rma(minusDM, len) / truerange)
    [plus, minus]
adx(dilen, adxlen) =>
    [plus, minus] = dirmov(dilen)
    sum = plus + minus
    adx = 100 * ta.rma(math.abs(plus - minus) / (sum == 0 ? 1 : sum), adxlen)
sig = adx(dilen, adxlen)
if sig<filtroadx and AttivaAdx
    Mercatononlaterale:=false
// --- Funzione TEMA ---
tema(src, length) =>
    ema1 = ta.ema(src, length)
    ema2 = ta.ema(ema1, length)
    ema3 = ta.ema(ema2, length)
    3 * ema1 - 3 * ema2 + ema3
// --- Calcoli TEMA ---
temaFast = tema(src, lenFast)
temaSlow = tema(src, lenSlow)

// --- Trama (Plot) delle Linee ---
plot(temaFast, color=lenFastcolor, title="TEMA Veloce", linewidth=2)
plot(temaSlow, color=lenSlowcolor, title="TEMA Lenta", linewidth=2)
// --- Condizioni di Crossover (Incrocio) ---
bullishCross = ta.crossover(temaFast, temaSlow)
bearishCross = ta.crossunder(temaFast, temaSlow)
// Determina il colore della colonna in base alla posizione delle TEMA
temaCrossColor = temaFast > temaSlow ? sfondobuy : sfondosell
// Applica il colore allo sfondo del grafico
bgcolor(temaCrossColor,display= colorasfondo ? display.all : display.none)
//input segnale
attivasell=input.bool(defval=true,title="",group="Sell Signal",inline="sv")
shape_optionsell = input.string(title="Shape", options=["Triangle up","Triangle down", "Arrow up","Arrow down","Circle","Cross","Diamond","Flag"], defval="Triangle down", group="Sell Signal",inline="sv")
colorsell=input.color(defval=color.red,title="",group="Sell Signal",inline="sv")
size_optionsell = input.string(title="Size", options=["Tiny", "Normal", "Large", "Huge"], defval="Normal", group="Sell Signal")
attivatestosell=input.bool(defval=true,title="",group="Sell Signal",inline="gia")
testosell=input.string(defval="Sell",title="Sell Signal Text",group="Sell Signal",inline="gia")
attivabuy=input.bool(defval=true,title="",group="Buy Signal",inline="sb")
shape_optionbuy = input.string(title="Shape", options=["Triangle up","Triangle down","Arrow up","Arrow down","Circle","Cross","Diamond","Flag"], defval="Triangle up",inline="sb", group="Buy Signal")
colorbuy=input.color(defval=color.green,title="",group="Buy Signal",inline="sb")
size_optionbuy = input.string(title="Size", options=["Tiny", "Normal", "Large", "Huge"], defval="Normal", group="Buy Signal")
attivatestobuy=input.bool(defval=true,title="",group="Buy Signal",inline="giu")
testobuy=input.string(defval="Buy",title="Buy Signal Text",group="Buy Signal",inline="giu")
//dimensione
size_buy = switch size_optionbuy
    "Tiny" => size.tiny
    "Normal" => size.normal
    "Large" => size.large
    "Huge" => size.huge
    => size.normal // Valore di default
size_sell = switch size_optionsell
    "Tiny" => size.tiny
    "Normal" => size.normal
    "Large" => size.large
    "Huge" => size.huge
    => size.normal // Valore di default
//forma del segnale
custom_shapebuy = switch shape_optionbuy
    "Triangle up" => label.style_triangleup
    "Triangle down"=> label.style_triangledown
    "Arrow up" => label.style_arrowup
    "Arrow down"=>label.style_arrowdown
    "Circle" => label.style_circle
    "Cross"=>label.style_cross
    "Diamond"=>label.style_diamond
    "Flag"=>label.style_flag
    => label.style_triangleup // Default per Buy
custom_shapesell = switch shape_optionsell
    "Triangle up" => label.style_triangleup 
    "Triangle down"=> label.style_triangledown
    "Arrow up" => label.style_arrowdown
    "Arrow down"=>label.style_arrowdown     
    "Circle" => label.style_circle
    "Cross"=>label.style_cross
    "Diamond"=>label.style_diamond
    "Flag"=>label.style_flag
    => label.style_triangledown // Default per Sell
// Traccia i segnali di incrocio 
if bullishCross and Mercatononlaterale and attivabuy
    label.new(bar_index, low, text=attivatestobuy ? testobuy:na, yloc=yloc.belowbar,color=colorbuy,style =custom_shapebuy,size=size_buy)
if bearishCross and Mercatononlaterale and attivasell
    label.new(bar_index, high, text=attivatestosell ? testosell:na, yloc=yloc.abovebar,color=colorsell,style=custom_shapesell,size=size_sell)

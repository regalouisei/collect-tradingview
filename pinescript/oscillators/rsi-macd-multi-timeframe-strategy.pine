// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © RWCS_LTD

//@version=6
strategy("RSI + MACD Multi-Timeframe Strategy", overlay=false, default_qty_type=strategy.percent_of_equity, default_qty_value=100)

// === INPUTS ===
group1 = "Plotting Options"
group2 = "Relative Strength Index"
group3 = "Moving Average Convergence/Divergence"

plotChoice = input.string("RSI", title="Plot Selector", options=["RSI", "MACD"], group = group1)

RSI_TIME = input.timeframe("1D", "RSI Timeframe", group = group2)
rsiLength = input.int(14, "RSI Length", group = group2)
rsiOB = input.int(70, "RSI Overbought Level", group = group2)
rsiOS = input.int(30, "RSI Oversold Level", group = group2)

MACD_TIME = input.timeframe("240", "MACD Timeframe", group = group3)
macdFast = input.int(12, "MACD Fast Length", group = group3)
macdSlow = input.int(26, "MACD Slow Length", group = group3)
macdSignal = input.int(9, "MACD Signal Smoothing", group = group3)


// === RSI on 1D ===
rsi1D = request.security(syminfo.tickerid, RSI_TIME, ta.rsi(close, rsiLength))
isOB = rsi1D > rsiOB
isOS = rsi1D < rsiOS

// === MACD on 4H ===
[macdLine, signalLine, _] = request.security(syminfo.tickerid, MACD_TIME, ta.macd(close, macdFast, macdSlow, macdSignal))
macdCrossOver = ta.crossover(macdLine, signalLine)
macdCrossUnder = ta.crossunder(macdLine, signalLine)

// === Long Entry ===
longCondition = isOS and macdCrossOver
if (longCondition)
    strategy.entry("Long", strategy.long)

// === Long Exit ===
longExit = isOB and macdCrossUnder
if (longExit)
    strategy.close("Long")

// === Plotting ===
showRSI = plotChoice == "RSI"
showMACD = plotChoice == "MACD"

// === Conditional Plotting ===
plot(showRSI ? rsi1D : na, title="RSI (1D)", color=color.blue)
hline(showRSI ? rsiOB : na, "Overbought", color=color.red)
hline(showRSI ? rsiOS : na, "Oversold", color=color.green)

plot(showMACD ? macdLine : na, title="MACD Line (4H)", color=color.orange)
plot(showMACD ? signalLine : na, title="Signal Line (4H)", color=color.purple)

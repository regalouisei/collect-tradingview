// This Pine Script™ code is subject to the terms of the Creative Commons
// Attribution-NonCommercial-ShareAlike 4.0 International License:
// https://creativecommons.org/licenses/by-nc-sa/4.0/
//
// This license requires that reusers give credit to the creator.
// It allows reusers to distribute, remix, adapt, and build upon the material
// in any medium or format, for non-commercial purposes only.
// If others modify or adapt the material, they must license the modified
// material under identical terms.
//
// CC BY-NC-SA 4.0:
// BY: Credit must be given to the creator.
// NC: Only non-commercial use of this work is permitted. “Non-commercial” means
//     not primarily intended for or directed towards commercial advantage
//     or monetary compensation.
// SA: Adaptations must be shared under the same terms.
//
// © AdaptiveRSI

//@version=6
indicator(title = 'Rescaled RSI [AdaptiveRSI]',
          shorttitle = 'AdaptiveRSI · rescaled RSI',
          format = format.price,
          precision = 2,
          timeframe = '',
          timeframe_gaps = true)

// ─── RSI Settings ─────────────────────────────────────────────
Length_in  = input.int(70, minval = 2, title = 'Input Length',  group = 'RSI Settings')
Length_out = input.int(14, minval = 2, title = 'Output Length', group = 'RSI Settings')

// ─── Internal State for RSI Calculation ──────────────────────
middle = float(0.0)
CC_vol = float(0.0)
myRSI  = float(0.0)

// ─── Effective Length & Smoothing Factor ─────────────────────
Length = math.min(Length_in, bar_index + 1)
SF     = 1.0 / Length

// ─── RSI Components Calculation ──────────────────────────────
if bar_index == 0
    middle := close
    CC_vol := syminfo.mintick
    myRSI  := 50.0
else
    middle := (1.0 - SF) * middle[1] + SF * close
    CC_vol := (1.0 - SF) * CC_vol[1] + SF * math.abs(close - close[1])
    // Regular RSI(close) calculation in alternative-derivation form
    myRSI  := 50.0 + 50.0 * ((close - middle) / CC_vol / (Length - 1.0))
// End RSI components calculation

// ─── Logit-Normal Transformation Parameters ──────────────────
logit_StDev_in  = 2.0 / math.sqrt(Length_in  - 1.0)
logit_StDev_out = 2.0 / math.sqrt(Length_out - 1.0)

// ─── RSI → Z-Score → RSI Mapping ─────────────────────────────
// Clamp RSI slightly away from 0 and 100 to avoid log(0) / log(∞)
eps      = 1e-10
safe_RSI = math.min(math.max(myRSI, eps), 100.0 - eps)

RSI_Zscore = math.log(safe_RSI / (100.0 - safe_RSI)) / logit_StDev_in

output_RSI = 100.0 * math.exp(RSI_Zscore * logit_StDev_out) /
             (1.0 + math.exp(RSI_Zscore * logit_StDev_out))

// ─── Output Plot ─────────────────────────────────────────────
plot(output_RSI, 'Output RSI', color.new(#F27405, 25), linewidth = 2)

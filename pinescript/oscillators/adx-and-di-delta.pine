// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© BeikabuOyaji

//@version=6


indicator('ADX and DI delta for v6')
len = input(14)

TrueRange = math.max(math.max(high - low, math.abs(high - nz(close[1]))), math.abs(low - nz(close[1])))
DirectionalMovementPlus = high - nz(high[1]) > nz(low[1]) - low ? math.max(high - nz(high[1]), 0) : 0
DirectionalMovementMinus = nz(low[1]) - low > high - nz(high[1]) ? math.max(nz(low[1]) - low, 0) : 0

SmoothedTrueRange = 0.0
SmoothedTrueRange := nz(SmoothedTrueRange[1]) - nz(SmoothedTrueRange[1]) / len + TrueRange

SmoothedDirectionalMovementPlus = 0.0
SmoothedDirectionalMovementPlus := nz(SmoothedDirectionalMovementPlus[1]) - nz(SmoothedDirectionalMovementPlus[1]) / len + DirectionalMovementPlus

SmoothedDirectionalMovementMinus = 0.0
SmoothedDirectionalMovementMinus := nz(SmoothedDirectionalMovementMinus[1]) - nz(SmoothedDirectionalMovementMinus[1]) / len + DirectionalMovementMinus

DIPlus = SmoothedDirectionalMovementPlus / SmoothedTrueRange * 100
DIMinus = SmoothedDirectionalMovementMinus / SmoothedTrueRange * 100
DX = math.abs(DIPlus - DIMinus) / (DIPlus + DIMinus) * 100
ADX = ta.sma(DX, len)
Delta = DIPlus - DIMinus
DeltaColor = Delta > 0 ? color.green : color.red

shapeColor = Delta < 0 ? color.green : color.red
shapeLocation = Delta < 0 ? yloc.belowbar : yloc.abovebar
shapeStyle = Delta < 0 ? label.style_label_up : label.style_label_down
ADXdescend = ADX > 25 and ADX < ADX[1] and ADX[1] > ADX[2]


plot(math.abs(Delta), color = color.new(DeltaColor, 0), title = 'Delta', editable=false)
plot(ADX, color = color.new(color.yellow, 0), title = 'ADX', editable=false)
hline(0, color =color.new(color.white, 75), linestyle=hline.style_dashed, editable=false)
hline(20, color =color.new(color.white, 75), linestyle=hline.style_dotted)
hline(25, color =color.new(color.white, 75), linestyle=hline.style_dotted)

if ADXdescend
    label.new(bar_index, close, style=shapeStyle, yloc=shapeLocation, color=shapeColor, size=size.tiny, force_overlay=true)

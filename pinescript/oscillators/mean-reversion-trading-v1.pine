// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Sivchay
 
// The current parameters are optmized for NASDAQ 15M 
// MACD Histograms are currently inversed, optimizer shows better results for certain assets when it is inversed


//@version=5
strategy("Mean Reversion Trading V1", "MRTrading V1", overlay=false, initial_capital=100000, default_qty_type=strategy.percent_of_equity, default_qty_value=100, process_orders_on_close=true)

///*** Input ***\\\
RSI_Up_Threshold = input(80, "RSI Upper Threshold", tooltip="RSI Upper Threshold") 
RSI_Lower_Threshold = input(20, "RSI Lower Threshold", tooltip="RSI Lower Threshold")


stop_loss = input(0.01, tooltip="Percentage of equity")
take_profit = input(0.015, tooltip="Percentage of equity")
RSI_Len = input(7, "RSI Length", tooltip="Amount of previous candles used to calculate")
KC_Len = input(17, "Keltner Channel Length", tooltip="Amount of previous candles used to calculate")
KC_Mult = input(2.7, "Ketlner Channel Multiplier", tooltip="Keltner Channel Multiplier")
MACD_Sh_Len = input(7, "MACD Long Length", tooltip="Amount of previous candles used to calculate")
MACD_Lng_Len = input(16, "MACD Short Length", tooltip="Amount of previous candles used to calculate")
MACD_Signal_Len = input(9, "MACD Signal Length", tooltip="Amount of previous candles used to calculate")


datetime = input.time(timestamp("1999-06-01 16:30:00"))

///*** Indicators ***\\\
RSI = ta.rsi(close, RSI_Len)

ATR = ta.atr(KC_Len)
KC_Middle = ta.ema(close, KC_Len)
KC_Upper = KC_Middle + (KC_Mult * ATR)
KC_Lower = KC_Middle - (KC_Mult * ATR)

KC_Up = KC_Upper - KC_Middle 
KC_Down = KC_Lower - KC_Middle


MACD_Line = ta.ema(close, MACD_Lng_Len) - ta.ema(close, MACD_Sh_Len)
MACD_Signal = ta.sma(MACD_Line, MACD_Signal_Len)
MACD_Histogram = 10*(MACD_Line - MACD_Signal) //MACD value is multiplied to better visualization. 

///*** Entry Conditions ***\\\

//Buy Conditons
lng_ent_con1 = RSI < RSI_Lower_Threshold 
lng_ent_con2 = close < KC_Lower
lng_ent_con3 = MACD_Histogram > MACD_Histogram[1] and MACD_Histogram > 0

//Short Conditions 
Sh_ent_con1 = RSI > RSI_Up_Threshold
Sh_ent_con2 = close > KC_Upper
Sh_ent_con3 = MACD_Histogram < MACD_Histogram[1] and MACD_Histogram < 0

// If all long conditions are true, enter long
if lng_ent_con1 and lng_ent_con2 and lng_ent_con3 and strategy.position_size == 0 and time>= datetime 
    strategy.entry("Long", strategy.long)

// If all short conditions are true, enter short
if Sh_ent_con1 and Sh_ent_con2 and Sh_ent_con3 and strategy.position_size == 0 and time >= datetime
    strategy.entry("Short", strategy.short)

///*** Exit Conditions ***\\\ 

//Long Exit Conditioons
Lng_ex_con1 = close < strategy.position_avg_price * (1 - stop_loss) //Stop Loss
Lng_ex_con2 = close > strategy.position_avg_price * (1 + take_profit) //Take Profit
Lng_ex_con3 = MACD_Histogram < 0 and MACD_Histogram < MACD_Histogram[1]//Cross Over 0 

if Lng_ex_con1 
    strategy.close("Long")

if Lng_ex_con2 
    strategy.close("Long")

if Lng_ex_con3 
    strategy.close("Long")

//Short Exit Conditions 
Sh_ex_con1 = strategy.position_avg_price * (1 + stop_loss) //Stop Loss 
Sh_ex_con2 = strategy.position_avg_price * (1 - take_profit) //Take profit
Sh_ex_con3 = MACD_Histogram > 0 and MACD_Histogram > MACD_Histogram[1] //Cross Over 0 

if close > Sh_ex_con1 
    strategy.close("Short")

if close < Sh_ex_con2
    strategy.close("Short")

if Sh_ex_con3
    strategy.close("Short")

//Plotting 
macd_color = MACD_Histogram < 0 ? color.new(color.red, 30) : color.new(color.green, 30)

plot(MACD_Histogram, color=macd_color, style=plot.style_histogram, force_overlay=false)

plot(RSI, color=color.blue, force_overlay=false)
plot(RSI_Up_Threshold, color=color.new(color.red, 50))
plot(RSI_Lower_Threshold, color=color.new(color.red, 50))

KC_Middle_Plot = plot(KC_Middle, color=color.purple, force_overlay=true)
KC_Upper_Plot = plot(KC_Upper, color=color.purple, force_overlay=true)
KC_Lower_Plot = plot(KC_Lower, color=color.purple, force_overlay=true)

fill(KC_Upper_Plot, KC_Lower_Plot, color.new(color.blue, 75))

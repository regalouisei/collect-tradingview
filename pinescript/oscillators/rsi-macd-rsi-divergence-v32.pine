// This source code is subject to the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// M.Khoa, mkhoa99dv32@gmail.com
//+------------------------------------------------------------------+
//       \\*//               \*/              \\*//
// Hihi  ($ $)              (- -)             (ô ô)   Haha
//        (")                (-)               (")
//___o0o___I___o0o___oo0oo___ L___oo0oo___o0o___U___o0o___
//+------------------------------------------------------------------+
//@version=6
indicator(title = 'RSI + MACD (RSI Divergence)', shorttitle = 'RSI + MACD', overlay = false, max_lines_count = 500, max_bars_back = 1000)

// =============================================================================
// GROUP 1: RSI DIVERGENCE SETTINGS
// =============================================================================
g_rsi = "RSI Divergence Settings"

src            = input.source(close, 'RSI Source', group = g_rsi, tooltip = "Source price for RSI calculation (usually close)")
len            = input.int(14, 'RSI Period', minval = 1, group = g_rsi, tooltip = "RSI calculation period (default 14)")
lbR            = input.int(5,  'Pivot Lookback Right', minval = 1, group = g_rsi, tooltip = "Number of bars to the right for pivot detection")
lbL            = input.int(3,  'Pivot Lookback Left', minval = 1, group = g_rsi, tooltip = "Number of bars to the left for pivot detection")
rangeLower     = input.int(5,  'Min Lookback Range', minval = 1, group = g_rsi, tooltip = "Minimum distance (in bars) between two pivots to consider divergence")
rangeUpper     = input.int(60, 'Max Lookback Range', minval = 10, group = g_rsi, tooltip = "Maximum distance (in bars) between two pivots to consider divergence")

// Divergence options
plotBull       = input.bool(true,  'Plot Regular Bullish', group = g_rsi, tooltip = "Show regular bullish divergence (price lower low, RSI higher low)")
plotHiddenBull = input.bool(false, 'Plot Hidden Bullish', group = g_rsi, tooltip = "Show hidden bullish divergence (price higher low, RSI lower low)")
plotBear       = input.bool(true,  'Plot Regular Bearish', group = g_rsi, tooltip = "Show regular bearish divergence (price higher high, RSI lower high)")
plotHiddenBear = input.bool(false, 'Plot Hidden Bearish', group = g_rsi, tooltip = "Show hidden bearish divergence (price lower high, RSI higher high)")

// Overbought/Oversold levels & color warning
overbought     = input.int(70, 'Overbought Level', group = g_rsi, tooltip = "Color warnings for RSI overbought zones")
oversold       = input.int(30, 'Oversold Level', group = g_rsi, tooltip = "Color warnings for RSI oversold zones")

// =============================================================================
// GROUP 2: MACD SETTINGS
// =============================================================================
g_macd = "MACD Settings"

srcd          = input.source(close, 'MACD Source', group = g_macd, tooltip = "Source price for MACD calculation")
fast_length   = input.int(12, 'Fast Length', minval = 1, group = g_macd, tooltip = "Fast EMA period for MACD")
slow_length   = input.int(26, 'Slow Length', minval = 1, group = g_macd, tooltip = "Slow EMA period for MACD")
signal_length = input.int(9, 'Signal Smoothing', minval = 1, group = g_macd, tooltip = "Signal line smoothing period")
sma_source    = input.bool(false, 'Simple MA (Oscillator)', group = g_macd, tooltip = "Use SMA instead of EMA for MACD line")
sma_signal    = input.bool(false, 'Simple MA (Signal Line)', group = g_macd, tooltip = "Use SMA instead of EMA for MACD line")
scale_lb      = input.int(200, 'Scale Lookback', minval = 10, group = g_macd, tooltip = "Lookback period to calculate range for scaling MACD to fit RSI pane")

// =============================================================================
// COLORS
// =============================================================================
bearColor = color.maroon
bullColor = color.green
textColor = color.white

col_grow_above = #26A69A
col_grow_below = #FFC1D5
col_fall_above = #B2DFDB
col_fall_below = #EF5550
col_macd = #2962ff
col_signal = #e65100

rsiNormalColor  = color.new(#d1d4dc, 11)
rsiOverbought   = color.orange
rsiOversold     = color.orange

// =============================================================================
// RSI CALCULATION & COLOR WARNING
// =============================================================================
rsi_raw = ta.rsi(src, len)
osc = rsi_raw - 50

rsiColor = rsi_raw >= overbought ? rsiOverbought :
           rsi_raw <= oversold   ? rsiOversold   :
                                  rsiNormalColor

plot(osc, title = 'RSI', linewidth = 1, color = rsiColor)

// Hlines & Fill
h70 = hline(overbought - 45, 'Overbought', color = color.gray, linestyle = hline.style_dashed)
h30 = hline(oversold - 55, 'Oversold', color = color.gray, linestyle = hline.style_dashed)
hline(0, 'Middle Line', linestyle = hline.style_dotted)

//fill(hline(30 - 50), hline(-30 + 50), color = color.new(#0c3299, 90))

// =============================================================================
// DIVERGENCE LABEL
// =============================================================================
plFound = not na(ta.pivotlow(osc, lbL, lbR))
phFound = not na(ta.pivothigh(osc, lbL, lbR))

barsSinceLow  = ta.barssince(plFound[1])
barsSinceHigh = ta.barssince(phFound[1])

inRangeLow  = barsSinceLow  >= rangeLower and barsSinceLow  <= rangeUpper
inRangeHigh = barsSinceHigh >= rangeLower and barsSinceHigh <= rangeUpper

oscHL = osc[lbR] > ta.valuewhen(plFound, osc[lbR], 1)
priceLL = low[lbR] < ta.valuewhen(plFound, low[lbR], 1)
bullCond = plotBull and plFound and priceLL and oscHL and inRangeLow

oscLL = osc[lbR] < ta.valuewhen(plFound, osc[lbR], 1)
priceHL = low[lbR] > ta.valuewhen(plFound, low[lbR], 1)
hiddenBullCond = plotHiddenBull and plFound and priceHL and oscLL and inRangeLow

oscLH = osc[lbR] < ta.valuewhen(phFound, osc[lbR], 1)
priceHH = high[lbR] > ta.valuewhen(phFound, high[lbR], 1)
bearCond = plotBear and phFound and priceHH and oscLH and inRangeHigh

oscHH = osc[lbR] > ta.valuewhen(phFound, osc[lbR], 1)
priceLH = high[lbR] < ta.valuewhen(phFound, high[lbR], 1)
hiddenBearCond = plotHiddenBear and phFound and priceLH and oscHH and inRangeHigh

plotshape(bullCond ? osc[lbR] : na, offset = -lbR, text = ' Bull ', style = shape.labelup, location = location.absolute, color = color.new(bullColor, 0), textcolor = textColor)
plotshape(hiddenBullCond ? osc[lbR] : na, offset = -lbR, text = ' Hidden Bull ', style = shape.labelup, location = location.absolute, color = color.new(bullColor, 0), textcolor = textColor)
plotshape(bearCond ? osc[lbR] : na, offset = -lbR, text = ' Bear ', style = shape.labeldown, location = location.absolute, color = color.new(bearColor, 0), textcolor = textColor)
plotshape(hiddenBearCond ? osc[lbR] : na, offset = -lbR, text = ' Hidden Bear ', style = shape.labeldown, location = location.absolute, color = color.new(bearColor, 0), textcolor = textColor)

// =============================================================================
// MACD & SCALING
// =============================================================================
fast_ma = sma_source ? ta.sma(srcd, fast_length) : ta.ema(srcd, fast_length)
slow_ma = sma_source ? ta.sma(srcd, slow_length) : ta.ema(srcd, slow_length)
macd = fast_ma - slow_ma
signal = sma_signal ? ta.sma(macd, signal_length) : ta.ema(macd, signal_length)
hist = macd - signal

rsi_high = ta.highest(osc, scale_lb)
rsi_low  = ta.lowest(osc, scale_lb)
rsi_range = rsi_high - rsi_low
rsi_range := rsi_range == 0 ? 0.0001 : rsi_range

hist_range   = ta.highest(hist, scale_lb) - ta.lowest(hist, scale_lb)
macd_range   = ta.highest(macd, scale_lb) - ta.lowest(macd, scale_lb)
signal_range = ta.highest(signal, scale_lb) - ta.lowest(signal, scale_lb)

overall_range = math.max(hist_range, math.max(macd_range, signal_range))
overall_range := overall_range == 0 ? 0.0001 : overall_range

scale_factor = rsi_range / overall_range

scaled_hist = hist * scale_factor
scaled_macd = macd * scale_factor
scaled_signal = signal * scale_factor

plot(scaled_hist, title = 'Histogram', style = plot.style_columns, 
     color = scaled_hist >= 0 ? (scaled_hist[1] < scaled_hist ? col_grow_above : col_fall_above) : 
                               (scaled_hist[1] < scaled_hist ? col_grow_below : col_fall_below),
     display = display.none)

plot(scaled_macd, title = 'MACD', color = color.new(col_macd, 66), style = plot.style_columns)
plot(scaled_signal, title = 'Signal', color = color.new(col_signal, 80), style = plot.style_columns)

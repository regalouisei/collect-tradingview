// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Uncle_the_shooter

//@version=6
indicator('Force Pulse', overlay=false, max_bars_back=500, precision=2)

//  Calculation Settings
lookback   = input.int(20,  'Calculation Period',      minval=10, maxval=200, group='Calculation Settings')
mode       = input.string('Body', 'Force Mode', options=['Body', 'AVG Body'], group='Calculation Settings')
smoothLen  = input.int(3,   'Smoothing Length (EMA)',               group='Calculation Settings')
maLength   = input.int(20,  'Moving Average Length (SMA)', minval=1, group='Calculation Settings')

//  Divergence Detection
calculateDivergence = input.bool(true, 'Enable Divergence Detection', group='Divergence Detection')
pivotLength         = input.int(3,    'Pivot Lookback (Left/Right)', minval=1, group='Divergence Detection')


//  Signal Settings
signalType = input.string('Overbought/Oversold', 'Signal Type',
     options=['None', 'Overbought/Oversold', 'Midline', 'MA Midline', 'All'],
     group='Signal Settings')

//  Threshold Levels
overbought    = input.float(73.0, 'Overbought Level',      step=0.1, group='Threshold Levels')
oversold      = input.float(27.0, 'Oversold Level',        step=0.1, group='Threshold Levels')
maOverbought  = input.float(65.0, 'MA Overbought Level',   step=0.1, group='Threshold Levels')
maOversold    = input.float(35.0, 'MA Oversold Level',     step=0.1, group='Threshold Levels')

//  Style & Colors
showMa             = input.bool(true,  'Show Moving Average',                   group='Style & Colors')
maColor            = input.color(color.yellow, 'MA Color',                       group='Style & Colors')
oscColorPositive   = input.color(color.rgb(6,162,47),  'Oscillator Bullish Color', group='Style & Colors')
oscColorNegative   = input.color(color.rgb(207,23,23), 'Oscillator Bearish Color', group='Style & Colors')
zeroLineColor      = input.color(color.gray,   'Midline Color',               group='Style & Colors')
overboughtColor    = input.color(color.red,    'Overbought Line Color',          group='Style & Colors')
oversoldColor      = input.color(color.green,  'Oversold Line Color',            group='Style & Colors')
bullDivColor       = input.color(color.green,  'Bullish Divergence Color',       group='Style & Colors')
bearDivColor       = input.color(color.red,    'Bearish Divergence Color',       group='Style & Colors')
textColor          = input.color(color.white,  'Label Text Color',               group='Style & Colors')

//  Gradient & Fill
fillEnabled           = input.bool(true,  'Enable Gradient Fill',                group='Gradient & Fill')
fillTransparency      = input.int(70,     'Fill Transparency',    minval=0, maxval=100, group='Gradient & Fill')
bandTransparency      = input.int(40,     'Band/Label Transparency', minval=0, maxval=100, group='Gradient & Fill')
showZeroLineGradient  = input.bool(true,  'Midline Gradient',                 group='Gradient & Fill')
showOverboughtGradient= input.bool(true,  'Overbought Gradient',                group='Gradient & Fill')
showOversoldGradient  = input.bool(true,  'Oversold Gradient',                  group='Gradient & Fill')
showOscillatorGradient= input.bool(true,  'Oscillator Gradient',                group='Gradient & Fill')
showMaGradient        = input.bool(true,  'MA Gradient',                        group='Gradient & Fill')

//  Force Pulse Calculation
body     = math.abs(close - open)
bullBody = close >= open ? body : 0
bearBody = close < open  ? body : 0
avgBody  = ta.sma(body, 20)

bullForce = mode == 'AVG Body' ? (close >= open ? avgBody : 0) : bullBody
bearForce = mode == 'AVG Body' ? (close < open  ? avgBody : 0) : bearBody

bullSum = ta.cum(bullForce) - ta.cum(bullForce)[lookback]
bearSum = ta.cum(bearForce) - ta.cum(bearForce)[lookback]
total   = bullSum + bearSum
imbalance = total == 0 ? 0 : (bullSum - bearSum) / total

norm = (imbalance + 1) * 50
norm := math.max(math.min(norm, 100), 0)

cpoSmooth = ta.ema(norm, smoothLen)
maCpo     = ta.sma(cpoSmooth, maLength)

//  Divergence Logic
lookbackLeft  = pivotLength
lookbackRight = pivotLength

ta.pivothigh(high,  lookbackLeft, lookbackRight)
ta.pivotlow(low,   lookbackLeft, lookbackRight)
ta.pivothigh(cpoSmooth, lookbackLeft, lookbackRight)
pivotLowOsc  = ta.pivotlow(cpoSmooth, lookbackLeft, lookbackRight)
pivotHighOsc = ta.pivothigh(cpoSmooth, lookbackLeft, lookbackRight)

var bool plFound = false
var bool phFound = false
var bool bullCond = false
var bool bearCond = false

oscLbr = cpoSmooth[lookbackRight]

_inRange(cond) =>
    bars = ta.barssince(cond)
    5 <= bars and bars <= 60

if calculateDivergence
    plFound := not na(pivotLowOsc)
    oscHl   = oscLbr > ta.valuewhen(plFound, oscLbr, 1) and _inRange(plFound[1])
    lowLbr  = low[lookbackRight]
    priceLl = lowLbr < ta.valuewhen(plFound, lowLbr, 1)
    bullCond := priceLl and oscHl and plFound

    phFound := not na(pivotHighOsc)
    oscLh   = oscLbr < ta.valuewhen(phFound, oscLbr, 1) and _inRange(phFound[1])
    highLbr = high[lookbackRight]
    priceHh = highLbr > ta.valuewhen(phFound, highLbr, 1)
    bearCond := priceHh and oscLh and phFound

//  Plots & Styling
overbought_gradient_color = maCpo >= maOverbought ? overboughtColor : zeroLineColor
oversold_gradient_color   = maCpo <= maOversold   ? oversoldColor   : zeroLineColor

hline(50, 'Midline', color=zeroLineColor, linestyle=hline.style_dashed)
plot(showZeroLineGradient ? 50 : na, color=color.new(zeroLineColor, bandTransparency), linewidth=2, editable=false)

hline(overbought, 'Overbought', color=overboughtColor, linestyle=hline.style_dashed)
plot(showOverboughtGradient ? overbought : na, color=color.new(overbought_gradient_color, bandTransparency), linewidth=10, editable=false)

hline(oversold, 'Oversold', color=oversoldColor, linestyle=hline.style_dashed)
plot(showOversoldGradient ? oversold : na, color=color.new(oversold_gradient_color, bandTransparency), linewidth=10, editable=false)

plotColor = cpoSmooth >= 50 ? oscColorPositive : oscColorNegative
p1 = plot(cpoSmooth, 'Force Pulse', color=plotColor, linewidth=2)

plot(showOscillatorGradient ? cpoSmooth : na, color=color.new(plotColor, 85), linewidth=10)
plot(showOscillatorGradient ? cpoSmooth : na, color=color.new(plotColor, 94), linewidth=16)

plot(showMa ? maCpo : na, 'MA', color=maColor, linewidth=2)
plot(showMa and showMaGradient ? maCpo : na, color=color.new(maColor, 85), linewidth=6)

// Gradient Fill
p_mid1 = plot(cpoSmooth * 0.8 + 50 * 0.2, color=na, display=display.none)
p_mid2 = plot(cpoSmooth * 0.6 + 50 * 0.4, color=na, display=display.none)
p_mid3 = plot(cpoSmooth * 0.4 + 50 * 0.6, color=na, display=display.none)
p_mid4 = plot(cpoSmooth * 0.2 + 50 * 0.8, color=na, display=display.none)
p0     = plot(50, color=na, display=display.none)

fill(p1,     p_mid1, color=fillEnabled ? color.new(plotColor, fillTransparency)     : na)
fill(p_mid1, p_mid2, color=fillEnabled ? color.new(plotColor, fillTransparency + 5) : na)
fill(p_mid2, p_mid3, color=fillEnabled ? color.new(plotColor, fillTransparency + 10): na)
fill(p_mid3, p_mid4, color=fillEnabled ? color.new(plotColor, fillTransparency + 15): na)
fill(p_mid4, p0,     color=fillEnabled ? color.new(plotColor, fillTransparency + 20): na)

//  Signals
longSignalObos   = ta.crossover(cpoSmooth, oversold)
shortSignalObos  = ta.crossunder(cpoSmooth, overbought)
longSignalZero   = ta.crossover(cpoSmooth, 50)
shortSignalZero  = ta.crossunder(cpoSmooth, 50)
longSignalMaZero = ta.crossover(maCpo, 50)
shortSignalMaZero= ta.crossunder(maCpo, 50)

showObos  = signalType == 'Overbought/Oversold' or signalType == 'All'
showZero  = signalType == 'Midline'         or signalType == 'All'
showMaZero= signalType == 'MA Midline'      or signalType == 'All'

plotshape(showObos  and longSignalObos,   location=location.bottom, color=oversoldColor,   style=shape.triangleup,   size=size.tiny)
plotshape(showObos  and shortSignalObos,  location=location.top,    color=overboughtColor,  style=shape.triangledown, size=size.tiny)
plotshape(showZero  and longSignalZero,   location=location.bottom, color=oversoldColor,   style=shape.triangleup,   size=size.tiny)
plotshape(showZero  and shortSignalZero,  location=location.top,    color=overboughtColor,  style=shape.triangledown, size=size.tiny)
plotshape(showMaZero and longSignalMaZero,location=location.bottom, color=oversoldColor,   style=shape.triangleup,   size=size.tiny)
plotshape(showMaZero and shortSignalMaZero,location=location.top,   color=overboughtColor,  style=shape.triangledown, size=size.tiny)

//  Divergence Visuals
plot(calculateDivergence and plFound ? oscLbr : na, offset=-lookbackRight, color=bullCond ? bullDivColor : color.new(color.white,100), linewidth=2)
plot(calculateDivergence and phFound ? oscLbr : na, offset=-lookbackRight, color=bearCond ? bearDivColor : color.new(color.white,100), linewidth=2)

plotshape(bullCond ? oscLbr : na, offset=-lookbackRight, text=' Bull ', style=shape.labelup,   location=location.absolute, color=color.new(bullDivColor, bandTransparency), textcolor=textColor, size=size.small)
plotshape(bearCond ? oscLbr : na, offset=-lookbackRight, text=' Bear ', style=shape.labeldown, location=location.absolute, color=color.new(bearDivColor, bandTransparency), textcolor=textColor, size=size.small)

//  Alerts
alertcondition(longSignalObos,  title='Buy – Oversold',      message='Force Pulse: Price crossed above oversold level')
alertcondition(shortSignalObos, title='Sell – Overbought',   message='Force Pulse: Price crossed below overbought level')
alertcondition(longSignalZero,  title='Midline Long',      message='Force Pulse: Oscillator crossed above Midline')
alertcondition(shortSignalZero, title='Midline Short',     message='Force Pulse: Oscillator crossed below Midline')
alertcondition(longSignalMaZero, title='MA Midline Long',  message='Force Pulse: MA crossed above Midline')
alertcondition(shortSignalMaZero,title='MA Midline Short', message='Force Pulse: MA crossed below Midline')
alertcondition(bullCond, title='Bullish Divergence', message='Force Pulse: Bullish divergence detected')
alertcondition(bearCond, title='Bearish Divergence', message='Force Pulse: Bearish divergence detected')

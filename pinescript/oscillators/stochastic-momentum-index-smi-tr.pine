// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Tiagorocha1989
//                              ████████╗ ████████╗  ████████╗  ███████╗   ██████╗        ██████╗   ██████╗   ██████╗ ██╗   ██╗ ████████╗
//                              ╚══██╔══╝ ╚══██╔══╝  ██╔═══██║  ██╔════╝  ██╔═══██╗       ██╔══██╗ ██╔═══██╗ ██╔════╝ ██║   ██║ ██╔═══██║
//                                 ██║       ██║     ████████║  ██║ ███║  ██║   ██║       ██████╔╝ ██║   ██║ ██║      ████████║ ████████║
//                                 ██║       ██║     ██╔═══██║  ██║  ██║  ██║   ██║       ██╔══██╗ ██║   ██║ ██║      ██║   ██║ ██╔═══██║
//                                 ██║    ████████╗  ██║   ██║  ███████║  ╚██████╔╝       ██║  ██║ ╚██████╔╝ ╚██████╗ ██║   ██║ ██║   ██║
//@version=6
indicator('Stochastic Momentum Index - SMI | TR', 'SMI | TR →', false)
//╔════════════════════════╗
//║ Choose Moving Averages ║
//╚════════════════════════╝
ma(source, length, type) =>
     type == 'EMA' ? ta.ema(source, length) :
     type == 'SMA' ? ta.sma(source, length) :
     type == 'RMA' ? ta.rma(source, length) :
     type == 'WMA' ? ta.wma(source, length) :
     type == 'VWMA' ? ta.vwma(source, length) :
     type == 'HMA' ? ta.hma(source, length) :
     na
//╔════════════════════════╗
//║     INPUT SETTINGS     ║
//╚════════════════════════╝
Color_Mode =        input.string('Classic', 'Color Choice', group = 'Color', options = ['Classic', 'Modern', 'Robust', 'Accented', 'Monochrome', 'Moderate', 'Aqua', 'Cosmic'])
Length_SMI =        input.int(101, title = 'Length SMI:', group='SMI Setting', inline = 'SMI')
Source_SMI =        input(close, title = '', group='SMI Setting', inline = 'SMI')
Signal_SMI =        input.string('50', 'Entry/Exit Signal', group='SMI Setting', options = ['50', 'SMI MA'],tooltip = 'Choose Entry/Exit Point')
Length_MA_SMI =     input.int(365, title = 'Length MA', group='SMI MA', inline = 'MA')
Source_MA_SMI =     input.string('EMA', '', group='SMI MA', inline = 'MA', options=['EMA', 'SMA', 'RMA', 'WMA', 'VWMA', 'HMA'],tooltip = 'Moving Averages')
//╔════════════════════════╗
//║      Calculation       ║
//╚════════════════════════╝
SMI =     ta.stoch(Source_SMI, high, low, Length_SMI)
SMI_MA =  ma(SMI, Length_MA_SMI, Source_MA_SMI)
float Entry_SMI = Signal_SMI == 'SMI MA' ? SMI_MA : 50.0
isBull_SMI = SMI > Entry_SMI 
isBear_SMI = SMI < Entry_SMI
Bull_SMI = ta.crossover(SMI, Entry_SMI)
Bear_SMI = ta.crossunder(SMI, Entry_SMI)
//╔════════════════════════╗
//║         Color          ║
//╚════════════════════════╝
[UpC, DnC] = switch Color_Mode
    'Classic' =>    [#008800, #ff0000]
    'Modern' =>     [#ffffff, #b721ff]
    'Robust' =>     [#ffbb00, #770737]
    'Accented' =>   [#8c5cf7, #e83e8c]
    'Monochrome' => [#e9ecef, #495057]
    'Moderate' =>   [#43a047, #e53935]
    'Aqua' =>       [#00a8e8, #f18f01]
    'Cosmic' =>     [#e83e8c, #6f2da8]
//╔════════════════════════╗
//║          Plot          ║
//╚════════════════════════╝
Plot_SMI = plot(SMI, color = SMI > 50 ? color.new(UpC, 50):color.new(DnC, 50), linewidth = 3)
Plot_Entry_SMI = plot(Entry_SMI,color = color.yellow)
fill(Plot_SMI, Plot_Entry_SMI, 100, Entry_SMI, top_color = color.new(UpC, 0),    bottom_color = color.new(UpC, 100), title = 'Overbought Fill')
fill(Plot_SMI, Plot_Entry_SMI, Entry_SMI, 0, top_color = color.new(DnC, 100), bottom_color = color.new(DnC, 0),    title = 'Oversold Fill')
fill(Plot_SMI, Plot_Entry_SMI, SMI < Entry_SMI ? SMI : Entry_SMI, SMI < Entry_SMI ? Entry_SMI : SMI, SMI < Entry_SMI ? DnC : #00000000, SMI < Entry_SMI ? #00000000 : UpC)
plotcandle(open, high, low, close, 'Bar Color', isBull_SMI ? UpC : isBear_SMI ? DnC : na, isBull_SMI ? UpC : isBear_SMI ? DnC : na, bordercolor = isBull_SMI ? UpC : isBear_SMI ? DnC : na, force_overlay = true)
plotshape(Bull_SMI, 'LONG' , shape.triangleup  , location.belowbar, UpC,    0, size = size.small, force_overlay = true)
plotshape(Bear_SMI, 'SHORT', shape.triangledown, location.abovebar, DnC,    0, size = size.small, force_overlay = true)
//╔════════════════════════╗
//║          VIEW          ║
//╚════════════════════════╝
var table Table_SMI = table.new(position.middle_right, 1, 1, border_width = 1)
var label SMILabel = na
if barstate.islast
    table.cell(Table_SMI, 0, 0, text = isBull_SMI ? '⬆️ Bullish' : '⬇️ Bearish', text_color = isBull_SMI ? UpC : DnC,text_size = size.huge)
    if not na(SMILabel)
        label.delete(SMILabel)
    SMILabel := label.new(bar_index + 5, SMI, text =  str.tostring(SMI, '#.##'), color = SMI > Entry_SMI ? color.new(UpC, 20) : SMI < Entry_SMI ? color.new(DnC, 20) : color.gray, style = label.style_label_left, size = size.large, textcolor = #000000)
//╔════════════════════════╗
//║         Alerts         ║
//╚════════════════════════╝
alertcondition(Bull_SMI, title= 'LONG', message= '{{ticker}} LONG')
alertcondition(Bear_SMI, title= 'SHORT', message= '{{ticker}} SHORT')

// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © theValleyMan

//@version=6
indicator("GTI B")

// =============================================================
// =============================================================
///
//// Calculations
///
// =============================================================
// =============================================================

// ─────────────────────────────────────────────────────────────
/// Standard RSI
// ─────────────────────────────────────────────────────────────

RSILength = input.int(14, minval=1, title="RSI Length")
RSI = ta.rsi(close,RSILength)

// ─────────────────────────────────────────────────────────────
/// Stochastic RSI
// ─────────────────────────────────────────────────────────────

StochLength = input.int(14, minval=1, title="Stochastic Length")
Sto = ta.stoch(RSI, RSI, RSI, StochLength)

smoothk = input.int(5, minval=1, title="%k")                            // stochastic line
smoothd = input.int(3, minval=1, title="%d")                            // signal line
K = ta.sma(Sto, smoothk)
D = ta.sma(K, smoothd)

// ─────────────────────────────────────────────────────────────
/// Smoothed RSI
// ─────────────────────────────────────────────────────────────

avgRsiLen  = input.int(21, "Avg RSI Length", minval=1)
avgRsiType = input.string("EMA", "Avg RSI Method", options=["SMA","EMA","RMA","WMA"])

macroSmoothLen = input.int(21, "Macro Smooth Length (2nd pass)", minval=1)
confirmBars    = input.int(5,  "Trend Confirm Bars", minval=1)
minSlope       = input.float(0.05, "Min Slope (RSI pts/bar)", step=0.01)

////////////////////
/// Average RSI (first pass)
avgRSI = avgRsiType == "SMA" ? ta.sma(RSI, avgRsiLen) : avgRsiType == "EMA" ? ta.ema(RSI, avgRsiLen) : avgRsiType == "RMA" ? ta.rma(RSI, avgRsiLen) : ta.wma(RSI, avgRsiLen)  // WMA fallback

////////////////////
/// Macro smoothing (second pass) to suppress minor reversals
macroRSI = ta.ema(avgRSI, macroSmoothLen)

////////////////////
/// Slope + persistence to define macro up/down
slope     = macroRSI - macroRSI[1]
risingOK  = ta.rising(macroRSI, confirmBars) and slope >  minSlope
fallingOK = ta.falling(macroRSI, confirmBars) and slope < -minSlope

////////////////////
/// Sticky regime (hysteresis): only flip on strong signals
var int smoothedRegime = 0                                              // 1 = up, -1 = down, 0 = neutral
if risingOK
    smoothedRegime := 1
else if fallingOK                                                       // else keep previous smoothedRegime (avoid noise-driven flips)
    smoothedRegime := -1


// =============================================================
// =============================================================
///
//// Plots and Colors
///
// =============================================================
// =============================================================

// ─────────────────────────────────────────────────────────────
/// Level Lines
// ─────────────────────────────────────────────────────────────

bearLineInput  = input.float(43.6, "Bear Line Input", minval=1)
bullLineInput  = input.float(61.1, "Bull Line Input", minval=1)

zeroLine = hline(0, 'Zero Line', color.new(#00b7ffcc, 20), linestyle=hline.style_solid, linewidth=1)
hunderedLine = hline(100, 'One Hundred Line', color.new(#ff0073cc, 20), linestyle=hline.style_solid, linewidth=1)
bearLine = hline(bearLineInput, 'Bear Line', color.new(#00b7ffcc, 0), linestyle=hline.style_solid, linewidth=1)
hline(50, 'Fifty Line', #fffdde99, linestyle=hline.style_dashed, linewidth=1)
bullLine = hline(bullLineInput, 'Bull Line', color.new(#ff0073cc, 0), linestyle=hline.style_solid, linewidth=1)

fill(zeroLine, bearLine, title = 'Bear Zone', color = color.new(#00b7ffcc, 80))
fill(bullLine, hunderedLine, title = 'Bear Zone', color = color.new(#ff0073cc, 80))

// ─────────────────────────────────────────────────────────────
/// Standard RSI
// ─────────────────────────────────────────────────────────────
plot(RSI, title="RSI", color= RSI >= bullLineInput ? color.new(#ff00ae, 0) : RSI <= bearLineInput ? color.new(#00e5ff, 10) : color.yellow, linewidth=1)

// ─────────────────────────────────────────────────────────────
/// Stochastic RSI
// ─────────────────────────────────────────────────────────────
stochDescending = K < D
stochAscending = K > D
stochBearish = RSI < bearLineInput
stochBullish = RSI > bullLineInput
plot(K, color = stochDescending ? color.new(#ffffff, 30) : stochAscending ? color.new(#ffffff, 15) : na, linewidth = 1)
plot(K, color = stochDescending and stochBearish ? color.new(#00ff51, 30) : stochAscending and stochBearish ? color.new(#00ff51, 0) : stochAscending and stochBullish ? color.new(#ff0d00, 0) : stochDescending and stochBullish ? color.new(#ff0d00, 30) : na, linewidth = 2)
plot(D, color = color.new(color.red, 30), linewidth = 1)

// ─────────────────────────────────────────────────────────────
/// Smoothed RSI
// ─────────────────────────────────────────────────────────────
plot(macroRSI, title="Avg RSI (Macro)", color=smoothedRegime == 1 ? color.new(#ff5252, 0) : smoothedRegime == -1 ? color.new(#52eeff, 0) : color.rgb(105, 105, 105), linewidth=2)

// =============================================================
// =============================================================
///
//// Standard RSI Reversals
///
// =============================================================
// =============================================================

// ─────────────────────────────────────────────────────────────
// Standard RSI Divergences (no offsets used)
// ─────────────────────────────────────────────────────────────

// Pivot parameters
std_pivot_left  = input.int(10, "RSI Pivot Left",  minval=0)
std_pivot_right = input.int(5,  "RSI Pivot Right", minval=0)

// Pivots on RSI (values sit at the *pivot bar*, revealed after `right` bars)
rsiPH = ta.pivothigh(RSI, std_pivot_left, std_pivot_right)          // value at pivot-high bar (else na)
rsiPL = ta.pivotlow (RSI, std_pivot_left, std_pivot_right)          // value at pivot-low  bar (else na)

// Detect the *confirmation bar* without offsets:
// "how many bars since the pivot bar?" → equals `right` on the confirmation bar
phConfirmNow = ta.barssince(not na(rsiPH)) == std_pivot_right
plConfirmNow = ta.barssince(not na(rsiPL)) == std_pivot_right

// Grab pivot data (no offsets) — the values at the pivot bar:
plRSI   = ta.valuewhen(not na(rsiPL), RSI,        0)
plPrice = ta.valuewhen(not na(rsiPL), low,        0)
plBar   = ta.valuewhen(not na(rsiPL), bar_index,  0)

phRSI   = ta.valuewhen(not na(rsiPH), RSI,        0)
phPrice = ta.valuewhen(not na(rsiPH), high,       0)
phBar   = ta.valuewhen(not na(rsiPH), bar_index,  0)

// Keep “previous pivot” memory to compare with the newest one
var int   lastLowBar    = na
var float lastLowRSI    = na
var float lastLowPrice  = na

var int   lastHighBar   = na
var float lastHighRSI   = na
var float lastHighPrice = na

// ---- Bullish divergence (at a *new confirmed pivot low*)
if plConfirmNow
    // Only test if we have a prior pivot low to compare
    if not na(lastLowBar)
        rsiHigherLow  = plRSI   > lastLowRSI
        priceLowerLow = plPrice < lastLowPrice

        if rsiHigherLow and priceLowerLow
            label.new(plBar, 0, text=" ⬆ ", style=label.style_none, color=color.rgb(255, 82, 82), textcolor=color.rgb(155, 255, 120), size=size.large)

    // Update memory
    lastLowBar   := plBar
    lastLowRSI   := plRSI
    lastLowPrice := plPrice

// ---- Bearish divergence (at a *new confirmed pivot high*)
if phConfirmNow
    if not na(lastHighBar)
        rsiLowerHigh    = phRSI   < lastHighRSI
        priceHigherHigh = phPrice > lastHighPrice

        if rsiLowerHigh and priceHigherHigh
            label.new(phBar, 90, text=" ⬇ ", style=label.style_none, color=color.rgb(255, 82, 82), textcolor=color.rgb(255, 120, 120), size=size.large)

    // Update memory
    lastHighBar   := phBar
    lastHighRSI   := phRSI
    lastHighPrice := phPrice


// =============================================================
// =============================================================
///
//// Alerts
///
// =============================================================
// =============================================================

// ────────────────────────────────
// Standard RSI Divergences
// ────────────────────────────────

rsiLowerHigh_now    = phConfirmNow and not na(lastHighBar) and (phRSI   < lastHighRSI)
priceHigherHigh_now = phConfirmNow and not na(lastHighBar) and (phPrice > lastHighPrice)
bearDiv_now = rsiLowerHigh_now and priceHigherHigh_now

alertcondition(bearDiv_now, title = "RSI BEARISH DIVERGENCE (Red ⬇)", message = "RSI LH + Price HH on {{ticker}} {{interval}} @ {{close}}")

rsiHigherLow_now   = plConfirmNow and not na(lastLowBar) and (plRSI   > lastLowRSI)
priceLowerLow_now  = plConfirmNow and not na(lastLowBar) and (plPrice < lastLowPrice)
bullDiv_now        = rsiHigherLow_now and priceLowerLow_now

alertcondition(bullDiv_now, title = "RSI BULLISH DIVERGENCE (Green ⬆)", message = "RSI HL + Price LL on {{ticker}} {{interval}} @ {{close}}")

// ────────────────────────────────
// Smoothed RSI Flip
// ────────────────────────────────

alertcondition(smoothedRegime  == 1, title = "AVG RSI BULLISH FLIP (aqua → red)", message = "BULLISH SMOOTH RSI FLIP on {{ticker}} {{interval}} @ {{close}}")

alertcondition(smoothedRegime == -1, title = "AVG RSI BEARISH FLIP (red → aqua)", message = "BEARISH SMOOTH RSI FLIP on {{ticker}} {{interval}} @ {{close}}")

// This Pine ScriptÂ® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© RWCS_LTD

//@version=6
indicator("Stochastic Pro+ Suite", overlay = false)

// === INPUTS ===
group1 = "Stochastic Settings"
group2 = "Divergence Settings"
group3 = "Plotting"

STOCH_TIME = input.timeframe("240", "Stochastic Timeframe", group = group1)
stochKLen = input.int(14, title="%K Length", group = group1)
stochSmoothK = input.int(1, title="%K Smoothing", group = group1)
stochDLen = input.int(3, title="%D Smoothing", group = group1)
OB = input.int(80, "Overbought Level", minval=50, maxval=100, group = group1)
OS = input.int(20, "Oversold Level", minval=0, maxval=50, group = group1)

lookbackLeft = input.int(5, "Pivot Lookback Left", minval = 1, group = group2)
lookbackRight = input.int(5, "Pivot Lookback Right", minval = 1, group = group2)
rangeLower = input.int(3, "Min Bars Between Pivots", group = group2)
rangeUpper = input.int(60, "Max Bars Between Pivots", group = group2)

showKD = input.bool(true, "Show %K & %D Line", group = group3)
showOver = input.bool(true, "Show Oversold/Overbought Levels?", group = group3)
showSpread = input.bool(true, "Show %K-%D Spread Histogram", group = group3)
showBG = input.bool(true, "Show Background Highlight?", group = group3)
enableDiv = input.bool(true, "Enable Divergence", group = group3)

// === MTF STOCHASTIC FUNCTION ===
f_stoch(kLen, kSmooth, dLen) =>
    k_val = ta.sma(ta.stoch(close, high, low, kLen), kSmooth)
    d_val = ta.sma(k_val, dLen)
    spread = k_val - d_val
    [k_val, d_val, spread]

// === MTF REQUEST ===
[k, d, spread] = request.security(syminfo.tickerid, STOCH_TIME, f_stoch(stochKLen, stochSmoothK, stochDLen))

// === DIVERGENCE DETECTION ===
_inRange(cond) =>
    bars = ta.barssince(cond)
    rangeLower <= bars and bars <= rangeUpper

pivotLow = ta.pivotlow(k, lookbackLeft, lookbackRight)
pivotHigh = ta.pivothigh(k, lookbackLeft, lookbackRight)

plFound = enableDiv and not na(pivotLow)
phFound = enableDiv and not na(pivotHigh)

kLBR = k[lookbackRight]
lowLBR = low[lookbackRight]
highLBR = high[lookbackRight]

foundPL = _inRange(plFound[1])
foundPH = _inRange(phFound[1])

kHL = kLBR > ta.valuewhen(plFound, kLBR, 1) and foundPL
priceLL = lowLBR < ta.valuewhen(plFound, lowLBR, 1)
bullDiv = plFound and priceLL and kHL

kLH = kLBR < ta.valuewhen(phFound, kLBR, 1) and foundPH
priceHH = highLBR > ta.valuewhen(phFound, highLBR, 1)
bearDiv = phFound and priceHH and kLH

// === PLOTTING ===
plot(showKD ? k : na, title="%K", color=color.blue)
plot(showKD ? d : na, title="%D", color=color.orange)
plot(showSpread ? spread : na, title="%K-%D Spread", color=spread > 0 ? color.aqua : color.fuchsia, style = plot.style_histogram)

hline(showOver ? OB : na, "OB", color=color.red)
hline(showOver ? OS : na, "OS", color=color.green)

crossUp = ta.crossunder(k, d)
crossDown = ta.crossover(k, d)

bgColorVal = showBG ? (
     crossUp and d > OB ? color.new(color.red, 85) :
     crossDown and d < OS ? color.new(color.green, 85) : na
     ) : na

bgcolor(bgColorVal, title = "Stoch Zone Highlight")

plotshape(bullDiv ? kLBR : na, offset = -lookbackRight, style = shape.labelup, color=color.green, text = "Bull", textcolor=color.white, location=location.absolute, title="Bullish Divergence")
plotshape(bearDiv ? kLBR : na, offset = -lookbackRight, style = shape.labeldown, color=color.red, text = "Bear", textcolor=color.white, location=location.absolute, title="Bearish Divergence")

// === ALERTS ===
alertcondition(bullDiv, title="Bullish Divergence", message="Bullish divergence detected between price and Stochastic %K.")
alertcondition(bearDiv, title="Bearish Divergence", message="Bearish divergence detected between price and Stochastic %K.")
alert("ðŸ” Bar closed. Check in. Dial in. Is there a setup?", alert.freq_once_per_bar_close)

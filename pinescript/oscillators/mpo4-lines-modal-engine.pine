// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Uncle_the_shooter

//@version=6
indicator('MPO4 Lines – Modal Engine', shorttitle = 'MPO4', overlay = false, max_bars_back = 500, precision = 2)

// Line Settings (Visibility + Coloring Mode)
slowColorMode = input.string('Direction', title = 'Slow Line Coloring Mode', options = ['Direction', 'Position vs Zero'], group = 'Line Settings')
showLine1 = input.bool(true, title = 'Show Slow Line', group = 'Line Settings')
showLine2 = input.bool(true, title = 'Show Fast Line A', group = 'Line Settings')
showLine3 = input.bool(true, title = 'Show Fast Line B', group = 'Line Settings')
showLine4 = input.bool(false, title = 'Show Line 4', group = 'Line Settings')

// Line Lengths
len1 = input.int(20, title = 'Slow Line Lookback (Base)', group = 'Line Lengths')
smoothLen1 = input.int(5, title = 'Slow Line Primary Smoothing (EMA)', group = 'Line Lengths')
extraSmoothLen = input.int(5, title = 'Slow Line Extra Smoothing (EMA)', minval = 1, group = 'Line Lengths')
len2 = input.int(6, title = 'Fast Line A Lookback', group = 'Line Lengths')
smoothLen2 = input.int(7, title = 'Fast Line A Smoothing (EMA)', group = 'Line Lengths')
len3 = input.int(6, title = 'Fast Line B Lookback', group = 'Line Lengths')
smoothLen3 = input.int(10, title = 'Fast Line B Smoothing (EMA)', group = 'Line Lengths')
len4 = input.int(14, title = 'Line 4 Lookback', group = 'Line Lengths')
smoothLen4 = input.int(1, title = 'Line 4 Smoothing (EMA)', group = 'Line Lengths')

// Bands & Thresholds
overbought = input.float(30.0, title = 'Overbought Threshold', step = 0.1, group = 'Bands & Thresholds')
oversold = input.float(-30.0, title = 'Oversold Threshold', step = 0.1, group = 'Bands & Thresholds')

// Signals
showObosSignals = input.bool(true, title = 'Show Fast A OB/OS Return Signals', group = 'Signals')

// Divergence Settings
calculateDivergence = input.bool(true, title = 'Enable Divergence (on Fast Line A)', group = 'Divergence')
pivotLength = input.int(2, title = 'Pivot Length (Left/Right)', minval = 1, group = 'Divergence')

// Colors & Appearance
line1ColorPositive = input.color(color.rgb(6, 162, 47), title = 'Slow Line Bullish', group = 'Colors & Appearance')
line1ColorNegative = input.color(color.rgb(207, 23, 23), title = 'Slow Line Bearish', group = 'Colors & Appearance')
line2ColorPositive = input.color(color.rgb(163, 231, 172), title = 'Fast Line A Bullish', group = 'Colors & Appearance')
line2ColorNegative = input.color(color.rgb(237, 121, 121), title = 'Fast Line A Bearish', group = 'Colors & Appearance')
line3ColorPositive = input.color(color.rgb(6, 162, 47), title = 'Fast Line B Bullish', group = 'Colors & Appearance')
line3ColorNegative = input.color(color.rgb(207, 23, 23), title = 'Fast Line B Bearish', group = 'Colors & Appearance')
line4ColorPositive = input.color(color.rgb(6, 162, 47), title = 'Line 4 Bullish', group = 'Colors & Appearance')
line4ColorNegative = input.color(color.rgb(207, 23, 23), title = 'Line 4 Bearish', group = 'Colors & Appearance')
zeroLineColor = input.color(color.gray, title = 'Zero Line Color', group = 'Colors & Appearance')
overboughtLineColor = input.color(color.red, title = 'Overbought Line Color', group = 'Colors & Appearance')
oversoldLineColor = input.color(color.green, title = 'Oversold Line Color', group = 'Colors & Appearance')
bullDivColor = input.color(color.green, title = 'Bullish Divergence Color', group = 'Colors & Appearance')
bearDivColor = input.color(color.red, title = 'Bearish Divergence Color', group = 'Colors & Appearance')
textColor = input.color(color.white, title = 'Divergence Text Color', group = 'Colors & Appearance')
gradientTransparency = input.int(75, title = 'Fast B to Zero Gradient Transparency', minval = 0, maxval = 100, group = 'Colors & Appearance')
line2Line3GradientTransparency = input.int(50, title = 'Fast A to B Fill Transparency', minval = 0, maxval = 100, group = 'Colors & Appearance')
bandTransparency = input.int(40, title = 'Band Gradient Transparency', minval = 0, maxval = 100, group = 'Colors & Appearance')
showOverboughtGradient = input.bool(true, title = 'Show Overbought Gradient', group = 'Colors & Appearance')
showOversoldGradient = input.bool(true, title = 'Show Oversold Gradient', group = 'Colors & Appearance')
showFastBToZeroGradient = input.bool(true, title = 'Show Fast B to Zero Gradient', group = 'Colors & Appearance')

// Additional Settings
rangeUpper = 60
rangeLower = 5

// Oscillator Calculation Function
calcCPO(_len, _smooth) =>
    body = math.abs(close - open)
    avgBody = ta.sma(body, _len)
    direction = close > open ? 1.0 : close < open ? -1.0 : 0.0
    weight = avgBody != 0 ? body / avgBody : 1.0
    contrib = direction * weight
    rolling = 0.0
    for i = 0 to _len - 1
        rolling := rolling + contrib[i]
    norm = rolling / (_len * 2) * 100
    ta.ema(norm, _smooth)

// Oscillator Lines
line1_raw = calcCPO(len1, smoothLen1)
line1 = ta.ema(line1_raw, extraSmoothLen)
line2 = calcCPO(len2, smoothLen2)
line3 = calcCPO(len3, smoothLen3)
line4 = calcCPO(len4, smoothLen4)

// Dynamic Colors
line1Color = slowColorMode == 'Position vs Zero'
     ? (line1 >= 0 ? color.new(line1ColorPositive, 0) : color.new(line1ColorNegative, 0))
     : (line1 > line1[1] ? color.new(line1ColorPositive, 0) : color.new(line1ColorNegative, 0))
line2Color = line2 >= 0 ? color.new(line2ColorPositive, 0) : color.new(line2ColorNegative, 0)
line3Color = line3 >= 0 ? color.new(line3ColorPositive, 0) : color.new(line3ColorNegative, 0)
line4Color = line4 >= 0 ? color.new(line4ColorPositive, 0) : color.new(line4ColorNegative, 0)

// Slow Line Color Change Detection
bool colorChangeDirection = slowColorMode == 'Direction' and (line1 > line1[1]) != (line1[1] > line1[2])
bool colorChangePosition = slowColorMode == 'Position vs Zero' and (line1 >= 0) != (line1[1] >= 0)

// Dynamic Colors for Bands
ob_gradient_color = line2 <= line3 ? overboughtLineColor : zeroLineColor
os_gradient_color = line2 > line3 ? oversoldLineColor : zeroLineColor

// Return Signals (Fast A OB/OS Cross)
longSignalFast = ta.crossover(line2, oversold)
shortSignalFast = ta.crossunder(line2, overbought)
supBuySignal = showObosSignals and longSignalFast
supSellSignal = showObosSignals and shortSignalFast

// Divergence
lookbackRight = pivotLength
lookbackLeft = pivotLength
pivotHighPrice = ta.pivothigh(high, lookbackLeft, lookbackRight)
pivotLowPrice = ta.pivotlow(low, lookbackLeft, lookbackRight)
pivotHighOsc = ta.pivothigh(line2, lookbackLeft, lookbackRight)
pivotLowOsc = ta.pivotlow(line2, lookbackLeft, lookbackRight)

var bool plFound = false
var bool phFound = false
var bool bullCond = false
var bool bearCond = false

oscLbr = line2[lookbackRight]
_inRange(bool cond) =>
    bars = ta.barssince(cond)
    rangeLower <= bars and bars <= rangeUpper

if calculateDivergence
    plFound := not na(pivotLowOsc)
    oscHl = oscLbr > ta.valuewhen(plFound, oscLbr, 1) and _inRange(plFound[1])
    lowLbr = low[lookbackRight]
    priceLl = lowLbr < ta.valuewhen(plFound, lowLbr, 1)
    bullCond := priceLl and oscHl and plFound

    phFound := not na(pivotHighOsc)
    oscLh = oscLbr < ta.valuewhen(phFound, oscLbr, 1) and _inRange(phFound[1])
    highLbr = high[lookbackRight]
    priceHh = highLbr > ta.valuewhen(phFound, highLbr, 1)
    bearCond := priceHh and oscLh and phFound

// Plot Lines
plot_line1 = plot(showLine1 ? line1 : na, title = 'Slow Line', color = line1Color, linewidth = 3)
plot_line2 = plot(showLine2 ? line2 : na, title = 'Fast Line A', color = line2Color, linewidth = 2)
plot_line3 = plot(showLine3 ? line3 : na, title = 'Fast Line B', color = line3Color, linewidth = 2)
plot_line4 = plot(showLine4 ? line4 : na, title = 'Line 4', color = line4Color, linewidth = 2)

// Zero Line
hline(0, title = 'Zero Line', color = zeroLineColor, linestyle = hline.style_dashed)

// Gradient for Fast Line B to Zero (with toggle)
p_mid1 = plot(showLine3 and showFastBToZeroGradient ? line3 * 0.8 : na, title = 'Intermediate Layer 1', color = na, display = display.none)
p_mid2 = plot(showLine3 and showFastBToZeroGradient ? line3 * 0.6 : na, title = 'Intermediate Layer 2', color = na, display = display.none)
p_mid3 = plot(showLine3 and showFastBToZeroGradient ? line3 * 0.4 : na, title = 'Intermediate Layer 3', color = na, display = display.none)
p_mid4 = plot(showLine3 and showFastBToZeroGradient ? line3 * 0.2 : na, title = 'Intermediate Layer 4', color = na, display = display.none)
p0 = plot(0, title = 'Zero Line for Fill', color = na, display = display.none)
fillColor = line3 >= 0 ? color.new(line3ColorPositive, gradientTransparency) : color.new(line3ColorNegative, gradientTransparency)
fill(plot_line3, p_mid1, color = showLine3 and showFastBToZeroGradient ? fillColor : na, title = 'Fill Layer 1')
fill(p_mid1, p_mid2, color = showLine3 and showFastBToZeroGradient ? color.new(line3Color, gradientTransparency + 5) : na, title = 'Fill Layer 2')
fill(p_mid2, p_mid3, color = showLine3 and showFastBToZeroGradient ? color.new(line3Color, gradientTransparency + 10) : na, title = 'Fill Layer 3')
fill(p_mid3, p_mid4, color = showLine3 and showFastBToZeroGradient ? color.new(line3Color, gradientTransparency + 15) : na, title = 'Fill Layer 4')
fill(p_mid4, p0, color = showLine3 and showFastBToZeroGradient ? color.new(line3Color, gradientTransparency + 20) : na, title = 'Fill Layer 5')

// Fill Between Fast Line A and B
fill(plot_line2, plot_line3, color = showLine2 and showLine3 ? color.new(line2Color, line2Line3GradientTransparency) : na, title = 'Fast A to B Fill')

// Bands
hline(overbought, title = 'Overbought', color = overboughtLineColor, linestyle = hline.style_dashed)
plot(showOverboughtGradient ? overbought : na, title = 'Overbought Gradient', color = color.new(ob_gradient_color, bandTransparency), linewidth = 10, editable = false)
hline(oversold, title = 'Oversold', color = oversoldLineColor, linestyle = hline.style_dashed)
plot(showOversoldGradient ? oversold : na, title = 'Oversold Gradient', color = color.new(os_gradient_color, bandTransparency), linewidth = 10, editable = false)

// Plotshape – Return Signals
plotshape(supBuySignal ? line2 : na, title = "Buy: Return from Oversold", location = location.bottom, color = color.new(oversoldLineColor, 60), style = shape.circle, size = size.tiny)
plotshape(supSellSignal ? line2 : na, title = "Sell: Return from Overbought", location = location.top, color = color.new(overboughtLineColor, 60), style = shape.circle, size = size.tiny)

// Divergence Plots
plotBullDiv = plot(calculateDivergence and plFound ? oscLbr : na, offset = -lookbackRight, title = 'Bullish Divergence', linewidth = 2, color = bullCond ? bullDivColor : color.new(color.white, 100))
plotBearDiv = plot(calculateDivergence and phFound ? oscLbr : na, offset = -lookbackRight, title = 'Bearish Divergence', linewidth = 2, color = bearCond ? bearDivColor : color.new(color.white, 100))

plotshape(bullCond ? oscLbr : na, offset = -lookbackRight, title = 'Bullish Divergence Label', text = ' Bull ', style = shape.labelup, location = location.absolute, color = color.new(bullDivColor, 40), textcolor = textColor, size = size.small)
plotshape(bearCond ? oscLbr : na, offset = -lookbackRight, title = 'Bearish Divergence Label', text = ' Bear ', style = shape.labeldown, location = location.absolute, color = color.new(bearDivColor, 40), textcolor = textColor, size = size.small)

// Alerts
alertcondition(supBuySignal, title = 'Buy: Fast A Return from Oversold', message = 'Fast Line A crossed above oversold level.')
alertcondition(supSellSignal, title = 'Sell: Fast A Return from Overbought', message = 'Fast Line A crossed below overbought level.')
alertcondition(colorChangeDirection, title = 'Slow Line Color Change (Direction Mode)', message = 'Slow Line changed direction.')
alertcondition(colorChangePosition, title = 'Slow Line Color Change (Position vs Zero Mode)', message = 'Slow Line crossed zero level.')
alertcondition(bullCond, title = 'Bullish Divergence (Fast A)', message = 'Bullish divergence detected on Fast Line A.')
alertcondition(bearCond, title = 'Bearish Divergence (Fast A)', message = 'Bearish divergence detected on Fast Line A.')

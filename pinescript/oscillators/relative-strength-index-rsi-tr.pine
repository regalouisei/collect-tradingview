// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Tiagorocha1989
//                              ████████╗ ████████╗  ████████╗  ███████╗   ██████╗        ██████╗   ██████╗   ██████╗ ██╗   ██╗ ████████╗
//                              ╚══██╔══╝ ╚══██╔══╝  ██╔═══██║  ██╔════╝  ██╔═══██╗       ██╔══██╗ ██╔═══██╗ ██╔════╝ ██║   ██║ ██╔═══██║
//                                 ██║       ██║     ████████║  ██║ ███║  ██║   ██║       ██████╔╝ ██║   ██║ ██║      ████████║ ████████║
//                                 ██║       ██║     ██╔═══██║  ██║  ██║  ██║   ██║       ██╔══██╗ ██║   ██║ ██║      ██║   ██║ ██╔═══██║
//                                 ██║    ████████╗  ██║   ██║  ███████║  ╚██████╔╝       ██║  ██║ ╚██████╔╝ ╚██████╗ ██║   ██║ ██║   ██║
//@version=6
indicator('Relative Strength Index - RSI | TR', 'RSI | TR →', false)
//╔════════════════════════╗
//║ Choose Moving Averages ║
//╚════════════════════════╝
ma(source, length, type) =>
     type == 'EMA' ? ta.ema(source, length) :
     type == 'SMA' ? ta.sma(source, length) :
     type == 'RMA' ? ta.rma(source, length) :
     type == 'WMA' ? ta.wma(source, length) :
     type == 'VWMA' ? ta.vwma(source, length) :
     type == 'HMA' ? ta.hma(source, length) :
     na
//╔════════════════════════╗
//║     INPUT SETTINGS     ║
//╚════════════════════════╝
Color_Mode =        input.string('Classic', 'Color Choice', group = 'Color', options = ['Classic', 'Modern', 'Robust', 'Accented', 'Monochrome', 'Moderate', 'Aqua', 'Cosmic'])
Length_RSI =        input.int(14, title = 'Length RSI:', group='RSI Setting', inline = 'RSI')
Source_RSI =        input(close, title = '', group='RSI Setting', inline = 'RSI')
Signal_RSI =        input.string('50', 'Entry/Exit Signal', group='RSI Setting', options = ['50', 'RSI MA'],tooltip = 'Choose Entry/Exit Point')
Length_MA_RSI =     input.int(365, title = 'Length MA', group='RSI MA', inline = 'RSI MA')
Source_MA_RSI =     input.string('EMA', '', group='RSI MA', inline = 'RSI MA', options=['EMA', 'SMA', 'RMA', 'WMA', 'VWMA', 'HMA'], tooltip = 'Moving Averages')
//╔════════════════════════╗
//║      Calculation       ║
//╚════════════════════════╝
RSI =     ta.rsi(Source_RSI,Length_RSI)
RSI_MA =  ma(RSI, Length_MA_RSI, Source_MA_RSI)
float Entry_RSI = Signal_RSI == 'RSI MA' ? RSI_MA : 50.0
isBull_RSI = RSI > Entry_RSI
isBear_RSI = RSI < Entry_RSI
Bull_RSI = ta.crossover(RSI,Entry_RSI)
Bear_RSI = ta.crossunder(RSI,Entry_RSI)
//╔════════════════════════╗
//║         Color          ║
//╚════════════════════════╝
[UpC, DnC] = switch Color_Mode
    'Classic' =>    [#008800, #ff0000]
    'Modern' =>     [#ffffff, #b721ff]
    'Robust' =>     [#ffbb00, #770737]
    'Accented' =>   [#8c5cf7, #e83e8c]
    'Monochrome' => [#e9ecef, #495057]
    'Moderate' =>   [#43a047, #e53935]
    'Aqua' =>       [#00a8e8, #f18f01]
    'Cosmic' =>     [#e83e8c, #6f2da8]
//╔════════════════════════╗
//║          Plot          ║
//╚════════════════════════╝
Plot_RSI =     plot(RSI, color = RSI > Entry_RSI ? color.new(UpC, 20):color.new(DnC, 20), linewidth = 2)
Plot_Entry_RSI =  plot(Entry_RSI,color = color.yellow)
fill(Plot_RSI, Plot_Entry_RSI, 80, Entry_RSI, top_color = color.new(UpC, 0),    bottom_color = color.new(UpC, 100), title = 'Overbought Fill')
fill(Plot_RSI, Plot_Entry_RSI, Entry_RSI, 20, top_color = color.new(DnC, 100), bottom_color = color.new(DnC, 0),    title = 'Oversold Fill')
fill(Plot_RSI, Plot_Entry_RSI, RSI < Entry_RSI ? RSI : Entry_RSI, RSI < Entry_RSI ? Entry_RSI : RSI, RSI < Entry_RSI ? DnC : #00000000, RSI < Entry_RSI ? #00000000 : UpC)
plotcandle(open, high, low, close, 'Bar Color', isBull_RSI ? UpC : isBear_RSI ? DnC : na, isBull_RSI ? UpC : isBear_RSI ? DnC : na, bordercolor = isBull_RSI ? UpC : isBear_RSI ? DnC : na, force_overlay = true)
plotshape(Bull_RSI, 'LONG' , shape.triangleup  , location.belowbar, UpC,    0, size = size.small, force_overlay = true)
plotshape(Bear_RSI, 'SHORT', shape.triangledown, location.abovebar, DnC,    0, size = size.small, force_overlay = true)
//╔════════════════════════╗
//║          VIEW          ║
//╚════════════════════════╝
var table Table_RSI = table.new(position.middle_right, 1, 1, border_width = 1)
var label RSILabel = na
if barstate.islast
    table.cell(Table_RSI, 0, 0, text = isBull_RSI ? '⬆️ Bullish' : '⬇️ Bearish', text_color = isBull_RSI ? UpC : DnC,text_size = size.huge)
    if not na(RSILabel)
        label.delete(RSILabel)
    RSILabel := label.new(bar_index + 5, RSI, text =  str.tostring(RSI, '#.##'), color = RSI > Entry_RSI ? color.new(UpC, 20) : RSI < Entry_RSI ? color.new(DnC, 20) : color.gray, style = label.style_label_left, size = size.large, textcolor = #000000)
//╔════════════════════════╗
//║         Alerts         ║
//╚════════════════════════╝
alertcondition(Bull_RSI, title= 'LONG', message= '{{ticker}} LONG')
alertcondition(Bear_RSI, title= 'SHORT', message= '{{ticker}} SHORT')

// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Tiagorocha1989
//                              ████████╗ ████████╗  ████████╗  ███████╗   ██████╗        ██████╗   ██████╗   ██████╗ ██╗   ██╗ ████████╗
//                              ╚══██╔══╝ ╚══██╔══╝  ██╔═══██║  ██╔════╝  ██╔═══██╗       ██╔══██╗ ██╔═══██╗ ██╔════╝ ██║   ██║ ██╔═══██║
//                                 ██║       ██║     ████████║  ██║ ███║  ██║   ██║       ██████╔╝ ██║   ██║ ██║      ████████║ ████████║
//                                 ██║       ██║     ██╔═══██║  ██║  ██║  ██║   ██║       ██╔══██╗ ██║   ██║ ██║      ██║   ██║ ██╔═══██║
//                                 ██║    ████████╗  ██║   ██║  ███████║  ╚██████╔╝       ██║  ██║ ╚██████╔╝ ╚██████╗ ██║   ██║ ██║   ██║
//@version=6
indicator('Chande Momentum Oscillator - CMO | TR', 'CMO | TR →', false)
//╔════════════════════════╗
//║ Choose Moving Averages ║
//╚════════════════════════╝
ma(source, length, type) =>
     type == 'EMA' ? ta.ema(source, length) :
     type == 'SMA' ? ta.sma(source, length) :
     type == 'RMA' ? ta.rma(source, length) :
     type == 'WMA' ? ta.wma(source, length) :
     type == 'VWMA' ? ta.vwma(source, length) :
     type == 'HMA' ? ta.hma(source, length) :
     na
//╔════════════════════════╗
//║     INPUT SETTINGS     ║
//╚════════════════════════╝
Color_Mode =        input.string('Classic', 'Color Choice', group = 'Color', options = ['Classic', 'Modern', 'Robust', 'Accented', 'Monochrome', 'Moderate', 'Aqua', 'Cosmic'])
Length_CMO =        input.int(14, title = 'Length CMO:', group='CMO Setting', inline = 'CMO')
Source_CMO =        input(close, title = '', group='CMO Setting', inline = 'CMO')
Signal_CMO =        input.string('0', 'Entry/Exit Signal', group='CMO Setting', options = ['0', 'CMO MA'],tooltip = 'Choose Entry/Exit Point')
Length_MA_CMO =     input.int(365, title = 'Length MA', group='CMO MA', inline = 'CMO MA')
Source_MA_CMO =     input.string('EMA', '', group='CMO MA', inline = 'CMO MA', options=['EMA', 'SMA', 'RMA', 'WMA', 'VWMA', 'HMA'],tooltip = 'Moving Averages')
//╔════════════════════════╗
//║      Calculation       ║
//╚════════════════════════╝
CMO =     ta.cmo(Source_CMO,Length_CMO)
CMO_MA =  ma(CMO, Length_MA_CMO, Source_MA_CMO)
float Entry_CMO = Signal_CMO == 'CMO MA' ? CMO_MA : 0.0
isBull_CMO = CMO > CMO_MA
isBear_CMO = CMO < CMO_MA
Bull_CMO =ta.crossover(CMO, CMO_MA)
Bear_CMO = ta.crossunder(CMO, CMO_MA)
//╔════════════════════════╗
//║         Color          ║
//╚════════════════════════╝
[UpC, DnC] = switch Color_Mode
    'Classic' =>    [#008800, #ff0000]
    'Modern' =>     [#ffffff, #b721ff]
    'Robust' =>     [#ffbb00, #770737]
    'Accented' =>   [#8c5cf7, #e83e8c]
    'Monochrome' => [#e9ecef, #495057]
    'Moderate' =>   [#43a047, #e53935]
    'Aqua' =>       [#00a8e8, #f18f01]
    'Cosmic' =>     [#e83e8c, #6f2da8]
//╔════════════════════════╗
//║          Plot          ║
//╚════════════════════════╝
Plot_CMO =     plot(CMO, color = CMO > Entry_CMO ? color.new(UpC, 50):color.new(DnC, 50), linewidth = 3)
Plot_Entry_CMO =  plot(Entry_CMO,color = color.yellow)
fill(Plot_CMO, Plot_Entry_CMO, 50, Entry_CMO, top_color = color.new(UpC, 0),    bottom_color = color.new(UpC, 100), title = 'Overbought Fill')
fill(Plot_CMO, Plot_Entry_CMO, Entry_CMO, -50, top_color = color.new(DnC, 100), bottom_color = color.new(DnC, 0), title = 'Oversold Fill')
fill(Plot_CMO, Plot_Entry_CMO, CMO < Entry_CMO ? CMO : Entry_CMO, CMO < Entry_CMO ? Entry_CMO : CMO, CMO < Entry_CMO ? DnC : #00000000, CMO < Entry_CMO ? #00000000 : UpC)
plotcandle(open, high, low, close, 'Bar Color', isBull_CMO ? UpC : isBear_CMO ? DnC : na, isBull_CMO ? UpC : isBear_CMO ? DnC : na, bordercolor = isBull_CMO ? UpC : isBear_CMO ? DnC : na, force_overlay = true)
plotshape(Bull_CMO, 'LONG' , shape.triangleup  , location.belowbar, UpC,    0, size = size.small, force_overlay = true)
plotshape(Bear_CMO, 'SHORT', shape.triangledown, location.abovebar, DnC,    0, size = size.small, force_overlay = true)
//╔════════════════════════╗
//║          VIEW          ║
//╚════════════════════════╝
var table Table_CMO = table.new(position.middle_right, 1, 1, border_width = 1)
var label CMOLabel = na
if barstate.islast
    table.cell(Table_CMO, 0, 0, text = isBull_CMO ? '⬆️ Bullish' : '⬇️ Bearish', text_color = isBull_CMO ? UpC : DnC,text_size = size.huge)
    if not na(CMOLabel)
        label.delete(CMOLabel)
    CMOLabel := label.new(bar_index + 5, CMO, text =  str.tostring(CMO, '#.##'), color = CMO > Entry_CMO ? color.new(UpC, 20) : CMO < Entry_CMO ? color.new(DnC, 20) : color.gray, style = label.style_label_left, size = size.large, textcolor = #000000)
//╔════════════════════════╗
//║         Alerts         ║
//╚════════════════════════╝
alertcondition(Bull_CMO, title= 'LONG', message= '{{ticker}} LONG')
alertcondition(Bear_CMO, title= 'SHORT', message= '{{ticker}} SHORT')

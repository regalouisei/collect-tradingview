// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © PaulWegelin

//@version=6
indicator("RSI For Loop", max_labels_count = 500)
import TradingView/ta/11

//              ╔══════════════════════════╗              //
//              ║         SETTINGS         ║              //
//              ╚══════════════════════════╝              //

//Indicator Settings
var string RS       = "RSi Settings"
var string RMS      = "RSI median Settings" 
var string FL       = "For Loop Settings" 
var string CS       = "Color Settings"

//Color Settings
Col                 = input.string("SciFi", title = "Color Settings", group = CS, display = display.none, 
     options = [ 
         "SciFi", 
         "Mythic", 
         "Quantum Pulse", 
         "Sunny", 
         "Sunset", 
         "Washed Out", 
         "Ultra Violet", 
         "Monochrom", 
         "Ocean", 
         "Ice",
         "Matrix",
         "Royal",
         "Void"
         ])

src                 = input.source(close,       "RSI Source",           group = RS) 
rsiLength           = input.int(46,             "RSI Length",           group = RS, minval = 1, step=1) 

longTH              = input.int(15,             "Longr Threshold",      group = RS) 
shortTH             = input.int(-28,            "Short Threshold",      group = RS) 

a                   = input.int(1,              "from",                 group = FL, minval = 1, step=1)
b                   = input.int(99,             "to",                   group = FL, minval = 50, step=1) 

length              = input.int(225,            "Median Length",        group = RMS, minval = 1, step=1)
sdLength            = input.int(60,             "SD Length",            group = RMS, minval = 1, step=1)

//              ╔══════════════════════════╗              //
//              ║       CALCULATIONS       ║              //
//              ╚══════════════════════════╝              //

//RSI
rsi                 = ta.rsi(src, rsiLength)

//For Loop
system(a, b) =>
    total  = 0.0
    for i  = a to b by 1
        total += (rsi > rsi[i] ? 1 : -1)
        total
    total

score               = system(a, b)
median              = ta.percentile_nearest_rank(score, length,50)
score               := math.max(math.min(score, 99), -99)

stdev               = ta.stdev(rsi , sdLength)
u3                  = median + 3 * stdev
d3                  = median - 3 * stdev

//              ╔══════════════════════════╗              //
//              ║     TREND CONDITIONS     ║              //
//              ╚══════════════════════════╝              //

//RSI For Loop trend
L                   = score > longTH
S                   = score < shortTH

var trend = 0

if L and not S 
    trend := 1

if S 
    trend := -1

//------------------//

//SD trend
Lcol                = score > u3
Scol                = score < d3

var SD = 0

if Lcol and not Scol 
    SD := 1

if Scol 
    SD := -1
    
//              ╔══════════════════════════╗              //
//              ║          COLORS          ║              //
//              ╚══════════════════════════╝              //

[color_up, color_dn]=  switch   Col
    "SciFi"         => [#00FFFF, #FF00FF]
    "Mythic"        => [#00D1FF, #FF006E]
    "Quantum Pulse" => [#39FF14, #FF1744]
    "Sunny"         => [#ffd93d, #ff6bcb]
    "Sunset"        => [#ffbb00, #E10075] 
    "Washed Out"    => [#5ffae0, #c22ed0]
    "Ultra Violet"  => [#9618f7, #ff0078]
    "Monochrom"     => [#dee2e6, #495057]
    "Ocean"         => [#18fae3, #249bfd]
    "Ice"           => [#00e4ff, #ffffff]
    "Matrix"        => [#00FF41, #0D7377]
    "Royal"         => [#8A2BE2, #FFD700]
    "Void"          => [#00FFB2, #9500FF]

    
    

color col_up0       = color_up
color col_up25      = color.new(color_up, 25)
color col_up50      = color.new(color_up, 50)
color col_up65      = color.new(color_up, 65)
color col_up75      = color.new(color_up, 75)
color col_up90      = color.new(color_up, 90)
color col_dn0       = color_dn
color col_dn25      = color.new(color_dn, 25)
color col_dn50      = color.new(color_dn, 50)
color col_dn65      = color.new(color_dn, 65)
color col_dn75      = color.new(color_dn, 75)
color col_dn90      = color.new(color_dn, 90)

//Boolean options
barswitch           = input.bool(true,          "Barcolor On/Off",                                  group = "Visual Settings")
show_sig            = input.bool(true,          "Show Signal",                                      group = "Visual Settings")

//Color Conditions
barcolor            = trend >  0 ? col_up50 : trend < 0 ?  col_dn50 : na
sdCOL1              = SD < 0 ? color.new(color.gray, 100) : SD > 0  ? col_up65 : na
sdCOL2              = SD < 0 ? col_dn65 : SD > 0  ? color.new(color.gray, 100) : na
chartCol            = barswitch ? barcolor : na

//              ╔══════════════════════════╗              //
//              ║           PLOTS          ║              //
//              ╚══════════════════════════╝              //

// RSI Plot
rsi_p               = plot(score, linewidth = 2, color = trend > 0 ? col_up0 : trend < 0 ?  col_dn25 : na)

// Median RSI For Loop and SD
sd3up               = plot(u3, color = sdCOL1, linewidth = 1, style = plot.style_stepline_diamond)
sd3dn               = plot(d3, color = sdCOL2, linewidth = 1, style = plot.style_stepline_diamond)

//Zone and Line Plots
U1                  = plot(1+(b-a), color = col_up0, style = plot.style_circles, linewidth = 1)
L1                  = plot((a-b)-1, color = col_dn0, style = plot.style_circles, linewidth = 1)

Lth                 = plot(longTH, color = col_up65, style = plot.style_circles, linewidth = 1)
Sth                 = plot(shortTH, color = col_dn65, style = plot.style_circles, linewidth = 1)

//Plot On-Chart Signals
plotshape(show_sig and trend == 1 and trend[1] != 1 ? score : na, 
     title="Long Signal", 
     location=location.belowbar, style=shape.triangleup, size=size.tiny,
     color = col_up25, textcolor = col_up25,
     force_overlay = true
 )
 
plotshape(show_sig and trend == -1 and trend[1] != -1 ? score : na, 
     title="Cash Signal", 
     location=location.abovebar, style=shape.triangledown, size=size.tiny, 
     color = col_dn25, textcolor = col_dn25,
     force_overlay = true
 )

// Chart color
plotcandle(open, high, low, close, 
     'BarColor', 
     color = chartCol, bordercolor = chartCol, wickcolor = chartCol, 
     force_overlay = true
 )

//              ╔══════════════════════════╗              //
//              ║          ALERTS          ║              //
//              ╚══════════════════════════╝              //

// Long (Bullish) Condition Alert.
alertcondition(trend > 0,  title = "RSI For Loop Long",  message = "RSI For Loop is trending on: {{exchange}}:{{ticker}}")
alertcondition(Lcol,  title = "RSI For Loop increasing strength",  message = "RSI For Loop increasing strength on: {{exchange}}:{{ticker}}")

// Short/Cash (Bearish) Condition Alert.
alertcondition(trend < 0,  title = "RSI For Loop Short",  message = "RSI For Loop is short on: {{exchange}}:{{ticker}}")
alertcondition(Scol,  title = "RSI For Loop decreasing strength",  message = "RSI For Loop decreasing strength on: {{exchange}}:{{ticker}}")
//Love life:)

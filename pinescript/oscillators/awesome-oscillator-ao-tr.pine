// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Tiagorocha1989
//                              ████████╗ ████████╗  ████████╗  ███████╗   ██████╗        ██████╗   ██████╗   ██████╗ ██╗   ██╗ ████████╗
//                              ╚══██╔══╝ ╚══██╔══╝  ██╔═══██║  ██╔════╝  ██╔═══██╗       ██╔══██╗ ██╔═══██╗ ██╔════╝ ██║   ██║ ██╔═══██║
//                                 ██║       ██║     ████████║  ██║ ███║  ██║   ██║       ██████╔╝ ██║   ██║ ██║      ████████║ ████████║
//                                 ██║       ██║     ██╔═══██║  ██║  ██║  ██║   ██║       ██╔══██╗ ██║   ██║ ██║      ██║   ██║ ██╔═══██║
//                                 ██║    ████████╗  ██║   ██║  ███████║  ╚██████╔╝       ██║  ██║ ╚██████╔╝ ╚██████╗ ██║   ██║ ██║   ██║
//@version=6
indicator('Awesome Oscillator - AO | TR', 'AO | TR →', false)
//╔════════════════════════╗
//║ Choose Moving Averages ║
//╚════════════════════════╝
ma(source, length, type) =>
     type == 'EMA' ? ta.ema(source, length) :
     type == 'SMA' ? ta.sma(source, length) :
     type == 'RMA' ? ta.rma(source, length) :
     type == 'WMA' ? ta.wma(source, length) :
     type == 'VWMA' ? ta.vwma(source, length) :
     type == 'HMA' ? ta.hma(source, length) :
     na
//╔════════════════════════╗
//║     INPUT SETTINGS     ║
//╚════════════════════════╝
Color_Mode =    input.string('Classic', 'Color Choice', group = 'Color', options = ['Classic', 'Modern', 'Robust', 'Accented', 'Monochrome', 'Moderate', 'Aqua', 'Cosmic'])
Bool_AO =       input.bool(true,'', group='AO Setting', inline = 'AO')
Length_AO_1 =   input.int(22, title = 'Length 1º AO', group='AO Setting', inline = 'AO')
Length_AO_2 =   input.int(49, title = 'Length 2º AO', group='AO Setting', inline = 'AO')
Source_AO =     input(close, title = '', group='AO Setting', inline = '2AO')
MA_AO =         input.string('EMA', '', group='AO Setting', inline = '2AO', options=['EMA', 'SMA', 'RMA', 'WMA', 'VWMA', 'HMA'])
Signal_AO =     input.string('AO MA', 'Entry/Exit Signal', group='AO Setting', options = ['0', 'AO MA'],tooltip = 'Choose Entry/Exit Point')
Length_MA_AO =  input.int(730, title = 'Length MA', group='AO MA', inline = 'MA AO')
Source_MA_AO =  input.string('EMA', '', group='AO MA', inline = 'MA AO', options=['EMA', 'SMA', 'RMA', 'WMA', 'VWMA', 'HMA'], tooltip = 'Moving Averages')
//╔════════════════════════╗
//║      Calculation       ║
//╚════════════════════════╝
AO = ma(Source_AO,Length_AO_1,MA_AO) - ma(Source_AO,Length_AO_2,MA_AO)
AO_MA =  ma(AO, Length_MA_AO, Source_MA_AO)
float Entry_AO = Signal_AO == 'AO MA' ? AO_MA : 0.0
float AO_Adjusted = Signal_AO == 'AO MA' ? AO - AO_MA : AO
isBull_AO = Bool_AO ? (AO > Entry_AO) : (AO_Adjusted > 0)
isBear_AO = Bool_AO ? (AO < Entry_AO) : (AO_Adjusted < 0)
Bull_AO = Bool_AO ? ta.crossover(AO, Entry_AO) : ta.crossover(AO_Adjusted, 0)
Bear_AO = Bool_AO ? ta.crossunder(AO, Entry_AO) : ta.crossunder(AO_Adjusted, 0)
//╔════════════════════════╗
//║         Color          ║
//╚════════════════════════╝
[UpC, DnC] = switch Color_Mode
    'Classic' =>    [#008800, #ff0000]
    'Modern' =>     [#ffffff, #b721ff]
    'Robust' =>     [#ffbb00, #770737]
    'Accented' =>   [#8c5cf7, #e83e8c]
    'Monochrome' => [#e9ecef, #495057]
    'Moderate' =>   [#43a047, #e53935]
    'Aqua' =>       [#00a8e8, #f18f01]
    'Cosmic' =>     [#e83e8c, #6f2da8]
//╔════════════════════════╗
//║          Plot          ║
//╚════════════════════════╝
Plot_AO =       plot(Bool_AO ? AO : na, title = "AO Line", color = (Bool_AO ? (AO > Entry_AO ? color.new(UpC, 20) : color.new(DnC, 20)) : na), linewidth = 2)
Plot_AO_Bar =   plot(Bool_AO ? na : AO_Adjusted, title = "AO Columns", color = (Bool_AO ? na : (AO_Adjusted > 0 ? color.new(UpC, 20) : color.new(DnC, 20))), linewidth = 2, style = plot.style_columns)
Plot_Entry_AO = plot(Bool_AO ? Entry_AO : 0, title = "Signal Line", color = color.yellow)
fill(Plot_AO, Plot_Entry_AO, 7000, Entry_AO, top_color = color.new(UpC, 0),    bottom_color = color.new(UpC, 100), title = 'Overbought Fill')
fill(Plot_AO, Plot_Entry_AO, Entry_AO, -7000, top_color = color.new(DnC, 100), bottom_color = color.new(DnC, 0),    title = 'Oversold Fill')
fill(Plot_AO, Plot_Entry_AO, AO < Entry_AO ? AO : Entry_AO, AO < Entry_AO ? Entry_AO : AO, AO < Entry_AO ? DnC : #00000000, AO < Entry_AO ? #00000000 : UpC)
plotcandle(open, high, low, close, 'Bar Color', isBull_AO ? UpC : isBear_AO ? DnC : na, isBull_AO ? UpC : isBear_AO ? DnC : na, bordercolor = isBull_AO ? UpC : isBear_AO ? DnC : na, force_overlay = true)
plotshape(Bull_AO, 'LONG' , shape.triangleup  , location.belowbar, UpC,    0, size = size.small, force_overlay = true)
plotshape(Bear_AO, 'SHORT', shape.triangledown, location.abovebar, DnC,    0, size = size.small, force_overlay = true)
//╔════════════════════════╗
//║          VIEW          ║
//╚════════════════════════╝
var table Table_AO = table.new(position.middle_right, 1, 1, border_width = 1)
var label AOLabel = na
if barstate.islast
    table.cell(Table_AO, 0, 0, text = isBull_AO ? '⬆️ Bullish' : '⬇️ Bearish', text_color = isBull_AO ? UpC : DnC,text_size = size.huge)
    if not na(AOLabel)
        label.delete(AOLabel)
    float label_value = Bool_AO ? AO : AO_Adjusted
    string label_text = Bool_AO ? str.tostring(AO, '#.##') : 'StDev : ' + str.tostring(AO_Adjusted, '#.##')
    color label_color = Bool_AO ? 
         (AO > Entry_AO ? color.new(UpC, 20) : AO < Entry_AO ? color.new(DnC, 20) : color.gray) : 
         (AO_Adjusted > 0 ? color.new(UpC, 20) : AO_Adjusted < 0 ? color.new(DnC, 20) : color.gray)
    AOLabel := label.new(bar_index + 5, label_value, text = label_text, color = label_color, style = label.style_label_left, size = size.large, textcolor = #000000)
//╔════════════════════════╗
//║         Alerts         ║
//╚════════════════════════╝
alertcondition(Bull_AO, title= 'LONG', message= '{{ticker}} LONG')
alertcondition(Bear_AO, title= 'SHORT', message= '{{ticker}} SHORT')

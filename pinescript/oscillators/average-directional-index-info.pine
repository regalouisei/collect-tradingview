// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Jawortrader

//@version=6

// Dane wejściowe
indicator("Average Directional Index info", shorttitle="ADX info", format=format.price, precision=2)
adxlen = input(14, title="Wygładzanie")
dilen = input(14, title="Długość wskaźnika")
trendPeriod = input(10, title="Długość trendu", tooltip = "odniesienie do średniej z tolerancją ±5%" )

// Obliczenia
dirmov(len) =>
    up = ta.change(high)
    down = -ta.change(low)
    plusDM = na(up) ? na : (up > down and up > 0 ? up : 0)
    minusDM = na(down) ? na : (down > up and down > 0 ? down : 0)
    truerange = ta.rma(ta.tr, len)
    plus = fixnan(100 * ta.rma(plusDM, len) / truerange)
    minus = fixnan(100 * ta.rma(minusDM, len) / truerange)
    [plus, minus]
adx(dilen, adxlen) =>
    [plus, minus] = dirmov(dilen)
    sum = plus + minus
    adx = 100 * ta.rma(math.abs(plus - minus) / (sum == 0 ? 1 : sum), adxlen)
sig = adx(dilen, adxlen)

// Wykres
plot(sig, color=color.purple, title="ADX")

// Analiza trendu (średnia krocząca)
maADX = ta.sma(sig, trendPeriod)
isRising = sig > 1.05 * maADX
isFalling = sig < 0.95 * maADX

// Strzałka do tabeli
trendArrow = isRising ? " ↑" : isFalling ? " ↓" : " ⮆"

// Tabela informacyjna
var table infoTable = table.new(position.top_right, 2, 1, border_width=0, bgcolor=color(na))
table.cell(infoTable, 0, 0, text="ADX (" + str.tostring(adxlen)+")", text_color=color.new(color.black, 0), text_size=size.normal, bgcolor=color(na))
table.cell(infoTable, 1, 0, text=str.tostring(sig, format.mintick)+trendArrow, 
         text_color=isRising ? color.new(color.green, 0) : isFalling ? color.new(color.red, 0) : color.new(color.gray, 0), 
         text_size=size.normal, 
         bgcolor=color(na))

// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © AquaTickTrader

//@version=6
//──────────────────────────────────────────────────────────────────────────────
//  [TL5 MACD] Moving Average Convergence Divergence
//──────────────────────────────────────────────────────────────────────────────
//  Author      : AquaTickTrader
//  Version     : 1.0
//  Pine Script : v6
//──────────────────────────────────────────────────────────────────────────────
//
//  OVERVIEW
//  This is a structurally enhanced MACD that integrates momentum,
//  RSI bias, and adaptive trend filtering.
//
//  Enhancements vs default MACD:
//  • RSI(14) 50-level bias background
//  • WMA-smoothed RSI momentum confirmation
//  • ALMA(30) adaptive trend filter
//  • SMA(8) short-term momentum alignment
//  • Dynamic MACD & Signal line coloring
//
//  Core MACD math remains standard.
//──────────────────────────────────────────────────────────────────────────────

indicator("[TL5 MACD] Moving Average Convergence Divergence", timeframe = "", timeframe_gaps = true)

//──────────────────────────────────────────────────────────────────────────────
// RSI (14) – Market Momentum Bias
//──────────────────────────────────────────────────────────────────────────────

rsi = ta.rsi(close, 14)
wmarsi = ta.wma(rsi, 14)

// Background Bias (RSI 50 Level)
bgcolor(
     rsi > 50 ? color.new(color.lime, 80) :
     rsi < 50 ? color.new(color.red, 80)  :
     na
)

// Background Bias (RSI 50 Level) => Optional to force_overlay
bgcolor(
     rsi > 50 ? color.new(color.lime, 80) :
     rsi < 50 ? color.new(color.red, 80)  :
     na, force_overlay = true, display = display.none, title = "bgcolor on main chart"
)

//──────────────────────────────────────────────────────────────────────────────
// Moving Averages (Structure + Momentum)
//──────────────────────────────────────────────────────────────────────────────

// Short-term momentum
sma8 = ta.sma(close, 8)

// Adaptive trend filter
alma30 = ta.alma(close, 30, 0.85, 8)

//──────────────────────────────────────────────────────────────────────────────
// MACD Inputs
//──────────────────────────────────────────────────────────────────────────────

float  sourceInput  = input.source(close, "Source")
int    fastLenInput = input.int(12, "Fast length",   minval = 1)
int    slowLenInput = input.int(26, "Slow length",   minval = 1)
int    sigLenInput  = input.int(9,  "Signal length", minval = 1)

string oscTypeInput = input.string("EMA", "Oscillator MA type", ["EMA", "SMA"], display = display.none)
string sigTypeInput = input.string("EMA", "Signal MA type",     ["EMA", "SMA"], display = display.none)

//──────────────────────────────────────────────────────────────────────────────
// MA Function
//──────────────────────────────────────────────────────────────────────────────

ma(float source, int length, simple string maType) =>
    switch maType
        "EMA" => ta.ema(source, length)
        "SMA" => ta.sma(source, length)

//──────────────────────────────────────────────────────────────────────────────
// MACD Calculation (Standard Formula)
//──────────────────────────────────────────────────────────────────────────────

float maFast = ma(sourceInput, fastLenInput, oscTypeInput)
float maSlow = ma(sourceInput, slowLenInput, oscTypeInput)

float macd   = maFast - maSlow
float signal = ma(macd, sigLenInput, sigTypeInput)
float hist   = macd - signal

// Histogram Color Logic
color hColor =
     hist >= 0 ?
         hist > hist[1] ? #26a69a : #b2dfdb :
         hist > hist[1] ? #ffcdd2 : #ff5252

//──────────────────────────────────────────────────────────────────────────────
// Zero Line (Trend State via ALMA slope)
//──────────────────────────────────────────────────────────────────────────────

plot(0,
     title = "Zero Line",
     color = alma30 > alma30[1] ? color.blue : color.red,
     linewidth = 2, linestyle = plot.linestyle_dotted)


//──────────────────────────────────────────────────────────────────────────────
// MACD & Signal Plot Coloring Logic
//──────────────────────────────────────────────────────────────────────────────

// MACD Line Color Logic
macdColor =
     sma8 > sma8[1] and sma8 > alma30 and wmarsi > wmarsi[1] ? color.blue :
     sma8 < sma8[1] and sma8 < alma30 and wmarsi < wmarsi[1] ? color.red :
     sma8 < sma8[1] and sma8 < alma30 ? color.yellow :
     sma8 > sma8[1] ? color.green :
     sma8 < sma8[1] ? color.fuchsia :
     color.gray

// Signal Line Color Logic
signalColor =
     wmarsi > wmarsi[1] and sma8 > alma30 ? color.green :
     wmarsi < wmarsi[1] and sma8 < alma30 ? color.red :
     sma8 > sma8[1] and sma8 > alma30 ? color.white :
     sma8 < sma8[1] and sma8 < alma30 ? color.yellow :
     color.gray

//──────────────────────────────────────────────────────────────────────────────
// Plots 
//──────────────────────────────────────────────────────────────────────────────

plot(hist,
     title = "Histogram",
     color = hColor,
     style = plot.style_columns,
     display = display.none)

plot(macd,
     title = "MACD",
     color = macdColor,
     linewidth = 3, linestyle = plot.linestyle_dotted)

plot(signal,
     title = "Signal Line",
     color = signalColor,
     linewidth = 3, linestyle = plot.linestyle_solid)
//==================================================

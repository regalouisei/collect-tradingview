// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © theValleyMan

//@version=6
indicator("AlgoYields - A", overlay = true, max_labels_count=500)

// ─────────────────────────────────────────────────────────────
// ────────────────────────
// Plot EMAs
// ────────────────────────
// ─────────────────────────────────────────────────────────────

emaLength80  = input.int ( 80,  "Shorter Macro EMA" )
emaLength200 = input.int ( 200, "Longer Macro EMA"  )

emaValue5   = ta.ema ( close, 5            )
emaValue10  = ta.ema ( close, 10           )
emaValue17  = ta.ema ( close, 17           )
emaValue25  = ta.ema ( close, 25           )
emaValue30  = ta.ema ( close, 30           )
emaValue40  = ta.ema ( close, 40           )
emaValue50  = ta.ema ( close, 50           )
emaValue60  = ta.ema ( close, 60           )
emaValue70  = ta.ema ( close, 70           )
emaValue80  = ta.ema ( close, emaLength80  )
emaValue200 = ta.ema ( close, emaLength200 )

plot(series=emaValue10,  title = "EMA 10",            linewidth = 2, color = emaValue10  > emaValue17  ? color.new(#5E5C5D, 80) : color.new(#473537, 80))
plot(series=emaValue17,  title = "EMA 17",            linewidth = 2, color = emaValue17  > emaValue25  ? color.new(#5E5C5D, 80) : color.new(#4E4D5C, 80))
plot(series=emaValue25,  title = "EMA 25",            linewidth = 2, color = emaValue25  > emaValue30  ? color.new(#8C898B, 80) : color.new(#5B5A6B, 80))
plot(series=emaValue30,  title = "EMA 30",            linewidth = 2, color = emaValue30  > emaValue40  ? color.new(#8C898B, 80) : color.new(#6A697D, 80))
plot(series=emaValue40,  title = "EMA 40",            linewidth = 2, color = emaValue40  > emaValue50  ? color.new(#ADAAAB, 80) : color.new(#75748A, 80))
plot(series=emaValue50,  title = "EMA 50",            linewidth = 2, color = emaValue50  > emaValue60  ? color.new(#ADAAAB, 80) : color.new(#84839C, 80))
plot(series=emaValue60,  title = "EMA 60",            linewidth = 2, color = emaValue60  > emaValue70  ? color.new(#DBD7D9, 80) : color.new(#9190AB, 80))
plot(series=emaValue70,  title = "EMA 70",            linewidth = 2, color = emaValue70  > emaValue80  ? color.new(#FAF5F7, 60) : color.new(#5E5C5D, 60))
plot(series=emaValue80,  title = "Shorter Macro EMA", linewidth = 2, color = emaValue80  > emaValue200 ? color.new(#d06d2c,  0) : color.new(#2e34e5,  0))
plot(series=emaValue200, title = "Longer Macro EMA",  linewidth = 2, color = emaValue200 > emaValue5   ? color.new(#1215db, 20) : color.new(#d26f2d, 20))

// ─────────────────────────────────────────────────────────────
// ────────────────────────
// Create Background colors from trading sessions
// ────────────────────────
// ─────────────────────────────────────────────────────────────

// ===== Calendar helpers
f_is_leap_year() =>
    if (year % 4) != 0
        false
    else if (year % 100) != 0
        true
    else
        (year % 400) == 0

f_get_last_day() =>
    (month == 1 or month == 3 or month == 5 or month == 7 or month == 8 or month == 10 or month == 12) ? 31 :
     (month == 4 or month == 6 or month == 9 or month == 11) ? 30 :
     (f_is_leap_year() ? 29 : 28)  // February

// ===== Week-of-month flags (simple buckets)
is_first_week  = dayofmonth <= 7
is_second_week = dayofmonth >= 8  and dayofmonth <= 14
is_third_week  = dayofmonth >= 15 and dayofmonth <= 21

// ===== Quarter index from current month (0..3)
qtrIndex =
     (month >= 1  and month <= 3 ) ? 0 :
     (month >= 4  and month <= 6 ) ? 1 :
     (month >= 7  and month <= 9 ) ? 2 : 3

// ===== Transparency by week helper
weekTransp(_w1, _w2, _w3) => _w1 ? 82 : _w2 ? 85 : _w3 ? 88 : 91

// ===== Session color palettes (per QUARTER)
// "PastelDeep"
var color[] pre_PastelDeep     = array.from(color.rgb(150,250,255), color.rgb(209,255,163), color.rgb(255,217,122), color.rgb(125,125,255))
var color[] mkt_PastelDeep     = array.from(color.rgb(  0,242,255), color.rgb(128,255,  0), color.rgb(254,182,  0), color.rgb(  0,  0,255))
var color[] post_PastelDeep    = array.from(color.rgb(  0,148,156), color.rgb( 88,177,  0), color.rgb(201,144,  0), color.rgb(  0,  0,162))

// "Mono Gray/Blue"
var color[] pre_MonoGrayBlue   = array.from(color.rgb(224,224,224), color.rgb(200,210,230), color.rgb(230,215,200), color.rgb(205,220,255))
var color[] mkt_MonoGrayBlue   = array.from(color.rgb( 25, 25, 25), color.rgb( 50, 70,110), color.rgb( 70, 60, 50), color.rgb( 20, 50,120))
var color[] post_MonoGrayBlue  = array.from(color.rgb(120,120,120), color.rgb( 80,100,140), color.rgb(110, 90, 70), color.rgb( 60, 85,150))

// “Sunset”
var color[] pre_Sunset         = array.from(color.rgb(255,220,180), color.rgb(255,228,190), color.rgb(255,248,230), color.rgb(225,205,255))
var color[] mkt_Sunset         = array.from(color.rgb(255,115, 80), color.rgb(255,155, 60), color.rgb(255,190, 70), color.rgb(230, 90,255))
var color[] post_Sunset        = array.from(color.rgb(150, 40, 30), color.rgb(175, 70, 50), color.rgb(130, 85, 40), color.rgb(120, 60,170))

// “OceanRoyal”
var color[] pre_OceanRoyal     = array.from(color.rgb(175,235,255), color.rgb(230,255,255), color.rgb(200,245,215), color.rgb(250,215,255))
var color[] mkt_OceanRoyal     = array.from(color.rgb(  0,105,145), color.rgb(135,230,250), color.rgb(110, 55,190), color.rgb( 70,105,170))
var color[] post_OceanRoyal    = array.from(color.rgb(  0, 80,115), color.rgb(105,175,205), color.rgb( 85, 60,130), color.rgb(105,130,170))

// “Red/White/Green/Blue”
var color[] pre_RedWhiteGreen  = array.from(color.rgb(240,240,240), color.rgb(240,240,240), color.rgb(240,240,240), color.rgb(240,240,240))
var color[] mkt_RedWhiteGreen  = array.from(color.rgb(200, 30, 30), color.rgb(240,240,240), color.rgb( 30,180, 70), color.rgb( 30,120,220))
var color[] post_RedWhiteGreen = array.from(color.rgb(150, 20, 20), color.rgb(200,200,200), color.rgb( 20,120, 50), color.rgb( 20, 80,160))

// “Heatmap”
var color[] pre_Heatmap        = array.from(color.rgb( 27,120,210), color.rgb(120,180,250), color.rgb(240,240,240), color.rgb(255,220,160))
var color[] mkt_Heatmap        = array.from(color.rgb(  5, 31, 64), color.rgb( 27,120,210), color.rgb(255,165,  0), color.rgb(200,  0,  0))
var color[] post_Heatmap       = array.from(color.rgb( 20, 60,110), color.rgb( 40,140,220), color.rgb(230,150,  0), color.rgb(160,  0,  0))

// "PastelSoft"
var color[] pre_PastelSoft     = array.from(color.rgb(210,240,255), color.rgb(215,255,225), color.rgb(255,240,210), color.rgb(220,220,255))
var color[] mkt_PastelSoft     = array.from(color.rgb(120,200,255), color.rgb(140,240,170), color.rgb(255,210,120), color.rgb(140,140,255))
var color[] post_PastelSoft    = array.from(color.rgb( 90,160,210), color.rgb(100,180,140), color.rgb(210,160, 90), color.rgb(100,100,200))

// ===== Palette selector
paletteName = input.string( "Pastel Deep", "Session (Background) Palette", options = ["Pastel Soft","Pastel Deep","Mono Gray/Blue","Sunset","Red-White-Green-Blue","Ocean Royal","Heatmap"] )

preArr =
     paletteName == "Pastel Deep"    ? pre_PastelDeep     :
     paletteName == "Mono Gray/Blue" ? pre_MonoGrayBlue   :
     paletteName == "Sunset"         ? pre_Sunset         :
     paletteName == "Red-White-Green-Blue"  ? pre_RedWhiteGreen  :
     paletteName == "Ocean Royal"    ? pre_OceanRoyal     :
     paletteName == "Heatmap"        ? pre_Heatmap        :
                                       pre_PastelSoft

mktArr =
     paletteName == "Pastel Deep"    ? mkt_PastelDeep     :
     paletteName == "Mono Gray/Blue" ? mkt_MonoGrayBlue   :
     paletteName == "Sunset"         ? mkt_Sunset         :
     paletteName == "Red-White-Green-Blue"  ? mkt_RedWhiteGreen  :
     paletteName == "Ocean Royal"    ? mkt_OceanRoyal     :
     paletteName == "Heatmap"        ? mkt_Heatmap        :
                                       mkt_PastelSoft

postArr =
     paletteName == "Pastel Deep"    ? post_PastelDeep    :
     paletteName == "Mono Gray/Blue" ? post_MonoGrayBlue  :
     paletteName == "Sunset"         ? post_Sunset        :
     paletteName == "Red-White-Green-Blue"  ? post_RedWhiteGreen :
     paletteName == "Ocean Royal"    ? post_OceanRoyal    :
     paletteName == "Heatmap"        ? post_Heatmap       :
                                       post_PastelSoft

// ===== Base colors for *this* quarter
basePre  = array.get(preArr,  qtrIndex)
baseMkt  = array.get(mktArr,  qtrIndex)
basePost = array.get(postArr, qtrIndex)

// ===== Apply week-based transparency
trp = weekTransp(is_first_week, is_second_week, is_third_week)

// ===== Final session colors
premarket  = color.new(basePre,  trp)
market     = color.new(baseMkt,  trp)
postmarket = color.new(basePost, trp)
color notrading = na

// ===== Session detection + background
timeinrange(res, sess) => time(res, sess) != 0

sessioncolor =
     timeinrange("30", "2200-0600:23456") ? postmarket :
     timeinrange("30", "0600-1400:23456") ? premarket  :
     timeinrange("30", "1400-2200:2345")  ? market     :
     timeinrange("30", "1400-2100:6")     ? market     : notrading

bgcolor(sessioncolor)


// ─────────────────────────────────────────────────────────────
// ────────────────────────
// Create and Assign Bar Colors
// ────────────────────────
// ─────────────────────────────────────────────────────────────

// ===== Inputs
barColorPalette = input.string ("Heatmap", "Bar Palette",                                   options = ["Heatmap", "Mono Gold","Red→White→Green","Ocean Royal","Gray-on-White","Twilight"] )
alphaPct        = input.int    (0,         "Bar Transparency (0 = solid, 100 = invisible)", minval=0, maxval=100                                                                          )
rsiLen          = input.int    (10,        "RSI Length (for coloring bars)"                                                                                                               )

// ===== RSI levels to apply palette colors
var float[] lvlsCommon = array.from(0.0, 25, 35, 50.0, 65, 75, 100.0)

// 1) Black → White → Gold (mono + metallic highlight)
var color[] colsMonoGold = array.from(
    color.rgb(  0,   0,   0),
    color.rgb( 48,  48,  48),
    color.rgb(112, 112, 112),
    color.rgb(185, 185, 185),
    color.rgb(243, 209, 100),
    color.rgb(232, 184, 27),
    color.rgb(193, 151, 0)
)

// 2) Deep Red → White → Deep Green (bear↔bull classic)
var color[] colsRedWhiteGreen = array.from(
    color.rgb(139,   0,   0),
    color.rgb(200,  30,  30),
    color.rgb(220,  20,  60),
    color.rgb(245, 245, 245),
    color.rgb(120, 200, 140),
    color.rgb( 60, 179, 113),
    color.rgb(  0, 100,   0)
)

// 3) Ocean: Deep Teal → White → Royal Purple
var color[] colsOceanRoyal = array.from(
    color.rgb(  0,  64,  80),
    color.rgb(  0,  96, 120),
    color.rgb(  0, 128, 160),
    color.rgb(185, 185, 185),
    color.rgb(150, 120, 190),
    color.rgb(120,  90, 170),
    color.rgb( 60,  35, 110)
)

// 4) Heatmap: Navy → Sky → White → Orange → Red
var color[] colsHeatmap = array.from(
    color.rgb(  5,  31,  64),
    color.rgb( 17,  80, 150),
    color.rgb( 27, 120, 210),
    color.rgb(185, 185, 185),
    color.rgb(255, 165,   0),
    color.rgb(235,  80,   0),
    color.rgb(200,   0,   0)
)

// 5) Grays for LIGHT backgrounds
var color[] colsGrayOnWhite = array.from(
    color.rgb(  0,   0,   0),
    color.rgb( 48,  48,  48),
    color.rgb( 96,  96,  96),
    color.rgb(122, 122, 122),
    color.rgb(166, 166, 166),
    color.rgb(210, 210, 210),
    color.rgb(255, 255, 255)
)

// 6) Twilight: Indigo → Violet → White → Peach
var color[] colsTwilight = array.from(
    color.rgb( 25,  25, 112),
    color.rgb( 60,  45, 130),
    color.rgb( 90,  60, 150),
    color.rgb(210, 210, 210),
    color.rgb(255, 190, 150),
    color.rgb(240, 120,  70),
    color.rgb(200,  70,  35)
)

/////
// Pick colors by palette name
/////
getPaletteColors(_name) =>
    switch _name
        "Heatmap"         => colsHeatmap
        "Mono Gold"       => colsMonoGold
        "Red→White→Green" => colsRedWhiteGreen
        "Ocean Royal"     => colsOceanRoyal
        "Gray-on-White"   => colsGrayOnWhite
        => colsTwilight

/////
// Build colors from RSI and palette selection
/////

// ===== Helpers
clampAgain(val, lo, hi) => math.max(lo, math.min(val, hi))

safeDiv(num, den) => den == 0.0 ? 0.0 : num / den

lerp(a, b, t) => a + (b - a) * t

// ===== Interpolate between two colors c1→c2 by t in [0..1]
lerpColor(c1, c2, t) =>
    r = int(lerp(color.r(c1), color.r(c2), t))
    g = int(lerp(color.g(c1), color.g(c2), t))
    b = int(lerp(color.b(c1), color.b(c2), t))
    color.rgb(r, g, b)

// ===== Build
colorFromStops(v, minVal, maxVal, levels, cols) =>
    _v = clampAgain(v, minVal, maxVal)
    n  = array.size(levels)
    // default to last color
    result = array.get(cols, n - 1)
    for i = 0 to n - 2
        l0 = array.get(levels, i)
        l1 = array.get(levels, i + 1)
        if _v >= l0 and _v <= l1
            c0 = array.get(cols, i)
            c1 = array.get(cols, i + 1)
            tDen = math.max(l1 - l0, 1e-10)
            t  = safeDiv(_v - l0, tDen)
            result := lerpColor(c0, c1, t)
    result

barPaletteSelected = getPaletteColors(barColorPalette)

RSI = ta.rsi(close, rsiLen)

barColorFromRSI = colorFromStops(RSI, 0.0, 100.0, lvlsCommon, barPaletteSelected)

/////
// Apply Bar Color
/////

// ===== Apply global transparency
barColor = color.new(barColorFromRSI, alphaPct)

barcolor(barColor)

// ─────────────────────────────────────────────────────────────
// ─────────────
// Price Channel
// ─────────────
// ─────────────────────────────────────────────────────────────

channelDays        = input.int   ( 7,     "Channel Days",                                                       minval = 1 )
useClosesForBands  = input.bool  ( true,  "Use Closes for channel bands (tightest) - else HLC3 or Raw High/Low"            )
useHlc3ForBands    = input.bool  ( false, "Use HLC3 for channel bands (medium) - else Raw High/Low (widest)"               )

/////
// Compute bars per channelDays for current timeframe (fallback if needed)
/////
var int _DAYSEC = 24 * 60 * 60
_tfSec = timeframe.in_seconds(timeframe.period)
barsPerDay = na(_tfSec) ? 24 : math.max(1, int(math.round(_DAYSEC / _tfSec))) // assume 1h if unknown

chLen = math.max(1, channelDays * barsPerDay)

/////
// Base series to build bands: closes (tightest), HLC3 (medium), or raw highs/lows (widest)
/////
baseHiSrc = useClosesForBands ? close : (useHlc3ForBands ? hlc3 : high)
baseLoSrc = useClosesForBands ? close : (useHlc3ForBands ? hlc3 : low)

/////
// Rolling channel
/////
chHigh = ta.highest(baseHiSrc, chLen)
chLow  = ta.lowest(baseLoSrc,  chLen)

/////
// Plot channel lines
/////
plot(chHigh, "Channel High", color=color.new(#de9564, 30), linewidth=2)
plot(chLow,  "Channel Low",  color=color.new(#5c5ed5, 30), linewidth=2)


// ─────────────────────────────────────────────────────────────
// ───────────────────
// Range Breakout Dots
// ───────────────────
// ─────────────────────────────────────────────────────────────

intrabarBreak   = input.bool  ( false, "Breakout with any price - else require close above/below"                  )
bufferPct       = input.float ( 0.0,   "Breakout Buffer %",                                           step = 0.1   )
usePercentShift = input.bool  ( true,  "Shift Range Dots by price percent (or price ticks)"                        )
shiftTicks      = input.int   ( 1000,  "Range Dot price ticks shift",                                 step = 100   )
shiftPct        = input.float ( 0.005, "Range Dot price percent shift",                               step = 0.001 )

/////
// Prior channel + buffer
/////
priorHigh = chHigh[1]
priorLow  = chLow[1]
bufUp     = priorHigh * (1 + bufferPct / 100.0)
bufDown   = priorLow  * (1 - bufferPct / 100.0)

/////
// TOP shifts for range dot placement
/////
yTopPct   = high * (1.0 + shiftPct)
yTopTicks = high + syminfo.mintick * shiftTicks
yTop      = usePercentShift ? yTopPct : yTopTicks

/////
// BOTTOM shifts for range dot placement
/////
yBottomPct   = low * (1.0 - shiftPct)
yBottomTicks = low - syminfo.mintick * shiftTicks
yBottom      = usePercentShift ? yBottomPct : yBottomTicks

/////
// Build price series for when and where to draw range dots
/////
rangeBreakUp   = intrabarBreak ? high > bufUp : close > bufUp
rangeBreakDown = intrabarBreak ? low  < bufDown : close < bufDown

shortEMAabove200EMA = open[0] > emaValue200

breakDownLongCondition  = rangeBreakDown and shortEMAabove200EMA
breakUpLongCondition    = rangeBreakUp   and shortEMAabove200EMA

breakDownShortCondition = rangeBreakDown and not shortEMAabove200EMA
breakUpShortCondition   = rangeBreakUp   and not shortEMAabove200EMA

//
longDotsLow   = breakDownLongCondition  and not na(yBottom) ? yBottom : na
longDotsHigh  = breakUpLongCondition    and not na(yTop)    ? yTop    : na

shortDotsLow  = breakDownShortCondition and not na(yBottom) ? yBottom : na
shortDotsHigh = breakUpShortCondition   and not na(yTop)    ? yTop    : na

/////
// Plot range dots
/////
plot(longDotsLow,  title="Bullish Low Dot Fill",     style=plot.style_circles, linewidth=5, color=color.new(#15b700, 0))
plot(longDotsLow,  title="Bullish Low Dot Border",   style=plot.style_circles, linewidth=3, color=color.new(#0d6100, 0))

plot(longDotsHigh, title="Bullish High Dot Fill",    style=plot.style_circles, linewidth=5, color=color.new(#b7b100, 0))
plot(longDotsHigh, title="Bullish High Dot Border",  style=plot.style_circles, linewidth=3, color=color.new(#615e00, 0))

plot(shortDotsLow, title="Bearish Low Dot Fill",     style=plot.style_circles, linewidth=5, color=color.new(#b56700, 0))
plot(shortDotsLow, title="Bearish Low Dot Border",   style=plot.style_circles, linewidth=3, color=color.new(#5e2d00, 0))

plot(shortDotsHigh, title="Bearish High Dot Fill",   style=plot.style_circles, linewidth=5, color=color.new(#b50000, 0))
plot(shortDotsHigh, title="Bearish High Dot Border", style=plot.style_circles, linewidth=3, color=color.new(#5e0000, 0))


// ─────────────────────────────────────────────────────────────
// ──────
// Alerts
// ──────
// ─────────────────────────────────────────────────────────────

/////
// Range alerts
/////
bullishRangeLow  = breakDownLongCondition[0]  and not breakDownLongCondition[1]
bullishRangeHigh = breakUpLongCondition[0]    and not breakUpLongCondition[1]

bearishRangeLow  = breakDownShortCondition[0] and not breakDownShortCondition[1]
bearishRangeHigh = breakUpShortCondition[0]   and not breakUpShortCondition[1]

alertcondition(bullishRangeLow,  title="Bullish Range Low",  message="Bullish Range Low - {{ticker}} {{interval}} @ {{close}}")
alertcondition(bullishRangeHigh, title="Bullish Range High", message="Bullish Range High - {{ticker}} {{interval}} @ {{close}}")

alertcondition(bearishRangeLow,  title="Bearish Range Low",  message="Bearish Range Low - {{ticker}} {{interval}} @ {{close}}")
alertcondition(bearishRangeHigh, title="Bearish Range High", message="Bearish Range High - {{ticker}} {{interval}} @ {{close}}")

/////
// EMA alerts
/////
bullishCrossEMA200 = close[0] > emaValue200 and close[1] < emaValue200
bearishCrossEMA200 = close[0] < emaValue200 and close[1] > emaValue200

bullishCrossEMA80  = close[0] > emaValue80  and close[1] < emaValue80
bearishCrossEMA80  = close[0] < emaValue80  and close[1] > emaValue80

alertcondition(bullishCrossEMA200, title="Bullish Cross - EMA 200", message="Bullish Cross - EMA 200 - {{ticker}} {{interval}} @ {{close}}")
alertcondition(bearishCrossEMA200, title="Bearish Cross - EMA 200", message="Bearish Cross - EMA 200 - {{ticker}} {{interval}} @ {{close}}")

alertcondition(bullishCrossEMA80,  title="Bullish Cross - EMA 80",  message="Bullish Cross - EMA 80 - {{ticker}} {{interval}} @ {{close}}")
alertcondition(bearishCrossEMA80,  title="Bearish Cross - EMA 80",  message="Bearish Cross - EMA 80 - {{ticker}} {{interval}} @ {{close}}")

/////
// RSI alerts
/////
rsiStrongThreshold = input.float(61.1, "Strong RSI Threshold", step=0.1)
rsiWeakThreshold   = input.float(43.6, "Weak RSI Threshold",   step=0.1)

rsiWeakening      = RSI[0] <= rsiWeakThreshold   and RSI[1] > rsiWeakThreshold
rsiLosingWeakness = RSI[0] >= rsiWeakThreshold   and RSI[1] < rsiWeakThreshold

rsiStrengthening  = RSI[0] >= rsiStrongThreshold and RSI[1] < rsiStrongThreshold
rsiLosingStrnegth = RSI[0] <= rsiStrongThreshold and RSI[1] > rsiStrongThreshold

alertcondition(rsiWeakening,      title="RSI Weakening",       message="RSI is Weakening - {{ticker}} {{interval}} @ {{close}}")
alertcondition(rsiLosingWeakness, title="RSI Losing Weakness", message="RSI is Losing Weakness - {{ticker}} {{interval}} @ {{close}}")

alertcondition(rsiStrengthening,  title="RSI Strengthening",   message="RSI is Strengthening - {{ticker}} {{interval}} @ {{close}}")
alertcondition(rsiLosingStrnegth, title="RSI Losing Strength", message="RSI is Losing Strength - {{ticker}} {{interval}} @ {{close}}")

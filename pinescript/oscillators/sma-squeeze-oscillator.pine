// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Uncle_the_shooter

//@version=6
indicator("SMA Squeeze Oscillator", overlay = false, max_lines_count = 500)

// INPUTS

// SMA Parameters
len1 = input.int(6, "SMA 1 (used for Squeeze & Momentum)")
len2 = input.int(18, "SMA 2 (used for Squeeze & Momentum)")
len3 = input.int(26, "SMA 3 (used for Squeeze & Momentum)")

useSmaFilter = input.bool(true, "Enable SMA Filter")
smaFilterLen = input.int(50, "SMA Filter Length")

// ATR / Squeeze 
atrLen  = input.int(14, "ATR Length")
atrMult = input.float(0.8, "ATR Multiplier", step = 0.1)

// Momentum 
momentumSmoothing = input.int(3, "Momentum SMA Smoothing")

// Candle Settings 
enableCandleColor = input.bool(true, "Enable candle coloring (wave style)")

// Divergence 
calculateDivergence = input.bool(true, "Enable Divergence")
pivotLength         = input.int(2, "Pivot Length", minval = 1)

// COLORS 
bullColor     = input.color(#54b859, "Bullish color (green)")
bearColor     = input.color(#e06161, "Bearish color (red)")
squeezeColor  = input.color(#e9cd31, "Squeeze color (yellow)")

bullCrossColor  = input.color(#16b900, "Bull Cross color")
bearCrossColor  = input.color(color.rgb(223, 3, 3), "Bear Cross color")

firstLongColor  = input.color(#16b900, "1st after Squeeze LONG color")
firstShortColor = input.color(color.rgb(223, 3, 3), "1st after Squeeze SHORT color")

textColor = input.color(color.white, "Divergence Text Color")


// CALCULATIONS 
sma1 = ta.sma(close, len1)
sma2 = ta.sma(close, len2)
sma3 = ta.sma(close, len3)

smaFilter = ta.sma(close, smaFilterLen)

atr = ta.atr(atrLen)
maxSMA = math.max(sma1, math.max(sma2, sma3))
minSMA = math.min(sma1, math.min(sma2, sma3))
spread = maxSMA - minSMA

isSqueeze = spread < atr * atrMult

rawMomentum = (sma1 - sma2 + sma1 - sma3) / 2
momentum = ta.sma(rawMomentum, momentumSmoothing)

// COLORS 
colBullStrong = color.new(bullColor, 0)
colBullFade   = color.new(bullColor, 90)

colBearStrong = color.new(bearColor, 0)
colBearFade   = color.new(bearColor, 90)

sqStrong = color.new(squeezeColor, 0)
sqFade   = color.new(squeezeColor, 90)

// Zero line filter color
zeroBarColor = not useSmaFilter ? bullColor :
     close > smaFilter ? bullColor : bearColor

plot(0, "Zero Line Filter", color = zeroBarColor, linewidth = 3)

// MOMENTUM PLOT 
momPlot = plot(momentum, "Momentum", color = momentum >= 0 ? bullColor : bearColor, linewidth = 2)

p0 = plot(0, display = display.none)

// Normal trend fills (outside squeeze) 
fill(momPlot, p0,
     top_value    = not isSqueeze and momentum > 0 ? momentum : na,
     bottom_value = not isSqueeze and momentum > 0 ? 0 : na,
     top_color    = not isSqueeze and momentum > 0 ? colBullStrong : na,
     bottom_color = not isSqueeze and momentum > 0 ? colBullFade   : na)

fill(momPlot, p0,
     top_value    = not isSqueeze and momentum < 0 ? 0 : na,
     bottom_value = not isSqueeze and momentum < 0 ? momentum : na,
     top_color    = not isSqueeze and momentum < 0 ? colBearFade   : na,
     bottom_color = not isSqueeze and momentum < 0 ? colBearStrong : na)

// Squeeze fills 
fill(momPlot, p0, color = isSqueeze ? color.new(squeezeColor, 85) : na, title = "Squeeze Fill")

fill(momPlot, p0,
     top_value    = isSqueeze and momentum > 0 ? momentum : na,
     bottom_value = isSqueeze and momentum > 0 ? 0 : na,
     top_color    = isSqueeze and momentum > 0 ? sqStrong : na,
     bottom_color = isSqueeze and momentum > 0 ? sqFade   : na)

fill(momPlot, p0,
     top_value    = isSqueeze and momentum < 0 ? 0 : na,
     bottom_value = isSqueeze and momentum < 0 ? momentum : na,
     top_color    = isSqueeze and momentum < 0 ? sqFade   : na,
     bottom_color = isSqueeze and momentum < 0 ? sqStrong : na)

// CROSS SIGNALS 
bullCross = ta.crossover(momentum, 0) and (not useSmaFilter or close > smaFilter)
bearCross = ta.crossunder(momentum, 0) and (not useSmaFilter or close < smaFilter)

plotshape(bullCross ? momentum : na,
     title  = "Bull Cross",
     style  = shape.diamond,
     location = location.absolute,
     color  = bullCrossColor,
     size   = size.small)

plotshape(bearCross ? momentum : na,
     title  = "Bear Cross",
     style  = shape.diamond,
     location = location.absolute,
     color  = bearCrossColor,
     size   = size.small)

// FIRST BAR AFTER SQUEEZE 
squeezeWasOn = isSqueeze[1]
nowOutOfSqueeze = not isSqueeze and squeezeWasOn

trendUp   = not useSmaFilter or close > smaFilter
trendDown = not useSmaFilter or close < smaFilter

firstAfterSqueezeLong  = nowOutOfSqueeze and momentum > 0 and trendUp
firstAfterSqueezeShort = nowOutOfSqueeze and momentum < 0 and trendDown

plotshape(firstAfterSqueezeLong ? momentum : na,
     title  = "1st after Squeeze LONG",
     style  = shape.circle,
     location = location.absolute,
     color  = firstLongColor,
     size   = size.small)

plotshape(firstAfterSqueezeShort ? momentum : na,
     title  = "1st after Squeeze SHORT",
     style  = shape.circle,
     location = location.absolute,
     color  = firstShortColor,
     size   = size.small)


// WAVE CANDLES 
waveColor = isSqueeze   ? sqStrong :momentum > 0 ? colBullStrong :momentum < 0 ? colBearStrong : na

barcolor(enableCandleColor ? waveColor : na)

plotcandle(open, high, low, close,
     title       = "Wave Candles",
     color       = enableCandleColor ? waveColor : na,
     wickcolor   = enableCandleColor ? waveColor : na,
     bordercolor = enableCandleColor ? waveColor : na,
     force_overlay = true)


// DIVERGENCE 
lookbackRight = pivotLength
lookbackLeft  = pivotLength
rangeUpper    = 60
rangeLower    = 1

// pivots
pivotHighOsc = ta.pivothigh(momentum, lookbackLeft, lookbackRight)
pivotLowOsc  = ta.pivotlow (momentum, lookbackLeft, lookbackRight)

oscLBR = momentum[lookbackRight]

// previous pivots
prevOscLow   = ta.valuewhen(not na(pivotLowOsc),  oscLBR, 1)
prevPriceLow = ta.valuewhen(not na(pivotLowOsc),  low[lookbackRight], 1)

prevOscHigh   = ta.valuewhen(not na(pivotHighOsc), oscLBR, 1)
prevPriceHigh = ta.valuewhen(not na(pivotHighOsc), high[lookbackRight], 1)

// bars since previous pivot
barsSincePrevLowPivot  = ta.barssince(not na(pivotLowOsc))[1]
barsSincePrevHighPivot = ta.barssince(not na(pivotHighOsc))[1]

// range filter
inRangeLow  = barsSincePrevLowPivot  >= rangeLower and barsSincePrevLowPivot  <= rangeUpper
inRangeHigh = barsSincePrevHighPivot >= rangeLower and barsSincePrevHighPivot <= rangeUpper

// bullish divergence
plFound  = not na(pivotLowOsc)
bullCond = plFound and (low[lookbackRight] < prevPriceLow) and (oscLBR > prevOscLow) and inRangeLow

// bearish divergence
phFound  = not na(pivotHighOsc)
bearCond = phFound and (high[lookbackRight] > prevPriceHigh) and (oscLBR < prevOscHigh) and inRangeHigh

// plotting divergence lines & labels 
plot(calculateDivergence and plFound ? oscLBR : na, offset=-lookbackRight, linewidth=2, color = calculateDivergence and bullCond ? bullColor : na)
plot(calculateDivergence and phFound ? oscLBR : na, offset=-lookbackRight, linewidth=2, color = calculateDivergence and bearCond ? bearColor : na)

plotshape(calculateDivergence and bullCond ? oscLBR : na,
     offset    = -lookbackRight,
     text      = "Bull",
     style     = shape.labelup,
     location  = location.absolute,
     color     = color.new(bullColor, 10),
     textcolor = textColor,
     size      = size.small)

plotshape(calculateDivergence and bearCond ? oscLBR : na,
     offset    = -lookbackRight,
     text      = "Bear",
     style     = shape.labeldown,
     location  = location.absolute,
     color     = color.new(bearColor, 10),
     textcolor = textColor,
     size      = size.small)


// ALERT CONDITIONS 
alertcondition(bullCross,  title="Bull Cross Alert",  message="Momentum crossed above zero")
alertcondition(bearCross,  title="Bear Cross Alert",  message="Momentum crossed below zero")
alertcondition(firstAfterSqueezeLong,  title="1st after Squeeze LONG Alert",  message="First bullish momentum after squeeze")
alertcondition(firstAfterSqueezeShort, title="1st after Squeeze SHORT Alert", message="First bearish momentum after squeeze")
alertcondition(calculateDivergence and bullCond, title="Bullish Divergence Alert", message="Bullish divergence detected on momentum")
alertcondition(calculateDivergence and bearCond, title="Bearish Divergence Alert", message="Bearish divergence detected on momentum")

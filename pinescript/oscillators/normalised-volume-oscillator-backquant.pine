// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © BackQuant

import TradingView/ta/11 as ta

//@version=6
indicator(
 title="Normalised Volume Oscillator [BackQuant]",
 overlay=false
 )

// Define consts
const string KVOcalc = "Klinger Volume Oscillator"
const string Normcalc = "Normalisation"
const string ma = "Moving Averages"
const string plt = "Plotting and UI Settings"

// User Inputs 
simple int fastLen       =    input.int(34, "Fast Length", minval=1, group=KVOcalc, inline="kvo")
simple int slowLen       =    input.int(55, "Slow Length", minval=1, group=KVOcalc, inline="kvo")

simple int norm_period   =    input.int(50, "Normalisation Period", group = Normcalc)

string matype            =    input.string("SMA", title = "Moving Average Type", options=["SMA","EMA","DEMA","TEMA","RMA","WMA","HMA","T3","ALMA","LINREG","VWMA"], group = ma, inline="1")
simple int malen         =    input.int(15, "MA Period", group = ma, inline="1")

simple bool showosc      =    input.bool(true, "Show Oscillator?", group = plt)
simple bool showma       =    input.bool(true, "Show Moving Average", group = plt, tooltip = "If you are going to use the moving average as the signal line, go onto the style menu and change the moving average line into columns")
simple bool paintCandles =    input.bool(true, "Paint Candles according to Trend?", group = plt)
simple bool showstatic   =    input.bool(true, "Show Static Levels? (Overbought & Oversold)", group = plt)

// Script Logic
[kvo,sig] = ta.kvo(fastLen, slowLen, 1)
highK = ta.highest(kvo, norm_period)
lowK = ta.lowest(kvo, norm_period)
normalized=((kvo-lowK)/(highK-lowK))-0.5 // Normalisation

ma(src, len, type) =>
    switch type 
        "SMA"    => ta.sma(src, len)
        "EMA"    => ta.ema(src, len)
        "DEMA"   => ta.dema(src, len)
        "TEMA"   => ta.tema(src, len)
        "RMA"    => ta.rma(src, len)
        "WMA"    => ta.wma(src, len)
        "HMA"    => ta.hma(src, len)
        "T3"     => ta.t3(src, len, 0.7)
        "ALMA"   => ta.alma(src, len, 0, 0.6)
        "LINREG" => ta.linreg(src, len, 0)
        "VWMA"   => ta.vwma(src, len)
// Moving Average Out
sig_ma = ma(normalized, malen, matype)

// Conditional Oscillator Plot Color
var plotcol = #1dcaff4d
if normalized > 0 and normalized<0.125
    plotcol := #1dcaff4d
if normalized > 0 and normalized>0.125
    plotcol := #1e9b254d
if normalized > 0 and normalized>0.25
    plotcol := #00ff003d
if normalized > 0 and normalized>0.4
    plotcol := #00ff0080
if normalized > 0 and normalized>0.49
    plotcol := #33ff00fc
if normalized < 0 and normalized>-0.125
    plotcol := #e651004d
if normalized < 0 and normalized<-.125
    plotcol := #7715154d
if normalized < 0 and normalized<-0.25
    plotcol := #ff00004d
if normalized < 0 and normalized<-0.4
    plotcol := #ff000080
if normalized < 0 and normalized<-0.49
    plotcol := #ff0000

// Colouring
osbgcol          =       #00e6764d
obbgcol          =       #ff52524d
obcol            =       #ff0000fc
oscol            =       #00ff00fc 
midcol           =       #ffffff4d 

// Plotting
plot(showosc ? normalized : na, 'KVO Normalised', plotcol, 2, plot.style_columns)
// Midline
midline = hline(0, "0 Line", midcol, hline.style_solid)
// MA
plot(showma ? sig_ma : na, "Moving Average", color = #ffffff66)
// OB Levels
obupper = plot(showstatic ? 0.5 : na, "Upper Level 2", obcol )
oblower = plot(showstatic ? 0.4 : na, "Upper Level 1", obcol )
// OS Levels
osupper = plot(showstatic ? -0.4 : na, "Lower Level 1", oscol)
oslower = plot(showstatic ? -0.5 : na, "Lower Level 2", oscol)
// OB/OS Fills
fill(obupper, oblower, #7715154d, 'OB Fill')
fill(osupper, oslower, #1e9b254d, 'OS Fill')
// Color based on Trend
plotcandle(open,high,low,close, "KVO Candles", paintCandles?plotcol:na, paintCandles?plotcol:na, true, bordercolor = paintCandles?plotcol:na, force_overlay = true, display = display.all)

// Normalised KVO Alerts
alertcondition(ta.crossover(normalized,0), "Normalised KVO Long", "Normalised KVO Long {{exchange}}:{{ticker}}")
alertcondition(ta.crossunder(normalized,0), "Normalised KVO Short", "Normalised KVO Short {{exchange}}:{{ticker}}")
alertcondition(ta.cross(normalized,0), "Normalised KVO Midline Cross", "Normalised KVO Midline Cross {{exchange}}:{{ticker}}")
alertcondition(ta.crossover(normalized,0.4), "Normalised KVO Overbought", "Normalised KVO Overbought {{exchange}}:{{ticker}}")
alertcondition(ta.crossunder(normalized,-0.4), "Normalised KVO Oversold", "Normalised KVO Oversold {{exchange}}:{{ticker}}")

// Moving Average Alerts, used for confirmations. Used standalone can be late (Since this is a lagging signal)
alertcondition(ta.crossover(sig_ma,0), "Normalised KVO MA Long", "Normalised KVO MA Long {{exchange}}:{{ticker}}")
alertcondition(ta.crossunder(sig_ma,0), "Normalised KVO MA Short", "Normalised KVO MA Short {{exchange}}:{{ticker}}")
alertcondition(ta.cross(sig_ma,0), "Normalised KVO MA Midline Cross", "Normalised KVO MA Midline Cross {{exchange}}:{{ticker}}")

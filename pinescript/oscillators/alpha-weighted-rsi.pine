// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © gordito03

//@version=6
indicator("Alpha-Weighted RSI", format=format.price, precision=2)

// ===== INPUTS =====
// RSI Settings
rsiLength = input.int(14, "Length", minval=1, group="RSI Settings")
rsiSource = input.source(close, "Calculation Source", group="RSI Settings")

// Lévy Flight Settings
alpha = input.float(1.5, "Alpha", minval=1, maxval=2, step=0.01, 
                   tooltip="Lower alpha = more responsive", group="Lévy Flight Settings")

// Moving Average Settings
maType = input.string("EMA", "MA Type", 
                      options=["SMA", "EMA", "SMMA", "WMA", "VWMA", "LSMA", "HMA", "ALMA"], 
                      group="MA Settings")
maLength = input.int(8, "MA Length", group="MA Settings")

// ===== COLORS =====
bullColor = color.rgb(0, 255, 255)
bearColor = color.rgb(233, 113, 237)
neutralColor = color.gray

// ===== FUNCTIONS =====
// Moving Average Function
f_ma(source, length, type) =>
    switch type
        "SMA"   => ta.sma(source, length)
        "EMA"   => ta.ema(source, length)
        "SMMA"  => ta.rma(source, length)
        "WMA"   => ta.wma(source, length)
        "VWMA"  => ta.vwma(source, length)
        "TEMA"  =>
            ema1 = ta.ema(source, length)
            ema2 = ta.ema(ema1, length)
            ema3 = ta.ema(ema2, length)
            3 * (ema1 - ema2) + ema3
        "DEMA"  =>
            ema1 = ta.ema(source, length)
            ema2 = ta.ema(ema1, length)
            2 * ema1 - ema2
        "LSMA"  => ta.linreg(source, length, 0)
        "HMA"   => ta.wma(2 * ta.wma(source, length / 2) - ta.wma(source, length), math.floor(math.sqrt(length)))
        "ALMA"  => ta.alma(source, length, 0.85, 6)

// ===== CALCULATIONS =====
// Lévy Flight RSI Calculation
priceChange = ta.change(rsiSource)
weightedGain = math.pow(math.max(priceChange, 0), alpha)
weightedLoss = math.pow(-math.min(priceChange, 0), alpha)

avgGain = ta.rma(weightedGain, rsiLength)
avgLoss = ta.rma(weightedLoss, rsiLength)

// RSI Formula
rawRSI = avgLoss == 0 ? 100 : avgGain == 0 ? 0 : 100 - (100 / (1 + avgGain / avgLoss))
levyRSI = f_ma(rawRSI, maLength, maType)

// Centered RSI for plotting
centeredRSI = levyRSI - 50

// Color Logic
plotColor = centeredRSI > 0 ? bullColor : bearColor

// ===== PLOTTING =====
// Zero Line
plot(0, "Zero Line", color=neutralColor, style=plot.style_line)

// Overbought Levels
upperLine1 = plot(35, "Upper Line 1", color=color.new(neutralColor, 0))
upperLine2 = plot(25, "Upper Line 2", color=color.new(neutralColor, 0))
upperFill = centeredRSI > 0 ? color.new(color.from_gradient(centeredRSI, 0, 20, color.black, bearColor), 55) : na
fill(upperLine1, upperLine2, upperFill, "Upper Gradient")

// Oversold Levels
lowerLine1 = plot(-25, "Lower Line 1", color=color.new(neutralColor, 0))
lowerLine2 = plot(-35, "Lower Line 2", color=color.new(neutralColor, 0))
lowerFill = centeredRSI < 0 ? color.new(color.from_gradient(centeredRSI, -20, 0, bullColor, color.black), 55) : na
fill(lowerLine1, lowerLine2, lowerFill, "Lower Gradient")

// Main RSI Plot
plot(centeredRSI, "Alpha-Weighted RSI", color=plotColor, style=plot.style_histogram, linewidth=2)

// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Truth_Strategy_Indie

//@version=6
strategy("W%R Pullback+EMA Trend [TS_Indie]", overlay=true, max_labels_count=500 , max_lines_count = 500 ,max_boxes_count = 500, max_bars_back = 5000 , margin_long=0, margin_short=0 ,  initial_capital=10000 , process_orders_on_close = true )

GROUP_1 = "====== Conditions Entry ======"
GROUP_2 = "====== Setup Risk/Reward Ratio ======"
GROUP_3 = "====== Setup Stop loss ATR ======"

// === Input Parameters ===
rr_ratio = input.float(1.5, "Risk Reward Ratio", step=0.1 , inline="1" , group = GROUP_2 )
length_atr = input.int(title="Length ATR", defval=14, minval=1 , inline="2" , group = GROUP_3 )
x_ATR = input.float( 0 , "x ATR", step=0.1 , inline="2" , group = GROUP_3 )

maTypeInput_fast = input.string("EMA", "Type MA Fast", options = ["EMA", "SMA", "SMMA (RMA)", "WMA", "VWMA"], group = GROUP_1 , inline="3" , display = display.data_window)
ema_fast_len = input.int(20, "Length Fast" , inline="3.5" , group = GROUP_1 )
src_1 = input(close, "Source" , inline="3.5" , group = GROUP_1 )

maTypeInput_slow = input.string("EMA", "Type MA Slow", options = ["EMA", "SMA", "SMMA (RMA)", "WMA", "VWMA"], group = GROUP_1 , inline="4", display = display.data_window)
ema_slow_len = input.int(250, "Length Slow", inline="4.5" , group = GROUP_1 )
src_2 = input(close, "Source" , inline="4.5" , group = GROUP_1 )

wr_len = input.int(37, "Williams %R Length", inline="5"  , group = GROUP_1 )
Overbought = input.int(-20, "Ov_Bought", inline="6" , group = GROUP_1 )
Oversold = input.int(-80, "Ov_Sold", inline="6" , group = GROUP_1 )
long_t = input(true,"Entry Longㅤㅤ", inline="10" , group = GROUP_1 )
short_t = input(true, "Entry Short" , inline="10" , group = GROUP_1 )

//====== Time Filter ======
Time_Filter = "====== Time Filter ======"
s_date = input.time(timestamp("01 Jan 1970"),"Start" , inline="1" , group = Time_Filter )
en_date = input.time(timestamp("01 Jan 2500"), "End " , inline="2" , group = Time_Filter )

con_date = time >= s_date and time <= en_date
//======== session ======== 
_session(sess) =>
    not na(time(timeframe.period, sess, "UTC+0" ))

s_New_York = input.bool( false ,"New York" , inline = "3" , group = Time_Filter )
t_New = input.session( '1300-2200', "" , inline = "3" , group = Time_Filter )
Session_1 = _session(t_New) , cf_ses1 = s_New_York ? Session_1 : false

s_London = input.bool( false ,"London" , inline = "4" , group = Time_Filter )
t_Lon = input.session( '0700-1600' , "" , inline = "4" , group = Time_Filter )
Session_2 = _session(t_Lon) , cf_ses2 = s_London ? Session_2 : false

s_Tokyo = input.bool( false ,"Tokyo" , inline = "5" , group = Time_Filter )
t_Tokyo = input.session( '0000-0900' , "" , inline = "5" , group = Time_Filter )
Session_3 = _session(t_Tokyo) , cf_ses3 = s_Tokyo ? Session_3 : false

s_Sydney = input.bool( false ,"Sydney" , inline = "6" , group = Time_Filter )
t_Syd = input.session( '2100-0600' , "" , inline = "6" , group = Time_Filter )
Session_4 = _session(t_Syd) , cf_ses4 = s_Sydney ? Session_4 : false

ses_check = s_New_York or s_London or s_Tokyo or s_Sydney
time_con = cf_ses1 or cf_ses2 or cf_ses3 or cf_ses4
con_time = ses_check ? time_con : true

//======== Table ======== 
position(select) =>
    switch select
        "bottom_right"  => position.bottom_right
        "bottom_center" => position.bottom_center
        "bottom_left"   => position.bottom_left
        "top_center"    => position.top_center
        "top_left"      => position.top_left
        "top_right"     => position.top_right
        "mid_center"    => position.middle_center
        "mid_left"      => position.middle_left
        "mid_right"     => position.middle_right

result_group = "====== Trading Result Table ======"
Show_result = input.bool( true ,"Show Result" , inline = "1" , group = result_group ,tooltip = "The Profit Factor here is calculated using the formula:\n\(Number of Wins x Risk-Reward Ratio) / Number of Losses\n\This formula is based on the assumption that the risk per losing trade is fixed — for example, every losing trade costs exactly 1% of the account.\n\Because of this assumption, the calculated Profit Factor will not match TradingView’s result, since TradingView uses:\n\Gross Profit / Gross Loss\n\And in actual TradingView trades, the loss amount of each trade is not always equal, because the system doesn’t enforce a fixed-risk rule for every losing trade. " )
select_position = input.string("bottom_right", "", options = ["bottom_right", "bottom_center", "bottom_left", "top_center", "top_left" , "top_right" , "mid_center" , "mid_left" , "mid_right" ], group = result_group  , inline="1" , display = display.data_window)
Factor_up = input.float( 1.3 ,"Profit Factor > " , step=0.1 , inline = "2" , group = result_group )
Factor_up_color = input(title = "   " , defval = #a5d6a7  , inline = "2" , group = result_group)
Factor_donw = input.float( 1 ,"Profit Factor < " , step=0.1 , inline = "3" , group = result_group )
Factor_donw_color = input(title = "   " , defval = #faa1a4  , inline = "3" , group = result_group)

// MA Calculation
ma(source, length, MAtype) =>
    switch MAtype
        "SMA"                   => ta.sma(source, length)
        "EMA"                   => ta.ema(source, length)
        "SMMA (RMA)"            => ta.rma(source, length)
        "WMA"                   => ta.wma(source, length)
        "VWMA"                  => ta.vwma(source, length)


atr_sl = ta.ema(ta.tr(true), length_atr)

var int c_order_buy = 0 , var int c_order_sell = 0
bool pending_buy = false , bool pending_sell = false
var float stop_loss_long = 0 , var float stop_loss_short = 0
var float take_profit_long = 0 , var float take_profit_short = 0
var float open_long = 0 , var float open_short = 0
var int time_buy = 0 , var int time_sell = 0

// === Indicator Calculation ===
ema_fast = ma(src_1, ema_fast_len, maTypeInput_fast)
ema_slow = ma(src_2, ema_slow_len, maTypeInput_slow)

_pr(length) =>
    max = ta.highest(length)
    min = ta.lowest(length)
    100 * (close - max) / (max - min)
wr = _pr(wr_len)

// === Trend Filter ===
uptrend = ema_fast > ema_slow
downtrend = ema_fast < ema_slow

Wr_Overbought = math.abs(Overbought)*-1
Wr_Oversold = math.abs(Oversold)*-1

wr_crossover_up = ( wr[1]<Wr_Oversold and wr > Wr_Oversold ) and uptrend
wr_crossover_down = ( wr[1] > Wr_Overbought and wr < Wr_Overbought ) and downtrend

// === Entry Conditions ===
buy_signal = barstate.isconfirmed and long_t and close > ema_fast and wr_crossover_up  and strategy.position_size == 0 and con_time and con_date
sell_signal = barstate.isconfirmed and short_t and close < ema_fast and wr_crossover_down and strategy.position_size == 0 and con_time and con_date

// === Position Size ===
risk_pips = atr_sl * x_ATR
value_remove2(va1 , va2 , size) =>
    array.remove(va1,size) , array.remove(va2,size)
var tp_buy = array.new_float(0,na) , var sl_buy = array.new_float(0,na)  
var tp_sell = array.new_float(0,na) , var sl_sell = array.new_float(0,na)  

// === Long Entry ===
if (buy_signal) and barstate.isconfirmed 
    sl_cal_long = low < low[1] ? low : low[1]
    stop_loss_long := sl_cal_long - risk_pips
    take_profit_long := close + (close - stop_loss_long)*rr_ratio
    c_order_buy += 1 
    cs_buy = str.tostring(c_order_buy)
    strategy.entry("Long "+cs_buy, strategy.long , na , limit = close , stop = close )
    strategy.exit("Long Exit "+cs_buy , "Long "+cs_buy , stop = stop_loss_long , limit = take_profit_long , comment_profit = "Long(Tp) "+cs_buy ,comment_loss = "Long(SL) "+cs_buy )
    p_entry = "\n\nEntry price = "+str.tostring( close , format.mintick)
    p_sl = "\n\nStop loss = "+str.tostring(stop_loss_long, format.mintick)
    p_tp = "\n\nTarget profit = "+str.tostring(take_profit_long, format.mintick)
    alert("Long_"+p_entry+p_sl+p_tp, alert.freq_all)
    pending_buy := true , open_long := close , time_buy := time
    array.unshift(tp_buy, take_profit_long) , array.unshift(sl_buy,stop_loss_long)

// === Short Entry ===
if (sell_signal) and barstate.isconfirmed 
    sl_cal_short = high > high[1] ? high : high[1]
    stop_loss_short := sl_cal_short + risk_pips
    take_profit_short := close - (stop_loss_short - close)*rr_ratio
    c_order_sell += 1
    cs_sell = str.tostring(c_order_sell)
    strategy.entry("Short "+cs_sell, strategy.short , na , limit = close , stop = close )
    strategy.exit("Short Exit "+cs_sell ,"Short "+cs_sell , stop = stop_loss_short , limit = take_profit_short , comment_profit = "Short(Tp) "+cs_sell ,comment_loss = "Short(SL) "+cs_sell )
    p_entry = "\n\nEntry price = "+str.tostring( close , format.mintick)
    p_sl = "\n\nStop loss = "+str.tostring(stop_loss_short, format.mintick)
    p_tp = "\n\nTarget profit = "+str.tostring(take_profit_short, format.mintick)
    alert("Short_"+p_entry+p_sl+p_tp, alert.freq_all)
    pending_sell := true , open_short := close  , time_sell := time
    array.unshift(tp_sell, take_profit_short) , array.unshift(sl_sell,stop_loss_short)

//show pending
n_time = timeframe.in_seconds(timeframe.period) * 1000 * 5
col_1 = color.rgb(8, 153, 129, 80)  , col_2 = color.rgb(242, 54, 70, 80)  
text_line2_col = #ffffff , TRANSP = #ffffff00

box box_entrybuy_1_his = na , box box_entrybuy_2_his = na
box box_entrysell_1_his = na , box box_entrysell_2_his = na
label lbl_entry_his = na , label lbl_tp_his = na , label lbl_sl_his = na
//show Buy
if pending_buy or strategy.position_size > 0
    box.delete(box_entrybuy_1_his[1]) , box.delete(box_entrybuy_2_his[1]) , label.delete(lbl_entry_his[1]) , label.delete(lbl_tp_his[1]) , label.delete(lbl_sl_his[1])
    box_entrybuy_1_his := box.new(xloc = xloc.bar_time ,left = time_buy , top = take_profit_long, right = time+n_time , bottom = open_long , border_color = col_1 ,bgcolor = col_1)
    box_entrybuy_2_his := box.new(xloc = xloc.bar_time ,left = time_buy , top = open_long, right = time+n_time , bottom = stop_loss_long , border_color = col_2 ,bgcolor = col_2)
    lbl_entry_his := label.new(time+n_time, open_long , str.tostring(open_long,format.mintick) ,xloc = xloc.bar_time, color = TRANSP, textcolor = text_line2_col, style = label.style_label_center, size = size.small)
    lbl_tp_his := label.new(time+n_time, take_profit_long , str.tostring(take_profit_long,format.mintick),xloc = xloc.bar_time, color = TRANSP, textcolor = text_line2_col, style = label.style_label_center, size = size.small)
    lbl_sl_his := label.new(time+n_time, stop_loss_long , str.tostring(stop_loss_long,format.mintick) ,xloc = xloc.bar_time, color = TRANSP, textcolor = text_line2_col, style = label.style_label_center, size = size.small)

//show sell
if  pending_sell or strategy.position_size < 0
    box.delete(box_entrysell_1_his[1]) , box.delete(box_entrysell_2_his[1]) , label.delete(lbl_entry_his[1]) , label.delete(lbl_tp_his[1]) , label.delete(lbl_sl_his[1])
    box_entrysell_1_his := box.new(xloc = xloc.bar_time , left = time_sell , top = take_profit_short, right = time+n_time  , bottom = open_short , border_color = col_1 ,bgcolor = col_1)
    box_entrysell_2_his := box.new(xloc = xloc.bar_time , left = time_sell , top = open_short, right = time+n_time  , bottom = stop_loss_short , border_color = col_2 ,bgcolor = col_2)
    lbl_entry_his := label.new(time+n_time , open_short , str.tostring(open_short,format.mintick) ,xloc = xloc.bar_time, color = TRANSP, textcolor = text_line2_col, style = label.style_label_center, size = size.small)
    lbl_tp_his := label.new(time+n_time , take_profit_short , str.tostring(take_profit_short,format.mintick),xloc = xloc.bar_time, color = TRANSP, textcolor = text_line2_col, style = label.style_label_center, size = size.small)
    lbl_sl_his := label.new(time+n_time , stop_loss_short , str.tostring(stop_loss_short,format.mintick) ,xloc = xloc.bar_time, color = TRANSP, textcolor = text_line2_col, style = label.style_label_center, size = size.small)

var int Loss_Buy = 0 , var int Win_Buy = 0 , var float sl_trade_buy = na , var float tp_trade_buy = na
var int Loss_Sell = 0 , var int Win_Sell = 0 , var float sl_trade_sell = na , var float tp_trade_sell = na
if Show_result
    //Check SL buy
    if array.size(sl_buy) > 0 and not na(array.get(sl_buy,0))
        sl_trade_buy := array.get(sl_buy,0)
    if low <= sl_trade_buy and ( strategy.position_size[1] > 0 or buy_signal[1] ) and array.size(sl_buy) > 0
        sl_trade_buy := na
        for i = array.size(sl_buy)-1 to 0
            if low <= array.get(sl_buy,i) and not na(array.get(sl_buy,i))
                Loss_Buy += 1
                value_remove2( tp_buy , sl_buy , i)
    //check TP buy
    if array.size(tp_buy) > 0 and not na(array.get(tp_buy,0))
        tp_trade_buy := array.get(tp_buy,0)
    if high >= tp_trade_buy and ( strategy.position_size[1] > 0 or buy_signal[1] ) and array.size(tp_buy) > 0
        tp_trade_buy := na
        for i = array.size(tp_buy)-1 to 0
            if high >= array.get(tp_buy,i) and not na(array.get(tp_buy,i))
                Win_Buy += 1 
                value_remove2( tp_buy , sl_buy , i)
    //Check SL Sell
    if array.size(sl_sell) > 0 and not na(array.get(sl_sell,0))
        sl_trade_sell := array.get(sl_sell,0)
    if high >= sl_trade_sell and ( strategy.position_size[1] < 0 or sell_signal[1] ) and array.size(sl_sell) > 0
        sl_trade_sell := na
        for i = array.size(sl_sell)-1 to 0
            if high >= array.get(sl_sell,i) and not na(array.get(sl_sell,i))
                Loss_Sell += 1
                value_remove2( tp_sell , sl_sell , i)
    //check TP Sell
    if array.size(tp_sell) > 0 and not na(array.get(tp_sell,0))
        tp_trade_sell := array.get(tp_sell,0)
    if low <= tp_trade_sell and ( strategy.position_size[1] < 0 or sell_signal[1] ) and array.size(tp_sell) > 0
        tp_trade_sell := na
        for i = array.size(tp_sell)-1 to 0
            if low <= array.get(tp_sell,i) and not na(array.get(tp_sell,i))
                Win_Sell += 1 
                value_remove2( tp_sell , sl_sell , i)

// === Plotting ===
p1 = plot(ema_fast, color= #f23645, title="EMA fast")
p2 = plot(ema_slow, color=#ffee58, title="EMA slow")

fill(p1, p2, color = ema_fast > ema_slow ? color.rgb(67, 160, 72, 85) : color.rgb(244, 67, 54, 85))
plot(wr,"Williams %R" ,color = color.new(color.blue, 0), display = display.status_line , precision = 2  )

con_Overbought = wr > math.abs(Overbought)*-1 and downtrend
con_Oversold = wr < math.abs(Oversold)*-1 and uptrend

barcolor( con_Oversold ? color.blue : na ,title = "Bar Oversold")
barcolor( wr_crossover_up ? color.orange  : na ,title = "Bar Crossover Up")

barcolor( con_Overbought ? color.aqua : na ,title = "Bar Overbought")
barcolor( wr_crossover_down ? color.fuchsia : na ,title = "Bar Crossover Down")

bgcolor(s_New_York and cf_ses1 ? color.rgb(255, 251, 0, 92) : na ,title = "New York")
bgcolor(s_London and cf_ses2 ? color.rgb(255, 0, 255, 92) : na ,title = "London")
bgcolor(s_Tokyo and cf_ses3? color.rgb(111, 255, 82, 92) : na ,title = "Tokyo")
bgcolor(s_Sydney and cf_ses4? color.rgb(82, 122, 255, 92) : na ,title = "Sydney")

Location_session = ((9*60*60) / timeframe.in_seconds(timeframe.period)) / 2 
Lbl_New_York = not cf_ses1 and cf_ses1 != cf_ses1[1] and s_New_York and t_New == '1300-2200' and timeframe.in_seconds(timeframe.period) <= 32400
plotshape( Lbl_New_York , "Session New York", shape.labeldown, location.bottom, na , offset = -Location_session  , text = "New York", textcolor = #fffb00, size = size.tiny, display = display.all - display.status_line, editable = false)
Lbl_London = not cf_ses2 and cf_ses2 != cf_ses2[1] and s_London and t_Lon == '0700-1600' and timeframe.in_seconds(timeframe.period) <= 32400
plotshape( Lbl_London , "Session London", shape.labeldown, location.bottom, na , offset = -Location_session  , text = "London", textcolor = #ff00ff , size = size.tiny, display = display.all - display.status_line, editable = false)
Lbl_Tokyo = not cf_ses3 and cf_ses3 != cf_ses3[1] and s_Tokyo and t_Tokyo == '0000-0900' and timeframe.in_seconds(timeframe.period) <= 32400
plotshape( Lbl_Tokyo , "Session Tokyo", shape.labeldown, location.bottom, na , offset = -Location_session  , text = "Tokyo", textcolor = #6fff52 , size = size.tiny, display = display.all - display.status_line, editable = false)
Lbl_Sydney = not cf_ses4 and cf_ses4 != cf_ses4[1] and s_Sydney and t_Syd == '2100-0600' and timeframe.in_seconds(timeframe.period) <= 32400
plotshape( Lbl_Sydney , "Session Sydney", shape.labeldown, location.bottom, na , offset = -Location_session  , text = "Sydney", textcolor = #527aff , size = size.tiny, display = display.all - display.status_line, editable = false)


var table Balane_status = table.new( position(select_position) , 6 , 6 , border_width = 1)

Total_all = Win_Buy + Loss_Buy + Win_Sell + Loss_Sell
if Show_result and (Total_all > Total_all[1])
    Long_all = Win_Buy + Loss_Buy , Short_all = Win_Sell + Loss_Sell
    Total_win = Win_Buy + Win_Sell , Total_loss = Loss_Buy + Loss_Sell
    Factor_all = (Total_win*rr_ratio)/Total_loss
    Factor_buy = (Win_Buy*rr_ratio)/Loss_Buy
    Factor_sell = (Win_Sell*rr_ratio)/Loss_Sell
    txt1_1 = str.tostring(Total_all,format.volume)
    txt1_2 = str.tostring(Long_all,format.volume)
    txt1_3 = str.tostring(Short_all,format.volume)
    txt2_1 = str.tostring(Total_win,format.volume)
    txt3_1 = str.tostring(Total_loss,format.volume)
    txt4_1 = str.tostring((Total_win/Total_all)*100,format.percent)
    txt5_1 = str.format("{0,number,#.##}", Factor_all )

    txt2_2 = str.tostring(Win_Buy,format.volume)
    txt3_2 = str.tostring(Loss_Buy,format.volume)
    txt4_2 = str.tostring((Win_Buy/Long_all)*100,format.percent)
    txt5_2 = str.format("{0,number,#.##}", Factor_buy )

    txt2_3 = str.tostring(Win_Sell,format.volume)
    txt3_3 = str.tostring(Loss_Sell,format.volume)
    txt4_3 = str.tostring((Win_Sell/Short_all)*100,format.percent)
    txt5_3 = str.format("{0,number,#.##}", Factor_sell)

    Factor_all_color = Factor_all > Factor_up ? Factor_up_color : Factor_all < Factor_donw ? Factor_donw_color : color.white 
    Factor_buy_color = Factor_buy > Factor_up ? Factor_up_color : Factor_buy < Factor_donw ? Factor_donw_color : color.white 
    Factor_sell_color = Factor_sell > Factor_up ? Factor_up_color : Factor_sell < Factor_donw ? Factor_donw_color : color.white 

    table.cell(Balane_status , 1 , 1 , txt1_1 , bgcolor = color.black , text_color = Factor_all_color )
    table.cell(Balane_status , 2 , 1 , txt2_1 , bgcolor = color.black , text_color = Factor_all_color )
    table.cell(Balane_status , 3 , 1 , txt3_1 , bgcolor = color.black , text_color = Factor_all_color )
    table.cell(Balane_status , 4 , 1 , txt4_1 , bgcolor = color.black , text_color = Factor_all_color )
    table.cell(Balane_status , 5 , 1 , txt5_1 , bgcolor = color.black , text_color = Factor_all_color )

    table.cell(Balane_status , 1 , 2 , txt1_2 , bgcolor = color.black , text_color = Factor_buy_color )
    table.cell(Balane_status , 2 , 2 , txt2_2 , bgcolor = color.black , text_color = Factor_buy_color )
    table.cell(Balane_status , 3 , 2 , txt3_2 , bgcolor = color.black , text_color = Factor_buy_color )
    table.cell(Balane_status , 4 , 2 , txt4_2 , bgcolor = color.black , text_color = Factor_buy_color )
    table.cell(Balane_status , 5 , 2 , txt5_2 , bgcolor = color.black , text_color = Factor_buy_color )
    
    table.cell(Balane_status , 1 , 3 , txt1_3 , bgcolor = color.black , text_color = Factor_sell_color )
    table.cell(Balane_status , 2 , 3 , txt2_3 , bgcolor = color.black , text_color = Factor_sell_color )
    table.cell(Balane_status , 3 , 3 , txt3_3 , bgcolor = color.black , text_color = Factor_sell_color )
    table.cell(Balane_status , 4 , 3 , txt4_3 , bgcolor = color.black , text_color = Factor_sell_color )
    table.cell(Balane_status , 5 , 3 , txt5_3 , bgcolor = color.black , text_color = Factor_sell_color )

    table.cell(Balane_status , 0 , 1 , "Total" , bgcolor = color.black , text_color = color.white )
    table.cell(Balane_status , 0 , 2 , "Long" , bgcolor = color.black , text_color = color.white )
    table.cell(Balane_status , 0 , 3 , "Short" , bgcolor = color.black , text_color = color.white )
    table.cell(Balane_status , 1 , 0 , "All" , bgcolor = color.black , text_color = color.white )
    table.cell(Balane_status , 2 , 0 , "Win" , bgcolor = color.black , text_color = color.white )
    table.cell(Balane_status , 3 , 0 , "Loss" , bgcolor = color.black , text_color = color.white )
    table.cell(Balane_status , 4 , 0 , "Winrate" , bgcolor = color.black , text_color = color.white )
    table.cell(Balane_status , 5 , 0 , "Profit\n\Factor" , bgcolor = color.black , text_color = color.white )

// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© SenorBufoAlvarius

//@version=6
indicator(title = '[CASH] Crypto And Stocks Helper (MultiPack w. Alerts)', shorttitle = 'CASH', overlay = true, max_lines_count = 500, max_labels_count = 500)

disclaimer = input.bool(false, title = 'Read ALL the information circles as they contain useful knowledge and instructions for beginners to understand this indicator and TA in general. Default settings are optimized for crypto and volatile stocks. The alerts should be used on the 1D timeframe, click on "Add alert to CASH..." (or better yet, add CASH alert to an entire watchlist) and create "Any alert() function call" and select 1D.', tooltip = 'The goal with this indicator is to teach you basic TA so you can visually see and understand where the "smart money" buy and sell and so you can start making better investments. Take your time to read my information boxes and study the alerts on the chart to get a grasp on how they work and how to approach your trading. Other indicators I use and recommend are MACD, RSI, Volume and "LuxAlgo - Smart Money Concepts". I also recommend scaling in and out of positions, because the price can always go lower or higher, to reduce the risk of the trade.', group = 'Crypto And Stocks Helper (MultiPack with EMA/SMA, Guides & Alerts)', confirm = true)

// Gap Up/Down

enableGapUp = input.bool(true, "Gap Up alert", tooltip = "Gaps up on high volume can be really profitable to trade, especially on good news/earnings. There is however a particular science/psychology behind gaps so I recommend you learn about advanced gap trading on Youtube before trading gaps.",group = 'Gaps Up/Down - Alerts when price gaps up or down from previous close.')
enableGapDown = input.bool(true, "Gap Down alert", tooltip = 'Gaps down can not only present good shorting opportunities, but also warn if a stop loss might have been jumped over. Gaps down, for several days in a row, into the extremes and at a support could mark a good entry because in certain situations there is a thing called "gaps down are for buying and gaps up are for selling".', group = 'Gaps Up/Down - Alerts when price gaps up or down from previous close.')
gapUpPercent  = input.float(4.0, "Gap Up % threshold", minval=0.1, step=0.1, tooltip="Minimum gap up percentage to trigger alert.", group = 'Gaps Up/Down - Alerts when price gaps up or down from previous close.')
gapDownPercent = input.float(4.0, "Gap Down % threshold", minval=0.1, step=0.1, tooltip="Minimum gap down percentage to trigger alert.", group = 'Gaps Up/Down - Alerts when price gaps up or down from previous close.')

getPrice() =>
    prevClose = close[1]
    dailyOpen = open
    [prevClose, dailyOpen]

[prevClose, dailyOpen] = request.security(syminfo.tickerid, "D", getPrice(), lookahead=barmerge.lookahead_on)

gapPercentage = ((dailyOpen - prevClose) / prevClose) * 100

if gapPercentage >= gapUpPercent and enableGapUp
    alert("Gap Up " + str.tostring(gapPercentage, "#.#") + "% from previous close", alert.freq_once_per_bar)

if gapPercentage <= (-gapDownPercent) and enableGapDown
    alert("Gap Down " + str.tostring(math.abs(gapPercentage), "#.#") + "% from previous close", alert.freq_once_per_bar)

// BITCOIN BULL MARKET SUPPORT BANDS

BMSB = input(false, title = 'Bitcoin Bull Market Support Band', tooltip = 'Plots the weekly Bitcoin Bull Market Support Band (or Bear Market Resistance Band in darker times) on whatever timeframe you\'re looking at. The band acts as strong support in a bull market or a heavy resistance in a bear market. This is something Benjamin Cowen at "Into the Cryptoverse" often dubious speculate and makes a lot of videos about. He might even be the inventor of it? Check out his YT channel for one of the best crypto content out there.', inline = 'colors')
col1 = input(color.lime, title = '', inline = 'colors')
col2 = input(color.red, title = '', inline = 'colors')
col3 = input(color.new(color.orange, 75), title = '', inline = 'colors')

sma20 = request.security(syminfo.tickerid, 'W', ta.sma(close, 20), gaps = barmerge.gaps_on)
ema21 = request.security(syminfo.tickerid, 'W', ta.ema(close, 21), gaps = barmerge.gaps_on)

ema = plot(BMSB ? ema21 : na, color = BMSB ? col1 : na, title = 'Bitcoin Bull Market Support Band EMA 21')
sma = plot(BMSB ? sma20 : na, color = BMSB ? col2 : na, title = 'Bitcoin Bull Market Support Band SMA 20')
fill(sma, ema, col3, fillgaps = true, title = 'Bitcoin Bull Market Support Band FILL')

// EMA/SMA PACK

emagrp = "EMA/SMA PACK - Great indicators of support and resistances. Note: If you don't want certain MA's plotted go into style and uncheck the related boxes."
len1 =  input(8,   title = 'EMA 8 :', tooltip = "Default: Off. Acts as support or resistance in very strong trends and best used on shorter timeframes. Part of the Fibonacci EMA's (8, 13, 21 & 55 periods).", group = emagrp, display = display.none)
len2 =  input(12,  title = 'EMA 12 :', tooltip = "In strong trends you often see the price ride EMA12 for quite some time. Together with EMA26, they are the most common EMA's used by traders and also by the MACD indicator.", group = emagrp, display = display.none)
len3 =  input(26,  title = 'EMA 26 :', tooltip = 'Short to medium term support/resistance on many timeframes and the higher the timeframe, the stronger they will be defended. Especially in conjunction with price action and other indicators.', group = emagrp, display = display.none)
len4 =  input(55,  title = 'EMA 55 :', tooltip = 'Medium to long term support/resistance. If you spot that a particular MA has historically acted as support, chances are it will continue to do so.', group = emagrp, display = display.none)
len5 =  input(200, title = 'EMA 200 :', tooltip = "Very strong support, especially for indexes, as it's considered to be the threshold for a bear market or crash when crossed below in a term called 'Mega Trend Plus'. Which mean you also find long term support and bottoms on this MA. Just look at SPY and QQQ on the weekly timeframe.", group = emagrp, display = display.none)
len6 =  input(50,  title = 'SMA 50 :', tooltip = "Default: Off. Widely used by traders to determine bullish or bearish sentiment for when the price is above or below this MA. It's also used together with SMA200 to determmine the classical Golden Cross or Death Cross.", group = emagrp, display = display.none)
len7 =  input(100, title = 'SMA 100 :', tooltip = "Default: Off. Sometimes takes turns with EMA100 in acting as support or resistance. Good example: BTCUSDT 1D in sep 2021.", group = emagrp, display = display.none)
len8 =  input(200, title = 'SMA 200 :', tooltip = 'Often superb support/resistance on higher timeframes. The price often pivots here down to the penny.', group = emagrp, display = display.none)
len9 =  input(400, title = 'SMA 400 :', tooltip = 'Default: Off. For long term support finding on higher timeframes in prolonged bear markets or crashes. Many tickers found support here in the Covid dump f.e.', group = emagrp, display = display.none)
len10 = input(500, title = 'SMA 500 :', tooltip = "Default: Off. The more overextended the price have been the more space, or percentages, has been formed between the lines. This will more often than not produce significant supports and bounces on all MA's on the way down from a top f.e.", group = emagrp, display = display.none)
len11 = input(100, title = 'EMA 100 (Signal) :', tooltip = 'Very strong support/resistance and often indicates long term tops and bottoms which can often be seen riding for years in both trends.', group = 'SUPER TREND SETTINGS - A bullish or bearish SUPER Trend is determined by the golden cross or death cross of the EMA Signal and the SMA Base. Indicated by green or red background.', display = display.none)
len12 = input(300, title = 'SMA 300 (Base) :', tooltip = 'Very strong support/resistance and often indicates long term tops and bottoms, especially when SMA300 is the highest (for tops) or the lowest (for bottoms) MA on the chart.', group = 'SUPER TREND SETTINGS - A bullish or bearish SUPER Trend is determined by the golden cross or death cross of the EMA Signal and the SMA Base. Indicated by green or red background.', display = display.none)

ema1 = ta.ema(close, len1)
ema2 = ta.ema(close, len2)
ema3 = ta.ema(close, len3)
ema4 = ta.ema(close, len4)
ema5 = ta.ema(close, len5)
sma1 = ta.sma(close, len6)
sma2 = ta.sma(close, len7)
sma3 = ta.sma(close, len8)
sma4 = ta.sma(close, len9)
sma5 = ta.sma(close, len10)
EMAsignal = ta.ema(close, len11)
SMAbase = ta.sma(close, len12)

showma = input(15, title = "Minimum timeframe (in minutes) to show MA's on :", tooltip = "240 min for 4 hours, 1440 min for 1 day and so on. I don't use MA's on 5 min timeframe and below. They don't mean as much there and just fills the chart with garbage where instead vwap and price action is king in my opinion.", group = "VISIBILITY SETTINGS FOR MA'S, SUPER TREND & ALERTS", display = display.none)

f_resInMinutes() =>
    _resInMinutes = timeframe.multiplier * (timeframe.isseconds ? 1. / 60 : timeframe.isminutes ? 1. : timeframe.isdaily ? 60. * 24 : timeframe.isweekly ? 60. * 24 * 7 : timeframe.ismonthly ? 60. * 24 * 30.4375 : na)
    _resInMinutes

res_in_min = f_resInMinutes()
can_plot_ma = res_in_min >= showma

plot(can_plot_ma ? ema1 : na, title = 'EMA 8', color = #ff1493, display=display.none)
plot(can_plot_ma ? ema2 : na, title = 'EMA 12', color = #ff0000)
plot(can_plot_ma ? ema3 : na, title = 'EMA 26', color = #ffa500)
plot(can_plot_ma ? ema4 : na, title = 'EMA 55', color = #ffff00)
plot(can_plot_ma ? ema5 : na, title = 'EMA 200', color = #00ffff)
plot(can_plot_ma ? sma1 : na, title = 'SMA 50', color = color.white, display=display.none)
plot(can_plot_ma ? sma2 : na, title = 'SMA 100', color = #008000, display=display.none)
plot(can_plot_ma ? sma3 : na, title = 'SMA 200', color = #0000ff)
plot(can_plot_ma ? sma4 : na, title = 'SMA 400', color = #9598a1, display=display.none)
plot(can_plot_ma ? sma5 : na, title = 'SMA 500', color = color.gray, display=display.none)
plot(can_plot_ma ? EMAsignal : na, title = 'EMA 100 (signal)', color = #00ff00)
plot(can_plot_ma ? SMAbase : na, title = 'SMA 300 (Base)', color = #ff00ff)

// SUPER TREND

f2_resInMinutes() =>
    _resInMinutes = timeframe.multiplier * (timeframe.isseconds ? 1. / 60 : timeframe.isminutes ? 1. : timeframe.isdaily ? 60. * 24 : timeframe.isweekly ? 60. * 24 * 7 : na)
    _resInMinutes

res_in_min2 = f2_resInMinutes()

showtrend = input(1440, title = 'Minimum timeframe to show SUPER Trend & alerts on :', tooltip = 'I only plot the SUPER Trend\'s background color with alerts on the daily timeframe. But I included the settings here because sometimes I use it on 4h timeframe as well. ATTENTION: Volume, extreme RSI and divergence alerts plots on all timeframes because of its usefulness.', group = "VISIBILITY SETTINGS FOR MA'S, SUPER TREND & ALERTS", display = display.none)
can_plot_bgcolor = res_in_min2 >= showtrend
SUPERtrend = EMAsignal > SMAbase ? color.new(color.green, 90) : color.new(color.red, 90)
bgcolor(can_plot_bgcolor ? SUPERtrend : na, title = 'Background color to indicate the SUPER Trend. (EMA signal line crossing the SMA base line. Always remember what the 1D SUPER Trend is when used on lower timeframes.)')

// REPAINT

disableRepaint = input.bool(title = 'Disable repaint', group = 'Repaint - Highly recommended to have repaint disabled and wait for daily close to trigger as to not recieve fake alerts.', tooltip = "ATTENTION: The Volume Spike, MA Crossunders and Extreme RSI stack alerts are confirmed in realtime and will trigger intraday.", defval = true)

// DIVERGENCES

enabledivalerts  = input.bool(true, "Daily and weekly divergence alerts", group = 'Divergences', tooltip = "Plots (but only alerts on daily and weekly closes) a green B below bar or red S above bar for when it's usually a very good time to buy or sell. Check BTCUSDT on weekly TF to see how insanely accurate it can work. This alert nailed the 2022 bottom and 2025 top.") 
divLookback = input.int(50, 'Lookback period', minval = 1, group = 'Divergences', tooltip = 'Default value: between 30-50. Number of historical bars to check for new high/low in price.')

rsi =  ta.rsi(close, 14)
[macd, signal, hist] = ta.macd(close, 12, 26, 9)

isNewLow = low == ta.lowest(low, divLookback + 1)
isNewHigh = high == ta.highest(high, divLookback + 1)

DivUp   = macd > signal and (barstate.isconfirmed or not disableRepaint) and isNewLow
DivDown = macd < signal and (barstate.isconfirmed or not disableRepaint) and isNewHigh

getWeeklyData() =>
    [w_macd, w_signal, w_hist] = ta.macd(close, 12, 26, 9)
    w_low = low
    w_high = high
    [w_macd, w_signal, w_low, w_high]

[w_macd, w_signal, w_low, w_high] = request.security(syminfo.tickerid, "1W", getWeeklyData(), barmerge.gaps_off, barmerge.lookahead_on)

w_isNewLow = w_low == ta.lowest(w_low, divLookback + 1)
w_isNewHigh = w_high == ta.highest(w_high, divLookback + 1)

w_DivUp = w_macd > w_signal and (barstate.isconfirmed or not disableRepaint) and w_isNewLow
w_DivDown = w_macd < w_signal and (barstate.isconfirmed or not disableRepaint) and w_isNewHigh

plotshape((DivUp and enabledivalerts) ? 1 : na, title = 'Bullish Divergence', text = 'B', textcolor = color.black, style = shape.labelup, color = color.green, location = location.belowbar, display = display.all - display.status_line)
plotshape((DivDown and enabledivalerts) ? 1 : na, title = 'Bearish Divergence', text = 'S', textcolor = color.white, style = shape.labeldown, color = color.red, location = location.abovebar, display = display.all - display.status_line)

if (DivUp or DivDown) and barstate.isconfirmed and enabledivalerts
    alert('Daily divergence detected. Potentially an opportunity of a lifetime to scale in or take profit at. Read tooltip about divergences.', alert.freq_once_per_bar_close)

if (w_DivUp or w_DivDown) and enabledivalerts
    alert('Weekly divergence detected. Potentially an opportunity of a lifetime to scale in or take profit at.', alert.freq_once_per_bar_close)

alertcondition(DivUp or DivDown and barstate.isconfirmed, 'Divergences', 'Divergence detected. Potentially an opportunity of a lifetime to scale in or take profit at. Read tooltip about divergences.')

// BULL RSI

W_rsi = request.security(syminfo.tickerid, 'W', rsi, lookahead = barmerge.lookahead_on)
enablebullRSIalerts  = input.bool(true, "Bull RSI alerts", group = 'RSI settings in Bullish SUPER Trend', tooltip = 'Bullish SUPER Trends usually last multiple years, very common is around 4.') 

OS_rsi_bull = input(30, title = 'Oversold Entry Target:', group = 'RSI settings in Bullish SUPER Trend', tooltip = 'Plots alert (green cross below bar). The strategy is to buy as soon as we hit daily oversold, especially in the beginning of a bullish SUPER Trend. Buying in the middle part often gives massive returns because it\'s normally the first daily oversold conditions after a huge move. Bottoms are usually found at previous big supports and/or long term EMA/SMA 55-400. Be careful buying in the last part of the trend because it\'s normally the last bounce before rolling over into a bearish SUPER trend.', display = display.none)
Buy_rsi_bull = input(28, title = 'Oversold Entry Target if also W oversold:', group = 'RSI settings in Bullish SUPER Trend', tooltip = 'Plots alert (green uparrow below bar) only if we at the same time are oversold on the weekly. Unusual in bullish SUPER trend but offers a really great buy opportunity if it happens.', display = display.none)
OB_rsi_bull = input(80, title = 'Overbought Profit Target if also W overbought:', group = 'RSI settings in Bullish SUPER Trend', tooltip = 'Plots alert (red cross above bar) only if we at the same time are overbought on the weekly! The strategy is to take profit in daily and weekly overbought conditions which normally happens in the middle part of a bullish SUPER Trend. If the alert go off in the beginning of the trend, don\'t sell too much too soon because more upside is likely coming.', display = display.none)
Top = input(87, title = 'Blow off top?', group = 'RSI settings in Bullish SUPER Trend', tooltip = 'Default value: 87. SELL INTO STRENGHT! Plots alert (purple cross above bar) when extreme RSI is reached. Same as Capitulation dump but the reverse.', display = display.none)

OS_in_Bull = rsi < OS_rsi_bull and (barstate.isconfirmed or not disableRepaint) and EMAsignal > SMAbase
Buy_in_Bull = rsi < Buy_rsi_bull and (barstate.isconfirmed or not disableRepaint) and EMAsignal > SMAbase and W_rsi < 30
Sell_in_Bull = rsi > OB_rsi_bull and (barstate.isconfirmed or not disableRepaint) and EMAsignal > SMAbase and W_rsi > 70
Blowofftop = rsi > Top and (barstate.isconfirmed or not disableRepaint)

plotshape((enablebullRSIalerts and can_plot_bgcolor and isNewLow) ? OS_in_Bull : false, title = 'Entry Target in bullish HT', style = shape.xcross, color = color.green, location = location.belowbar, display = display.all - display.status_line)
plotshape((enablebullRSIalerts and can_plot_bgcolor and isNewLow) ? Buy_in_Bull : false, title = 'D/W ET in bullish HT', style = shape.arrowup, color = color.green, location = location.belowbar, display = display.all - display.status_line)
plotshape((enablebullRSIalerts and can_plot_bgcolor and isNewHigh) ? Sell_in_Bull : false, title = 'D/W PT in bullish HT', style = shape.xcross, color = color.red, location = location.abovebar, display = display.all - display.status_line)
plotshape((enablebullRSIalerts and Blowofftop and isNewHigh) ? 1 : na, title = 'Blow off top?', style = shape.xcross, color = color.fuchsia, location = location.abovebar, display = display.all - display.status_line)

if OS_in_Bull and barstate.isconfirmed and enablebullRSIalerts and isNewLow
    alert('Oversold Entry Target in bullish SUPER Trend. Look for divergences and scale in!', alert.freq_once_per_bar_close)

if Buy_in_Bull and barstate.isconfirmed and enablebullRSIalerts and isNewLow
    alert('D/W Oversold Entry Target in bullish SUPER Trend. Look for divergences and scale in!', alert.freq_once_per_bar_close)

if Sell_in_Bull and barstate.isconfirmed and enablebullRSIalerts and isNewHigh
    alert('Overbought Profit Target in bullish SUPER Trend. Look for divergences and take profit!', alert.freq_once_per_bar_close)

if Blowofftop and barstate.isconfirmed and enablebullRSIalerts and isNewHigh
    alert('Blow off top?. Look for divergences and take profit!', alert.freq_once_per_bar_close)

// BEAR RSI

enablebearRSIalerts  = input.bool(true, "Bear RSI alerts", group = 'RSI settings in bearish SUPER Trend', tooltip = 'Bearish SUPER Trends usually last shorter than bullish SUPER Trends, very common is 6 months to 2 years. Zoom out on weekly and monthly to spot long term supports.') 

OS_rsi_bear = input(28, title = 'Oversold Entry Target if also W oversold:', group = 'RSI settings in bearish SUPER Trend', tooltip = 'Plots alert (green cross below bar) only if we at the same time are oversold on the weekly! The strategy is to scale in on daily & weekly oversold conditions which normally happens in the middle part of a bearish SUPER Trend. Be careful if the alert go off in the beginning of the trend, more downside is likely coming. If not in ATL, the bottoms are usually found at weekly EMA/SMA 100-400 and/or previous supports. A great example is ETHUSD on Bitstamp 13 Jun \'22, after a few buy alerts from this indicator price pivots right on weekly SMA 300. Down to the penny almost!', display = display.none)
Bottom = input(16, title = 'Capitulation dump bottom?', group = 'RSI settings in bearish SUPER Trend', tooltip = 'Plots alert (purple cross below bar) when extreme RSI is reached. Together with other alerts this can be a great entry target. Because traders are getting liquidated, others are panic selling and the order book is getting cleared. When this happens, zoom out to the weekly and find weekly EMA/SMA 55-300 and establish your trading game plan around these levels depending on where you are in a SUPER Trend. BTCUSD on Bitstamp is a great example. Check where price pivoted on the weekly chart on 10 Dec \'18 and 9 Mar \'20 shortly after the Panic dump alert indicated a bottom. Again, on the SMA 200 and 300, down to the penny almost. Catch the knife at high probability levels but be careful and have a plan if it doesn\'t respect the EMA/SMA.', display = display.none)
OB_rsi_bear = input(70, title = 'Overbought Profit Target:', group = 'RSI settings in bearish SUPER Trend', tooltip = 'Plots alert (red cross above bar). The strategy is to sell as soon as we hit daily overbought, especially in the beginning of a bearish SUPER Trend. Selling in the middle part often proves to be a great choice because it\'s normally the first overbought condition after a huge move. Tops are normally found at previous big resistances and/or long term EMA/SMA 55-400. However, in the last part, this indicator can give way to early sell alerts because it might be the first top before rallying into a bullish SUPER Trend. Being greedy and just SUPER could pay off BIG TIME!', display = display.none)
Sell_rsi_bear = input(74, title = 'Overbought Profit Target if also W overbought:', group = 'RSI settings in bearish SUPER Trend', tooltip = 'Plots alert (red downarrow above bar) only if we at the same time are overbought on the weekly. Unusual in bearish SUPER trend but can happen, especially if we are shooing up into a bullish SUPER trend.', display = display.none)

Buy_in_Bear = rsi < OS_rsi_bear and (barstate.isconfirmed or not disableRepaint) and EMAsignal < SMAbase and W_rsi < 30
OB_in_Bear = rsi > OB_rsi_bear and (barstate.isconfirmed or not disableRepaint) and EMAsignal < SMAbase
Sell_in_Bear = rsi > Sell_rsi_bear and (barstate.isconfirmed or not disableRepaint) and EMAsignal < SMAbase and W_rsi > 70
Panicdump = rsi < Bottom and (barstate.isconfirmed or not disableRepaint)

plotshape((enablebearRSIalerts and can_plot_bgcolor and isNewLow) ? Buy_in_Bear : false, title = 'D/W ET in bearish HT', style = shape.xcross, color = color.green, location = location.belowbar, display = display.all - display.status_line)
plotshape((enablebearRSIalerts and can_plot_bgcolor and isNewHigh) ? OB_in_Bear : false, title = 'Profit Target in bearish HT', style = shape.xcross, color = color.red, location = location.abovebar, display = display.all - display.status_line)
plotshape((enablebearRSIalerts and can_plot_bgcolor and isNewHigh) ? Sell_in_Bear : false, title = 'D/W PT in bearish HT', style = shape.arrowdown, color = color.red, location = location.abovebar, display = display.all - display.status_line)
plotshape((enablebearRSIalerts and Panicdump and isNewLow) ? 1 : na, title = 'Capitulation dump bottom?', style = shape.xcross, color = color.fuchsia, location = location.belowbar, display = display.all - display.status_line)

if Buy_in_Bear and barstate.isconfirmed and enablebearRSIalerts and isNewLow
    alert('D/W Oversold Entry Target in bearish SUPER Trend. Look for divergences and scale in!', alert.freq_once_per_bar_close)

if OB_in_Bear and barstate.isconfirmed and enablebearRSIalerts and isNewHigh
    alert('Overbought Profit Target in bearish SUPER Trend. Look for divergences and take profit!', alert.freq_once_per_bar_close)

if Sell_in_Bear and barstate.isconfirmed and enablebearRSIalerts and isNewHigh
    alert('D/W Overbought Profit Target in bearish SUPER Trend. Look for divergences and take profit!', alert.freq_once_per_bar_close)

if Panicdump and barstate.isconfirmed and enablebearRSIalerts and isNewLow
    alert('Capitulation dump bottom?. Read tooltip about capitulation dumps. Look for divergences, then ALL IN and SUPER! ;)', alert.freq_once_per_bar_close)

// VOLUME SPIKE

enablevolumealerts  = input.bool(true, "Volume Spike alert", group = 'Volume Spike', tooltip = 'By far the most profitable indicator to master alongside everything else. Volume Spikes usually means either the order book gets cleared and reversal is coming, or that the price will continue in the direction of the alert. If you learn to figure out which way and have a good risk to reward ratio, this alert can be traded on all timeframes. ATTENTION: This alert runs in realtime, otherwise we could miss the moves.') 

VolxDump = input.float(2.2, title = 'Volume MA dump multiplier', minval = 1, maxval = 100, step = 0.1, group = 'Volume Spike', tooltip = 'Default value: 2.2. Plots alert (yellow plus sign below bar) if volume is 2.2 times higher than the average volume 20 periods back. Normally indicates the start of a crash in the beginning of a dump or a potential bottom at the end of the dump.', display = display.none)
VolxPump = input.float(2.6, title = 'Volume MA pump multiplier', minval = 1, maxval = 100, step = 0.1, group = 'Volume Spike', tooltip = 'Default value: 2.6. Plots alert (yellow plus sign above bar) if volume is 2.6 times higher than the average volume 20 periods back. Normally indicates the start of a rally in the beginning of a pump or a potential top at the end of the pump.', display = display.none)
VolMAlenght = input(20, title = 'Volume MA lenght', group = 'Volume Spike', tooltip = 'Default period: 20. Works best for me in both the crypto and stock market.', display = display.none)

VolMA = ta.sma(volume, VolMAlenght)
VolDump = volume > VolMA * VolxDump and close < open[1]
VolPump = volume > VolMA * VolxPump and close > open[1]

plotshape((VolDump and enablevolumealerts) ? 1 : na, title = 'Volume Dump Alert', style = shape.cross, color = color.yellow, location = location.belowbar, display = display.all - display.status_line)
plotshape((VolPump and enablevolumealerts) ? 1 : na, title = 'Volume Pump Alert', style = shape.cross, color = color.yellow, location = location.abovebar, display = display.all - display.status_line)

if VolDump and enablevolumealerts
    alert('Volume Spike / Start of crash or local bottom!. Read tooltip about Volume Dumps.', alert.freq_once_per_bar)

if VolPump and enablevolumealerts
    alert('Volume Spike / Start of bull run or local top!. Read tooltip about Volume Pumps.', alert.freq_once_per_bar)

// $ VOLUME BOX

showBox       = input.bool(true, "Show $ volume box", tooltip='Watch out for cryptos and stocks with low liquidity, the spread can be very high and it can be hard to find buyers when you want to exit.', group = '$ Dollar Volume Box')
cornerOption  = input.string("Bottom Left", "Box position", group = '$ Dollar Volume Box', options = ["Top Left", "Top Right", "Bottom Left", "Bottom Right"], display = display.none)
textSize      = input.string("Small", "Text size", options=["Tiny","Small","Normal","Large"], group = '$ Dollar Volume Box', display = display.none)
avgLength     = input.int(20, "Average $ volume period", minval=1, group = '$ Dollar Volume Box', tooltip="Default value: 20 days", display = display.none)
textColor     = input.color(color.white, "Text color", group = '$ Dollar Volume Box')
boxColor      = input.color(color.new(#1e88e5, 88), "Box background color", group = '$ Dollar Volume Box')

float dailyDollarVolume = request.security(syminfo.tickerid, "D", hlcc4 * volume)
float avgDollarVolume = request.security(syminfo.tickerid, "D", ta.sma(hlcc4 * volume, avgLength))

f_formatVol(float vol) =>
    vol >= 1e9 ? str.format("{0,number,#.##}B", vol / 1e9) : 
     vol >= 1e6 ? str.format("{0,number,#.##}M", vol / 1e6) : 
     vol >= 1e3 ? str.format("{0,number,#.##}K", vol / 1e3) : 
     str.format("{0,number,#,##0}", vol)

sz = textSize == "Tiny"   ? size.tiny :
     textSize == "Small"  ? size.small :
     textSize == "Large"  ? size.large : size.normal

dailyText = na(dailyDollarVolume) ? "Loading..." : f_formatVol(dailyDollarVolume)
avgText = na(avgDollarVolume) ? "Loading..." : f_formatVol(avgDollarVolume)

textContent = "Today's $Vol\n" + dailyText + "\n\n" + "Avg $Vol (" + str.tostring(avgLength) + "D)\n" + avgText

tablePos =
     cornerOption == "Top Left"     ? position.top_left :
     cornerOption == "Top Right"    ? position.top_right :
     cornerOption == "Bottom Left"  ? position.bottom_left :
                                      position.bottom_right

var table volTable = table.new(tablePos, 1, 1, bgcolor=boxColor, border_width = 1)

if barstate.islast
    if showBox
        table.cell(volTable, 0, 0, text = textContent, text_color = textColor, text_size = sz)
    else
        table.clear(volTable, 0, 0)

// FINAL NOTE

FinalNote = input.bool(defval = false, title = 'I understand!', tooltip = 'Stocks or crypto in All Time High or All Time Low means TA gets thrown out the window. There\'s no support or resistances to be found. The alerts can be meaningless in those conditions. Huge news, bad earnings and other abnormal events could also have this effect but can f.e. find support at very long MA\'s (MA 300-500) on the W & M timeframe.', group = 'Technical Analysis is not an exact science and does NOT predict the future price. It\'s used to calculate probabilities and most likely outcomes out of historical data. A solid risk management is the key for success!')

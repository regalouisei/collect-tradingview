// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Uncle_the_shooter

//@version=6
indicator("RSI Momentum SuperTrend", overlay = false)

// INPUTS
rsiLen       = input.int(14,  "RSI Length")
smooth       = input.int(10,  "RSI Smoothing")
atrLen       = input.int(100, "ATR Length (on RSI)")
scale        = input.float(5.0, "ATR Multiplier", step = 0.1)

showRawRSI   = input.bool(true, "Show Raw RSI")
rawRSIcolor  = input.color(color.rgb(165, 165, 164), "Raw RSI Color")

// TREND COLORS
colorUp      = input.color(color.rgb(6, 162, 47),   "Trend UP Color")
colorDown    = input.color(color.rgb(207, 23, 23),  "Trend DOWN Color")

colorCandles = input.bool(true, "Color Candles by Trend")

// OB/OS ZONE TRANSPARENCY
obosAlpha    = input.int(70, "OB/OS Zones Fill Transparency", minval=50, maxval=98, step=1)


// FOG ON PRICE INPUTS
show_fog     = input.bool(true,  "Show Fog on Price Chart", group="FOG ON PRICE")
offset_mult  = input.float(0.7,  "Fog Height × ATR(14)", minval=0.1, step=0.1, group="FOG ON PRICE")
base_transp  = input.int(82,     "Base Transparency", minval=40, maxval=98, group="FOG ON PRICE")
transp_inc   = input.int(5,      "Transparency Increment per Layer", minval=1, maxval=12, group="FOG ON PRICE")



// CALCULATIONS
rsiRaw = ta.rsi(close, rsiLen)
rsi    = ta.ema(rsiRaw, smooth)

rsiChange = math.abs(rsi - rsi[1])
rawAtr    = ta.rma(rsiChange, atrLen)
avgAtr    = ta.sma(rawAtr, 50)
normAtr   = avgAtr > 0 ? rawAtr / avgAtr : 1

basicUpper = math.min(100, rsi + normAtr * scale)
basicLower = math.max(0,   rsi - normAtr * scale)

var float finalUpper = na
var float finalLower = na

finalUpper := na(finalUpper[1]) ? basicUpper :
     basicUpper < finalUpper[1] or rsi[1] > finalUpper[1] ? basicUpper : finalUpper[1]

finalLower := na(finalLower[1]) ? basicLower :
     basicLower > finalLower[1] or rsi[1] < finalLower[1] ? basicLower : finalLower[1]

var bool  trendUp = true
var float st      = na

if trendUp
    if rsi < finalLower
        trendUp := false
        st      := finalUpper
    else
        st := finalLower
else
    if rsi > finalUpper
        trendUp := true
        st      := finalLower
    else
        st := finalUpper

trendColor = trendUp ? colorUp : colorDown



// FOG ON PRICE
atr_price = ta.atr(14)
offset    = atr_price * offset_mult
mid       = hl2

// BULL FOG
b0 = plot(show_fog and trendUp  ? mid           : na, color=color.new(colorUp,100),   display=display.none, force_overlay=true)
b1 = plot(show_fog and trendUp  ? mid - offset   : na, color=color.new(colorUp,100),   display=display.none, force_overlay=true)
b2 = plot(show_fog and trendUp  ? mid - 2*offset : na, color=color.new(colorUp,100),   display=display.none, force_overlay=true)
b3 = plot(show_fog and trendUp  ? mid - 3*offset : na, color=color.new(colorUp,100),   display=display.none, force_overlay=true)
b4 = plot(show_fog and trendUp  ? mid - 4*offset : na, color=color.new(colorUp,100),   display=display.none, force_overlay=true)
b5 = plot(show_fog and trendUp  ? mid - 5*offset : na, color=color.new(colorUp,100),   display=display.none, force_overlay=true)

// BEAR FOG
d0 = plot(show_fog and not trendUp ? mid           : na, color=color.new(colorDown,100), display=display.none, force_overlay=true)
d1 = plot(show_fog and not trendUp ? mid + offset   : na, color=color.new(colorDown,100), display=display.none, force_overlay=true)
d2 = plot(show_fog and not trendUp ? mid + 2*offset : na, color=color.new(colorDown,100), display=display.none, force_overlay=true)
d3 = plot(show_fog and not trendUp ? mid + 3*offset : na, color=color.new(colorDown,100), display=display.none, force_overlay=true)
d4 = plot(show_fog and not trendUp ? mid + 4*offset : na, color=color.new(colorDown,100), display=display.none, force_overlay=true)
d5 = plot(show_fog and not trendUp ? mid + 5*offset : na, color=color.new(colorDown,100), display=display.none, force_overlay=true)

// GRADIENT FILLS
fill(b0, b1, color.new(colorUp,   base_transp                      ), title="Bull Fog 1")
fill(b1, b2, color.new(colorUp,   base_transp + 1 * transp_inc     ), title="Bull Fog 2")
fill(b2, b3, color.new(colorUp,   base_transp + 2 * transp_inc     ), title="Bull Fog 3")
fill(b3, b4, color.new(colorUp,   base_transp + 3 * transp_inc     ), title="Bull Fog 4")
fill(b4, b5, color.new(colorUp,   base_transp + 4 * transp_inc     ), title="Bull Fog 5")

fill(d0, d1, color.new(colorDown, base_transp                      ), title="Bear Fog 1")
fill(d1, d2, color.new(colorDown, base_transp + 1 * transp_inc     ), title="Bear Fog 2")
fill(d2, d3, color.new(colorDown, base_transp + 2 * transp_inc     ), title="Bear Fog 3")
fill(d3, d4, color.new(colorDown, base_transp + 3 * transp_inc     ), title="Bear Fog 4")
fill(d4, d5, color.new(colorDown, base_transp + 4 * transp_inc     ), title="Bear Fog 5")



// OSCILLATOR
barcolor(colorCandles ? trendColor : na)

pRsi = plot(rsi, "RSI", color=trendColor, linewidth=2)
pST  = plot(st,  "SuperTrend", color=trendColor, linewidth=1)

fill(pRsi, pST, color=color.new(trendColor, 80), title="RSI-ST Area")

plot(rsi, "", color.new(trendColor, 75), 3)
plot(rsi, "", color.new(trendColor, 90), 5)

obLevel = 70
osLevel = 30
hline(50,      "Neutral", color.new(color.gray, 50), hline.style_dashed)
hline(obLevel, "Overbought", color=colorDown, linestyle=hline.style_dashed, linewidth=1)  
hline(osLevel, "Oversold",   color=colorUp,   linestyle=hline.style_dashed, linewidth=1) 

fill(plot(100, display=display.none), plot(obLevel, display=display.none),
     color=color.new(colorDown, obosAlpha), title="Overbought Zone")

fill(plot(osLevel, display=display.none), plot(0, display=display.none),
     color=color.new(colorUp, obosAlpha), title="Oversold Zone")

plot(showRawRSI ? rsiRaw : na, "Raw RSI", color=rawRSIcolor, linewidth=2)



// ALERTS
trendUpChangeUp   = trendUp and not trendUp[1]   
trendUpChangeDown = not trendUp and trendUp[1]   

alertcondition(trendUpChangeUp,   title="Trend UP",   message="RSI Momentum SuperTrend: Trend changed to UP")
alertcondition(trendUpChangeDown, title="Trend DOWN", message="RSI Momentum SuperTrend: Trend changed to DOWN")

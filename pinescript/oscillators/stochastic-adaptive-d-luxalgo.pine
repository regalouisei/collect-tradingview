// This work is licensed under a Attribution-NonCommercial-ShareAlike 4.0 International (CC BY-NC-SA 4.0) https://creativecommons.org/licenses/by-nc-sa/4.0/
// Â© LuxAlgo

//@version=6
indicator("Stochastic Adaptive %D [LuxAlgo]", "LuxAlgo - Stochastic Adaptive %D", false)

//---------------------------------------------------------------------------------------------------------------------}
// Inputs
//---------------------------------------------------------------------------------------------------------------------{
stochGroup          = 'Stochastic Settings'
kLengthInput        = input.int(20, 'Stochastic Length', minval = 1, group = stochGroup)
dSmoothingInput     = input.int(9, '%K Smoothing', minval = 1, group = stochGroup, tooltip = 'Determines the smoothing applied to the raw %K values to produce the standard %D line.')
preSmoothInput      = input.int(20, 'Price Pre-Smoothing', minval = 1, group = stochGroup, tooltip = 'Smooths the High, Low, and Close sources before calculation.')

amaGroup            = 'Adaptive Smoothing Settings'
attenuationInput    = input.float(2.0, 'Attenuation Factor', minval = 0.1, step = 0.1, group = amaGroup, tooltip = 'Controls the intensity of the stochastic input and the smoothing speed of the Adaptive %D.')

colorGroup          = 'Colors'
stochColorInput     = input.color(#5b9cf6, 'Standard %D Color', group = colorGroup)
amaColorInput       = input.color(#ff5d00, 'Adaptive %D Color', group = colorGroup)
bullColorInput      = input.color(#089981, 'Bullish Color',     group = colorGroup)
bearColorInput      = input.color(#f23645, 'Bearish Color',     group = colorGroup)

//---------------------------------------------------------------------------------------------------------------------}
// Calculations
//---------------------------------------------------------------------------------------------------------------------{
// Price Pre-Smoothing
sHigh  = ta.sma(high, preSmoothInput)
sLow   = ta.sma(low, preSmoothInput)
sClose = ta.sma(close, preSmoothInput)

// Standard Stochastic calculation
// Raw stochastic is %K, smoothed is %D, both dampened by 2
stochRaw = ta.stoch(sClose, sHigh, sLow, kLengthInput)
stoch    = 50 + (stochRaw - 50) / 2
stochD   = 50 + (ta.sma(stochRaw, dSmoothingInput) - 50) / 2

// Adaptive Moving Average (AMA) logic applied to %D
float alpha  = (math.abs(stochD - 50) / 100) / attenuationInput
float srcAma = (stochD - 50) / attenuationInput + 50

var float amaValue = 50.0
amaValue := nz(amaValue[1]) + alpha * (srcAma - nz(amaValue[1]))

// Difference calculation (Centered at 50)
float diffPlotValue = 50 + (stochD - amaValue) * 2

//---------------------------------------------------------------------------------------------------------------------}
// Visuals
//---------------------------------------------------------------------------------------------------------------------{
// Levels
hline(80, 'Overbought', color.new(chart.fg_color, 50), linestyle = hline.style_dashed)
hline(50, 'Midline',    color.new(chart.fg_color, 80), linestyle = hline.style_dotted)
hline(20, 'Oversold',   color.new(chart.fg_color, 50), linestyle = hline.style_dashed)

// Midline plot for fills
midPlotId = plot(50, 'Midline Plot', display = display.none)

// Difference Oscillator Line & Fill
diffPlotId = plot(diffPlotValue, 'Difference Oscillator Line', color.new(chart.fg_color, 50), 1)

fill(diffPlotId, midPlotId, 
     top_value    = math.max(diffPlotValue, 50), 
     bottom_value = math.min(diffPlotValue, 50),
     top_color    = diffPlotValue > 50 ? color.new(bullColorInput, 50) : color.new(chart.bg_color, 100),
     bottom_color = diffPlotValue > 50 ? color.new(chart.bg_color, 100) : color.new(bearColorInput, 50),
     title        = 'Difference Oscillator Fill')

// Standard Stochastic Plot (%D)
plot(stochD, 'Standard %D', stochColorInput, 1, plot.style_line, linestyle = plot.linestyle_dotted)

// Adaptive Stochastic Plot (Adaptive %D)
plot(amaValue, 'Adaptive %D', amaColorInput, 1, linestyle = plot.linestyle_dashed)

//---------------------------------------------------------------------------------------------------------------------}

//@version=6
// © Quantura
// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/

indicator('Quantura - Trendchange Zones', overlay = true, behind_chart = false)

// === RSI Settings ===
RSI_Calculations_Group          = "RSI"
RSI_Length                      = input.int(14, title = "RSI Length", group = RSI_Calculations_Group)
RSI_Upper_Level_Calculation     = input.int(70, title = "Upper Level", group = RSI_Calculations_Group)
RSI_Lower_Level_Calculation     = input.int(30, title = "Lower Level", group = RSI_Calculations_Group)

// === Overlay Settings ===
Overlays_Group                  = "Overlays"
RSI_Signal_Color_Bullish        = input.color(color.new(#75fbfe, 0), title = "Bullish Color", group = Overlays_Group)
RSI_Signal_Color_Bearish        = input.color(color.new(#f23645, 0), title = "Bearish Color", group = Overlays_Group)

// === Signal Settings ===
Signals_Group                   = "Signals"
Show_RSI_Signals                = input.bool(true, title = "Signals", group = Signals_Group)
Show_RSI_Box                    = input.bool(true, title = "Zones", group = Signals_Group)


// === Zone Function ===
var array<box> Zone_Boxes = array.new_box(0)

draw_zone_box(Start_Bar, End_Bar, High_Price, Low_Price, Border_Color, Color) =>
    if Show_RSI_Box
        Zone_Box = box.new(left = Start_Bar, top = High_Price, right = End_Bar, bottom = Low_Price, border_color = color.new(Border_Color, 0), border_width = 2, bgcolor = color.new(Color, 85))
        array.push(Zone_Boxes, Zone_Box)


// === RSI Calculation ===
RSI_Calculation = ta.rsi(close, RSI_Length)


// === Zone Variables ===
var color Box_Color = na
var float Zone_High = na
var float Zone_Low = na
var int Zone_Start_Bar = na
var bool In_Overbought_Zone = false


// === Overbought Zone Calculation ===
if RSI_Calculation > RSI_Upper_Level_Calculation
    if not In_Overbought_Zone
        In_Overbought_Zone := true
        Zone_High := high
        Zone_Low := low
        Zone_Start_Bar := bar_index
    Zone_High := math.max(Zone_High, high)
    Zone_Low := math.min(Zone_Low, low)

var int Last_Box_End_Bar = na
var float Last_Zone_High = na
var float Last_Zone_Low = na

if RSI_Calculation <= RSI_Upper_Level_Calculation and In_Overbought_Zone
    draw_zone_box(Zone_Start_Bar, bar_index, Zone_High, Zone_Low, color.new(RSI_Signal_Color_Bearish, 33), RSI_Signal_Color_Bearish)
    Last_Box_End_Bar := bar_index
    Box_Color := RSI_Signal_Color_Bearish
    Last_Zone_High := Zone_High
    Last_Zone_Low := Zone_Low
    In_Overbought_Zone := false
    Zone_High := na
    Zone_Low := na
    Zone_Start_Bar := na


// === Oversold Zone Calculation ===
var bool In_Oversold_Zone = false

if RSI_Calculation < RSI_Lower_Level_Calculation
    if not In_Oversold_Zone
        In_Oversold_Zone := true
        Zone_High := high
        Zone_Low := low
        Zone_Start_Bar := bar_index
    Zone_High := math.max(Zone_High, high)
    Zone_Low := math.min(Zone_Low, low)

if RSI_Calculation >= RSI_Lower_Level_Calculation and In_Oversold_Zone
    draw_zone_box(Zone_Start_Bar, bar_index, Zone_High, Zone_Low, color.new(RSI_Signal_Color_Bullish, 33), RSI_Signal_Color_Bullish)
    Last_Box_End_Bar := bar_index
    Box_Color := RSI_Signal_Color_Bullish
    Last_Zone_High := Zone_High
    Last_Zone_Low := Zone_Low
    In_Oversold_Zone := false
    Zone_High := na
    Zone_Low := na
    Zone_Start_Bar := na


// === Signal Logic ===
var bool New_Label_Bullish = false
var bool New_Label_Bearish = false
var bool RSI_Signal = false

if not na(Last_Box_End_Bar) and bar_index == Last_Box_End_Bar and Show_RSI_Signals
    if Box_Color == RSI_Signal_Color_Bearish
        New_Label_Bullish := false
        New_Label_Bearish := true
        RSI_Signal := true
        RSI_Signal := false
    else
        New_Label_Bearish := false
        New_Label_Bullish := true
        RSI_Signal := true
        RSI_Signal := false


// === Plot Labels ===
plotshape(series = Show_RSI_Signals ? New_Label_Bullish : false, title = "Bullish Signal", text = '▲', style = shape.labelup, location = location.belowbar, color = RSI_Signal_Color_Bullish, textcolor = color.white, size = size.tiny, editable = false)

plotshape(series = Show_RSI_Signals ? New_Label_Bearish : false, title = "Bearish Signal", text = '▼', style = shape.labeldown, location = location.abovebar, color = RSI_Signal_Color_Bearish, textcolor = color.white, size = size.tiny, editable = false)


// === Reset ===
Last_Box_End_Bar := na
New_Label_Bullish := false
New_Label_Bearish := false

// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © PaulWegelin

//@version=6
indicator("RSI Median Deviation", max_labels_count = 500)
import TradingView/ta/11

//              ╔══════════════════════════╗              //
//              ║         SETTINGS         ║              //
//              ╚══════════════════════════╝              //

var string CS       = "RSi Settings", var string CSS = "RSI Smoothing Settings"

src                 = input.source(close,       "RSI Source",                                       group = CS)
rsiLength           = input.int(10,             "RSI Length",                                       group = CS)

longTH              = input.int(60,             "Long Threshold",                                   group = CS)
shortTH             = input.int(47,             "Short Threshold",                                  group = CS)

length              = input.int(28,             "Median Length",                                    group= CSS ,minval = 1, step=1)
sdLength            = input.int(27,             "SD Length",                                        group = CSS)
rsiMA               = input.string("TEMA",      "RSI MA-Type",                                      group = CSS,  options = ["SMA", "EMA", "DEMA", "TEMA", "WMA", "VWMA","HMA" , "ALMA"])
smoothLEN           = input.int(27,             "RSI MA Length",                                    group = CSS)

bandLEN             = input.int(37,             "RSI MA Band Length",                               group = "MA Bands")
rsiMA2              = input.string("WMA",       "RSI Band MA-Type",                                 group = "MA Bands",  options = ["SMA", "EMA", "DEMA", "TEMA", "WMA", "VWMA", "HMA", "ALMA"])

//              ╔══════════════════════════╗              //
//              ║     DYNAMIC MA INPUT     ║              //
//              ╚══════════════════════════╝              //

ma(type, src, len) =>
    float result = na
    if type == "SMA"  
        result := ta.sma(src, len)
        result
    else if type == "EMA"  
        result := ta.ema(src, len)
        result
    else if type == "DEMA"  
        result := ta.dema(src, len)
        result
    else if type == "TEMA"  
        result := ta.tema(src, len)
        result
    else if type == "WMA"  
        result := ta.wma(src, len)
        result
    else if type == "VWMA"
        result := ta.vwma(src, len)
        result
    else if type == "HMA"  
        result := ta.hma(src, len)
        result
    else if type == "ALMA"
        result := ta.alma(src, len, 0, 6)
        result
    result

//              ╔══════════════════════════╗              //
//              ║       Calculations       ║              //
//              ╚══════════════════════════╝              //

rsi         = ta.rsi(src, rsiLength)
dynRSIfl    = ma(rsiMA, rsi, smoothLEN)
median      = ta.percentile_nearest_rank(rsi, length,50)

//              ╔══════════════════════════╗              //
//              ║    Standard Deviation    ║              //
//              ╚══════════════════════════╝              //

stdev       = ta.stdev(rsi , sdLength)
u1          = median + 1 * stdev
u2          = median + 2 * stdev

d1          = median - 1 * stdev
d2          = median - 2 * stdev

bandL       = close > d1
bandS       = close < u1

dynRSIfl2    = ma(rsiMA2, rsi, bandLEN)
dynBandUP   = dynRSIfl2 + 2 * stdev
dynBandDOWN = dynRSIfl2 - 2 * stdev

//              ╔══════════════════════════╗              //
//              ║     Trend Condition      ║              //
//              ╚══════════════════════════╝              //

L           = rsi > longTH
S           = rsi < shortTH

var trend = 0

if L and not S 
    trend := 1

if S 
    trend := -1

//              ╔══════════════════════════╗              //
//              ║      Colors & Bools      ║              //
//              ╚══════════════════════════╝              //

//Switches
Calc                 = input.string("Median SD", title="OBOS Calculations", group="Switches", 
     options = ["Median SD", "Moving Average SD"], display = display.none)


Col                 = input.string("SciFi", title="Color Settings", group="Switches", 
     options = ["Original", "Classic", "SciFi", "Ocean","Lush", "Evening", "Ultra Violet", "Monochrom", "Ice"], display = display.none)


[color_up, color_dn]=  switch   Col
    "Original"      => [#00d400, #b10000]
    "Classic"       => [#00ffdd, #ff0095]
    "SciFi"         => [#00FFFF, #FF00FF] 
    "Ocean"         => [#10cab8, #2196f3]
    "Lush"          => [#5ffae0, #c22ed0]
    "Evening"       => [#ffbb00, #770737]
    "Ultra Violet"  => [#9618f7, #ff0078]
    "Monochrom"     => [#dee2e6, #495057]
    "Ice"           => [#049ef7, #ffffff]

color col_up0       = color_up
color col_up25      = color.new(color_up, 25)
color col_up50      = color.new(color_up, 50)
color col_up75      = color.new(color_up, 75)
color col_up80      = color.new(color_up, 80)
color col_dn0       = color_dn
color col_dn25      = color.new(color_dn, 25)
color col_dn50      = color.new(color_dn, 50)
color col_dn75      = color.new(color_dn, 75)
color col_dn80      = color.new(color_dn, 80)

//              ╔══════════════════════════╗              //
//              ║     OS/OB Conditions     ║              //
//              ╚══════════════════════════╝              //

//OB & OS switches and bools
OnOff               = input.bool(true,          "Turn BG On/Off",                                  group = "OS and OB")
criticalOSOB        = input.bool(true,          "Extreme OS/OB Signal",                            group = "OS and OB")
barswitch           = input.bool(false,         "Barcolor On/Off",                                 group = "OS and OB")


//OB & OS calculations
medianOS            = (rsi < d2) and rsi < 42
medianOB            = (rsi > u2)

maOS                = (rsi < dynBandDOWN)
maOB                = (rsi > dynBandUP)

medianEXTREMES      = medianOS ? col_up80 : medianOB ? col_dn80 : na
maEXTREMES          = maOS     ? col_up80 : maOB     ? col_dn80 : na

oppertunityZone     = rsi < 25 
dangerZone          = rsi > 85

OSOBswitch =  switch Calc
    "Median SD"            => medianEXTREMES
    "Moving Average SD"    => maEXTREMES

OBOS_COL            = OnOff ? OSOBswitch : na
barcolor            = trend >  0 ? col_up50 : trend < 0 ?  col_dn50 : na
chartCol            = barswitch ? barcolor : na

//              ╔══════════════════════════╗              //
//              ║           PLOTS          ║              //
//              ╚══════════════════════════╝              //

//RSI Plots
rsi_p               = plot(rsi, linewidth = 2, color = trend >  0 ? col_up0 : trend < 0 ?  col_dn0 : na)                                        // RSI plot                                       

rsi_dny             = plot(dynRSIfl, linewidth = 1, color = dynRSIfl < median ? col_dn50 : dynRSIfl > median ? col_up50 : color.gray)         // Dynamic RSI plot

up                  = plot(longTH, display = display.none, editable = false)
lw                  = plot(shortTH, display = display.none, editable = false)

//Zone and Line Plots
U2                  = plot(dynBandUP, color = col_dn25, linewidth = 2)                                                                          // Dynamic MA upper Band
L1                  = plot(dynBandDOWN, color = col_up25, linewidth = 2)                                                                        // Dynamic MA lower Band

//Median and Bands
maRSI               = plot(median, title = "smoothed RSI", linewidth = 1,color = dynRSIfl < median ? col_dn25 : dynRSIfl > median ? col_up25 : color.gray)
maRSIband2          = plot(u2, color = col_dn75, linewidth = 2, display = display.all)
maRSIband5          = plot(d2, color = col_up75, linewidth = 2, display = display.all)

//              ╔══════════════════════════╗              //
//              ║ Fill, Shapes and Candles ║              //
//              ╚══════════════════════════╝              //

//Shapes, fills and background
fill(maRSIband2, maRSI, u2, median, col_dn75, color.new(chart.bg_color, 75), "Second Band", display = display.all)
fill(maRSI, maRSIband5, median, d2, color.new(chart.bg_color, 75), col_up75, "Second Band", display = display.all)

plotshape(oppertunityZone and criticalOSOB, "OS", style = shape.diamond, location = location.bottom, color = col_up50, size = size.tiny)
plotshape(dangerZone and criticalOSOB,      "OB", style = shape.diamond, location = location.top,    color = col_dn50, size = size.tiny)

bgcolor(OBOS_COL, force_overlay = false)

plotcandle(open, high, low, close, 'BarColor', color = chartCol, bordercolor = chartCol, wickcolor = chartCol, force_overlay = true)

//              ╔══════════════════════════╗              //
//              ║          Alerts          ║              //
//              ╚══════════════════════════╝              //

// Long (Bullish) Condition Alert.
alertcondition(trend > 0,  title = "RSI Long",  message = "RSI Median Deviation above threshold on: {{exchange}}:{{ticker}}")
alertcondition(medianOS,  title = "RSI Median Deviation Bands oversold",  message = "RSI Median Deviation Bands oversold on: {{exchange}}:{{ticker}}")
alertcondition(maOS,  title = "RSI Median Deviation MA oversold",  message = "RSI Median Deviation oversold MA on: {{exchange}}:{{ticker}}")
alertcondition(dynRSIfl > median,  title = "RSI Median Deviation increasing momentum",  message = "RSI Median Deviation increasing momentum on: {{exchange}}:{{ticker}}")

// Short/Cash (Bearish) Condition Alert.
alertcondition(trend < 0,  title = "RSI Short",  message = "RSI Median Deviation below threshold on: {{exchange}}:{{ticker}}")
alertcondition(medianOB,  title = "RSI Median Deviation Bands overbought",  message = "RSI Median Deviation Bands overbought on: {{exchange}}:{{ticker}}")
alertcondition(maOB,  title = "RSI Median Deviation MA overbought",  message = "RSI Median Deviation overbought MA on: {{exchange}}:{{ticker}}")
alertcondition(dynRSIfl < median,  title = "RSI Median Deviation slowing momentum",  message = "RSI Median Deviation slowing momentum on: {{exchange}}:{{ticker}}")

//:)

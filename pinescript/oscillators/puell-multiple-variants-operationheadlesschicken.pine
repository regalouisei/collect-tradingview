// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © OperationHeadLessChicken

//@version=6
indicator("Puell Multiple Variants [OperationHeadLessChicken]", max_labels_count = 500, overlay = false)

const int INT_MAX = 2147483647
const int INT_MIN = -2147483648

const color OVERVALUED_COLOUR = color.new(color.red, 66)
const color UNDERVALUED_COLOUR = color.new(color.green, 66)

var int FIRST_HALVING_TS = timestamp(2012, 11, 28)
var int SECOND_HALVING_TS = timestamp(2016, 7, 9)
var int THIRD_HALVING_TS = timestamp(2020, 5, 11)
var int FOURTH_HALVING_TS = timestamp(2024, 4, 20)
var array<int> HALVING_EVENTS = array.from(FIRST_HALVING_TS, SECOND_HALVING_TS, THIRD_HALVING_TS, FOURTH_HALVING_TS)

const string MAIN_SWITCHES_GROUP = "Main switches"

const string PUELL_ORIGINAL_GROUP = "Puell original"
const string PUELL_ORIGINAL_THRESHOLDS_INLINE = "poti"

const string PUELL_HALVING_GROUP = "Puell halving corrected"
const string PUELL_HALVING_THRESHOLDS_INLINE = "phci"

const string REVENUE_RSI_GROUP = "Revenue RSI"
const string REVENUE_RSI_THRESHOLDS_INLINE = "prsi"

const string HALVING_EVENTS_GROUP = "Halving events"

const string UNDERVALUED_THRESHOLD = "Undervalued threshold"
const string OVERVALUED_THRESHOLD = "Overvalued threshold"
const string UNDERVALUED_COLOUR_STR = "Undervalued colour"
const string OVERVALUED_COLOUR_STR = "Overvalued colour"
const string LINE_COLOUR = "Line colour"

bool showPuellOriginalInput = input.bool(true, "Show Puell original", group = MAIN_SWITCHES_GROUP)
bool showRawRevenueDataInput = input.bool(false, "Show raw revenue data", group = MAIN_SWITCHES_GROUP)
bool showPuellHalvingInput = input.bool(true, "Show Puell halving corrected", group = MAIN_SWITCHES_GROUP)
bool showRevenueRsiInput = input.bool(false, "Show Revenue RSI", group = MAIN_SWITCHES_GROUP)
bool showBuySellBackground = input.bool(true, "Show overvalued/undervalued background", group = MAIN_SWITCHES_GROUP)
bool showHalvingEventsInput = input.bool(true, "Show halving events", group = MAIN_SWITCHES_GROUP)

float puellOriginalUndervaluedThresholdInput = input.float(0.5, UNDERVALUED_THRESHOLD, group = PUELL_ORIGINAL_GROUP, inline = PUELL_ORIGINAL_THRESHOLDS_INLINE, active = showPuellOriginalInput)
float puellOriginalOvervaluedThresholdInput = input.float(5.0, OVERVALUED_THRESHOLD, group = PUELL_ORIGINAL_GROUP, inline = PUELL_ORIGINAL_THRESHOLDS_INLINE, active = showPuellOriginalInput)
color puellOriginalLineColourInput = input.color(color.white, LINE_COLOUR, group = PUELL_ORIGINAL_GROUP, active = showPuellOriginalInput)
color puellOrigOvervaluedColourInput = input.color(OVERVALUED_COLOUR, OVERVALUED_COLOUR_STR, group = PUELL_ORIGINAL_GROUP, active = showPuellOriginalInput)
color puellOrigUndervaluedColourInput = input.color(UNDERVALUED_COLOUR, UNDERVALUED_COLOUR_STR, group = PUELL_ORIGINAL_GROUP, active = showPuellOriginalInput)

float puellHalvingUndervaluedThresholdInput = input.float(0.5, UNDERVALUED_THRESHOLD, group = PUELL_HALVING_GROUP, inline = PUELL_HALVING_THRESHOLDS_INLINE, active = showPuellHalvingInput)
float puellHalvingOvervaluedThresholdInput = input.float(12.49, OVERVALUED_THRESHOLD, group = PUELL_HALVING_GROUP, inline = PUELL_HALVING_THRESHOLDS_INLINE, active = showPuellHalvingInput)
float correctionFactor = input.float(1.63, "Correction factor", group = PUELL_HALVING_GROUP, active = showPuellHalvingInput, step = 0.1)
color puellHalvingLineColourInput = input.color(color.new(color.yellow, 0), LINE_COLOUR, group = PUELL_HALVING_GROUP, active = showPuellHalvingInput)
color puellHalvingOvervaluedColourInput = input.color(OVERVALUED_COLOUR, OVERVALUED_COLOUR_STR, group = PUELL_HALVING_GROUP, active = showPuellHalvingInput)
color puellHalvingUndervaluedColourInput = input.color(UNDERVALUED_COLOUR, UNDERVALUED_COLOUR_STR, group = PUELL_HALVING_GROUP, active = showPuellHalvingInput)

float revenueRsiUndervaluedThresholdInput = input.float(35.0, UNDERVALUED_THRESHOLD, group = REVENUE_RSI_GROUP, inline = REVENUE_RSI_THRESHOLDS_INLINE, active = showRevenueRsiInput)
float revenueRsiOvervaluedThresholdInput = input.float(90.0, OVERVALUED_THRESHOLD, group = REVENUE_RSI_GROUP, inline = REVENUE_RSI_THRESHOLDS_INLINE, active = showRevenueRsiInput)
int revenueRsiPeriodInput = input.int(6, "Revenue RSI period", group = REVENUE_RSI_GROUP, active = showRevenueRsiInput)
color revenueRsiLineColourInput = input.color(color.aqua, LINE_COLOUR, group = REVENUE_RSI_GROUP, active = showRevenueRsiInput)
color revenueRsiOvervaluedColourInput = input.color(OVERVALUED_COLOUR, OVERVALUED_COLOUR_STR, group = REVENUE_RSI_GROUP, active = showRevenueRsiInput)
color revenueRsiUndervaluedColourInput = input.color(UNDERVALUED_COLOUR, UNDERVALUED_COLOUR_STR, group = REVENUE_RSI_GROUP, active = showRevenueRsiInput)

color halvingEventsColourInput = input.color(color.fuchsia, "Halving events colour", group = HALVING_EVENTS_GROUP, active = showHalvingEventsInput)

// functions ============================================================================

getFirstHalvingTsOnChart() =>
    switch
        time <= FIRST_HALVING_TS => FIRST_HALVING_TS
        time <= SECOND_HALVING_TS => SECOND_HALVING_TS
        time <= THIRD_HALVING_TS => THIRD_HALVING_TS
        time <= FOURTH_HALVING_TS => FOURTH_HALVING_TS

getCurrentMultiplier() =>
    var int firstHalvingTsOnChart = getFirstHalvingTsOnChart()
    var float multiplier = na
    multiplier := 1
    for halving_ts in HALVING_EVENTS
        if halving_ts < firstHalvingTsOnChart
            continue
        if time >= halving_ts
            multiplier := multiplier * correctionFactor
    multiplier

// calculations =========================================================================
series float miningRevenue = request.security("QUANDL:BCHAIN/MIREV", "D", close[1], barmerge.gaps_off, barmerge.lookahead_on)
series float miningRevMa = request.security("QUANDL:BCHAIN/MIREV", "D", ta.sma(close, 365)[1], barmerge.gaps_off, barmerge.lookahead_on)
series float originalPuellMultiple = miningRevenue / miningRevMa

plot(showRawRevenueDataInput ? miningRevenue : na, color = color.yellow, title="Revenue")
plot(showRawRevenueDataInput ? miningRevMa : na, color = color.green, title="Revenue MA")

var float halvingCorrection = 1
halvingCorrection := getCurrentMultiplier()

series float puellHalvingCorrected = originalPuellMultiple * halvingCorrection
series float revenueRsi = ta.rsi(miningRevenue, revenueRsiPeriodInput)

// plotting =============================================================================


// Halving events
if showHalvingEventsInput
    var float bottomCoordinate = na
    bottomCoordinate := math.min(
         showPuellOriginalInput ? ta.min(originalPuellMultiple) : INT_MAX, 
         showPuellHalvingInput ? ta.min(puellHalvingCorrected) : INT_MAX, 
         showRevenueRsiInput ? ta.min(revenueRsi) : INT_MAX)
    var float topCoordinate = na
    topCoordinate := math.max(
         showPuellOriginalInput ? ta.max(originalPuellMultiple) : INT_MIN, 
         showPuellHalvingInput ? ta.max(puellHalvingCorrected) : INT_MIN,
         showRevenueRsiInput ? ta.max(revenueRsi) : INT_MIN)

    for i = 0 to HALVING_EVENTS.size() - 1
        var int halvingTs = na
        halving_ts = HALVING_EVENTS.get(i)
        if time >= halving_ts and time[1] < halving_ts
            line.new(bar_index, bottomCoordinate, bar_index, topCoordinate, xloc.bar_index, extend.both, color=halvingEventsColourInput)
            var string labelText = na
            labelText := str.format("Halving - {0}", str.format_time(halving_ts, "yyyy-MM-dd"))
            var array<label> labels = array.new<label>()
            if labels.size() > 0
                for j = 0 to labels.size() - 1
                    labels.get(j).set_y(bottomCoordinate)   //aligns previous labels with the last bottom coordinate
            labels.push(label.new(bar_index, bottomCoordinate, text=labelText, style=label.style_label_up, textcolor=color.black, color=halvingEventsColourInput, textalign=text.align_right))


// Puell original
hline(showPuellOriginalInput ? puellOriginalOvervaluedThresholdInput : na, color = puellOriginalLineColourInput, title="Puell original overvalued line")
hline(showPuellOriginalInput ? puellOriginalUndervaluedThresholdInput : na, color = puellOriginalLineColourInput, title="Puell original undervalued line")
plot(showPuellOriginalInput ? originalPuellMultiple : na, color = puellOriginalLineColourInput, title="Puell original")

var color puellOriginalBgCol = na
puellOriginalBgCol := if showPuellOriginalInput and showBuySellBackground
    originalPuellMultiple < puellOriginalUndervaluedThresholdInput ? puellOrigUndervaluedColourInput :
     originalPuellMultiple > puellOriginalOvervaluedThresholdInput ? puellOrigOvervaluedColourInput : na
bgcolor(puellOriginalBgCol, title="Puell original background")

// Puell halving corrected
hline(showPuellHalvingInput ? puellHalvingOvervaluedThresholdInput : na, color=puellHalvingLineColourInput, title="Puell corrected overvalued line")
hline(showPuellHalvingInput ? puellHalvingUndervaluedThresholdInput : na, color=puellHalvingLineColourInput, title="Puell corrected undervalued line")
plot(showPuellHalvingInput ? puellHalvingCorrected : na, color = puellHalvingLineColourInput, title="Puell corrected")

var color puellHalvingBgCol = na
puellHalvingBgCol := if showPuellHalvingInput and showBuySellBackground
    puellHalvingCorrected < puellHalvingUndervaluedThresholdInput ? puellHalvingUndervaluedColourInput :
     puellHalvingCorrected > puellHalvingOvervaluedThresholdInput ? puellHalvingOvervaluedColourInput : na
bgcolor(puellHalvingBgCol, title="Puell corrected background")

// Revenue RSI
hline(showRevenueRsiInput ? revenueRsiOvervaluedThresholdInput : na, color = revenueRsiLineColourInput, title="Revenue RSI overvalued line")
hline(showRevenueRsiInput ? revenueRsiUndervaluedThresholdInput : na, color = revenueRsiLineColourInput, title="Puell corrected undervalued line")
plot(showRevenueRsiInput ? revenueRsi : na, color = revenueRsiLineColourInput, title="Revenue RSI")

var color revenueRsiBgCol = na
revenueRsiBgCol := if showRevenueRsiInput and showBuySellBackground
    revenueRsi < revenueRsiUndervaluedThresholdInput ? revenueRsiUndervaluedColourInput :
     revenueRsi > revenueRsiOvervaluedThresholdInput ? revenueRsiOvervaluedColourInput : na
bgcolor(revenueRsiBgCol, title="Revenue RSI background")

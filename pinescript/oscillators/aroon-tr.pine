// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Tiagorocha1989
//                              ████████╗ ████████╗  ████████╗  ███████╗   ██████╗        ██████╗   ██████╗   ██████╗ ██╗   ██╗ ████████╗
//                              ╚══██╔══╝ ╚══██╔══╝  ██╔═══██║  ██╔════╝  ██╔═══██╗       ██╔══██╗ ██╔═══██╗ ██╔════╝ ██║   ██║ ██╔═══██║
//                                 ██║       ██║     ████████║  ██║ ███║  ██║   ██║       ██████╔╝ ██║   ██║ ██║      ████████║ ████████║
//                                 ██║       ██║     ██╔═══██║  ██║  ██║  ██║   ██║       ██╔══██╗ ██║   ██║ ██║      ██║   ██║ ██╔═══██║
//                                 ██║    ████████╗  ██║   ██║  ███████║  ╚██████╔╝       ██║  ██║ ╚██████╔╝ ╚██████╗ ██║   ██║ ██║   ██║
//@version=6
indicator('Aroon | TR', 'Aroon | TR →', false)
import TradingView/ta/12
//╔════════════════════════╗
//║ Choose Moving Averages ║
//╚════════════════════════╝
ma(source, length, type) =>
     type == 'EMA' ? ta.ema(source, length) :
     type == 'SMA' ? ta.sma(source, length) :
     type == 'RMA' ? ta.rma(source, length) :
     type == 'WMA' ? ta.wma(source, length) :
     type == 'VWMA' ? ta.vwma(source, length) :
     type == 'HMA' ? ta.hma(source, length) :
     na
//╔═════════════════════════╗
//║          Input          ║
//╚═════════════════════════╝
Color_Mode =        input.string('Classic', 'Color Choice', group = 'Color', options = ['Classic', 'Modern', 'Robust', 'Accented', 'Monochrome', 'Moderate', 'Aqua', 'Cosmic'])
Length_Aroon =      input.int(6, minval=1, title='Length Aroon', group='Aroon Setting')
Bool_Average =      input.bool(true,'Average Aroon', group='Aroon Setting', inline = 'Aroon')
Bool_Aroon =        input.bool(false,'Normal Aroon', group='Aroon Setting', inline = 'Aroon')
Signal_Aroon =      input.string('0', 'Entry/Exit Signal', group='Aroon Setting', options = ['0', 'Aroon MA'],tooltip = 'Choose Entry/Exit Point')
Length_MA_Aroon =   input.int(365, title = 'Length MA', group='Aroon MA', inline = 'Aroon MA')
Source_MA_Aroon =   input.string('EMA', '', group='Aroon MA', inline = 'Aroon MA', options=['EMA', 'SMA', 'RMA', 'WMA', 'VWMA', 'HMA'],tooltip = 'Moving Averages')
//╔════════════════════════╗
//║      Calculation       ║
//╚════════════════════════╝
[aroonUp, aroonDown] = ta.aroon(Length_Aroon)
Aroon = aroonUp - aroonDown
Aroon_MA =  ma(Aroon, Length_MA_Aroon, Source_MA_Aroon)
float Entry_Aroon = Signal_Aroon == 'Aroon MA' ? Aroon_MA : 0.0
isBull_Aroon = aroonUp > aroonDown
isBear_Aroon = aroonUp < aroonDown
Bull_Aroon = ta.crossover(aroonUp,aroonDown)
Bear_Aroon = ta.crossunder(aroonUp,aroonDown)
//╔════════════════════════╗
//║         Color          ║
//╚════════════════════════╝
[UpC, DnC] = switch Color_Mode
    'Classic' =>    [#008800, #ff0000]
    'Modern' =>     [#ffffff, #b721ff]
    'Robust' =>     [#ffbb00, #770737]
    'Accented' =>   [#8c5cf7, #e83e8c]
    'Monochrome' => [#e9ecef, #495057]
    'Moderate' =>   [#43a047, #e53935]
    'Aqua' =>       [#00a8e8, #f18f01]
    'Cosmic' =>     [#e83e8c, #6f2da8]
//╔════════════════════════╗
//║          Plot          ║
//╚════════════════════════╝
Plot_Entry_Aroon = plot(Bool_Average ? Entry_Aroon : na, color = Bool_Average ? color.yellow : na)
Plot_Aroon = plot(Bool_Average ? Aroon : na, color = Bool_Average ? Aroon > 0 ? color.new(UpC, 50) : color.new(DnC, 50) : na, linewidth = 3)
plot(Bool_Aroon ? aroonUp : na, color=UpC, linewidth = 3,title='+DI')
plot(Bool_Aroon ? aroonDown : na, color=DnC, linewidth = 3, title='-DI')
fill(Plot_Aroon, Plot_Entry_Aroon, 100, Entry_Aroon, top_color = color.new(UpC, 0),    bottom_color = color.new(UpC, 100), title = 'Overbought Fill')
fill(Plot_Aroon, Plot_Entry_Aroon, Entry_Aroon, -100, top_color = color.new(DnC, 100), bottom_color = color.new(DnC, 0), title = 'Oversold Fill')
fill(Plot_Aroon, Plot_Entry_Aroon, Aroon > Entry_Aroon ? Aroon : (Aroon < Entry_Aroon ? Aroon : Entry_Aroon), Aroon > Entry_Aroon ? Entry_Aroon : (Aroon < Entry_Aroon ? Entry_Aroon : Aroon), Aroon > Entry_Aroon ? color.new(UpC, 50) : (Aroon < Entry_Aroon ? color.new(DnC, 50) : #00000000), Aroon > Entry_Aroon ? #00000000 : (Aroon < Entry_Aroon ? #00000000 : #00000000),title = 'Dynamic Fill')
plotcandle(open, high, low, close, 'Bar Color', isBull_Aroon ? UpC : isBear_Aroon ? DnC : na, isBull_Aroon ? UpC : isBear_Aroon ? DnC : na, bordercolor = isBull_Aroon ? UpC : isBear_Aroon ? DnC : na, force_overlay = true)
plotshape(Bull_Aroon, 'LONG' , shape.triangleup  , location.belowbar, UpC,    0, size = size.small, force_overlay = true)
plotshape(Bear_Aroon, 'SHORT', shape.triangledown, location.abovebar, DnC,    0, size = size.small, force_overlay = true)
//╔════════════════════════╗
//║          VIEW          ║
//╚════════════════════════╝
var table Table_Aroon = table.new(position.middle_right, 1, 1, border_width = 1)
var label AroonLabel1 = na
var label AroonLabel2 = na
var label AroonLabel3 = na
if barstate.islast
    table.cell(Table_Aroon, 0, 0, 
         text = isBull_Aroon ? '⬆️ Bullish' : '⬇️ Bearish', 
         text_color = isBull_Aroon ? UpC : DnC,
         text_size = size.huge)
    if Bool_Average and not na(Aroon)
        if not na(AroonLabel1)
            label.delete(AroonLabel1)
        AroonLabel1 := label.new(bar_index + 5, Aroon, text = str.tostring(Aroon, '#.##'), color = Aroon > Entry_Aroon ? color.new(UpC, 20) : Aroon < Entry_Aroon ? color.new(DnC, 20) : color.gray, style = label.style_label_left, size = size.large, textcolor = #000000)
    else
        if not na(AroonLabel1)
            label.delete(AroonLabel1)
            AroonLabel1 := na
    if Bool_Aroon and not na(aroonUp)
        if not na(AroonLabel2)
            label.delete(AroonLabel2)
        AroonLabel2 := label.new(bar_index + 5, aroonUp, text = 'Aroon +', color = UpC, style = label.style_label_left, size = size.large, textcolor = #000000)
    else
        if not na(AroonLabel2)
            label.delete(AroonLabel2)
            AroonLabel2 := na
    if Bool_Aroon and not na(aroonDown)
        if not na(AroonLabel3)
            label.delete(AroonLabel3)
        AroonLabel3 := label.new(bar_index + 5, aroonDown, text = 'Aroon -', color = DnC, style = label.style_label_left, size = size.large, textcolor = #000000)
    else
        if not na(AroonLabel3)
            label.delete(AroonLabel3)
            AroonLabel3 := na
//╔════════════════════════╗
//║         Alerts         ║
//╚════════════════════════╝
alertcondition(Bull_Aroon, title= 'LONG', message= '{{ticker}} LONG')
alertcondition(Bear_Aroon, title= 'SHORT', message= '{{ticker}} SHORT')

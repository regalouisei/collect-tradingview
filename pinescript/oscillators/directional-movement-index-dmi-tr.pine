// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Tiagorocha1989
//                              ████████╗ ████████╗  ████████╗  ███████╗   ██████╗        ██████╗   ██████╗   ██████╗ ██╗   ██╗ ████████╗
//                              ╚══██╔══╝ ╚══██╔══╝  ██╔═══██║  ██╔════╝  ██╔═══██╗       ██╔══██╗ ██╔═══██╗ ██╔════╝ ██║   ██║ ██╔═══██║
//                                 ██║       ██║     ████████║  ██║ ███║  ██║   ██║       ██████╔╝ ██║   ██║ ██║      ████████║ ████████║
//                                 ██║       ██║     ██╔═══██║  ██║  ██║  ██║   ██║       ██╔══██╗ ██║   ██║ ██║      ██║   ██║ ██╔═══██║
//                                 ██║    ████████╗  ██║   ██║  ███████║  ╚██████╔╝       ██║  ██║ ╚██████╔╝ ╚██████╗ ██║   ██║ ██║   ██║
//@version=6
indicator('Directional Movement Index - DMI | TR', 'DMI | TR →',false)
//╔════════════════════════╗
//║ Choose Moving Averages ║
//╚════════════════════════╝
ma(source, length, type) =>
     type == 'EMA' ? ta.ema(source, length) :
     type == 'SMA' ? ta.sma(source, length) :
     type == 'RMA' ? ta.rma(source, length) :
     type == 'WMA' ? ta.wma(source, length) :
     type == 'VWMA' ? ta.vwma(source, length) :
     type == 'HMA' ? ta.hma(source, length) :
     na
//╔═════════════════════════╗
//║          Input          ║
//╚═════════════════════════╝
Color_Mode =    input.string('Classic', 'Color Choice', group = 'Color', options = ['Classic', 'Modern', 'Robust', 'Accented', 'Monochrome', 'Moderate', 'Aqua', 'Cosmic'])
Bool_Average =  input.bool(true,'Oscillator DMI', inline='Bool', group='DMI Setting')
Bool_DMI =      input.bool(false,'Normal DMI', inline='Bool', group='DMI Setting')
Bool_ADX=       input.bool(false,'ADX Smoothing', group='DMI Setting', inline = 'Bool')
Lenght_DMI =    input.int(8, minval=1, title='Length DMI', inline='DMI', group='DMI Setting')
Signal_DMI =    input.string('0', 'Entry/Exit Signal', group='DMI Setting', options = ['0', 'DMI MA'],tooltip = 'Choose Entry/Exit Point')
Smooth_ADX =    input.int(1, title='Smoothing ADX', minval=1, group='DMI Setting', inline = 'ADX')
Length_MA_DMI = input.int(365, title = 'Length MA', group='DMI MA', inline = 'DMI MA')
Source_MA_DMI = input.string('EMA', '', group='DMI MA', inline = 'DMI MA', options=['EMA', 'SMA', 'RMA', 'WMA', 'VWMA', 'HMA'],tooltip = 'Moving Averages')
//╔════════════════════════╗
//║      Calculation       ║
//╚════════════════════════╝
[diplus, diminus, ADX] = ta.dmi(Lenght_DMI, Smooth_ADX)
DMI = diplus - diminus
DMI_MA =  ma(DMI, Length_MA_DMI, Source_MA_DMI)
float Entry_DMI = Signal_DMI == 'DMI MA' ? DMI_MA : 0.0
isBull_DMI = diplus > diminus
isBear_DMI = diplus < diminus
Bull_DMI = ta.crossover(diplus,diminus)
Bear_DMI = ta.crossunder(diplus,diminus)
//╔════════════════════════╗
//║         Color          ║
//╚════════════════════════╝
[UpC, DnC] = switch Color_Mode
    'Classic' =>    [#008800, #ff0000]
    'Modern' =>     [#ffffff, #b721ff]
    'Robust' =>     [#ffbb00, #770737]
    'Accented' =>   [#8c5cf7, #e83e8c]
    'Monochrome' => [#e9ecef, #495057]
    'Moderate' =>   [#43a047, #e53935]
    'Aqua' =>       [#00a8e8, #f18f01]
    'Cosmic' =>     [#e83e8c, #6f2da8]
//╔════════════════════════╗
//║          Plot          ║
//╚════════════════════════╝
Plot_ADX =          plot(Bool_ADX ? ADX : na, color=color.blue, title='ADX')
Plot_Entry_DMI =    plot(Bool_Average ? Entry_DMI : na, color = Bool_Average ?color.yellow : na)
Plot_Average =      plot(Bool_Average ? DMI : na, color = Bool_Average ? DMI > Entry_DMI ? color.new(UpC, 50) : color.new(DnC, 50) : na, linewidth = 3)
Plot_diplus =       plot(Bool_DMI ? diplus : na, color=UpC, linewidth = 3,title='+DI')
Plot_diminus =      plot(Bool_DMI ? diminus : na, color=DnC, linewidth = 3, title='-DI')
fill(Plot_Average, Plot_Entry_DMI, 40, Entry_DMI, top_color = color.new(UpC, 0),    bottom_color = color.new(UpC, 100), title = 'Overbought Fill')
fill(Plot_Average, Plot_Entry_DMI, Entry_DMI, -40, top_color = color.new(DnC, 100), bottom_color = color.new(DnC, 0), title = 'Oversold Fill')
fill(Plot_Average, Plot_Entry_DMI, DMI > Entry_DMI ? DMI : (DMI < Entry_DMI ? DMI : Entry_DMI), DMI > Entry_DMI ? Entry_DMI : (DMI < Entry_DMI ? Entry_DMI : DMI), DMI > Entry_DMI ? color.new(UpC, 50) : (DMI < Entry_DMI ? color.new(DnC, 50) : #00000000), DMI > Entry_DMI ? #00000000 : (DMI < Entry_DMI ? #00000000 : #00000000),title = 'Dynamic Fill')
plotcandle(open, high, low, close, 'Bar Color', isBull_DMI ? UpC : isBear_DMI ? DnC : color.gray, isBull_DMI ? UpC : isBear_DMI ? DnC : color.gray, bordercolor = isBull_DMI ? UpC : isBear_DMI ? DnC : color.gray, force_overlay = true)
plotshape(Bull_DMI, 'LONG', shape.triangleup, location.belowbar, UpC, 0, size = size.small, force_overlay = true)
plotshape(Bear_DMI, 'SHORT', shape.triangledown, location.abovebar, DnC, 0, size = size.small, force_overlay = true)
//╔════════════════════════╗
//║          VIEW          ║
//╚════════════════════════╝
var table Table_DMI = table.new(position.middle_right, 1, 1, border_width = 1)
var label DMILabel1 = na
var label DMILabel2 = na
var label DMILabel3 = na
var label DMILabel4 = na
if barstate.islast
    table.cell(Table_DMI, 0, 0, text = isBull_DMI ? '⬆️ Bullish' : '⬇️ Bearish', text_color = isBull_DMI ? UpC : DnC,text_size = size.huge)
    if not na(DMILabel1)
        label.delete(DMILabel1)
        DMILabel1 := na
    if not na(DMILabel2)
        label.delete(DMILabel2)
        DMILabel2 := na
    if not na(DMILabel3)
        label.delete(DMILabel3)
        DMILabel3 := na
    if not na(DMILabel4)
        label.delete(DMILabel4)
        DMILabel4 := na
    if Bool_Average and not na(DMI)
        DMILabel1 := label.new(bar_index + 5, DMI, text = str.tostring(DMI, '#.##'), color = DMI > Entry_DMI ? color.new(UpC, 20) : DMI < Entry_DMI ? color.new(DnC, 20) : color.gray, style = label.style_label_left, size = size.large, textcolor = #000000)
    if Bool_ADX and not na(ADX)
        DMILabel2 := label.new(bar_index + 5, ADX, text = 'ADX', color = color.blue, style = label.style_label_left,size = size.large, textcolor = #000000)
    if Bool_DMI and not na(diplus)
        DMILabel3 := label.new(bar_index + 5, diplus, text = 'DI +', color = UpC, style = label.style_label_left, size = size.large, textcolor = #000000)
    if Bool_DMI and not na(diminus)
        DMILabel4 := label.new(bar_index + 5, diminus, text = 'DI -', color = DnC, style = label.style_label_left, size = size.large, textcolor = #000000)
//╔════════════════════════╗
//║         Alerts         ║
//╚════════════════════════╝
alertcondition(Bull_DMI, title= 'LONG', message= '{{ticker}} LONG')
alertcondition(Bear_DMI, title= 'SHORT', message= '{{ticker}} SHORT')

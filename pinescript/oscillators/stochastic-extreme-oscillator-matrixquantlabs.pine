//  __  __         _          _         ___                        _   
// |  \/  |  __ _ | |_  _ __ (_)__  __ / _ \  _   _   __ _  _ __  | |_ 
// | |\/| | / _` || __|| '__|| |\ \/ /| | | || | | | / _` || '_ \ | __|
// | |  | || (_| || |_ | |   | | >  < | |_| || |_| || (_| || | | || |_ 
// |_|  |_| \__,_| \__||_|   |_|/_/\_\ \__\_\ \__,_| \__,_||_| |_| \__|
// 
// This Pine ScriptÂ® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© MatrixQuantLabs
//
//@version=6
indicator(title="Stochastic Extreme Oscillator [MatrixQuantLabs]", format=format.price, precision=2, timeframe="", timeframe_gaps=true)

// Input parameters
periodK = input.int(14, title="%K Length", minval=1, group="Basic Settingsä¸¨åŸºç¡€è®¾ç½®")
smoothK = input.int(1, title="%K Smoothing", minval=1, group="Basic Settingsä¸¨åŸºç¡€è®¾ç½®")
periodD = input.int(3, title="%D Smoothing", minval=1, group="Basic Settingsä¸¨åŸºç¡€è®¾ç½®")
showSignals = input.bool(true, title="Show Peak and Trough Signalsä¸¨æ˜¾ç¤ºé¡¶åº•ä¿¡å·", group="Basic Settingsä¸¨åŸºç¡€è®¾ç½®")

// Divergence settings
showDivergence = input.bool(false, title='Show Divergenceä¸¨æ˜¾ç¤ºèƒŒç¦»', group='Divergenceä¸¨èƒŒç¦»è®¾ç½®', tooltip='â–¶ï¸ Pivot Left: Number of bars to the left used to confirm a pivot high or low. Higher values detect more significant pivots but reduce signal frequency.\nâ–¶ï¸ Pivot Right: Number of bars to the right required to confirm a pivot. Larger values increase confirmation reliability but add delay; smaller values are more sensitive and faster.\nâ–¶ï¸ Min Range: Minimum number of bars between divergence points. Helps filter out signals that are too close and likely to be noise.\nâ–¶ï¸ Max Range: Maximum number of bars allowed between divergence points. Limits how far back divergence signals can be detected.\n\nâ–¶ï¸ å‘å·¦å›æº¯ï¼šç”¨äºç¡®è®¤é«˜ç‚¹æˆ–ä½ç‚¹çš„å·¦ä¾§å›æº¯ K çº¿æ•°é‡ã€‚æ•°å€¼è¶Šå¤§ï¼Œè¯†åˆ«çš„æ‹ç‚¹è¶Šé‡è¦ï¼Œä½†ä¿¡å·å‡ºç°é¢‘ç‡ä¼šé™ä½ã€‚\nâ–¶ï¸ å‘å³å›æº¯ï¼šç”¨äºç¡®è®¤æ‹ç‚¹æ‰€éœ€çš„å³ä¾§ K çº¿æ•°é‡ã€‚æ•°å€¼è¶Šå¤§ï¼Œç¡®è®¤æ›´å¯é ä½†ä¿¡å·å»¶è¿Ÿæ›´é«˜ï¼›æ•°å€¼è¶Šå°ï¼Œçµæ•åº¦æ›´é«˜ã€å“åº”æ›´å¿«ã€‚\nâ–¶ï¸ æœ€å°èŒƒå›´ï¼šèƒŒç¦»ç‚¹ä¹‹é—´çš„æœ€å° K çº¿æ•°é‡ã€‚ç”¨äºè¿‡æ»¤è·ç¦»è¿‡è¿‘ã€å™ªéŸ³è¾ƒå¤§çš„èƒŒç¦»ä¿¡å·ã€‚\nâ–¶ï¸ æœ€å¤§èŒƒå›´ï¼šèƒŒç¦»ç‚¹ä¹‹é—´å…è®¸çš„æœ€å¤§ K çº¿æ•°é‡ã€‚ç”¨äºé™åˆ¶èƒŒç¦»ä¿¡å·å‘å†å²å›æº¯çš„èŒƒå›´ã€‚')
PivotLookbackLeft = input.int(5, title='Pivot Leftä¸¨å‘å·¦å›æº¯', minval=1, inline='Pivot', group='Divergenceä¸¨èƒŒç¦»è®¾ç½®')
PivotLookbackRight = input.int(5, title='Pivot Rightä¸¨å‘å³å›æº¯', minval=1, inline='Pivot', group='Divergenceä¸¨èƒŒç¦»è®¾ç½®')
MinLookbackRange = input.int(5, title='Min Rangeä¸¨æœ€å°èŒƒå›´', minval=1, inline='Range', group='Divergenceä¸¨èƒŒç¦»è®¾ç½®')
MaxLookbackRange = input.int(60, title='Max Rangeä¸¨æœ€å¤§èŒƒå›´', minval=1, inline='Range', group='Divergenceä¸¨èƒŒç¦»è®¾ç½®')

// Overbought area
OverboughtLine1 = hline(100, color=color.new(color.green, 0), linestyle=hline.style_solid, title='Overbought Upper Bandä¸¨è¶…ä¹°ä¸Šè½¨')
OverboughtLine2 = hline(90, color=color.new(color.green, 25), linestyle=hline.style_dashed, title='Overbought Middle Bandä¸¨è¶…ä¹°ä¸­è½¨')
OverboughtLine3 = hline(80, color=color.new(color.green, 25), linestyle=hline.style_dotted, title='Overbought Lower Bandä¸¨è¶…ä¹°ä¸‹è½¨')
// Oversold area
OversoldLine3 = hline(20, color=color.new(color.red, 25), linestyle=hline.style_dotted, title='Oversold Upper Bandä¸¨è¶…å–ä¸Šè½¨')
OversoldLine2 = hline(10, color=color.new(color.red, 25), linestyle=hline.style_dashed, title='Oversold Middle Bandä¸¨è¶…å–ä¸­è½¨')
OversoldLine1 = hline(0, color=color.new(color.red, 0), linestyle=hline.style_solid, title='Oversold Lower Bandä¸¨è¶…å–ä¸‹è½¨')

hline(50, color=color.new(color.gray, 0), linestyle=hline.style_dotted, title='Zero Lineä¸¨é›¶è½´çº¿')

// Calculation indicators
k = ta.sma(ta.stoch(close, high, low, periodK), smoothK)
d = ta.sma(k, periodD)

// Overbought zone filling logic (90-100 range)
overboughtRed = k < d and d >= 80 and d <= 100
overboughtOrange = d > 90 and not overboughtRed
overboughtYellow = k > 80 and not overboughtRed and not overboughtOrange
overboughtColor = overboughtRed ? color.new(color.red, 0) : overboughtOrange ? color.new(color.red, 40) : overboughtYellow ? color.new(color.red, 80) : na
fill(OverboughtLine2, OverboughtLine1, color=overboughtColor, title='Overbought Fillä¸¨è¶…ä¹°åŒºå¡«å……')

// Oversold zone filling logic (0-10 range)
oversoldGreen = k > d and d >= 0 and d <= 20
oversoldBlue = d < 10 and not oversoldGreen
oversoldYellow = k < 20 and not oversoldGreen and not oversoldBlue
oversoldColor = oversoldGreen ? color.new(color.green, 0) : oversoldBlue ? color.new(color.green, 40) : oversoldYellow ? color.new(color.green, 80) : na
fill(OversoldLine1, OversoldLine2, color=oversoldColor, title='Oversold Fillä¸¨è¶…å–åŒºå¡«å……')

dColor = d >= 60 ? color.new(#00ffaa, 0) : d >= 40 ? color.new(color.gray, 0) : color.new(#ff3355, 0)
plot(k, title="%K", color=#ffffff, style=plot.style_line, display=display.none)
dPlot = plot(d, title="%D", color=dColor, style=plot.style_line)

zeroLinePlot = plot(50, color=na, display=display.none, editable=false)
fill(dPlot, zeroLinePlot, 100, 50, top_color=color.new(#00ffaa, 0), bottom_color=color.new(#00ffaa, 100), title="Above Zero Fillä¸¨é›¶è½´ä»¥ä¸Šå¡«å……")
fill(dPlot, zeroLinePlot, 50, 0, top_color=color.new(#ff3355, 100), bottom_color=color.new(#ff3355, 0), title="Below Zero Fillä¸¨é›¶è½´ä»¥ä¸‹å¡«å……")

bearishCross = showSignals and ta.crossunder(k, d) and d >= 80 and d <= 100
bullishCross = showSignals and ta.crossover(k, d) and d >= 0 and d <= 20
plotshape(bearishCross, title='Bearish Signalä¸¨çœ‹è·Œä¿¡å·', style=shape.triangledown, location=location.top, color=color.new(#ffffff, 0), display=display.pane, editable=false)
plotshape(bullishCross, title='Bullish Signalä¸¨çœ‹æ¶¨ä¿¡å·', style=shape.triangleup, location=location.bottom, color=color.new(#ffffff, 0), display=display.pane, editable=false)

// Divergence detection
bearColor = color.red
bullColor = color.green
textColor = color.white
noneColor = color.new(color.white, 100)

// Range checking function
_inRange(cond) =>
    bars = ta.barssince(cond)
    MinLookbackRange <= bars and bars <= MaxLookbackRange

// Initialize variables (in the global scope)
plFound = false
phFound = false
bullCond = false
bearCond = false

stochLBR = d[PivotLookbackRight]

if showDivergence
    plFound := not na(ta.pivotlow(d, PivotLookbackLeft, PivotLookbackRight))
    stochHL = stochLBR > ta.valuewhen(plFound, stochLBR, 1) and _inRange(plFound[1])
    lowLBR = low[PivotLookbackRight]
    priceLL = lowLBR < ta.valuewhen(plFound, lowLBR, 1)
    bullCond := priceLL and stochHL and plFound

    phFound := not na(ta.pivothigh(d, PivotLookbackLeft, PivotLookbackRight))
    stochLH = stochLBR < ta.valuewhen(phFound, stochLBR, 1) and _inRange(phFound[1])
    highLBR = high[PivotLookbackRight]
    priceHH = highLBR > ta.valuewhen(phFound, highLBR, 1)
    bearCond := priceHH and stochLH and phFound

// Draw divergence
plot(plFound ? stochLBR : na, offset=-PivotLookbackRight, title="Bullish Divergence Lineä¸¨çœ‹æ¶¨èƒŒç¦»çº¿", color=bullCond ? bullColor : noneColor, display=display.pane)
plotshape(bullCond ? stochLBR : na, offset=-PivotLookbackRight, title="Bullish Divergence Signalä¸¨çœ‹æ¶¨èƒŒç¦»ä¿¡å·", text="â–²", style=shape.labelup, location=location.absolute, color=bullColor, textcolor=textColor, display=display.pane)
plot(phFound ? stochLBR : na, offset=-PivotLookbackRight, title="Bearish Divergence Lineä¸¨çœ‹è·ŒèƒŒç¦»çº¿", color=bearCond ? bearColor : noneColor, display=display.pane)
plotshape(bearCond ? stochLBR : na, offset=-PivotLookbackRight, title="Bearish Divergence Signalä¸¨çœ‹è·ŒèƒŒç¦»ä¿¡å·", text="â–¼", style=shape.labeldown, location=location.absolute, color=bearColor, textcolor=textColor, display=display.pane)

// Signal alert
alertcondition(bullishCross, title='Stoch Bullish Signalä¸¨éšæœºæŒ‡æ ‡çœ‹æ¶¨ä¿¡å·', message='âœ… Stochastic indicator enters oversold zone.')
alertcondition(bearishCross, title='Stoch Bearish Signalä¸¨éšæœºæŒ‡æ ‡çœ‹è·Œä¿¡å·', message='âŒ Stochastic indicator enters overbought  zone.')

// Divergence alert
alertcondition(bullCond, title='Stoch Bullish Divergenceä¸¨éšæœºæŒ‡æ ‡çœ‹æ¶¨èƒŒç¦»', message='ğŸ“ˆ Stochastic Bullish Divergence: Price lower low but indicator higher low')
alertcondition(bearCond, title='Stoch Bearish Divergenceä¸¨éšæœºæŒ‡æ ‡çœ‹è·ŒèƒŒç¦»', message='ğŸ“‰ Stochastic Bearish Divergence: Price higher high but indicator lower high')

// End

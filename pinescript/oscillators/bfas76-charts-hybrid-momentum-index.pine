// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © BFAS76 
// 
// CREDITS:
// • Vdubus - Original "BinaryPro 2" concept
// • TheLark (Chris Moody) - Laguerre PPO percentile rank code
// 
// ORIGINAL BY BFAS76:
// • DMI Stochastic system
// • MTF Channel background
// • Signal arrows
// • Pine v5 conversion
//@version=5
indicator(title="BFAS76 Charts - Hybrid Momentum Index", shorttitle="Hybrid Momentum Index", overlay=false)

//=====================================================================
// Inputs
//===================================================================== 
pctile = input.int(90, title="Over_Bought")
wrnpctile = input.int(70, title="Over_Sold")
Short = input.float(0.3, title="LONG")
Long = input.float(0.5, title="SHORT")
lkbT = input.int(250, title="'PRO-Top")
lkbB = input.int(250, title="'PRO-Bottom")

// Função Lag 
lag(g, p) =>
    var float L0 = 0.0
    var float L1 = 0.0
    var float L2 = 0.0
    var float L3 = 0.0
    L0 := (1 - g) * p + g * nz(L0[1])
    L1 := -g * L0 + nz(L0[1]) + g * nz(L1[1])
    L2 := -g * L1 + nz(L1[1]) + g * nz(L2[1])
    L3 := -g * L2 + nz(L2[1]) + g * nz(L3[1])
    (L0 + 2 * L1 + 2 * L2 + L3) / 6

lmas = lag(Short, hl2)
lmal = lag(Long, hl2)

ppoT = (lmas - lmal) / lmal * 100
pctRankT = ta.percentrank(ppoT, lkbT)

plot(pctRankT, title="Percentile Rank Columns", color=color.rgb(252, 0, 0), style=plot.style_line, linewidth=2)

// DMI Stochastic 
DMIlength = input.int(10, title="DMI")
Stolength = input.int(6, title="DMI Stoch")
os = input.int(15, title="OS %")
ob = input.int(85, title="OB %")

// Função WWMA 
custom_wwma(src, len) =>
    float res = 0.0
    res := (nz(res[1]) * (len - 1) + src) / len
    res

hiDiff = high - high[1]
loDiff = low[1] - low

plusDM = (hiDiff > loDiff) and (hiDiff > 0) ? hiDiff : 0
minusDM = (loDiff > hiDiff) and (loDiff > 0) ? loDiff : 0

// Cálculo do ATR e DI 
ATR = custom_wwma(ta.tr(true), DMIlength)
PlusDI = 100 * custom_wwma(plusDM, DMIlength) / ATR
MinusDI = 100 * custom_wwma(minusDM, DMIlength) / ATR

osc = PlusDI - MinusDI
hi = ta.highest(osc, Stolength)
lo = ta.lowest(osc, Stolength)

// Re-calculando 
Stoch = math.sum((osc - lo), Stolength) / math.sum((hi - lo), Stolength) * 100
plot(Stoch, color=color.blue, title='Stochastic', linewidth=1)

//=====================================================================
// Sinais de Seta
//===================================================================== 
crossUp = Stoch[1] < os and Stoch >= os
crossDo = Stoch[1] > ob and Stoch <= ob

plotshape(crossUp, title="Arrow up", style=shape.triangleup, location=location.bottom, color=color.rgb(0, 183, 255), size=size.tiny)
plotshape(crossDo, title="Arrow down", style=shape.triangledown, location=location.top, color=color.rgb(255, 255, 255), size=size.tiny)

hline(80, linestyle=hline.style_solid, color=color.rgb(207, 0, 0), linewidth=2)
hline(20, linestyle=hline.style_solid, color=color.rgb(21, 255, 0), linewidth=2)

//=====================================================================
// Background e MTF
//===================================================================== 
Sml_TF = input.timeframe("720", title="Small Channel TF")
RangeVal = input.int(1, title="Range")

SELL = request.security(syminfo.tickerid, Sml_TF, ta.highest(RangeVal), lookahead=barmerge.lookahead_on)
BUY = request.security(syminfo.tickerid, Sml_TF, ta.lowest(RangeVal), lookahead=barmerge.lookahead_on)

Hcon = high >= SELL
Lcon = low <= BUY

// Cores 
bgcolor(Hcon ? color.new(color.white, 90) : color.new(color.blue, 90))
bgcolor(Lcon ? color.new(color.white, 80) : color.new(#9771C5, 80))

// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © DotGain

//@version=6
indicator('Dip Buy/Sell Signals (Vix Fix + MA Deviation + TRMAD)', shorttitle = 'DipSig', overlay = true)

// === Inputs ===

// CM Williams Vix Fix
group_vix = 'CM Williams Vix Fix'
pd = input.int(22, title = 'LookBack Period Standard Deviation High', group = group_vix)
bbl = input.int(20, title = 'Bolinger Band Length', group = group_vix)
mult = input.float(2.0, minval = 1, maxval = 5, title = 'Bollinger Band Standard Deviation Up', group = group_vix)
lb = input.int(50, title = 'Look Back Period Percentile High', group = group_vix)
ph = input.float(.85, title = 'Highest Percentile', group = group_vix)
pl = input.float(1.01, title = 'Lowest Percentile', group = group_vix)

// Deviation from MA
group_dev = 'Deviation from MA'
mat_dev = input.string(defval = 'ema', options = ['ema', 'sma', 'rma', 'vwma', 'wma', 'tema'], title = 'Moving Average Type', group = group_dev)
len_dev = input.int(title = 'Length of MA', defval = 200, group = group_dev)
percent_dev = input.float(title = 'Deviation Percent', defval = 10, group = group_dev)

// TRMAD
group_trmad = 'TRMAD'
trTypeInput_trmad = input.string('SMA', title = 'TR Type', options = ['SMA', 'EMA', 'SMMA (RMA)', 'WMA', 'VWMA'], group = group_trmad)
trLengthInput_trmad = input.int(14, 'TR Length', minval = 2, group = group_trmad)
maTypeInput_trmad = input.string('SMA', title = 'MA Type', options = ['SMA', 'EMA', 'SMMA (RMA)', 'WMA', 'VWMA'], group = group_trmad)
maLengthInput_trmad = input.int(20, 'MA Length', minval = 2, group = group_trmad)
trmad_level_buy = input.float(-3.0, title = 'TRMAD Buy Level', group = group_trmad, step = 0.1)
trmad_level_sell = input.float(3.0, title = 'TRMAD Sell Level', group = group_trmad, step = 0.1)


// === Helper Functions ===

f_tema_dev(s, p) =>
    ema1 = ta.ema(s, p)
    ema2 = ta.ema(ema1, p)
    ema3 = ta.ema(ema2, p)
    3 * ema1 - 3 * ema2 + ema3

f_ma_dev(t, s, p) =>
    switch t
        'ema' => ta.ema(s, p)
        'rma' => ta.rma(s, p)
        'vwma' => ta.vwma(s, p)
        'wma' => ta.wma(s, p)
        'tema' => f_tema_dev(s, p)
        => ta.sma(s, p)

f_ma_trmad(source, length, type) =>
    switch type
        'SMA' => ta.sma(source, length)
        'EMA' => ta.ema(source, length)
        'SMMA (RMA)' => ta.rma(source, length)
        'WMA' => ta.wma(source, length)
        'VWMA' => ta.vwma(source, length)
        => ta.sma(source, length)


// === Calculations ===

// 1. CM Williams Vix Fix
wvf = (ta.highest(close, pd) - low) / ta.highest(close, pd) * 100
sDev = mult * ta.stdev(wvf, bbl)
midLine = ta.sma(wvf, bbl)
upperBand = midLine + sDev
rangeHigh = ta.highest(wvf, lb) * ph
bool cond1_vix_lime = wvf >= upperBand or wvf >= rangeHigh

// 2. Deviation from MA
src_dev = close
ma1_dev = f_ma_dev(mat_dev, src_dev, len_dev)
def_val = (src_dev - ma1_dev) / ma1_dev * 100
bool cond2_dev_buy = def_val < -percent_dev
bool cond2_dev_sell = def_val > percent_dev

// 3. TRMAD
atr_trmad = f_ma_trmad(ta.tr(true), trLengthInput_trmad, trTypeInput_trmad)
distance_trmad = close - f_ma_trmad(close, maLengthInput_trmad, maTypeInput_trmad)
trmad_val = nz(distance_trmad / atr_trmad)
bool cond3_trmad_buy = trmad_val < trmad_level_buy
bool cond3_trmad_sell = trmad_val > trmad_level_sell

// Combined Signals
bool buySignal = cond1_vix_lime and cond2_dev_buy and cond3_trmad_buy
bool sellSignal = cond1_vix_lime and cond2_dev_sell and cond3_trmad_sell


// === Plotting ===

// Bar Color
signalColor = buySignal ? color.new(color.green, 0) : sellSignal ? color.new(color.red, 0) : na
barcolor(signalColor, title = 'Signal Bar Color', display = display.none)

// Background Color
bgColor = buySignal ? color.new(color.green, 85) : sellSignal ? color.new(color.red, 85) : na
bgcolor(bgColor, title = 'Signal Background', display = display.none)

// Triangles
plotshape(buySignal, title = 'Buy', style = shape.labelup, location = location.belowbar, color = color.new(color.green, 60), size = size.small)
plotshape(sellSignal, title = 'Sell', style = shape.labeldown, location = location.abovebar, color = color.new(color.red, 60), size = size.small, display = display.none)

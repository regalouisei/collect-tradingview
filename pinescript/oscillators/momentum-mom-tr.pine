// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Tiagorocha1989
//                              ████████╗ ████████╗  ████████╗  ███████╗   ██████╗        ██████╗   ██████╗   ██████╗ ██╗   ██╗ ████████╗
//                              ╚══██╔══╝ ╚══██╔══╝  ██╔═══██║  ██╔════╝  ██╔═══██╗       ██╔══██╗ ██╔═══██╗ ██╔════╝ ██║   ██║ ██╔═══██║
//                                 ██║       ██║     ████████║  ██║ ███║  ██║   ██║       ██████╔╝ ██║   ██║ ██║      ████████║ ████████║
//                                 ██║       ██║     ██╔═══██║  ██║  ██║  ██║   ██║       ██╔══██╗ ██║   ██║ ██║      ██║   ██║ ██╔═══██║
//                                 ██║    ████████╗  ██║   ██║  ███████║  ╚██████╔╝       ██║  ██║ ╚██████╔╝ ╚██████╗ ██║   ██║ ██║   ██║
//@version=6
indicator('Momentum - MOM | TR', 'MOM | TR →', false)
//╔════════════════════════╗
//║ Choose Moving Averages ║
//╚════════════════════════╝
ma(source, length, type) =>
     type == 'EMA' ? ta.ema(source, length) :
     type == 'SMA' ? ta.sma(source, length) :
     type == 'RMA' ? ta.rma(source, length) :
     type == 'WMA' ? ta.wma(source, length) :
     type == 'VWMA' ? ta.vwma(source, length) :
     type == 'HMA' ? ta.hma(source, length) :
     na
//╔════════════════════════╗
//║     INPUT SETTINGS     ║
//╚════════════════════════╝
Color_Mode =        input.string('Classic', 'Color Choice', group = 'Color', options = ['Classic', 'Modern', 'Robust', 'Accented', 'Monochrome', 'Moderate', 'Aqua', 'Cosmic'])
Length_MOM =        input.int(36, title = 'Length MOM:', group='MOM Setting', inline = 'MOM')
Source_MOM =        input(close, title = '', group='MOM Setting', inline = 'MOM')
Signal_MOM =        input.string('0', 'Entry/Exit Signal', group='MOM Setting', options = ['0', 'MOM MA'],tooltip = 'Choose Entry/Exit Point')
Length_MA_MOM =     input.int(365, title = 'Length MA', group='MOM MA', inline = 'MA')
Source_MA_MOM =     input.string('EMA', '', group='MOM MA', inline = 'MA', options=['EMA', 'SMA', 'RMA', 'WMA', 'VWMA', 'HMA'],tooltip = 'Moving Averages')
//╔════════════════════════╗
//║      Calculation       ║
//╚════════════════════════╝
MOM =     ta.mom(Source_MOM,Length_MOM)
MOM_MA =  ma(MOM, Length_MA_MOM, Source_MA_MOM)
float Entry_MOM = Signal_MOM == 'MOM MA' ? MOM_MA : 0.0
isBull_MOM = MOM > Entry_MOM
isBear_MOM = MOM < Entry_MOM
Bull_MOM = ta.crossover(MOM, Entry_MOM)
Bear_MOM = ta.crossunder(MOM, Entry_MOM)
//╔════════════════════════╗
//║         Color          ║
//╚════════════════════════╝
[UpC, DnC] = switch Color_Mode
    'Classic' =>    [#008800, #ff0000]
    'Modern' =>     [#ffffff, #b721ff]
    'Robust' =>     [#ffbb00, #770737]
    'Accented' =>   [#8c5cf7, #e83e8c]
    'Monochrome' => [#e9ecef, #495057]
    'Moderate' =>   [#43a047, #e53935]
    'Aqua' =>       [#00a8e8, #f18f01]
    'Cosmic' =>     [#e83e8c, #6f2da8]
//╔════════════════════════╗
//║          Plot          ║
//╚════════════════════════╝
Plot_MOM =     plot(MOM, color = MOM > Entry_MOM ? color.new(UpC, 50):color.new(DnC, 50), linewidth = 3)
Plot_Entry_MOM =  plot(Entry_MOM,color = color.yellow)
fill(Plot_MOM, Plot_Entry_MOM, 6000, Entry_MOM, top_color = color.new(UpC, 0),    bottom_color = color.new(UpC, 100), title = 'Overbought Fill')
fill(Plot_MOM, Plot_Entry_MOM, Entry_MOM, -6000, top_color = color.new(DnC, 100), bottom_color = color.new(DnC, 0), title = 'Oversold Fill')
fill(Plot_MOM, Plot_Entry_MOM, MOM < Entry_MOM ? MOM : Entry_MOM, MOM < Entry_MOM ? Entry_MOM : MOM, MOM < Entry_MOM ? DnC : #00000000, MOM < Entry_MOM ? #00000000 : UpC)
plotcandle(open, high, low, close, 'Bar Color', isBull_MOM ? UpC : isBear_MOM ? DnC : na, isBull_MOM ? UpC : isBear_MOM ? DnC : na, bordercolor = isBull_MOM ? UpC : isBear_MOM ? DnC : na, force_overlay = true)
plotshape(Bull_MOM, 'LONG' , shape.triangleup  , location.belowbar, UpC,    0, size = size.small, force_overlay = true)
plotshape(Bear_MOM, 'SHORT', shape.triangledown, location.abovebar, DnC,    0, size = size.small, force_overlay = true)
//╔════════════════════════╗
//║          VIEW          ║
//╚════════════════════════╝
var table Table_MOM = table.new(position.middle_right, 1, 1, border_width = 1)
var label MOMLabel = na
if barstate.islast
    table.cell(Table_MOM, 0, 0, text = isBull_MOM ? '⬆️ Bullish' : '⬇️ Bearish', text_color = isBull_MOM ? UpC : DnC,text_size = size.huge)
    if not na(MOMLabel)
        label.delete(MOMLabel)
    MOMLabel := label.new(bar_index + 5, MOM, text =  str.tostring(MOM, '#.##'), color = MOM > Entry_MOM ? color.new(UpC, 20) : MOM < Entry_MOM ? color.new(DnC, 20) : color.gray, style = label.style_label_left, size = size.large, textcolor = #000000)
//╔════════════════════════╗
//║         Alerts         ║
//╚════════════════════════╝
alertcondition(Bull_MOM, title= 'LONG', message= '{{ticker}} LONG')
alertcondition(Bear_MOM, title= 'SHORT', message= '{{ticker}} SHORT')

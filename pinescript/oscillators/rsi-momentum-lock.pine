//@version=5
indicator("RSI Momentum Lock", overlay=true)

rsiLength   = input.int(14, title="RSI Length")
obLevel     = input.int(70, title="RSI Bullish Level")
osLevel     = input.int(30, title="RSI Bearish Level")
atrLength   = input.int(10, title="ATR Length")
atrFactor   = input.float(3.0, title="ATR Multiplier")

useTrailing = input.bool(false, title="Enable Trailing Stop Mode")

rsiValue = ta.rsi(close, rsiLength)
atr      = ta.atr(atrLength)

var int trend = 0
if (rsiValue > obLevel)
    trend := 1
else if (rsiValue < osLevel)
    trend := -1

var float stLine = na
rawUp   = hl2 - (atrFactor * atr)
rawDown = hl2 + (atrFactor * atr)

if (trend == 1)
    if (useTrailing and trend[1] == 1 and not na(stLine))
        stLine := math.max(rawUp, stLine)
    else
        stLine := rawUp

else if (trend == -1)
    if (useTrailing and trend[1] == -1 and not na(stLine))
        stLine := math.min(rawDown, stLine)
    else
        stLine := rawDown

lineColor = trend == 1 ? color.green : color.red
p1 = plot(stLine, title="Trend Line", color=lineColor, linewidth=2)
p2 = plot(close, title="Price", display=display.none)
fill(p1, p2, color=color.new(lineColor, 90))
barcolor(trend == 1 ? color.green : color.red)

buySignal  = ta.crossover(trend, 0)
sellSignal = ta.crossunder(trend, 0)

plotshape(buySignal ? stLine : na, title="Buy Signal", location=location.absolute, color=color.green, style=shape.labelup, text="BUY", textcolor=color.white, size=size.small)
plotshape(sellSignal ? stLine : na, title="Sell Signal", location=location.absolute, color=color.red, style=shape.labeldown, text="SELL", textcolor=color.white, size=size.small)

alertcondition(buySignal, title="Buy Alert", message="RSI Momentum Buy Signal")
alertcondition(sellSignal, title="Sell Alert", message="RSI Momentum Sell Signal")

// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Fullymobile - RSI Level Candles [fmb]
// https://www.fullymobile.ca
//@version=6
indicator("RSI Level Candles [fmb]", shorttitle="RSI Candles [fmb]", overlay=true, max_bars_back=5000, max_labels_count=500)

// Inputs
src      = input.source(close, "RSI Source")
len      = input.int(14, "RSI Length", minval=2)
extLow   = input.float(30.0, "Extreme low",  minval=0, maxval=100)
lvl40    = input.float(40.0, "Bear/Bull neutral low")
lvl60    = input.float(60.0, "Bear/Bull neutral high")
extHigh  = input.float(70.0, "Extreme high", minval=0, maxval=100)

colorProfile = input.string("Neutral Yellow", "Coloring profile", options=["Neutral Yellow","Neutral Off"])
paintBars    = input.bool(true, "Paint candles by RSI regime")

// Colors
colBull          = color.new(color.green, 0)
colBullExt       = color.new(color.lime, 0)
colBear          = color.new(color.red, 0)
colBearExt       = color.new(color.maroon, 0)
colNeutralYellow = color.new(color.yellow, 40)

dotHighCol = color.new(color.green, 0)   // top dot for high extreme
dotLowCol  = color.new(color.red, 0)     // bottom dot for low extreme

// RSI
r = ta.rsi(src, len)

// Candle coloring
color barCol = na
if   r >= extHigh
    barCol := colBullExt
else if r >= lvl60
    barCol := colBull
else if r >  lvl40
    barCol := colorProfile == "Neutral Yellow" ? colNeutralYellow : na
else if r >  extLow
    barCol := colBear
else
    barCol := colBearExt
barcolor(paintBars ? barCol : na)

// Extreme dots on any bar in the zone
hitHigh = r >= extHigh
hitLow  = r <= extLow

// Tiny dots at top and bottom
plotshape(hitHigh, title="RSI High Extreme Dot", style=shape.circle, location=location.top,    color=dotHighCol, size=size.tiny)
plotshape(hitLow,  title="RSI Low Extreme Dot",  style=shape.circle, location=location.bottom, color=dotLowCol,  size=size.tiny)

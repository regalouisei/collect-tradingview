// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © BackQuant

//@version=6
indicator(
 "Adaptive Strength Overlay (MTF) [BackQuant]", 
 overlay=true
 )

import TradingView/ta/11 as ta

const string m     = "Mode Select"
const string calc  = "Main Calculation Settings"
const string tf    = "Multi Timeframe"
const string bands = "Band Settings"
const string plot  = "Plotting/Visual Settings"
const string tbl   = "Table Settings"
// Mode Select 
string mode = input.string("Trend", "Mode Select", options=["Trend", "Mean-Reversion"], group=m, tooltip="Trend mode highlights strength in the direction of the move, Mean-Reversion mode highlights extremes against the move")

// Main Calculation Settings
float  price      = input.source(close, "Price Source", group=calc, inline="main")
string timeframe1 = input.timeframe("", "First Timeframe", group=calc, inline="main", tooltip="This is by default on the chart timeframe.")
int    len        = input.int(14, "RSI Period", group=calc, inline="main")
string ma         = input.string("EMA", "Smoothing Type", options=["SMA", "HMA", "EMA", "WMA", "DEMA", "RMA", "LINREG", "ALMA", "TEMA", "T3"], group =calc,inline="smooth")
int    slen       = input.int(7, "Smoothing Period", group=calc, inline="smooth")
// Multi Timeframe 
bool   show2      = input.bool(true, "Show Second Band", group=tf, inline="tf2", tooltip="Removing a band can remove the fill of the others and its own")
string timeframe2 = input.timeframe("D", "Second Timeframe", group=tf, inline="tf2")
int    len2       = input.int(14, "RSI Period", group=tf, inline="tf2")
int    slen2      = input.int(7, "Smoothing Period", group=tf, inline="tf2")

bool   show3      = input.bool(true, "Show Third Band", group=tf, inline="tf3", tooltip="Removing a band can remove the fill of the others and its own")
string timeframe3 = input.timeframe("W", "Third Timeframe", group=tf, inline="tf3")
int    len3       = input.int(14, "RSI Period", group=tf, inline="tf3")
int    slen3      = input.int(7, "Smoothing Period", group=tf, inline="tf3")

// Percent Bands Settings
int    scale      = input.int(1, "Scaling Factor", options=[1,2,3,4,5], group=bands, inline="pct")
float  percent1   = input.float(3, "Percent Band One", group=bands, inline="pct", tooltip="This is a simple % deviaiton from price, which is scaled with the scaling input.")
float  percent2   = input.float(6, "Percent Band Two", group=bands, inline="pct", tooltip="This is a simple % deviaiton from price, which is scaled with the scaling input.")
float  percent3   = input.float(9, "Percent Band Three", group=bands, inline="pct", tooltip="This is a simple % deviaiton from price, which is scaled with the scaling input.")

// Plotting/Visual Settings
color  poscol     = input.color(#00ff00, "Long Color", group=plot)
color  negcol     = input.color(#ff0000, "Short Color", group=plot)
int    transp     = input.int(80, "Reference Band Transparency", group=plot, maxval=100, minval=1, tooltip="If you would like to remove the reference bands from the chart set this to 100.")

bool   showtbl    = input.bool(true, "Show Table", group=tbl, inline=tbl)
color  tblcol     = input.color(#ffffff, "Table Colors", group=tbl, inline=tbl)

// Helper Functions 
// Percent Bands
percentbands(series float price, simple float percent, simple int scaling) =>
    upper = price*(1+(percent*scaling)/100) 
    lower = price*(1-(percent*scaling)/100) 
    [upper, lower]

// Moving Average Select
movingavg(src, len, type) =>
    switch type
        "SMA" => ta.sma(src, len)
        "HMA" => ta.hma(src, len)
        "EMA" => ta.ema(src, len)
        "WMA" => ta.wma(src, len)
        "DEMA" => ta.dema(src, len)
        "RMA" => ta.rma(src, len)
        "LINREG"=> ta.linreg(src, len, 0)
        "TEMA" => ta.tema(src, len)
        "ALMA" => ta.alma(src, len, 0, 6)
        "T3" => ta.t3(src,len, 0.7)

// Percent Bands (Upper and Lower)
[upper1, lower1] = percentbands(price, percent1, scale)
[upper2, lower2] = percentbands(price, percent2, scale)
[upper3, lower3] = percentbands(price, percent3, scale)

// Plotting Percent Bands
c = plot(price, "Price Source", color=#00000000, display=display.none, editable=false)
u1 = plot(upper1, "Upper Percent Band 1", color.new(negcol, transp), 1)
u2 = plot(show2?upper2:na, "Upper Percent Band 2", color.new(negcol, transp), 1)
u3 = plot(show3?upper3:na, "Upper Percent Band 3", color.new(negcol, transp), 1)
l1 = plot(lower1, "Lower Percent Band 1", color.new(poscol, transp), 1)
l2 = plot(show2?lower2:na, "Lower Percent Band 2", color.new(poscol, transp), 1)
l3 = plot(show3?lower3:na, "Lower Percent Band 3", color.new(poscol, transp), 1)

// Calculation Start 
relativestrength(series float price, simple int rsilen, type, simple int smoothlen) =>

    rsi = ta.rsi(price, rsilen)
    smoothed = movingavg(rsi, smoothlen, type)

    var int transp1 = 0
    var int transp2 = 0
    // Upper side
    if smoothed < 10
        transp1 := 0
    else if smoothed < 20
        transp1 := 50
    else if smoothed < 30
        transp1 := 60
    else if smoothed < 40
        transp1 := 70
    else if smoothed < 50
        transp1 := 90
    else
        transp1 := 100

    // Lower side
    if smoothed > 90
        transp2 := 0
    else if smoothed > 80
        transp2 := 50
    else if smoothed > 70
        transp2 := 60
    else if smoothed > 60
        transp2 := 70
    else if smoothed > 50
        transp2 := 90
    else
        transp2 := 100
    
    [smoothed, transp1, transp2]

[chart_rs, transp1, transp2] = request.security(syminfo.tickerid, timeframe1, relativestrength(price, len, ma, slen))
[second_rs, transp1_2, transp2_2] = request.security(syminfo.tickerid, timeframe2, relativestrength(price,len2,ma,slen2))
[third_rs, transp1_3, transp2_3] = request.security(syminfo.tickerid, timeframe3, relativestrength(price,len3,ma,slen3))

// Fill Logic 
fill(c, u1, color.new(negcol, mode=="Trend"?transp1:transp2), "Upper One Fill")
fill(u1, u2, color.new(negcol, mode=="Trend"?transp1_2:transp2_2), "Upper Two Fill")
fill(u2, u3, color.new(negcol, mode=="Trend"?transp1_3:transp2_3), "Upper Three Fill")

fill(c, l1, color.new(poscol, mode=="Trend"?transp2:transp1), "Lower One Fill")
fill(l1, l2, color.new(poscol, mode=="Trend"?transp2_2:transp1_2), "Lower Two Fill")
fill(l2, l3, color.new(poscol, mode=="Trend"?transp2_3:transp1_3), "Lower Three Fill")


// Table
table t = table.new(position.top_right, 2,4,#00000000, tblcol, 1, tblcol, 1)

rsis = array.from(chart_rs, second_rs, third_rs)
tfs = array.from(timeframe1, timeframe2, timeframe3) 

if showtbl 
    t.cell(0,0, "TF", text_color=tblcol)
    t.cell(1,0, "RSI", text_color=tblcol)

    for i=0 to rsis.size()-1
        t.cell(0,i+1,str.tostring(tfs.get(i)), text_color=tblcol)
        t.cell(1,i+1, str.tostring(rsis.get(i), "#.00"), text_color=tblcol)


// Alerts
// Primary RSI (Timeframe 1)
alertcondition(ta.crossover(chart_rs, 70), "Strength Enters Upper Extreme (TF1)", "Strength entered upper extreme zone on {{exchange}}:{{ticker}}")
alertcondition(ta.crossunder(chart_rs, 70), "Strength Exits Upper Extreme (TF1)", "Strength exited upper extreme zone on {{exchange}}:{{ticker}}")

alertcondition(ta.crossunder(chart_rs, 30), "Strength Enters Lower Extreme (TF1)", "Strength entered lower extreme zone on {{exchange}}:{{ticker}}")
alertcondition(ta.crossover(chart_rs, 30), "Strength Exits Lower Extreme (TF1)", "Strength exited lower extreme zone on {{exchange}}:{{ticker}}")

alertcondition(ta.crossover(chart_rs, 50), "Strength Bullish Midline Cross (TF1)", "Strength crossed above the midline on {{exchange}}:{{ticker}}")
alertcondition(ta.crossunder(chart_rs, 50), "Strength Bearish Midline Cross (TF1)", "Strength crossed below the midline on {{exchange}}:{{ticker}}" )

// Second RSI (Timeframe 2)
alertcondition(ta.crossover(second_rs, 70), "Strength Enters Upper Extreme (TF2)", "Strength entered upper extreme zone (TF2) on {{exchange}}:{{ticker}}")
alertcondition(ta.crossunder(second_rs, 70), "Strength Exits Upper Extreme (TF2)", "Strength exited upper extreme zone (TF2) on {{exchange}}:{{ticker}}")

alertcondition(ta.crossunder(second_rs, 30), "Strength Enters Lower Extreme (TF2)", "Strength entered lower extreme zone (TF2) on {{exchange}}:{{ticker}}")
alertcondition(ta.crossover(second_rs, 30), "Strength Exits Lower Extreme (TF2)", "Strength exited lower extreme zone (TF2) on {{exchange}}:{{ticker}}")

alertcondition(ta.crossover(second_rs, 50), "Strength Bullish Midline Cross (TF2)", "Strength crossed above the midline (TF2) on {{exchange}}:{{ticker}}")
alertcondition(ta.crossunder(second_rs, 50), "Strength Bearish Midline Cross (TF2)", "Strength crossed below the midline (TF2) on {{exchange}}:{{ticker}}")

// Third RSI (Timeframe 3)
alertcondition(ta.crossover(third_rs, 70), "Strength Enters Upper Extreme (TF3)", "Strength entered upper extreme zone (TF3) on {{exchange}}:{{ticker}}")
alertcondition(ta.crossunder(third_rs, 70), "Strength Exits Upper Extreme (TF3)", "Strength exited upper extreme zone (TF3) on {{exchange}}:{{ticker}}")

alertcondition(ta.crossunder(third_rs, 30), "Strength Enters Lower Extreme (TF3)", "Strength entered lower extreme zone (TF3) on {{exchange}}:{{ticker}}")
alertcondition(ta.crossover(third_rs, 30), "Strength Exits Lower Extreme (TF3)", "Strength exited lower extreme zone (TF3) on {{exchange}}:{{ticker}}")

alertcondition(ta.crossover(third_rs, 50), "Strength Bullish Midline Cross (TF3)", "Strength crossed above the midline (TF3) on {{exchange}}:{{ticker}}")
alertcondition(ta.crossunder(third_rs, 50), "Strength Bearish Midline Cross (TF3)", "Strength crossed below the midline (TF3) on {{exchange}}:{{ticker}}")

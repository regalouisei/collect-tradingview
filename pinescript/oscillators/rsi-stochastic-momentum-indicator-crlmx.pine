// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 | https://mozilla.org/MPL/2.0/
// [  crlmx.RSI-SMI v1.2  ]
// [    ••• © crlmx •••     ]
//
//@version=6
indicator(title="[crlmx] RSI & Stochastic Momentum Indicator", shorttitle="RSI & Stochastic [crlmx]", format=format.price, precision=2, timeframe="", timeframe_gaps=true)

// ══════════════════════════════════════════════════════════════════════════════
// INPUTS
// ══════════════════════════════════════════════════════════════════════════════

showRSI = input.bool(true, "Show RSI", group="__________ RSI __________")
rsiLengthInput = input.int(14, minval=1, title="RSI Length", group="__________ RSI __________")
rsiSourceInput = input.source(close, "Source", group="__________ RSI __________")

showSMI = input.bool(true, "Show Stochastic", group="__________ STOCHASTIC __________")
lengthK   = input.int(10, "%K Length",  minval=1, group="__________ STOCHASTIC __________")
lengthD   = input.int(3,  "%D Length",  minval=1, group="__________ STOCHASTIC __________")
lengthEMA = input.int(3,  "EMA Length", minval=1, group="__________ STOCHASTIC __________")

// ══════════════════════════════════════════════════════════════════════════════
// RSI
// ══════════════════════════════════════════════════════════════════════════════

change = ta.change(rsiSourceInput)
up = ta.rma(math.max(change, 0), rsiLengthInput)
down = ta.rma(-math.min(change, 0), rsiLengthInput)
rsi = down == 0 ? 100 : up == 0 ? 0 : 100 - (100 / (1 + up / down))

// ══════════════════════════════════════════════════════════════════════════════
// STOCHASTIC MOMENTUM INDEX (shifted to 0-100 scale)
// ══════════════════════════════════════════════════════════════════════════════

emaEma(source, length) => ta.ema(ta.ema(source, length), length)

highestHigh = ta.highest(lengthK)
lowestLow = ta.lowest(lengthK)
highestLowestRange = highestHigh - lowestLow
relativeRange = close - (highestHigh + lowestLow) / 2
smiRaw = 200 * (emaEma(relativeRange, lengthD) / emaEma(highestLowestRange, lengthD))
smiEmaRaw = ta.ema(smiRaw, lengthEMA)

smi = (smiRaw / 2.0) + 50
smiEma = (smiEmaRaw / 2.0) + 50

// ══════════════════════════════════════════════════════════════════════════════
// PLOTS - order controls Style tab listing
// ══════════════════════════════════════════════════════════════════════════════

midLinePlot = plot(50, color = na, editable = false, display = display.none)

// RSI
rsiPlot = plot(showRSI ? rsi : na, "RSI", color=#f0709b)
fill(rsiPlot, midLinePlot, 120, 70, top_color = color.new(#ee7472, 50), bottom_color = color.new(#ee7472, 100), title = "RSI Overbought")
fill(rsiPlot, midLinePlot, 30, -20, top_color = color.new(#089981, 100), bottom_color = color.new(#089981, 50), title = "RSI Oversold")

// Stochastic
smiPlot = plot(showSMI ? smi : na, title="Stochastic", color=#9c9c9c)
plot(showSMI ? smiEma : na, title="Stochastic EMA", color=#c39653)
fill(smiPlot, midLinePlot, 120, 70, top_color = color.new(#ee7472, 50), bottom_color = color.new(#ee7472, 100), title = "Stochastic Overbought")
fill(smiPlot, midLinePlot, 30, -20, top_color = color.new(#089981, 100), bottom_color = color.new(#089981, 50), title = "Stochastic Oversold")

// Background bands
rsiUpperBand = hline(70, "Band Upper", color=#413327, linestyle=hline.style_dotted, display = showRSI ? display.all : display.none)
rsiLowerBand = hline(30, "Band Lower", color=#413327, linestyle=hline.style_dotted, display = showRSI ? display.all : display.none)
midline = hline(50, "Band Middle", color=#5e422b, linestyle=hline.style_dotted, display=display.none)
fill(rsiUpperBand, rsiLowerBand, color = color.new(#483941, 90), title="Band Background")

//@version=6
indicator(title="Directional Movement Index", shorttitle="DMI", format=format.price, precision=4, timeframe="", timeframe_gaps=true)
lensig = input.int(14, title="ADX Smoothing", minval=1)
len = input.int(14, minval=1, title="DI Length")

bullishColor = input.color(color.new(color.green, 80), title="Bullish Fill (+DI > -DI)")
bearishColor = input.color(color.new(color.red, 80), title="Bearish Fill (-DI > +DI)")

up = ta.change(high)
down = -ta.change(low)
plusDM = na(up) ? na : (up > down and up > 0 ? up : 0)
minusDM = na(down) ? na : (down > up and down > 0 ? down : 0)
trur = ta.rma(ta.tr, len)
plus = fixnan(100 * ta.rma(plusDM, len) / trur)
minus = fixnan(100 * ta.rma(minusDM, len) / trur)
sum = plus + minus
adx = 100 * ta.rma(math.abs(plus - minus) / (sum == 0 ? 1 : sum), lensig)

var bool isBullish = true
if ta.crossover(plus, minus)
    isBullish := true
if ta.crossunder(plus, minus)
    isBullish := false

topPlot = plot(plus, color=na, display=display.none)
bottomPlot = plot(minus, color=na, display=display.none)
fill(topPlot, bottomPlot, color=isBullish ? bullishColor : bearishColor)

plot(adx, color=#F50057, title="ADX", linewidth=2)
plot(plus, color=#2962FF, title="+DI", linewidth=2)
plot(minus, color=#FF6D00, title="-DI", linewidth=2)

// Threshold lines
hline(10, "Exhaustion Low", color=color.gray, linestyle=hline.style_dotted, linewidth=1)
hline(50, "Exhaustion High", color=color.gray, linestyle=hline.style_dotted, linewidth=1)

//Default Connor RSI by Tradingview, but with better Visualization of the Signals

//@version=6
indicator(title="Connor RSI Histogram", format=format.price, precision=2, timeframe="", timeframe_gaps=true)

// --- Inputs ---
//Indicator
grInd = "Indicator Settings"
src       = close
lenrsi    = input.int(3,   "RSI Length", group=grInd)
lenupdown = input.int(2,   "UpDown Length", group=grInd)
lenroc    = input.int(100, "ROC Length", group=grInd)

//Visuals
grVis = "Visual Settings"
upperTh   = input.float(80, "Upper Threshold", minval=0, maxval=100, group=grVis)
lowerTh   = input.float(20, "Lower Threshold", minval=0, maxval=100, group=grVis)
col_over   = input.color(color.red,   "Bar Colors", inline="cols", group=grVis)
col_mid_up = input.color(color.gray,  "/",          inline="cols", group=grVis)
col_mid_dn = input.color(color.gray,  "/",          inline="cols", group=grVis)
col_under  = input.color(color.green, "/",          inline="cols", group=grVis)

// --- Connor RSI ---
updown(s) =>
    isEqual   = s == s[1]
    isGrowing = s > s[1]
    ud = 0.0
    ud := isEqual ? 0 : isGrowing ? (nz(ud[1]) <= 0 ? 1 : nz(ud[1]) + 1) : (nz(ud[1]) >= 0 ? -1 : nz(ud[1]) - 1)
    ud

rsi         = ta.rsi(src, lenrsi)
updownrsi   = ta.rsi(updown(src), lenupdown)
percentrank = ta.percentrank(ta.roc(src, 1), lenroc)
crsi        = math.avg(rsi, updownrsi, percentrank)

// --- Bar color logic ---
bar_col = crsi > upperTh ? col_over  :
          crsi < lowerTh ? col_under :
          crsi >= 50     ? col_mid_up :
                           col_mid_dn

plot(crsi, "CRSI", style=plot.style_columns, color=bar_col, histbase=50)

// --- Lines: 100% visible, adapts to light/dark theme ---
hline(upperTh, "Upper Band",  color=#9c9c9c, linestyle=hline.style_dotted,  linewidth=1)
hline(50,      "Middle Band", color=chart.fg_color, linestyle=hline.style_dotted, linewidth=1)
hline(lowerTh, "Lower Band",  color=#9c9c9c, linestyle=hline.style_dotted,  linewidth=1)

// --- Alerts ---
alert_high    = crsi > upperTh
alert_low     = crsi < lowerTh
alert_falling = crsi[1] > upperTh and crsi <= upperTh
alert_rising  = crsi[1] < lowerTh and crsi >= lowerTh

alertcondition(alert_high,    "CRSI High (red)",                 "CRSI High (red)")
alertcondition(alert_low,     "CRSI Low (green)",                "CRSI Low (green)")
alertcondition(alert_falling, "CRSI was high (red), is falling", "CRSI was high (red), is falling")
alertcondition(alert_rising,  "CRSI was low (green), is rising", "CRSI was low (green), is rising")

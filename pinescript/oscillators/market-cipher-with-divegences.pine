//@version=6
indicator('MC With Divegences', overlay=false) 

import raf_mak/libpublic/1 as lib //my library elements


// ------------------------------------ MarketCipher B | https://marketciphertrading.com ------------------------------------ //
// Get user input
version     = input.string('2.2.6', 'Version')
useCurrentRes = input(true, 'Use Current Chart Resolution')
customRes   = input.timeframe('60', 'Use Current Chart Resolution')
obLevel1    = input(60, 'Over Bought Level 1')
trigger1    = input(53, 'Trigger 1')
osLevel1    = input(-60, 'Over Sold Level 1')
trigger2    = input(-53, 'Trigger 2')
i_vwap    = input.bool(false, 'Vwap')
i_mf    = input.bool(true, 'Money Flow')
i_mf_fill    = input.bool(false, 'Money Flow Bottom')
i_rsi    = input.bool(false, 'RSI')
i_srsi    = input.bool(false, 'SRSI')
i_trigers = input.bool(false, 'Trigers')
i_levels = input.bool(false, 'OS/OB levels')

//divergences
lbR = input.int(5,'Pivot Lookback Right', display = display.data_window)
lbL = input.int(5,'Pivot Lookback Left',  display = display.data_window)
rangeUpper = input.int(60,'Max of Lookback Range',display = display.data_window)
rangeLower = input.int(5,'Min of Lookback Range', display = display.data_window)
plotBull = input.bool(true, 'Plot Bullish', display = display.data_window)
plotHiddenBull = input.bool(true, 'Plot Hidden Bullish', display = display.data_window)
plotBear = input.bool(true, 'Plot Bearish', display = display.data_window)
plotHiddenBear = input.bool(true, 'Plot Hidden Bearish', display = display.data_window)

// Heikin Ashi calculations
heikinAshiClose = (open + high + low + close) / 4
var heikinAshiOpen = array.new_float(1, (open + close) / 2)
if (bar_index > 0)
    array.set(heikinAshiOpen, 0, (array.get(heikinAshiOpen, 0) + heikinAshiClose[1]) / 2)
heikinAshiHigh = math.max(high, array.get(heikinAshiOpen, 0), heikinAshiClose)
heikinAshiLow = math.min(low, array.get(heikinAshiOpen, 0), heikinAshiClose)

// Calculating Money Flow using Heikin Ashi
moneyFlow(period, mult, y) => ta.sma(((heikinAshiClose - array.get(heikinAshiOpen, 0)) / (heikinAshiHigh - heikinAshiLow)) * mult, period) - y

// Get components
cond(_offset) =>
    [bw1, bw2, vwap] = lib.blueWaves(hlc3, 9, 12)
    moneyFlow = moneyFlow(60, 200, 2.25)
    rsiMod = ta.sma(ta.stoch(close, high, low, 40), 2)
    stcRsiMod = ta.sma(ta.stoch(close, high, low, 81), 2)
    [bw1[_offset], bw2[_offset], vwap[_offset], moneyFlow[_offset], rsiMod[_offset], stcRsiMod[_offset]]
tf = useCurrentRes ? timeframe.period : customRes
[bw1, bw2, vwap, moneyFlow, rsiMod, stcRsiMod] = request.security(syminfo.tickerid, tf, cond(not useCurrentRes and barstate.isrealtime ? 1 : 0))

//divergences
bearColor = color.red
bullColor = color.green
hiddenBullColor = color.new(color.green, 80)
hiddenBearColor = color.new(color.red, 80)
textColor = color.white
noneColor = color.new(color.white, 100)

plFound = na(ta.pivotlow(bw1, lbL, lbR)) ? false : true
phFound = na(ta.pivothigh(bw1, lbL, lbR)) ? false : true

_inRange(cond) =>
    bars = ta.barssince(cond == true)
    rangeLower <= bars and bars <= rangeUpper

//------------------------------------------------------------------------------
// Regular Bullish
// Osc: Higher Low

oscHL = bw1[lbR] > ta.valuewhen(plFound, bw1[lbR], 1) and _inRange(plFound[1])

// Price: Lower Low

priceLL = low[lbR] < ta.valuewhen(plFound, low[lbR], 1)
bullCondAlert = priceLL and oscHL and plFound
bullCond = plotBull and bullCondAlert

plot(
     plFound ? bw1[lbR] : na,
     offset=-lbR,
     title='Regular Bullish',
     linewidth=2,
     color=(bullCond ? bullColor : noneColor),
     display = display.pane
     )

// Hidden Bullish
// Osc: Lower Low

oscLL = bw1[lbR] < ta.valuewhen(plFound, bw1[lbR], 1) and _inRange(plFound[1])

// Price: Higher Low

priceHL = low[lbR] > ta.valuewhen(plFound, low[lbR], 1)
hiddenBullCondAlert = priceHL and oscLL and plFound
hiddenBullCond = plotHiddenBull and hiddenBullCondAlert

plot(
     plFound ? bw1[lbR] : na,
     offset=-lbR,
     title='Hidden Bullish',
     linewidth=2,
     color=(hiddenBullCond ? hiddenBullColor : noneColor),
     display = display.pane
     )


//------------------------------------------------------------------------------
// Regular Bearish
// Osc: Lower High

oscLH = bw1[lbR] < ta.valuewhen(phFound, bw1[lbR], 1) and _inRange(phFound[1])

// Price: Higher High

priceHH = high[lbR] > ta.valuewhen(phFound, high[lbR], 1)

bearCondAlert = priceHH and oscLH and phFound
bearCond = plotBear and bearCondAlert

plot(
     phFound ? bw1[lbR] : na,
     offset=-lbR,
     title='Regular Bearish',
     linewidth=2,
     color=(bearCond ? bearColor : noneColor),
     display = display.pane
     )

//------------------------------------------------------------------------------
// Hidden Bearish
// Osc: Higher High

oscHH = bw1[lbR] > ta.valuewhen(phFound, bw1[lbR], 1) and _inRange(phFound[1])

// Price: Lower High

priceLH = high[lbR] < ta.valuewhen(phFound, high[lbR], 1)

hiddenBearCondAlert = priceLH and oscHH and phFound
hiddenBearCond = plotHiddenBear and hiddenBearCondAlert

plot(
     phFound ? bw1[lbR] : na,
     offset=-lbR,
     title='Hidden Bearish',
     linewidth=2,
     color=(hiddenBearCond ? hiddenBearColor : noneColor),
     display = display.pane
     )


// Plots
plot(bw1, 'Lt Blue Wave', #90CAF9, 1, plot.style_area,display =  display.all)
plot(bw2, 'Blue Wave', color.new(#0C47A1, 10), 1, plot.style_area,display =  display.all)
plot(vwap, 'VWap', color.new(color.yellow, 25), 1, plot.style_area, display = i_vwap ? display.pane : display.none)
plot(moneyFlow, 'Mny Flow', moneyFlow > 0 ? color.new(#3FFB03, 40) : color.new(#FE1000, 40), 2, plot.style_area, display = i_mf ? display.all : display.none)
//plotshape(ta.crossover(bw1, bw2) and bw1 < osLevel1, 'Buy', shape.circle, location.bottom, #3FFB03, size=size.tiny, display = i_vwap ? display.pane : display.none)
plot(ta.crossover(bw1, bw2) ? bw2 : na, 'Blue Wave Crossing Up', color.new(#01E676, 20), 3, plot.style_circles,display =  display.pane)
plot(ta.crossunder(bw1, bw2) ? bw2 : na, 'Blue Wave Crossing Down', color.new(color.red, 20), 3, plot.style_circles,display =  display.pane)
plot(0, 'Zero', color.white,display =  display.pane)
plot(100, '100%', color.new(color.white, 50), 1, plot.style_line,display =  display.pane)
plot(obLevel1, 'OB 1 Solid', color.new(color.white, 25), 2,display = i_levels ? display.pane : display.none)
plot(osLevel1, 'OS 1 Solid', color.new(color.white, 25), 2,display = i_levels ? display.pane : display.none)
plot(trigger1, 'Trigger 1', color.new(color.white, 50), 1, plot.style_circles,display = i_trigers ? display.pane : display.none)
plot(trigger2, 'Trigger 2', color.new(color.white, 50), 1, plot.style_circles,display = i_trigers ? display.pane : display.none)
plot(rsiMod, 'RSI', #E52BE6, display = i_rsi ? display.pane : display.none)
plot(stcRsiMod, 'Sto RSI', rsiMod > stcRsiMod ? #3FFB03 : #FE1000, 2, display = i_srsi ? display.pane : display.none)
p0 = plot(-93, 'Plot', color.new(color.white, 100),display =  display.pane)
p1 = plot(-103, 'Plot', color.new(color.white, 100),display =  display.pane)
fill(p0, p1, moneyFlow > 0 ? color.new(#3FFB03, 70) : color.new(#FE1000, 70), display = i_mf_fill ? display.all : display.none)
// Alerts
// alertcondition(ta.crossover(bw1, bw2), 'Blue Wave Crossing Down [Sm. Red Dot]', 'Blue Wave Crossing Down')
// alertcondition(ta.crossunder(bw1, bw2), 'Blue Wave Crossing UP [Sm. Green Dot]', 'Blue Wave Crossing UP')
// alertcondition(ta.crossover(bw1, bw2) and bw1 < osLevel1, 'Green Dot', 'Green Dot')

// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0
// © Uncle_the_shooter

//@version=6
indicator('Regression Channel Oscillator', overlay = false, precision = 2, max_lines_count=500, max_labels_count=500)

// INPUTS
// Regression Settings
len = input.int(50, 'Regression Length', minval = 5, group='Regression Settings')
mult = input.float(2.5, 'RMSE Multiplier (channel)', group='Regression Settings')
src = input.source(close, 'Source', group='Regression Settings')

// Signal Settings 
showSignalLine = input.bool(true, 'Show Signal MA', group='Signal Settings')
signalColorMode = input.string('Position vs Zero', 'Signal Color Mode',
     options = ['Direction', 'Position vs Zero'],
     group='Signal Settings')
sigLen = input.int(30, 'Signal SMA Length', group='Signal Settings')

// Oscillator Smoothing
smoothType = input.string('SMA', 'Smooth Oscillator', options = ['None', 'SMA', 'EMA'], group='Signal Settings')
smoothLen = input.int(3, 'Smooth Length', minval = 1, group='Signal Settings')

// Threshold Levels 
obLevel = input.float(50, 'Overbought Level', minval=0, maxval=150, group='Threshold Levels')
osLevel = input.float(-50, 'Oversold Level', minval=-150, maxval=0, group='Threshold Levels')
maOverbought = input.float(25, 'MA Overbought Threshold', minval=0, maxval=150, group='Threshold Levels')
maOversold = input.float(-25, 'MA Oversold Threshold', minval=-150, maxval=0, group='Threshold Levels')


// Colors 
bullColor = input.color(color.rgb(22,193,67), 'Bullish Color', group='Style Settings')
bearColor = input.color(color.rgb(229,11,11), 'Bearish Color', group='Style Settings')
signalColorBull = input.color(color.rgb(22,193,67), 'Signal SMA Bullish', group='Style Settings')  
signalColorBear = input.color(color.rgb(229,11,11), 'Signal SMA Bearish', group='Style Settings')     
obColor = input.color(color.red, 'Overbought Line Color', group='Style Settings')
osColor = input.color(color.green, 'Oversold Line Color', group='Style Settings')
midLineColor = input.color(color.gray, 'Midline 0 Color', group='Style Settings') 

// Gradient Settings
showOverboughtGradient = input.bool(true, 'Show Overbought Gradient', group='Gradient Settings')
showOversoldGradient = input.bool(true, 'Show Oversold Gradient', group='Gradient Settings')
showOscillatorGradient = input.bool(true, 'Show Oscillator Gradient', group='Gradient Settings')
showMaGradient = input.bool(true, 'Show Moving Average Gradient', group='Gradient Settings')
fillEnabled = input.bool(true, 'Enable Gradient Fill', group='Gradient Settings')
fillTransparency = input.int(70, 'Gradient Fill Transparency', minval=0, maxval=100, group='Gradient Settings')
bandTransparency = input.int(40, 'Band Gradient Transparency', minval=0, maxval=100, group='Gradient Settings')

// INTERNAL
var int gradientTransparency = 85

// FUNCTIONS 
Sum(_src, _len) =>
    a = ta.cum(_src)
    a - a[_len]

Wma(_src, _len) =>
    denom = _len * (_len + 1) / 2
    a = ta.cum(_src)
    (_len * a - Sum(a[1], _len)) / denom

// LINEAR REGRESSION
a = Wma(src[1], len)
b = Sum(src[1], len) / len
A = 4 * b - 3 * a
B = 3 * a - 2 * b
m = (A - B) / (len - 1)

// RMSE 
float d = 0.0
for i = 0 to len - 1
    l = B + m * i
    d += math.pow(src[i + 1] - l, 2)

rmse = math.sqrt(d / (len - 1)) * mult
mid = B

// OSCILLATOR 
rawOsc = rmse != 0 ? (src - mid) / rmse : 0.0
osc = rawOsc * 50
osc := math.max(math.min(osc, 150), -150)

osc := switch smoothType
    'SMA' => ta.sma(osc, smoothLen)
    'EMA' => ta.ema(osc, smoothLen)
    => osc

// SIGNAL
signal = ta.sma(osc, sigLen)

// SIGNAL COLOR MODE 
isDirectionMode = signalColorMode == "Direction"
isPositionMode  = signalColorMode == "Position vs Zero"

color dynSignalColor = color.gray

if isDirectionMode
    if signal > signal[1]
        dynSignalColor := signalColorBull
    else if signal < signal[1]
        dynSignalColor := signalColorBear
    else
        dynSignalColor := color.gray

if isPositionMode
    if signal > 0
        dynSignalColor := signalColorBull
    else if signal < 0
        dynSignalColor := signalColorBear
    else
        dynSignalColor := color.gray

// OB/OS DYNAMIC COLORS
ob_gradient_color = signal >= maOverbought ? obColor : color.gray
os_gradient_color = signal <= maOversold ? osColor : color.gray

// PLOTTING
hline(obLevel, 'Overbought', color=obColor, linestyle=hline.style_dashed)
plot(showOverboughtGradient ? obLevel : na, color=color.new(ob_gradient_color, bandTransparency), linewidth=8)

hline(osLevel, 'Oversold', color=osColor, linestyle=hline.style_dashed)
plot(showOversoldGradient ? osLevel : na, color=color.new(os_gradient_color, bandTransparency), linewidth=8)

hline(0, 'Midline 0', color=midLineColor, linestyle=hline.style_dashed)
hline(-150, 'Min', color=color.new(color.gray, 80), linestyle=hline.style_dotted)
hline(150, 'Max', color=color.new(color.gray, 80), linestyle=hline.style_dotted)

lineColor = osc > 0 ? bullColor : bearColor
pOsc = plot(osc, 'Oscillator', color=lineColor, linewidth=3)
plot(showOscillatorGradient ? osc : na, color=color.new(lineColor, gradientTransparency), linewidth=10)

// SIGNAL PLOT
pSignal = plot(showSignalLine ? signal : na, "Signal SMA", dynSignalColor, 1)

// SIGNAL GRADIENT
plot(showSignalLine and showMaGradient ? signal : na, "", color.new(dynSignalColor, 60), 4)
plot(showSignalLine and showMaGradient ? signal : na, "", color.new(dynSignalColor, 35), 2)
plot(showSignalLine and showMaGradient ? signal : na, "", color.new(dynSignalColor, 0), 1)

// GRADIENT FILL 
pMid1 = plot(osc * 0.8, color=na, display=display.none)
pMid2 = plot(osc * 0.6, color=na, display=display.none)
pMid3 = plot(osc * 0.4, color=na, display=display.none)
pMid4 = plot(osc * 0.2, color=na, display=display.none)
pMid5 = plot(0, color=na, display=display.none)

fill(pOsc,  pMid1, color=fillEnabled ? color.new(lineColor, fillTransparency)     : na)
fill(pMid1, pMid2, color=fillEnabled ? color.new(lineColor, fillTransparency + 4)  : na)
fill(pMid2, pMid3, color=fillEnabled ? color.new(lineColor, fillTransparency + 8)  : na)
fill(pMid3, pMid4, color=fillEnabled ? color.new(lineColor, fillTransparency + 12) : na)
fill(pMid4, pMid5, color=fillEnabled ? color.new(lineColor, fillTransparency + 16) : na)

// OB/OS SIGNALS 
longOBOS  = ta.crossover(osc, osLevel)
shortOBOS = ta.crossunder(osc, obLevel)

plotshape(longOBOS  ? osLevel - 3 : na, style=shape.triangleup,   location=location.absolute, color=osColor,    size=size.small)
plotshape(shortOBOS ? obLevel + 3 : na, style=shape.triangledown, location=location.absolute, color=obColor,    size=size.small)

// ALERTY
alertcondition(longOBOS,  'Buy (Oversold)',  'Oscillator crossed above oversold.')
alertcondition(shortOBOS, 'Sell (Overbought)', 'Oscillator crossed below overbought.')


// SIGNAL MA ALERTS
// Zero line cross 
signalCrossUpZero   = ta.crossover(signal, 0)
signalCrossDownZero = ta.crossunder(signal, 0)

//  MA OB/OS cross 
signalCrossUpOS     = ta.crossover(signal, maOversold)
signalCrossDownOB   = ta.crossunder(signal, maOverbought)
// Alert conditions
alertcondition(signalCrossUpZero,   'Signal MA Cross Above 0',      'Signal MA crossed ABOVE zero line.')
alertcondition(signalCrossDownZero, 'Signal MA Cross Below 0',      'Signal MA crossed BELOW zero line.')

alertcondition(signalCrossUpOS,     'Signal MA Cross Above MA OS',  'Signal MA crossed ABOVE its oversold threshold.')
alertcondition(signalCrossDownOB,   'Signal MA Cross Below MA OB',  'Signal MA crossed BELOW its overbought threshold.')

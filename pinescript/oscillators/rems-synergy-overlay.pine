// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © KodiakMarket

//REMS Synergy. Uses confluence of RSI, EMAs, MACD, and Stochastic RSI as other REMS indicators, but with more control over filters in 3 confluence levels.
//MACD values hard-coded as 12/26/9 for both timeframes
//RSI smoothing hard-coded at 20 (both timeframes) w/ length of 14/7 (primary/secondary)
//StochasticRSI hard-coded as 3/3/14/8 on Primary timeframe and 3/2/7/7 on Secondary
//v1.9.8 with Added Histogram filter and adjustments to Stochastic Range Filter

//@version=6
indicator("REMS Synergy w/ Enhanced Candles",shorttitle = "REMS Synergy Overlay" ,overlay = true, max_labels_count = 500)

///////////////////////////
// Timeframes
///////////////////////////
primaryTF   = input.timeframe("", "Primary Timeframe", group="Timeframe")
secondaryTF = input.timeframe("2", "Secondary Timeframe", group="Timeframe")

waitPrimary   = input.bool(true,  "Wait for Primary Close", group="Timeframe")
waitSecondary = input.bool(true, "Wait for Secondary Close", group="Timeframe")

lookaheadSetting(wait) =>
    wait ? barmerge.lookahead_off : barmerge.lookahead_on

///////////////////////////
// EMA Inputs
///////////////////////////
emaAlphaLen = input.int(8,  "EMA Alpha Length", minval = 1, tooltip = "EMA Value used on both timeframes, calculated independently", group="EMA Parameters")
emaBetaLen  = input.int(21, "EMA Beta Length",  minval = 1, tooltip = "EMA Value used on both timeframes, calculated independently", group="EMA Parameters")

// Enhanced Candles Inputs
// ====================
colorTrigger     = input.string("Off", "Color Trigger", options=["Off", "EMA Cross - Primary", "EMA Cross - Secondary", "Confluence 1", "Confluence 2", "Confluence 3"], tooltip = "Select conditions for Enhanced Candles. Colors bullish/bearish conditions based on selecion. Visual Only.", group="Enhanced Candles")
directionFilter  = input.string("N/A", "Direction Filter", options=["N/A", "Bullish Only", "Bearish Only"], tooltip = "Select if you have a bias and only want to see respective candles.", group="Enhanced Candles")

// MACD Thresholds (single definition, next to EMAs)
macdLongThreshold  = input.float(0.0, "MACD Long Threshold MAX.", step=0.01, tooltip = "Maximum MACD (Primary) permitted for LONG signal", group="MACD Threshold (Momentum Cue)")
macdShortThreshold = input.float(0.0, "MACD Short Threshold MIN.", step=0.01, tooltip = "Minimum MACD (Primary) permitted for SHORT signal", group="MACD Threshold (Momentum Cue)")

// ────────────────────────────────
// VWAP
// ────────────────────────────────
vwapValue = ta.vwap
plot(vwapValue, "VWAP (Session)", color=color.white, linewidth=2)

//──────────────────────────────
// Stochastic RSI Range Inputs
//──────────────────────────────

// Primary
stochShortMaxK_P = input.float(100.0, "Max. %K Short", minval=0, maxval=100, step=0.1, group = "Primary Stochastic Range", inline = "PMAX")
stochShortMinK_P = input.float(20.0, "Min. %K Short ", minval=0, maxval=100, step=0.1, group = "Primary Stochastic Range", inline = "PMIN")
stochLongMaxK_P  = input.float(80.0, "Max. %K Long",  minval=0, maxval=100, step=0.1, group = "Primary Stochastic Range", inline = "PMAX")
stochLongMinK_P  = input.float(0, "Min. %K Long ",  minval=0, maxval=100, step=0.1, group = "Primary Stochastic Range", inline = "PMIN")

// Secondary
stochShortMaxK_S = input.float(100.0, "Max. %K Short", minval=0, maxval=100, step=0.1, group = "Secondary Stochastic Range", inline = "SMAX")
stochShortMinK_S = input.float(30.0, "Min. %K Short ", minval=0, maxval=100, step=0.1, group = "Secondary Stochastic Range", inline = "SMIN")
stochLongMaxK_S  = input.float(70.0, "Max. %K Long",  minval=0, maxval=100, step=0.1, group = "Secondary Stochastic Range", inline = "SMAX")
stochLongMinK_S  = input.float(0, "Min. %K Long ",  minval=0, maxval=100, step=0.1, group = "Secondary Stochastic Range", inline = "SMIN")

///////////////////////////
// EMA Series
///////////////////////////
emaPrimaryAlpha   = request.security(syminfo.tickerid, primaryTF,   ta.ema(close, emaAlphaLen), gaps=barmerge.gaps_off, lookahead=lookaheadSetting(waitPrimary))
emaPrimaryBeta    = request.security(syminfo.tickerid, primaryTF,   ta.ema(close, emaBetaLen),  gaps=barmerge.gaps_off, lookahead=lookaheadSetting(waitPrimary))
emaSecondaryAlpha = request.security(syminfo.tickerid, secondaryTF, ta.ema(close, emaAlphaLen), gaps=barmerge.gaps_off, lookahead=lookaheadSetting(waitSecondary))
emaSecondaryBeta  = request.security(syminfo.tickerid, secondaryTF, ta.ema(close, emaBetaLen),  gaps=barmerge.gaps_off, lookahead=lookaheadSetting(waitSecondary))

// Plot EMAs
pAlpha = plot(emaPrimaryAlpha, "Primary EMA Alpha", color=color.yellow, linewidth=2)
pBeta  = plot(emaPrimaryBeta,  "Primary EMA Beta",  color=color.blue,   linewidth=2)
plot(emaSecondaryAlpha, "Secondary EMA Alpha", color=color.orange, linewidth=2, display=display.none)
plot(emaSecondaryBeta,  "Secondary EMA Beta", color=color.red,    linewidth=2, display=display.none)

// EMA Gradient
fillColor = emaPrimaryAlpha >= emaPrimaryBeta ? color.new(color.green, 80) : color.new(color.red, 80)
fill(pAlpha, pBeta, color=fillColor)

///////////////////////////
// RSI Series
///////////////////////////
rsiSource = close
rsiLengthPrimary   = 14
rsiLengthSecondary = 7
rsiSmoothing       = 20

rsiPrimary   = request.security(syminfo.tickerid, primaryTF,   ta.rsi(rsiSource, rsiLengthPrimary), gaps=barmerge.gaps_off, lookahead=lookaheadSetting(waitPrimary))
rsiPrimaryMA = ta.sma(rsiPrimary, rsiSmoothing)
rsiSecondary = request.security(syminfo.tickerid, secondaryTF, ta.rsi(rsiSource, rsiLengthSecondary), gaps=barmerge.gaps_off, lookahead=lookaheadSetting(waitSecondary))
rsiSecondaryMA = ta.sma(rsiSecondary, rsiSmoothing)

///////////////////////////
// Stochastic RSI Helper
///////////////////////////
stochRsiK(rsiLen, stLen, kLen) =>
    r  = ta.rsi(close, rsiLen)
    lo = ta.lowest(r, stLen)
    hi = ta.highest(r, stLen)
    den = math.max(hi - lo, 1e-10)
    raw = (r - lo) / den * 100.0
    ta.sma(raw, kLen)

stochRsiD(kSeries, dLen) =>
    ta.sma(kSeries, dLen)

stochKPrimary    = 3
stochDPrimary    = 3
stochRSILengthP  = 14
stochStochLengthP = 8

stochKSecondary  = 3
stochDSecondary  = 2
stochRSILengthS  = 7
stochStochLengthS = 7

stochK_P = request.security(syminfo.tickerid, primaryTF,   stochRsiK(stochRSILengthP, stochStochLengthP, stochKPrimary), gaps=barmerge.gaps_off, lookahead=lookaheadSetting(waitPrimary))
stochD_P = stochRsiD(stochK_P, stochDPrimary)
stochK_S = request.security(syminfo.tickerid, secondaryTF, stochRsiK(stochRSILengthS, stochStochLengthS, stochKSecondary), gaps=barmerge.gaps_off, lookahead=lookaheadSetting(waitSecondary))
stochD_S = stochRsiD(stochK_S, stochDSecondary)

//──────────────────────────────
// Stochastic Range Check Helper
//──────────────────────────────
stochInRange(k, minK, maxK) =>
    minKReal = math.min(minK, maxK)
    maxKReal = math.max(minK, maxK)
    k >= minKReal and k <= maxKReal

///////////////////////////
// MACD Series
///////////////////////////
[macdP, sigP, _] = request.security(syminfo.tickerid, primaryTF,   ta.macd(close, 12, 26, 9), gaps=barmerge.gaps_off, lookahead=lookaheadSetting(waitPrimary))
[macdS, sigS, _] = request.security(syminfo.tickerid, secondaryTF, ta.macd(close, 12, 26, 9), gaps=barmerge.gaps_off, lookahead=lookaheadSetting(waitSecondary))

// MACD Histogram Direction Logic
macdHistP = macdP - sigP
macdHistS = macdS - sigS

macdHistRisingP  = macdHistP > macdHistP[1]
macdHistFallingP = macdHistP < macdHistP[1]
macdHistRisingS  = macdHistS > macdHistS[1]
macdHistFallingS = macdHistS < macdHistS[1]

///////////////////////////
// Confluence 1 Inputs (5 checkboxes)
///////////////////////////
useRSI1        = input.bool(true,  "RSI", tooltip = "RSI Primary (14) and RSI Secondary (7) are both above/below RSI SMA (20) in their respective timeframes.", group="Confluence 1 Signal Parameters")
useEMA1        = input.bool(true,  "EMA", tooltip = "EMA Alpha is above EMA Beta, in both respective timeframes (Primary + Secondary).", group="Confluence 1 Signal Parameters")
useMACD1       = input.bool(true,  "MACD - Signal Line", tooltip = "Primary MACD (12, 26, 9) is above/below Signal Line in both timeframes.", group="Confluence 1 Signal Parameters")
useMACDHistC1  = input.bool(false, "MACD - Histogram Filter", tooltip="Requires both MACD histograms (primary + secondary) to be rising for long or falling for short.", group="Confluence 1 Signal Parameters")
filterMACDC1   = input.bool(false, "Filter MACD Threshold (Primary Only)", tooltip = "Respects MACD Filter Threshold on the Primary Timeframe.", group="Confluence 1 Signal Parameters")
useStoch1      = input.bool(true,  "Stochastic RSI", tooltip = "Primary Stochastic RSI (3,3,14,8) %K and Secondary RSI (3,2,7,7) %K are both above/below %D in there respective timeframes.", group="Confluence 1 Signal Parameters")
useStochRangeC1  = input.bool(false, "Use Stochastic Range (Primary and Secondary)", tooltip = "Adheres to %K ranges set for both timeframes.", group="Confluence 1 Signal Parameters")
useVWAP_C1 = input.bool(false, "VWAP Filter (Session)", group="Confluence 1 Signal Parameters")

///////////////////////////
// Confluence 2 Inputs (9 checkboxes)
///////////////////////////
useRSIPrimaryC2    = input.bool(true,  "RSI Primary                    ", group="Confluence 2 Signal Parameters", inline = "C2RSI")
useRSISecondaryC2  = input.bool(true,  "RSI Secondary", group="Confluence 2 Signal Parameters", inline = "C2RSI")
useEMAPrimaryC2    = input.bool(true,  "EMA Primary                 ", group="Confluence 2 Signal Parameters", inline = "C2EMA")
useEMASecondaryC2  = input.bool(true,  "EMA Secondary", group="Confluence 2 Signal Parameters", inline = "C2EMA")
useMACDPrimaryC2   = input.bool(true,  "MACD (Primary)        ", group="Confluence 2 Signal Parameters", inline = "C2MACD")
useMACDSecondaryC2 = input.bool(false, "MACD (Secondary)", tooltip = "Evaluates MACD (12, 26, 9) as above/below Signal Line on respective timeframes.", group="Confluence 2 Signal Parameters", inline = "C2MACD")
useMACDHistC2_P = input.bool(false, "MACD Hist. (Prime)", group="Confluence 2 Signal Parameters", inline = "C2MACDHist")
useMACDHistC2_S = input.bool(false, "MACD Hist. (Second)", group="Confluence 2 Signal Parameters", inline = "C2MACDHist")
filterMACDC2       = input.bool(false, "Filter MACD Threshold (Primary Only)", group="Confluence 2 Signal Parameters")
useStochP_C2       = input.bool(true,  "Stoch RSI Primary   ", group="Confluence 2 Signal Parameters", inline = "C2Stoch")
useStochS_C2       = input.bool(true,  "Stoch RSI Secondary", group="Confluence 2 Signal Parameters", inline = "C2Stoch")
useStochRangePC2 = input.bool(false, "Use Primary Stochastic Range", group="Confluence 2 Signal Parameters")
useStochRangeSC2 = input.bool(false, "Use Secondary Stochastic Range", group="Confluence 2 Signal Parameters")
useVWAP_C2 = input.bool(false, "VWAP Filter (Session)", group="Confluence 2 Signal Parameters")

// ────────────────
// Confluence 3 Inputs (9 checkboxes)
// ────────────────
requiredTrueC3     = input.int(3, "Number of True Signals Required", minval=1, maxval=14, tooltip = "Select possible filters. Required number from selected filters must be true to trigger signal.", group="Confluence 3 Signal Parameters")
useRSIPrimaryC3    = input.bool(false,  "RSI Primary                    ", group="Confluence 3 Signal Parameters", inline = "C3RSI")
useRSISecondaryC3  = input.bool(false,  "RSI Secondary", group="Confluence 3 Signal Parameters", inline = "C3RSI")
useEMAPrimaryC3    = input.bool(false,  "EMA Primary                 ", group="Confluence 3 Signal Parameters", inline = "C3EMA")
useEMASecondaryC3  = input.bool(false,  "EMA Secondary", group="Confluence 3 Signal Parameters", inline = "C3EMA")
useMACDPrimaryC3   = input.bool(false,  "MACD (Primary)        ", group="Confluence 3 Signal Parameters", inline = "C3MACD")
useMACDSecondaryC3 = input.bool(false, "MACD (Secondary)", group="Confluence 3 Signal Parameters", inline = "C3MACD")
useMACDHistC3_P = input.bool(false, "MACD Hist. (Prime)", group="Confluence 3 Signal Parameters", inline = "C3MACDHist")
useMACDHistC3_S = input.bool(false, "MACD Hist. (Second)", group="Confluence 3 Signal Parameters", inline = "C3MACDHist")
filterMACDC3 = input.bool(false, "MACD Threshold Filter (Primary Only)", group="Confluence 3 Signal Parameters")
useStochP_C3       = input.bool(false,  "Stoch RSI Primary   ", group="Confluence 3 Signal Parameters", inline = "C3Stoch")
useStochS_C3       = input.bool(false,  "Stoch RSI Secondary", group="Confluence 3 Signal Parameters", inline = "C3Stoch")
useStochRangePC3 = input.bool(false, "Use Stochastic Range (Primary)", group="Confluence 3 Signal Parameters")
useStochRangeSC3 = input.bool(false, "Use Stochastic Range (Secondary)", group="Confluence 3 Signal Parameters")
useVWAP_C3 = input.bool(false, "VWAP Filter (Session)", group="Confluence 3 Signal Parameters")

////////////////////////////
// Confluence Helper Function (C1 / C2)
// Includes VWAP, Stoch Range, and MACD Histogram support
///////////////////////////
confluence(longDir, 
           rsiCheckP=false, rsiCheckS=false, 
           emaCheckP=false, emaCheckS=false, 
           macdCheckP=false, macdCheckS=false, 
           stochCheckP=false, stochCheckS=false, 
           filterMACD=false, 
           useStochRangeC1=false, useStochRangePC2=false, useStochRangeSC2=false, 
           useVWAP=false,
           useMACDHistC1=false, useMACDHistC2_P=false, useMACDHistC2_S=false) =>

    // ─── Skip if nothing selected ──────────────────────────────
    anySelected = rsiCheckP or rsiCheckS or emaCheckP or emaCheckS or macdCheckP or macdCheckS or stochCheckP or stochCheckS or useStochRangeC1 or useStochRangePC2 or useStochRangeSC2 or useVWAP or useMACDHistC1 or useMACDHistC2_P or useMACDHistC2_S
    if not anySelected
        false
    else
        // ─── RSI Checks ──────────────────────────────
        rsiOK = (not rsiCheckP or (longDir ? rsiPrimary > rsiPrimaryMA : rsiPrimary < rsiPrimaryMA)) and  
                 (not rsiCheckS or (longDir ? rsiSecondary > rsiSecondaryMA : rsiSecondary < rsiSecondaryMA))

        // ─── EMA Checks ──────────────────────────────
        emaOK = (not emaCheckP or (longDir ? emaPrimaryAlpha > emaPrimaryBeta : emaPrimaryAlpha < emaPrimaryBeta)) and  
                 (not emaCheckS or (longDir ? emaSecondaryAlpha > emaSecondaryBeta : emaSecondaryAlpha < emaSecondaryBeta))

        // ─── MACD Cross + Threshold ───────────────────
        macdOK = (not macdCheckP or (longDir ? macdP > sigP : macdP < sigP)) and
                 (not macdCheckS or (longDir ? macdS > sigS : macdS < sigS))
        if filterMACD
            macdOK := macdOK and (longDir ? macdP <= macdLongThreshold : macdP >= macdShortThreshold)

        // ─── MACD Histogram ───────────────────────────
        macdHistOK = (not useMACDHistC1 or (longDir ? (macdHistRisingP and macdHistRisingS) : (macdHistFallingP and macdHistFallingS))) and
                     (not useMACDHistC2_P or (longDir ? macdHistRisingP : macdHistFallingP)) and
                     (not useMACDHistC2_S or (longDir ? macdHistRisingS : macdHistFallingS))

        // ─── Stochastic Signal Cross ──────────────────
        stochOK = (not stochCheckP or (longDir ? stochK_P > stochD_P : stochK_P < stochD_P)) and
                  (not stochCheckS or (longDir ? stochK_S > stochD_S : stochK_S < stochD_S))

        // ─── VWAP ─────────────────────────────────────
        vwapOK = not useVWAP or (longDir ? close > vwapValue : close < vwapValue)

        // ─── Stochastic Range Logic ───────────────────
        // C1: one toggle; both must be within their respective ranges
        stochRangePass_C1 = not useStochRangeC1 or ((longDir ? stochInRange(stochK_P, stochLongMinK_P, stochLongMaxK_P) : stochInRange(stochK_P, stochShortMinK_P, stochShortMaxK_P)) and (longDir ? stochInRange(stochK_S, stochLongMinK_S, stochLongMaxK_S) : stochInRange(stochK_S, stochShortMinK_S, stochShortMaxK_S)))

        // C2: independent toggles for Primary and Secondary
        stochP_Pass_C2 = not useStochRangePC2 or (longDir ? stochInRange(stochK_P, stochLongMinK_P, stochLongMaxK_P) : stochInRange(stochK_P, stochShortMinK_P, stochShortMaxK_P))
        stochS_Pass_C2 = not useStochRangeSC2 or (longDir ? stochInRange(stochK_S, stochLongMinK_S, stochLongMaxK_S) : stochInRange(stochK_S, stochShortMinK_S, stochShortMaxK_S))
        stochRangePass_C2 = stochP_Pass_C2 and stochS_Pass_C2

        // Final Stochastic Range Evaluation
        stochRangePass = (useStochRangeC1 and stochRangePass_C1) or 
                         ((useStochRangePC2 or useStochRangeSC2) and stochRangePass_C2) or 
                         (not useStochRangeC1 and not useStochRangePC2 and not useStochRangeSC2)

        // ─── Final Confluence Evaluation ──────────────
        rsiOK and emaOK and macdOK and macdHistOK and stochOK and stochRangePass and vwapOK

////////////////////////////
// Confluence 3 Helper Function (Stochastic Range Fix)
///////////////////////////
confluence3(longDir, rsiCheckP=false, rsiCheckS=false, emaCheckP=false, emaCheckS=false, macdCheckP=false, macdCheckS=false, stochCheckP=false, stochCheckS=false, macdThresholdCheck=false, requiredTrue=1, useStochRangePC3=false, useStochRangeSC3=false, useVWAP=false, useMACDHistC3_P=false, useMACDHistC3_S=false) =>

    // ── Return false immediately if nothing is toggled ─────────────
    anyToggled = rsiCheckP or rsiCheckS or emaCheckP or emaCheckS or macdCheckP or macdCheckS or stochCheckP or stochCheckS or macdThresholdCheck or useStochRangePC3 or useStochRangeSC3 or useVWAP or useMACDHistC3_P or useMACDHistC3_S
    if not anyToggled
        false
    else
        // ── Build array of active signals ─────────────────────────
        signals = array.new_bool(0)

        // Only push a condition if the toggle is enabled
        if rsiCheckP
            array.push(signals, longDir ? rsiPrimary > rsiPrimaryMA : rsiPrimary < rsiPrimaryMA)
        if rsiCheckS
            array.push(signals, longDir ? rsiSecondary > rsiSecondaryMA : rsiSecondary < rsiSecondaryMA)

        if emaCheckP
            array.push(signals, longDir ? emaPrimaryAlpha > emaPrimaryBeta : emaPrimaryAlpha < emaPrimaryBeta)
        if emaCheckS
            array.push(signals, longDir ? emaSecondaryAlpha > emaSecondaryBeta : emaSecondaryAlpha < emaSecondaryBeta)

        if macdCheckP
            array.push(signals, longDir ? macdP > sigP : macdP < sigP)
        if macdCheckS
            array.push(signals, longDir ? macdS > sigS : macdS < sigS)

        if stochCheckP
            array.push(signals, longDir ? stochK_P > stochD_P : stochK_P < stochD_P)
        if stochCheckS
            array.push(signals, longDir ? stochK_S > stochD_S : stochK_S < stochD_S)

        // Stochastic Range is now a separate count
        if useStochRangePC3
            array.push(signals, stochInRange(stochK_P, stochLongMinK_P, stochLongMaxK_P))
        if useStochRangeSC3
            array.push(signals, stochInRange(stochK_S, stochLongMinK_S, stochLongMaxK_S))

        if useVWAP
            array.push(signals, longDir ? close > vwapValue : close < vwapValue)

        if macdThresholdCheck
            macdFilterOK = longDir ? macdP <= macdLongThreshold : macdP >= macdShortThreshold
            array.push(signals, macdFilterOK)

        if useMACDHistC3_P
            array.push(signals, longDir ? macdHistRisingP : macdHistFallingP)
        if useMACDHistC3_S
            array.push(signals, longDir ? macdHistRisingS : macdHistFallingS)

        // ── Count the number of true signals ───────────────────────
        trueCount = 0
        for i = 0 to array.size(signals)-1
            trueCount += array.get(signals, i) ? 1 : 0

        // ── Return true if trueCount meets or exceeds requiredTrue ──
        trueCount >= requiredTrue

     
//////////////////////////
// Evaluate Confluence 1
///////////////////////////
long1  = confluence(true,  rsiCheckP=useRSI1, rsiCheckS=useRSI1,  emaCheckP=useEMA1, emaCheckS=useEMA1,  macdCheckP=useMACD1, macdCheckS=useMACD1,  stochCheckP=useStoch1, stochCheckS=useStoch1,  filterMACD=filterMACDC1,  useStochRangeC1=useStochRangeC1, useVWAP=useVWAP_C1, useMACDHistC1=useMACDHistC1)
short1 = confluence(false, rsiCheckP=useRSI1, rsiCheckS=useRSI1,  emaCheckP=useEMA1, emaCheckS=useEMA1,  macdCheckP=useMACD1, macdCheckS=useMACD1,  stochCheckP=useStoch1, stochCheckS=useStoch1,  filterMACD=filterMACDC1, useStochRangeC1=useStochRangeC1, useVWAP=useVWAP_C1, useMACDHistC1=useMACDHistC1)

///////////////////////////
// Evaluate Confluence 2
///////////////////////////
long2  = confluence(true,  rsiCheckP=useRSIPrimaryC2, rsiCheckS=useRSISecondaryC2, emaCheckP=useEMAPrimaryC2, emaCheckS=useEMASecondaryC2, macdCheckP=useMACDPrimaryC2, macdCheckS=useMACDSecondaryC2, stochCheckP=useStochP_C2, stochCheckS=useStochS_C2, filterMACD=filterMACDC2, useStochRangePC2=useStochRangePC2, useStochRangeSC2=useStochRangeSC2, useVWAP=useVWAP_C2, useMACDHistC2_P=useMACDHistC2_P, useMACDHistC2_S=useMACDHistC2_S)
short2 = confluence(false, rsiCheckP=useRSIPrimaryC2, rsiCheckS=useRSISecondaryC2, emaCheckP=useEMAPrimaryC2, emaCheckS=useEMASecondaryC2, macdCheckP=useMACDPrimaryC2, macdCheckS=useMACDSecondaryC2, stochCheckP=useStochP_C2, stochCheckS=useStochS_C2, filterMACD=filterMACDC2, useStochRangePC2=useStochRangePC2, useStochRangeSC2=useStochRangeSC2, useVWAP=useVWAP_C2, useMACDHistC2_P=useMACDHistC2_P, useMACDHistC2_S=useMACDHistC2_S)

///////////////////////////
// Evaluate Confluence 3
///////////////////////////
long3  = confluence3(true, rsiCheckP=useRSIPrimaryC3, rsiCheckS=useRSISecondaryC3, emaCheckP=useEMAPrimaryC3, emaCheckS=useEMASecondaryC3, macdCheckP=useMACDPrimaryC3, macdCheckS=useMACDSecondaryC3, stochCheckP=useStochP_C3, stochCheckS=useStochS_C3, macdThresholdCheck=filterMACDC3, requiredTrue=requiredTrueC3, useStochRangePC3=useStochRangePC3, useStochRangeSC3=useStochRangeSC3, useVWAP=useVWAP_C3, useMACDHistC3_P=useMACDHistC3_P, useMACDHistC3_S=useMACDHistC3_S)
short3 = confluence3(false, rsiCheckP=useRSIPrimaryC3, rsiCheckS=useRSISecondaryC3, emaCheckP=useEMAPrimaryC3, emaCheckS=useEMASecondaryC3, macdCheckP=useMACDPrimaryC3, macdCheckS=useMACDSecondaryC3, stochCheckP=useStochP_C3, stochCheckS=useStochS_C3, macdThresholdCheck=filterMACDC3, requiredTrue=requiredTrueC3, useStochRangePC3=useStochRangePC3, useStochRangeSC3=useStochRangeSC3, useVWAP=useVWAP_C3, useMACDHistC3_P=useMACDHistC3_P, useMACDHistC3_S=useMACDHistC3_S)

// --- Baseline Logic
baselineLogic = switch colorTrigger
    "Off"                   => 0
    "EMA Cross - Primary"   => emaPrimaryAlpha > emaPrimaryBeta ? 1 : emaPrimaryAlpha < emaPrimaryBeta ? -1 : 0
    "EMA Cross - Secondary" => emaSecondaryAlpha > emaSecondaryBeta ? 1 : emaSecondaryAlpha < emaSecondaryBeta ? -1 : 0
    "Confluence 1"          => long1 ? 1 : short1 ? -1 : 0
    "Confluence 2"          => long2 ? 1 : short2 ? -1 : 0
    "Confluence 3"          => long3 ? 1 : short3 ? -1 : 0

// --- Neutral color (semi-transparent gray)
neutralCol = color.new(color.gray, 90)

// --- Preliminary Candle Color
preColor = baselineLogic == 1 and close > open ? color.new(#2c6457, 10) : 
           baselineLogic == -1 and close < open ? color.new(#b22d5b, 10) : 
           neutralCol

// --- Final Color Assignment
var color finalColor = na

if colorTrigger == "Off"
    finalColor := na  // default candles
else
    tmpColor = preColor
    // Apply Direction Filter
    if directionFilter == "Bullish Only" and tmpColor == color.new(#b22d5b, 10) //Used to keep candle states. Should be the same as baseline to be invisible in Style Tab
        tmpColor := neutralCol
    else if directionFilter == "Bearish Only" and tmpColor == color.new(#2c6457, 10) //Used to keep candle states. Should be the same as baseline to be invisible in Style Tab
        tmpColor := neutralCol
    finalColor := tmpColor

// --- Apply Coloring
barcolor(finalColor, title="Enhanced Candle Color")

///////////////////////////
// Plot Shapes
///////////////////////////
plotshape(long1,  title="Confluence 1 Long",  style=shape.triangleup,   location=location.belowbar, color=color.green, size=size.normal)
plotshape(short1, title="Confluence 1 Short", style=shape.triangledown, location=location.abovebar, color=color.red,   size=size.normal)
plotshape(long2,  title="Confluence 2 Long",  style=shape.triangleup,   location=location.belowbar, color=color.green, size=size.small)
plotshape(short2, title="Confluence 2 Short", style=shape.triangledown, location=location.abovebar, color=color.red,   size=size.small)
plotshape(long3,  title="Confluence 3 Long",  style=shape.triangleup,   location=location.belowbar, color=color.green, size=size.tiny)
plotshape(short3, title="Confluence 3 Short", style=shape.triangledown, location=location.abovebar, color=color.red, size=size.tiny)

// @LUPEN

//@version=6
indicator('Liquidity Sentiment Profile | LUPEN', shorttitle = 'LSP', precision = 2)

// =============================================================================
// SEZIONE INPUTS
// =============================================================================

grp_calc = '═══ SIGNAL INPUTS ═══'
inp_length = input.int(defval = 14, title = 'LENGHT', minval = 1, group = grp_calc)
inp_smooth = input.int(defval = 5, title = 'SMOOTHING', minval = 1, group = grp_calc)
show_crossings = input.bool(defval = false, title = 'Show the corsings?', group = grp_calc)

grp_vis = '═══ COLORS GROUP ═══'
c_bull_strong = input.color(#29ff7b, 'Bull Strong HEX', group = grp_vis)
c_bull_weak = input.color(#29ff7f, 'Bull Weak HEX', group = grp_vis)
c_bear_strong = input.color(#ff6685, 'Bear Strong HEX', group = grp_vis)
c_bear_weak = input.color(#ff668f, 'Bear Weak HEX', group = grp_vis)
c_neutral = input.color(#455A64, 'Neutral HEX', group = grp_vis)

// =============================================================================
// CANDLE MICRO-STRUCTURE
// =============================================================================

body_size = math.abs(close - open)
upper_wick = high - math.max(close, open)
lower_wick = math.min(close, open) - low
bull_pressure_raw = (close > open ? body_size : 0) + lower_wick
bear_pressure_raw = (close < open ? body_size : 0) + upper_wick
vol_weighted_bull = bull_pressure_raw * volume
vol_weighted_bear = bear_pressure_raw * volume
sum_bull = ta.sma(vol_weighted_bull, inp_length) * inp_length
sum_bear = ta.sma(vol_weighted_bear, inp_length) * inp_length
total_flow = sum_bull + sum_bear
lsp_raw = total_flow != 0 ? (sum_bull - sum_bear) / total_flow * 100 : 0
lsp_signal = ta.ema(lsp_raw, inp_smooth)

// =============================================================================
// COLORS
// =============================================================================

is_bullish = lsp_raw > 0
is_growing = lsp_raw > lsp_raw[1]
is_above_signal = lsp_raw > lsp_signal
col_dynamic = is_bullish ? is_growing ? c_bull_strong : c_bull_weak : is_growing ? c_bear_weak : c_bear_strong
fill_core_color = is_above_signal ? is_growing ? color.new(c_bull_strong, 40) : color.new(c_bull_weak, 60) : is_growing ? color.new(c_bear_weak, 60) : color.new(c_bear_strong, 40)
fill_base_color = is_bullish ? color.new(c_bull_strong, 90) : color.new(c_bear_strong, 90)
cross_up = ta.crossover(lsp_raw, lsp_signal)
cross_down = ta.crossunder(lsp_raw, lsp_signal)

// =============================================================================
// VISUALISATIONS
// =============================================================================

hline(50, 'Upper Zone', color = #546E7A, linestyle = hline.style_dotted)
hline(-50, 'Lower Zone', color = #546E7A, linestyle = hline.style_dotted)
p_zero = plot(0, 'Zero Base', color = #90A4AE, display = display.none)
plot(lsp_raw, 'LSP Intensity Area', style = plot.style_area, color = color.new(col_dynamic, 80))
p_lsp = plot(lsp_raw, 'LSP Line', color = col_dynamic, linewidth = 2, display = display.none)
p_sig = plot(lsp_signal, 'Signal Line', color = #FFEB3B, linewidth = 1, display = display.none)
fill(p_lsp, p_sig, color = fill_core_color, title = 'Trend Intensity Core')
fill(p_lsp, p_zero, color = fill_base_color, title = 'Volume Depth')

// SIGNALS
plotshape(show_crossings and cross_up ? -105 : na, title = 'Buy Signal', style = shape.triangleup, location = location.absolute, color = #00E676, size = size.small)
plotshape(show_crossings and cross_down ? 105 : na, title = 'Sell Signal', style = shape.triangledown, location = location.absolute, color = #FF1744, size = size.small)

// ALERTS
alertcondition(cross_up, title = 'LSP Buy', message = 'LSP Bullish Crossover')
alertcondition(cross_down, title = 'LSP Sell', message = 'LSP Bearish Crossover')

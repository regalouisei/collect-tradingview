// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © MisinkoMaster

//@version=6
indicator("Filtered Percentile Oscillator", "FPO | MisinkoMaster", overlay = false, format = format.percent,
     behind_chart = false)
/////////////////////////////////////////////////////////////////////////////////////////////////////////////
//Import Library
import TradingView/ta/12
/////////////////////////////////////////////////////////////////////////////////////////////////////////////
//Get User Inputs
len = input.int(29, "Length", step = 1, minval = 2,
     tooltip = "Changes the length of the Percentile Oscillator",
     group = "Percentile Oscillator | MisinkoMaster", inline = "PO")

src = input.source(close, "Source",
     tooltip = "Changes the source used for the Percentile Oscillator",
     group = "Percentile Oscillator | MisinkoMaster", inline = "PO")

adx_len = input.int(12, "Filter Length", step = 1, minval = 2,
     tooltip = "Changes the length of the filter",
     group = "Filter")

ap_len = input.int(8, "Filter % Length", step = 1, minval = 2,
     tooltip = "Changes the length for the Filter %",
     group = "Filter")

ut = input.int(10, "Trending Upper Treshhold", step = 1, minval = 0,
     tooltip = "Changes the Treshhold for uptrend",
     group = "Treshholds")
lt = input.int(-10, "Trending Lower Treshhold", step = 1, maxval = 0, 
     tooltip = "Changes the Treshhold for downtrend",
     group = "Treshholds")

utm = input.int(15, "Ranging Upper Treshhold", step = 1, minval = 0,
     tooltip = "Changes the Treshhold for uptrend",
     group = "Treshholds")
ltm = input.int(-15, "Ranging Lower Treshhold", step = 1, maxval = 0, 
     tooltip = "Changes the Treshhold for downtrend",
     group = "Treshholds")

pot_t = input.int(100, "Sum Filter Treshhold", step = 1, minval = 0,
     tooltip = "Changes the Treshhold for the sum filter",
     group = "Treshholds")

adx_t = input.int(50, "Filter Treshhold", step = 1, minval = 0,
     tooltip = "Changes the Treshhold for the filter",
     group = "Treshholds")

ob = input.int(80, "Overbought", step = 1, minval = 30,
     tooltip = "Changes the Overbought treshhold", 
     group = "Treshholds", inline = "O")
os = input.int(-80, "Oversold", step = 1, maxval = -30,
     tooltip = "Changes the Oversold treshhold", 
     group = "Treshholds", inline = "O")  
/////////////////////////////////////////////////////////////////////////////////////////////////////////////
//Calculations
po = (ta.percentrank(src, len)-50)*2

[diu, did, adx] = ta.dmi(adx_len, adx_len)
filter = ta.percentrank(adx, ap_len)

potential = math.abs(po)+filter >= pot_t

u = filter > adx_t ? ut : utm
l = filter > adx_t ? lt : ltm
/////////////////////////////////////////////////////////////////////////////////////////////////////////////
//Trend Logic
var trend = 0

L = po > u and potential
S = po < l and potential

if L and not S
    trend := 1
if S
    trend := -1
/////////////////////////////////////////////////////////////////////////////////////////////////////////////
//Plotting
pop = po/math.abs(po)
filter_p = filter*pop

var col = color.rgb(0, 0, 0, 100)
if L
    col := color.rgb(0, 255, 187)
if S
    col := color.rgb(255, 0, 157)

up = plot(u, "Upper Treshhold", color.rgb(72, 72, 72), linewidth = 2, display = display.pane)
lp = plot(l, "Lower Treshhold", color.rgb(72, 72, 72), linewidth = 2, display = display.pane)

fill(up, lp, color = color.rgb(72, 72, 72, 70))

obp = plot(ob, "Overbought Treshhold", color.rgb(255, 0, 157), linewidth = 2, display = display.pane)
obpt = plot(100, "Max", color.rgb(255, 0, 157), linewidth = 2, display = display.pane)

osp = plot(os, "Oversold Treshhold", color.rgb(0, 255, 187), linewidth = 2, display = display.pane)
ospt = plot(-100, "Min", color.rgb(0, 255, 187), linewidth = 2, display = display.pane)

fill(obp, obpt, color.rgb(255, 0, 157, 75))
fill(osp, ospt, color.rgb(0, 255, 187, 75))

fill(up, obp, color.rgb(0, 255, 187, 85))
fill(lp, osp, color.rgb(255, 0, 157, 85))

osc = plot(po, "Percentile Oscillator", col, linewidth = 3)
plotcandle(open, high, low, close, color = col, wickcolor = col, bordercolor = col, force_overlay = true,
      display = display.pane)

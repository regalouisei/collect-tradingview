//@version=5
indicator('MACD + Divergence Indicator [Dynamic Filter]', overlay=false, precision=4, shorttitle='MACD+Div [Dyn]')

//---------------------------------------------------------------------------------------------------------------------------------
//--- Define Colors ---------------------------------------------------------------------------------------------------------------
//---------------------------------------------------------------------------------------------------------------------------------
vWhite  = #FFFFFF
vViolet = #C77DF3
vIndigo = #8A2BE2
vBlue   = #009CDF
vGreen  = #5EBD3E
vYellow = #FFB900
vRed    = #E23838

longColor  = color.green
shortColor = color.red
textColor  = color.white

bullishColor = color.rgb(38, 166, 154, 0) //Used in the display table
bearishColor = color.rgb(239, 83, 79, 0) //Used in the display table
nomatchColor = color.silver //Used in the display table

// Colors for MACD Plot
color_macd = input.color(#2962FF, "MACD Line", group = "Plot Settings", inline="plot1")
color_signal = input.color(#FF6D00, "Signal Line", group = "Plot Settings", inline="plot1")
hist_color_bull_g = input.color(#26A69A, "Hist Bull Growing", group = "Plot Settings", inline="plot2")
hist_color_bull_f = input.color(#B2DFDB, "Hist Bull Fading", group = "Plot Settings", inline="plot2")
hist_color_bear_g = input.color(#FF5252, "Hist Bear Growing", group = "Plot Settings", inline="plot3")
hist_color_bear_f = input.color(#FFCDD2, "Hist Bear Fading", group = "Plot Settings", inline="plot3")


//---------------------------------------------------------------------------------------------------------------------------------------------------------------------
//--- Functions--------------------------------------------------------------------------------------------------------------------------------------------------------
//---------------------------------------------------------------------------------------------------------------------------------------------------------------------

// Function to create timeframe text for the table
TF2txt(TF) =>
    switch TF
        "S"     => "MACD 1s:"
        "5S"    => "MACD 5s:"
        "10S"   => "MACD 10s:"
        "15S"   => "MACD 15s:"
        "30S"   => "MACD 30s"
        "1"     => "MACD 1m:"
        "3"     => "MACD 3m:"
        "5"     => "MACD 5m:"
        "15"    => "MACD 15m:"
        "30"    => "MACD 30m"
        "45"    => "MACD 45m"
        "60"    => "MACD 1h:"
        "120"   => "MACD 2h:"
        "180"   => "MACD 3h:"
        "240"   => "MACD 4h:"
        "480"   => "MACD 8h:"
        "D"     => "MACD 1D:"
        "1D"    => "MACD 1D:"
        "2D"    => "MACD 2D:"
        "3D"    => "MACD 3D:"
        "W"     => "MACD 1W:"
        "1W"    => "MACD 1W:"
        "M"     => "MACD 1M:"
        "1M"    => "MACD 1M:"
        "3M"    => "MACD 3M:"
        "6M"    => "MACD 6M:"
        "12M"   => "MACD 12M:"

//---------------------------------------------------------------------------------------------------------------------------------------------------------------------
//--- Show/Hide Settings ----------------------------------------------------------------------------------------------------------------------------------------------
//---------------------------------------------------------------------------------------------------------------------------------------------------------------------

showMACDInput = input(true, title='Show MACD Line', group='Show/Hide Settings', inline="show")
showSignalInput = input(true, title='Show Signal Line', group='Show/Hide Settings', inline="show")
showHistInput = input(true, title='Show Histogram', group='Show/Hide Settings', inline="show")
DivergenceShowInput = input(true, title='Show MACD Divergence Labels', group='Show/Hide Settings')


//---------------------------------------------------------------------------------------------------------------------------------------------------------------------
//--- Table Settings --------------------------------------------------------------------------------------------------------------------------------------------------
//---------------------------------------------------------------------------------------------------------------------------------------------------------------------
macdShowTable = input(true, title='Show MACD Table Information box', group="MACD Table Settings")
macdTablePosition = input.string(title='Location', defval='middle_right', options=['top_left', 'top_center', 'top_right', 'middle_left', 'middle_center', 'middle_right', 'bottom_left', 'bottom_center', 'bottom_right'], group="MACD Table Settings", inline='1')
macdTextSize = input.string(title=' Size', defval='small', options=['auto', 'tiny', 'small', 'normal', 'large', 'huge'], group="MACD Table Settings", inline='1')

macdShowTF1 = input(true, title='Show TimeFrame1', group="MACD Table Settings", inline='tf1')
macdTF1 = input.timeframe("15", title=" Time", group="MACD Table Settings", inline='tf1')

macdShowTF2 = input(true, title='Show TimeFrame2', group="MACD Table Settings", inline='tf2')
macdTF2 = input.timeframe("60", title=" Time", group="MACD Table Settings", inline='tf2')

macdShowTF3 = input(true, title='Show TimeFrame3', group="MACD Table Settings", inline='tf3')
macdTF3 = input.timeframe("240", title=" Time", group="MACD Table Settings", inline='tf3')

macdShowTF4 = input(true, title='Show TimeFrame4', group="MACD Table Settings", inline='tf4')
macdTF4 = input.timeframe("D", title=" Time", group="MACD Table Settings", inline='tf4')

macdShowHist = input(true, title='Show MACD Historical Columns', group="MACD Table Settings", tooltip='Show the information of the 2 previous closed candles')


//---------------------------------------------------------------------------------------------------------------------------------------------------------------------
//--- MACD Input Settings ----------------------------------------------------------------------------------------------------------------------------------------------
//---------------------------------------------------------------------------------------------------------------------------------------------------------------------
fast_length = input.int(title="Fast Length", defval=12, group="MACD Settings")
slow_length = input.int(title="Slow Length", defval=26, group="MACD Settings")
src = input.source(title="Source", defval=close, group="MACD Settings")
signal_length = input.int(title="Signal Smoothing", minval=1, maxval=50, defval=9, group="MACD Settings")
sma_source = input.string(title="Oscillator MA Type", defval="EMA", options=["SMA", "EMA"], group="MACD Settings")
sma_signal = input.string(title="Signal Line MA Type", defval="EMA", options=["SMA", "EMA"], group="MACD Settings")
osc_src_input = input.string("MACD Line", "Divergence Oscillator Source", options=["MACD Line", "Histogram"], group="MACD Settings", tooltip="Which MACD component to use for finding divergences.")

// --- Dynamic Filter Settings ---
stdev_length = input.int(50, title="Dynamic Filter Length", group="Consolidation Filter")
stdev_mult   = input.float(0.5, title="Dynamic Filter Multiplier", group="Consolidation Filter", tooltip="Higher value = Wider consolidation zone")


//---------------------------------------------------------------------------------------------------------------------------------------------------------------------
//--- Divergence Input Settings ---------------------------------------------------------------------------------------------------------------------------------------
//---------------------------------------------------------------------------------------------------------------------------------------------------------------------
lbrInput = input(title="Pivot Lookback Right", defval=2, group='Divergence Settings')
lblInput = input(title="Pivot Lookback Left", defval=2, group='Divergence Settings')
lbRangeMaxInput = input(title="Max of Lookback Range", defval=10, group='Divergence Settings')
lbRangeMinInput = input(title="Min of Lookback Range", defval=2, group='Divergence Settings')

plotBullInput = input(title="Plot Bullish", defval=true, group='Divergence Settings')
plotHiddenBullInput = input(title="Plot Hidden Bullish", defval=true, group='Divergence Settings')
plotBearInput = input(title="Plot Bearish", defval=true, group='Divergence Settings')
plotHiddenBearInput = input(title="Plot Hidden Bearish", defval=true, group='Divergence Settings')

//---------------------------------------------------------------------------------------------------------------------------------------------------------------------
//--- MACD Calculation -------------------------------------------------------------------------------------------------------------------------------------------------
//---------------------------------------------------------------------------------------------------------------------------------------------------------------------
// Calculating
fast_ma = sma_source == "SMA" ? ta.sma(src, fast_length) : ta.ema(src, fast_length)
slow_ma = sma_source == "SMA" ? ta.sma(src, slow_length) : ta.ema(src, slow_length)
macd = fast_ma - slow_ma
signal = sma_signal == "SMA" ? ta.sma(macd, signal_length) : ta.ema(macd, signal_length)
hist = macd - signal

// Set the oscillator source for divergence
osc = osc_src_input == "MACD Line" ? macd : hist

// Get MTF MACD data
[osc1, osc1_1, osc1_2] = request.security(syminfo.tickerid, macdTF1, [osc, osc[1], osc[2]], lookahead=barmerge.lookahead_on)
[osc2, osc2_1, osc2_2] = request.security(syminfo.tickerid, macdTF2, [osc, osc[1], osc[2]], lookahead=barmerge.lookahead_on)
[osc3, osc3_1, osc3_2] = request.security(syminfo.tickerid, macdTF3, [osc, osc[1], osc[2]], lookahead=barmerge.lookahead_on)
[osc4, osc4_1, osc4_2] = request.security(syminfo.tickerid, macdTF4, [osc, osc[1], osc[2]], lookahead=barmerge.lookahead_on)


//---------------------------------------------------------------------------------------------------------------------------------------------------------------------
//--- Divergence Settings ------------------------------------------------------------------------------------------------------------------------------------------
//---------------------------------------------------------------------------------------------------------------------------------------------------------------------
bearColor = color.red
bullColor = color.green
hiddenBullColor = color.new(color.green, 50)
hiddenBearColor = color.new(color.red, 50)
noneColor = color.new(color.white, 100)

plFound = na(ta.pivotlow(osc, lblInput, lbrInput)) ? false : true
phFound = na(ta.pivothigh(osc, lblInput, lbrInput)) ? false : true
_inRange(cond) =>
    bars = ta.barssince(cond == true)
    lbRangeMinInput <= bars and bars <= lbRangeMaxInput


//---------------------------------------------------------------------------------------------------------------------------------------------------------------------
//--- Plot Lines & Dynamic Filter -------------------------------------------------------------------------------------------------------------------------------------
//---------------------------------------------------------------------------------------------------------------------------------------------------------------------
// Plot standard MACD
hist_color = hist >= 0 ? (hist[1] < hist ? hist_color_bull_g : hist_color_bull_f) : (hist[1] < hist ? hist_color_bear_f : hist_color_bear_g)
plot(showHistInput ? hist : na, title="Histogram", style=plot.style_columns, color=hist_color)
plot(showMACDInput ? macd : na, title="MACD", color=color_macd, linewidth=2)
plot(showSignalInput ? signal : na, title="Signal", color=color_signal, linewidth=2)
hline(0, "Zero Line", color=color.new(#787B86, 50))


// --- Dynamic Bands Calculation ---
macd_stdev = ta.stdev(macd, stdev_length)
upper_band = macd_stdev * stdev_mult
lower_band = -macd_stdev * stdev_mult

// --- Plot Dynamic Bands ---
p1 = plot(upper_band, title="Dynamic Upper Filter", color=color.new(color.gray, 50))
p2 = plot(lower_band, title="Dynamic Lower Filter", color=color.new(color.gray, 50))
fill(p1, p2, color=color.new(color.gray, 90), title="Consolidation Zone")

// ------------------------------------

//---------------------------------------------------------------------------------------------------------------------------------------------------------------------
//--- Plot Divergence -------------------------------------------------------------------------------------------------------------------------------------------------
//---------------------------------------------------------------------------------------------------------------------------------------------------------------------
// Regular Bullish
// Osc: Higher Low
oscHL = osc[lbrInput] > ta.valuewhen(plFound, osc[lbrInput], 1) and _inRange(plFound[1])
// Price: Lower Low
priceLL = low[lbrInput] < ta.valuewhen(plFound, low[lbrInput], 1)
bullCond = plotBullInput and priceLL and oscHL and plFound

plot(
     plFound ? osc[lbrInput] : na,
     offset=-lbrInput,
     title="Regular Bullish",
     linewidth=2,
     color=(bullCond ? bullColor : noneColor)
     )

plotshape(
     DivergenceShowInput ? bullCond ? osc[lbrInput] : na : na,
     offset=-lbrInput,
     title="Regular Bullish Label",
     text=" Bull ",
     style=shape.labelup,
     location=location.absolute,
     color=bullColor,
     textcolor=textColor
     )

//------------------------------------------------------------------------------
// Hidden Bullish
// Osc: Lower Low
oscLL = osc[lbrInput] < ta.valuewhen(plFound, osc[lbrInput], 1) and _inRange(plFound[1])
// Price: Higher Low
priceHL = low[lbrInput] > ta.valuewhen(plFound, low[lbrInput], 1)
hiddenBullCond = plotHiddenBullInput and priceHL and oscLL and plFound

plot(
     plFound ? osc[lbrInput] : na,
     offset=-lbrInput,
     title="Hidden Bullish",
     linewidth=2,
     color=(hiddenBullCond ? hiddenBullColor : noneColor)
     )

plotshape(
     DivergenceShowInput ? hiddenBullCond ? osc[lbrInput] : na : na,
     offset=-lbrInput,
     title="Hidden Bullish Label",
     text=" H Bull ",
     style=shape.labelup,
     location=location.absolute,
     color=bullColor,
     textcolor=textColor
     )

//------------------------------------------------------------------------------
// Regular Bearish
// Osc: Lower High
oscLH = osc[lbrInput] < ta.valuewhen(phFound, osc[lbrInput], 1) and _inRange(phFound[1])
// Price: Higher High
priceHH = high[lbrInput] > ta.valuewhen(phFound, high[lbrInput], 1)
bearCond = plotBearInput and priceHH and oscLH and phFound

plot(
     phFound ? osc[lbrInput] : na,
     offset=-lbrInput,
     title="Regular Bearish",
     linewidth=2,
     color=(bearCond ? bearColor : noneColor)
     )

plotshape(
     DivergenceShowInput ? bearCond ? osc[lbrInput] : na : na,
     offset=-lbrInput,
     title="Regular Bearish Label",
     text=" Bear ",
     style=shape.labeldown,
     location=location.absolute,
     color=bearColor,
     textcolor=textColor
     )

//------------------------------------------------------------------------------
// Hidden Bearish
// Osc: Higher High
oscHH = osc[lbrInput] > ta.valuewhen(phFound, osc[lbrInput], 1) and _inRange(phFound[1])
// Price: Lower High
priceLH = high[lbrInput] < ta.valuewhen(phFound, high[lbrInput], 1)
hiddenBearCond = plotHiddenBearInput and priceLH and oscHH and phFound

plot(
     phFound ? osc[lbrInput] : na,
     offset=-lbrInput,
     title="Hidden Bearish",
     linewidth=2,
     color=(hiddenBearCond ? hiddenBearColor : noneColor)
     )

plotshape(
     DivergenceShowInput ? hiddenBearCond ? osc[lbrInput] : na : na,
     offset=-lbrInput,
     title="Hidden Bearish Label",
     text=" H Bear ",
     style=shape.labeldown,
     location=location.absolute,
     color=bearColor,
     textcolor=textColor
     )


//---------------------------------------------------------------------------------------------------------------------------------------------------------------------
//--- Check MACD Lineup ------------------------------------------------------------------------------------------------------------------------------------------------
//---------------------------------------------------------------------------------------------------------------------------------------------------------------------
// Check if oscillator is trending up or down over last 3 bars
bullTF = osc > osc[1] and osc[1] > osc[2]
bearTF = osc < osc[1] and osc[1] < osc[2]
bullTF1 = osc1 > osc1_1 and osc1_1 > osc1_2
bearTF1 = osc1 < osc1_1 and osc1_1 < osc1_2
bullTF2 = osc2 > osc2_1 and osc2_1 > osc2_2
bearTF2 = osc2 < osc2_1 and osc2_1 < osc2_2
bullTF3 = osc3 > osc3_1 and osc3_1 > osc3_2
bearTF3 = osc3 < osc3_1 and osc3_1 < osc3_2
bullTF4 = osc4 > osc4_1 and osc4_1 > osc4_2
bearTF4 = osc4 < osc4_1 and osc4_1 < osc4_2

bbTxt(bull, bear) =>
    bull ? "BULLISH" : bear ? "BEARISH" : 'NO LINEUP'

bbColor(bull, bear) =>
    bull ? bullishColor : bear ? bearishColor : nomatchColor

// Function to create new table cell
newTC(tBox, col, row, txt, width, txtColor, bgColor, txtHA, txtSize) =>
    table.cell(table_id=tBox, column=col, row=row, text=txt, width=width, text_color=txtColor, bgcolor=bgColor, text_halign=txtHA, text_size=txtSize)


//---------------------------------------------------------------------------------------------------------------------------------------------------------------------
//--- Define MACD Table Setting ----------------------------------------------------------------------------------------------------------------------------------------
//---------------------------------------------------------------------------------------------------------------------------------------------------------------------
width_c0 = 0
width_c1 = 0

if macdShowTable
    var macdTable = table.new(position=macdTablePosition, columns=5, rows=6, bgcolor=color.rgb(18, 22, 33, 50), frame_color=color.black, frame_width=1, border_color=color.black, border_width=1)

    // Current Timeframe
    newTC(macdTable, 0, 1, "MACD Current", width_c0, color.orange, color.rgb(0, 0, 0, 100), 'right', macdTextSize)
    newTC(macdTable, 1, 1, str.format(" {0,number,#.####} ", osc), width_c0, vWhite, osc < 0 ? bearishColor : bullishColor, 'left', macdTextSize)
    newTC(macdTable, 4, 1, bbTxt(bullTF, bearTF), width_c0, vWhite, bbColor(bullTF, bearTF), 'center', macdTextSize)
    if macdShowHist
        newTC(macdTable, 2, 1, str.format(" {0,number,#.####} ", osc[1]), width_c0, vWhite, osc[1] < 0 ? bearishColor : bullishColor, 'left', macdTextSize)
        newTC(macdTable, 3, 1, str.format(" {0,number,#.####} ", osc[2]), width_c0, vWhite, osc[2] < 0 ? bearishColor : bullishColor, 'left', macdTextSize)

    // Timeframe 1
    if macdShowTF1
        newTC(macdTable, 0, 2, TF2txt(macdTF1), width_c0, vWhite, color.rgb(0, 0, 0, 100), 'right', macdTextSize)
        newTC(macdTable, 1, 2, str.format(" {0,number,#.####} ", osc1), width_c0, vWhite, osc1 < 0 ? bearishColor : bullishColor, 'left', macdTextSize)
        newTC(macdTable, 4, 2, bbTxt(bullTF1, bearTF1), width_c0, vWhite, bbColor(bullTF1, bearTF1), 'center', macdTextSize)
        if macdShowHist
            newTC(macdTable, 2, 2, str.format(" {0,number,#.####} ", osc1_1), width_c0, vWhite, osc1_1 < 0 ? bearishColor : bullishColor, 'left', macdTextSize)
            newTC(macdTable, 3, 2, str.format(" {0,number,#.####} ", osc1_2), width_c0, vWhite, osc1_2 < 0 ? bearishColor : bullishColor, 'left', macdTextSize)

    // Timeframe 2
    if macdShowTF2
        newTC(macdTable, 0, 3, TF2txt(macdTF2), width_c0, vWhite, color.rgb(0, 0, 0, 100), 'right', macdTextSize)
        newTC(macdTable, 1, 3, str.format(" {0,number,#.####} ", osc2), width_c0, vWhite, osc2 < 0 ? bearishColor : bullishColor, 'left', macdTextSize)
        newTC(macdTable, 4, 3, bbTxt(bullTF2, bearTF2), width_c0, vWhite, bbColor(bullTF2, bearTF2), 'center', macdTextSize)
        if macdShowHist
            newTC(macdTable, 2, 3, str.format(" {0,number,#.####} ", osc2_1), width_c0, vWhite, osc2_1 < 0 ? bearishColor : bullishColor, 'left', macdTextSize)
            newTC(macdTable, 3, 3, str.format(" {0,number,#.####} ", osc2_2), width_c0, vWhite, osc2_2 < 0 ? bearishColor : bullishColor, 'left', macdTextSize)

    // Timeframe 3
    if macdShowTF3
        newTC(macdTable, 0, 4, TF2txt(macdTF3), width_c0, vWhite, color.rgb(0, 0, 0, 100), 'right', macdTextSize)
        newTC(macdTable, 1, 4, str.format(" {0,number,#.####} ", osc3), width_c0, vWhite, osc3 < 0 ? bearishColor : bullishColor, 'left', macdTextSize)
        newTC(macdTable, 4, 4, bbTxt(bullTF3, bearTF3), width_c0, vWhite, bbColor(bullTF3, bearTF3), 'center', macdTextSize)
        if macdShowHist
            newTC(macdTable, 2, 4, str.format(" {0,number,#.####} ", osc3_1), width_c0, vWhite, osc3_1 < 0 ? bearishColor : bullishColor, 'left', macdTextSize)
            newTC(macdTable, 3, 4, str.format(" {0,number,#.####} ", osc3_2), width_c0, vWhite, osc3_2 < 0 ? bearishColor : bullishColor, 'left', macdTextSize)

    // Timeframe 4
    if macdShowTF4
        newTC(macdTable, 0, 5, TF2txt(macdTF4), width_c0, vWhite, color.rgb(0, 0, 0, 100), 'right', macdTextSize)
        newTC(macdTable, 1, 5, str.format(" {0,number,#.####} ", osc4), width_c0, vWhite, osc4 < 0 ? bearishColor : bullishColor, 'left', macdTextSize)
        newTC(macdTable, 4, 5, bbTxt(bullTF4, bearTF4), width_c0, vWhite, bbColor(bullTF4, bearTF4), 'center', macdTextSize)
        if macdShowHist
            newTC(macdTable, 2, 5, str.format(" {0,number,#.####} ", osc4_1), width_c0, vWhite, osc4_1 < 0 ? bearishColor : bullishColor, 'left', macdTextSize)
            newTC(macdTable, 3, 5, str.format(" {0,number,#.####} ", osc4_2), width_c0, vWhite, osc4_2 < 0 ? bearishColor : bullishColor, 'left', macdTextSize)



//------------------------------------------------------
//--- Alerts -------------------------------------------
//------------------------------------------------------

alertcondition(hist[1] >= 0 and hist < 0, title='Histogram Rising to Falling', message='The MACD histogram switched from a rising to falling state')
alertcondition(hist[1] <= 0 and hist > 0, title='Histogram Falling to Rising', message='The MACD histogram switched from a falling to rising state')

alertcondition(bullCond, title="Regular Bullish Divergence", message="Regular Bullish Divergence detected on {{ticker}} ({{interval}})")
alertcondition(hiddenBullCond, title="Hidden Bullish Divergence", message="Hidden Bullish Divergence detected on {{ticker}} ({{interval}})")
alertcondition(bearCond, title="Regular Bearish Divergence", message="Regular Bearish Divergence detected on {{ticker}} ({{interval}})")
alertcondition(hiddenBearCond, title="Hidden Bearish Divergence", message="Hidden Bearish Divergence detected on {{ticker}} ({{interval}})")

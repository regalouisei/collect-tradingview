// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © CharonQuant

//@version=6
// ______________________
//  (CharonQuant;:.:. .. )
//  T""""""""""""""""""""T
//  |.;....,..........;..|
//  |;;:: .  .    .      |
//  l;;;:. :   .     ..  ;
//  `;;:::.: .    .     .'
//   l;;:. ..  .     .: ;
//   `;;::.. .    .  ; .'
//    l;;:: .  .    /  ;
//     \;;:. .   .,'  /
//      `\;:.. ..'  .'
//        `\;:.. ..'
//          \;:. /
//           l; f
//           `;f'
//            ||
//            ;l.
//           ;: l
//          / ;  \
//        ,/  :   `.
//      ./' . :     `.
//     /' ,'  :       \
//    f  /  . :        i
//   ,' ;  .  :        `.
//   f ;  .   :      .  i
//  .'    :   :       . `.
//  f ,  .    ;       :  i
//  |    :  ,/`.       : |
//  |    ;,/;:. `.     . |
//  |___,/;;:. . .`._____|
// (!;:.:. .  CharonQuant)
//  """"""""""""""""""""""`
indicator("Forecast Trend Filter ~ CharonQuant","FTF ~ CharonQuant")

// ── Imports ────────────────────────────────────────────────────────────────
import TradingView/ta/7 as ta

// ── Groups ────────────────────────────────────────────────────────────────
group_core = "Core Oscillator Settings"
group_trend = "Trend Filter Settings"
group_signal = "Signal Quality Filter"
group_visual = "Visual Settings"

// ── Inputs ────────────────────────────────────────────────────────────────
len       = input.int(88,   "Forecast Length", minval=21, tooltip  = "Length of the linear regression lookback period.\nShorter values make the oscillator more responsive and catch trends earlier, but increase noise and false signals.\nLonger values are smoother and more stable but lag more",group = group_core)
src       = input.source(close, "Source",group = group_core)
maType    = input.string("WMA", "Trend Filter MA Type", options=["SMA", "EMA", "WMA", "HMA", "ALMA", "VWMA"],group=group_trend)
maLen     = input.int(81,   "Trend Filter MA Length", minval=20,tooltip  = "Period used for the selected trend filter moving average.\nLonger lengths create a stronger trend bias and reduce whipsaws.\nShorter lengths allow faster trend detection but may let more counter-trend noise through.",group = group_trend)
minDev    = input.float(1.4, "Min Deviation % Threshold", step=0.01, tooltip  = "Minimum absolute value of the Forecast Oscillator required to generate a signal.\nExample: 1.4 means |fosc| must be ≥ 1.4% for a valid long or short.\nHigher values (1.2–2.5) filter out weak / noisy moves and improve signal quality.\nLower values (0.3–0.8) allow more signals but increase false positives.",group  = group_signal)
showBars = input.bool(true,"Color Price Bars", tooltip = "Enable or disable trend-based bar coloring.",group = group_visual
)

// ── Forecast Oscillator Core ──────────────────────────────────────────────
linreg_val = ta.linreg(src, len, 0)
fosc       = 100 * (src - linreg_val) / src
fosc_slope = fosc - fosc[1]

// ── Trend Filter MA (user-selectable type) ────────────────────────────────
trendMA = switch maType
    "SMA"  => ta.sma(src, maLen)
    "EMA"  => ta.ema(src, maLen)
    "WMA"  => ta.wma(src, maLen)
    "VWMA" => ta.vwma(src, maLen)
    "HMA"  => ta.hma(src, maLen)
    "ALMA" => ta.alma(src, maLen, 0.85, 6)
    => na

inUptrend   = src > trendMA
inDowntrend = src < trendMA

// ── Conditions ───────────────────────────────────
longCond  = fosc > 0 and fosc_slope > 0 and fosc > minDev and inUptrend
shortCond = fosc < 0 and fosc_slope < 0 and fosc < -minDev and inDowntrend

var CharonQuant = 0
if longCond and not shortCond
    CharonQuant:=1
if shortCond 
    CharonQuant:=-1

// ── Plots ─────────────────────────────────────────────────────────────────
color BULL     = #D4A017
color BEAR     = #6B2A3A
color NEUTRAL  = color.new(#989898, 40) 
color foscColor = CharonQuant == 1 ? BULL : CharonQuant == -1 ? BEAR : NEUTRAL
plot(fosc, title    = "FOSC",linewidth = 2,color    = foscColor)
hline(0, title     = "Zero Level",linestyle = hline.style_dotted,color     = #989898)
plot(linreg_val, title    = "Forecast Regression Line",color    = foscColor, linewidth = 3, force_overlay = true)
plot(trendMA, title    = "Trend Filter MA",color    = color.new(color.orange, 65),linewidth = 2, force_overlay = true)
barcolor(showBars ?CharonQuant == 1 ? color.new(BULL, 25) :CharonQuant == -1 ? color.new(BEAR, 15) :na: na,title = "Trend Bar Coloring")
barcolor(showBars ?CharonQuant == 1 ? color.new(BULL, 25) :CharonQuant == -1 ? color.new(BEAR, 15) :na: na,title = "Trend Bar Coloring")

// ── Alerts ────────────────────────────────────────────────────────────────
alertcondition(longCond,  title="FTF Long",  message="TFF LONG {{exchange}}:{{ticker}}")
alertcondition(shortCond, title="FTF Short", message="TFF SHORT {{exchange}}:{{ticker}}")

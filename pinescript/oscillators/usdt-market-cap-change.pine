// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © qowazzz

//@version=6
indicator("USDT: Market cap change")

// Inputs
usdt = input.bool(true, "USDT", group = "Stablecoins")
usdc = input.bool(false, "USDC", group = "Stablecoins")
dai = input.bool(false, "DAI", group = "Stablecoins")
mcap_len = input.int(60, "Length (days)")
show_ma = input.bool(true, "Show MA")
ma_type = input.string("SMA", "MA Type", options = ["SMA", "EMA", "HMA", "WMA", "RMA"])
ma_length = input.int(30, "MA length")

// Mcap data
usdt_mcap = request.security("GLASSNODE:USDT_MARKETCAP", "D", close)
usdc_mcap = request.security("GLASSNODE:USDC_MARKETCAP", "D", close)
dai_mcap = request.security("GLASSNODE:DAI_MARKETCAP", "D", close)

// Calculate total mcap 
total_mcap = 0.0
total_mcap := usdt ? total_mcap+usdt_mcap : total_mcap
total_mcap := usdc ? total_mcap+usdc_mcap : total_mcap
total_mcap := dai ? total_mcap+dai_mcap : total_mcap

daily_change = total_mcap - total_mcap[1]
custom_change = total_mcap - total_mcap[mcap_len]

calcMA(_type, _length, _src) =>
    switch _type
        "EMA" => ta.ema(_src, _length)
        "SMA" => ta.sma(_src, _length)
        "WMA" => ta.wma(_src, _length)
        "HMA" => ta.hma(_src, _length)
        "RMA" => ta.rma(_src, _length)
        => na

ma = calcMA(ma_type, ma_length, custom_change)

fill_color = color.new(color.red, 80)
p2_color = color.new(color.red, 50)

p0 = plot(0, display = display.none)
plot(daily_change, title = "Daily change", style = plot.style_columns)
p2 = plot(custom_change, color = p2_color)
fill(p0, p2, color = fill_color)
plot(show_ma ? ma : na, color = color.red)

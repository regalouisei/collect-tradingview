// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Uncle_the_shooter

//@version=6
indicator('RSI Breakout Zones', overlay = true, max_boxes_count = 500, max_lines_count = 500)

// Settings
rsiLength = input.int(14, 'RSI Length')
rsiOB = input.int(70, 'Overbought Level')
rsiOS = input.int(30, 'Oversold Level')
boxTransp = input.int(90, 'Box Transparency (0 opaque - 100 transparent)', minval = 0, maxval = 100)
zoneTransp = input.int(90, 'Zone Transparency (0 opaque - 100 transparent)', minval = 0, maxval = 100)
obColor = input.color(color.rgb(207, 23, 23), 'Overbought Zone Color')
osColor = input.color(color.rgb(6, 162, 47), 'Oversold Zone Color')
upBreakoutColor = input.color(color.green, 'Upward Breakout Signal Color')
downBreakoutColor = input.color(color.red, 'Downward Breakout Signal Color')
keepBoxes = input.bool(true, 'Keep Boxes After RSI Returns to Normal?')

// RSI Calculations
rsi = ta.rsi(close, rsiLength)

// Persistent variables
var box obBox = na
var box osBox = na
var box obZoneBox = na
var box osZoneBox = na
var float obZoneTop = na
var float obZoneBottom = na
var float osZoneTop = na
var float osZoneBottom = na
var line obTopLine = na
var line obBottomLine = na
var line obMidLine = na
var line osTopLine = na
var line osBottomLine = na
var line osMidLine = na

// Breakout detection
upBreakoutOB = ta.crossover(close, obZoneTop) and not na(obZoneTop)
downBreakoutOB = ta.crossunder(close, obZoneBottom) and not na(obZoneBottom)
upBreakoutOS = ta.crossover(close, osZoneTop) and not na(osZoneTop)
downBreakoutOS = ta.crossunder(close, osZoneBottom) and not na(osZoneBottom)

// Plot breakout signals
plotshape(upBreakoutOB or upBreakoutOS, title = 'Upward Breakout', location = location.belowbar, style = shape.triangleup, color = upBreakoutColor, size = size.tiny)
plotshape(downBreakoutOB or downBreakoutOS, title = 'Downward Breakout', location = location.abovebar, style = shape.triangledown, color = downBreakoutColor, size = size.tiny)

// ALERTS 
alertcondition(upBreakoutOB, title = 'Upward Breakout (Overbought Zone)', message = 'RSI Breakout: Upward breakout from overbought zone')
alertcondition(downBreakoutOB, title = 'Downward Breakout (Overbought Zone)', message = 'RSI Breakout: Downward breakout from overbought zone')
alertcondition(upBreakoutOS, title = 'Upward Breakout (Oversold Zone)', message = 'RSI Breakout: Upward breakout from oversold zone')
alertcondition(downBreakoutOS, title = 'Downward Breakout (Oversold Zone)', message = 'RSI Breakout: Downward breakout from oversold zone')

// Combined alerts (optional)
alertcondition(upBreakoutOB or upBreakoutOS, title = 'Upward Breakout (Any Zone)', message = 'RSI Breakout: Upward breakout detected')
alertcondition(downBreakoutOB or downBreakoutOS, title = 'Downward Breakout (Any Zone)', message = 'RSI Breakout: Downward breakout detected')

// Overbought (RSI > OB)
if rsi > rsiOB
    if na(obBox)
        if not na(obZoneBox)
            box.set_right(obZoneBox, bar_index - 1)
            line.set_x2(obTopLine, bar_index - 1)
            line.set_x2(obBottomLine, bar_index - 1)
            line.set_x2(obMidLine, bar_index - 1)
        obBox := box.new(left = bar_index, top = high, right = bar_index + 1, bottom = low, xloc = xloc.bar_index, border_color = color.new(obColor, 0), bgcolor = color.new(obColor, boxTransp))
        obZoneBox := box.new(left = bar_index, top = high, right = bar_index + 1, bottom = low, xloc = xloc.bar_index, border_color = na, bgcolor = color.new(obColor, zoneTransp))
        obZoneTop := high
        obZoneBottom := low
        obTopLine := line.new(bar_index, high, bar_index + 1, high, xloc = xloc.bar_index, color = color.new(obColor, 0))
        obBottomLine := line.new(bar_index, low, bar_index + 1, low, xloc = xloc.bar_index, color = color.new(obColor, 0))
        float midLevel = (high + low) / 2
        obMidLine := line.new(bar_index, midLevel, bar_index + 1, midLevel, xloc = xloc.bar_index, color = color.new(obColor, 50), style = line.style_dashed, width = 1)
        obMidLine
    else
        box.set_right(obBox, bar_index + 1)
        box.set_right(obZoneBox, bar_index + 1)
        line.set_x2(obTopLine, bar_index + 1)
        line.set_x2(obBottomLine, bar_index + 1)
        line.set_x2(obMidLine, bar_index + 1)
        float curTop = box.get_top(obBox)
        float curBottom = box.get_bottom(obBox)
        if high > curTop
            box.set_top(obBox, high)
            box.set_top(obZoneBox, high)
            obZoneTop := high
            line.set_y1(obTopLine, high)
            line.set_y2(obTopLine, high)
            float midLevel = (high + curBottom) / 2
            line.set_y1(obMidLine, midLevel)
            line.set_y2(obMidLine, midLevel)
        if low < curBottom
            box.set_bottom(obBox, low)
            box.set_bottom(obZoneBox, low)
            obZoneBottom := low
            line.set_y1(obBottomLine, low)
            line.set_y2(obBottomLine, low)
            float midLevel = (curTop + low) / 2
            line.set_y1(obMidLine, midLevel)
            line.set_y2(obMidLine, midLevel)
else
    if not na(obBox)
        if not keepBoxes
            box.delete(obBox)
        obBox := na
        obBox
    if not na(obZoneBox)
        box.set_right(obZoneBox, bar_index + 1)
        line.set_x2(obTopLine, bar_index + 1)
        line.set_x2(obBottomLine, bar_index + 1)
        line.set_x2(obMidLine, bar_index + 1)

// Oversold (RSI < OS)
if rsi < rsiOS
    if na(osBox)
        if not na(osZoneBox)
            box.set_right(osZoneBox, bar_index - 1)
            line.set_x2(osTopLine, bar_index - 1)
            line.set_x2(osBottomLine, bar_index - 1)
            line.set_x2(osMidLine, bar_index - 1)
        osBox := box.new(left = bar_index, top = high, right = bar_index + 1, bottom = low, xloc = xloc.bar_index, border_color = color.new(osColor, 0), bgcolor = color.new(osColor, boxTransp))
        osZoneBox := box.new(left = bar_index, top = high, right = bar_index + 1, bottom = low, xloc = xloc.bar_index, border_color = na, bgcolor = color.new(osColor, zoneTransp))
        osZoneTop := high
        osZoneBottom := low
        osTopLine := line.new(bar_index, high, bar_index + 1, high, xloc = xloc.bar_index, color = color.new(osColor, 0))
        osBottomLine := line.new(bar_index, low, bar_index + 1, low, xloc = xloc.bar_index, color = color.new(osColor, 0))
        float midLevel = (high + low) / 2
        osMidLine := line.new(bar_index, midLevel, bar_index + 1, midLevel, xloc = xloc.bar_index, color = color.new(osColor, 50), style = line.style_dashed, width = 1)
        osMidLine
    else
        box.set_right(osBox, bar_index + 1)
        box.set_right(osZoneBox, bar_index + 1)
        line.set_x2(osTopLine, bar_index + 1)
        line.set_x2(osBottomLine, bar_index + 1)
        line.set_x2(osMidLine, bar_index + 1)
        float curTop2 = box.get_top(osBox)
        float curBottom2 = box.get_bottom(osBox)
        if high > curTop2
            box.set_top(osBox, high)
            box.set_top(osZoneBox, high)
            osZoneTop := high
            line.set_y1(osTopLine, high)
            line.set_y2(osTopLine, high)
            float midLevel = (high + curBottom2) / 2
            line.set_y1(osMidLine, midLevel)
            line.set_y2(osMidLine, midLevel)
        if low < curBottom2
            box.set_bottom(osBox, low)
            box.set_bottom(osZoneBox, low)
            osZoneBottom := low
            line.set_y1(osBottomLine, low)
            line.set_y2(osBottomLine, low)
            float midLevel = (curTop2 + low) / 2
            line.set_y1(osMidLine, midLevel)
            line.set_y2(osMidLine, midLevel)
else
    if not na(osBox)
        if not keepBoxes
            box.delete(osBox)
        osBox := na
        osBox
    if not na(osZoneBox)
        box.set_right(osZoneBox, bar_index + 1)
        line.set_x2(osTopLine, bar_index + 1)
        line.set_x2(osBottomLine, bar_index + 1)
        line.set_x2(osMidLine, bar_index + 1)

//@version=5
indicator("EndOfMoove", overlay=false, precision=2)

lookbackPeriodWVF = input.int(20, "WVF Period", minval=1, group="WVF Parameters")
wvfSmoothingPeriod = input.int(1, "WVF Smoothing", minval=1, group="WVF Parameters")

historicalPeriod = input.int(250, "Period for historical max", minval=50, maxval=500, group="Detection")
spikeConfirmationPeriod = input.int(3, "Spike confirmation period", minval=1, maxval=10, group="Detection")
thresholdPercentage = input.float(75, "% of max for threshold", minval=1, maxval=100, step=1, group="Detection")

baseColor = input.color(color.gray, "Base Color", group="Colors")
highlightColor = input.color(color.white, "Highlight Color", group="Colors")

highest_close_lookback = ta.highest(close, lookbackPeriodWVF)
wvf_raw = ((highest_close_lookback - low) / (highest_close_lookback + 0.000001)) * 100
wvf = wvfSmoothingPeriod > 1 ? ta.sma(wvf_raw, wvfSmoothingPeriod) : wvf_raw
//
max_wvf = ta.highest(wvf, historicalPeriod)
dynamicSpikeThreshold = max_wvf * (thresholdPercentage / 100)

isSpike = wvf >= dynamicSpikeThreshold
isConfirmedSpike = isSpike and ta.highest(wvf, spikeConfirmationPeriod) == wvf

float dynamicValue = math.min(wvf / 15, 1.0)
int dynamic_transparency = math.round((1 - dynamicValue) * 70)
dynamic_transparency := math.max(0, math.min(100, dynamic_transparency))

color bar_color = wvf >= dynamicSpikeThreshold ? highlightColor : color.new(baseColor, dynamic_transparency)

plot(wvf, title="Raw WVF (%)", style=plot.style_columns, color=bar_color, histbase=0)

hline(0, "Zero Line", color=color.new(baseColor, 50), linestyle=hline.style_dotted, linewidth=1)

plot(dynamicSpikeThreshold, title="Dynamic Threshold", color=color.new(highlightColor, 60), style=plot.style_line, linewidth=1)

plotshape(isConfirmedSpike, "Volatility Spike", shape.triangleup, location.top, highlightColor, 0, size=size.tiny)

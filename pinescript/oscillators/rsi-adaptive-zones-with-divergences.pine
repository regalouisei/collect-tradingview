// This Pine Script™ code is subject to the terms of the Creative Commons
// Attribution-NonCommercial-ShareAlike 4.0 International License:
// https://creativecommons.org/licenses/by-nc-sa/4.0/
//
// This license requires that reusers give credit to the creator.
// It allows reusers to distribute, remix, adapt, and build upon the material
// in any medium or format, for non-commercial purposes only.
// If others modify or adapt the material, they must license the modified
// material under identical terms.
//
// CC BY-NC-SA 4.0:
// BY: Credit must be given to the creator.
// NC: Only non-commercial use of this work is permitted. "Non-commercial" means
//     not primarily intended for or directed towards commercial advantage
//     or monetary compensation.
// SA: Adaptations must be shared under the same terms.
//
// © AdaptiveRSI


//@version=6
indicator(title = 'RSI adaptive zones [AdaptiveRSI]',
          shorttitle = 'AdaptRSI',
          format = format.price,
          precision = 2,
          overlay = false,
          max_lines_count = 500,
          max_labels_count = 500)

// ─── RSI Settings ─────────────────────────────────────────────
Length           = input.int(14, minval = 2, title = 'Length', group = 'RSI Settings')
RSIType          = input.string('candle', 'Plot RSI As:', options = ['line', 'bar', 'candle'], inline = 'RSI', group = 'RSI Settings')
color color_RSI  = input(color.gray, '', inline = 'RSI', group = 'RSI Settings')

// ─── Visibility & Colors ─────────────────────────────────────
SupRes           = input.bool(true,  'Support / Resistance Zones', inline = 'SupRes', group = 'Visibility and Colors')
color_SR         = input.color(color.new(color.gray, 50), '',    inline = 'SupRes', group = 'Visibility and Colors')

OO               = input.bool(true,  'Oversold / Overbought Zones', inline = 'zones_coloring', group = 'Visibility and Colors')
color_R          = input.color(#D92323, '',                       inline = 'zones_coloring', group = 'Visibility and Colors')
color_G          = input.color(#33A645, '',                       inline = 'zones_coloring', group = 'Visibility and Colors')

OO_coloring      = input.bool(true,  'Oversold / Overbought Coloring', inline = 'OO_coloring', group = 'Visibility and Colors')
color_GI         = input.color(#33E643, '',                          inline = 'OO_coloring', group = 'Visibility and Colors')
color_RI         = input.color(#F22738, '',                          inline = 'OO_coloring', group = 'Visibility and Colors')

// ─── Divergence Settings ─────────────────────────────────────
showDivergence    = input.bool(true, 'Show Divergences', group = 'Divergence Settings')
pivotLookback     = input.int(5, 'Pivot Lookback', minval = 1, group = 'Divergence Settings')
maxLookback       = input.int(60, 'Max Lookback Range', minval = 10, group = 'Divergence Settings')

showRegularBull   = input.bool(true, 'Regular Bullish', inline = 'reg', group = 'Divergence Settings')
showRegularBear   = input.bool(true, 'Regular Bearish', inline = 'reg', group = 'Divergence Settings')
showHiddenBull    = input.bool(true, 'Hidden Bullish', inline = 'hid', group = 'Divergence Settings')
showHiddenBear    = input.bool(true, 'Hidden Bearish', inline = 'hid', group = 'Divergence Settings')

colorRegBull      = input.color(#089981, 'Regular Bull', inline = 'colors1', group = 'Divergence Settings')
colorRegBear      = input.color(#F23645, 'Regular Bear', inline = 'colors1', group = 'Divergence Settings')
colorHidBull      = input.color(color.new(#089981, 50), 'Hidden Bull', inline = 'colors2', group = 'Divergence Settings')
colorHidBear      = input.color(color.new(#F23645, 50), 'Hidden Bear', inline = 'colors2', group = 'Divergence Settings')


// ─── Common Factors ───────────────────────────────────────────
SF                = 1.0 / Length
power_exponent    = 0.5  // Kept as a variable for potential future research; current best fit is √(Length−1)
inv_sqrt_lenm1    = 1.0 / math.pow(Length - 1.0, power_exponent)
half_range        = 50.0
eps               = 1e-10  // safe denominator floor


// ─── RSI Components (Close, and Intrabar O/H/L) ──────────────
middle    = ta.rma(close, Length)
middle_O  = (1 - SF) * middle[1] + SF * open
middle_H  = (1 - SF) * middle[1] + SF * high
middle_L  = (1 - SF) * middle[1] + SF * low

CC_vol    = ta.rma(math.abs(close - close[1]), Length)
CC_vol_O  = (1 - SF) * CC_vol[1] + SF * math.abs(open - close[1])
CC_vol_H  = (1 - SF) * CC_vol[1] + SF * math.abs(high - close[1])
CC_vol_L  = (1 - SF) * CC_vol[1] + SF * math.abs(low - close[1])

den_C  = math.max(CC_vol,   eps) * (Length - 1.0)
den_O  = math.max(CC_vol_O, eps) * (Length - 1.0)
den_H  = math.max(CC_vol_H, eps) * (Length - 1.0)
den_L  = math.max(CC_vol_L, eps) * (Length - 1.0)

myRSI = half_range + half_range * ((close - middle)   / den_C)
RSI_O = half_range + half_range * ((open  - middle_O) / den_O)
RSI_H = half_range + half_range * ((high  - middle_H) / den_H)
RSI_L = half_range + half_range * ((low   - middle_L) / den_L)


// ─── Zones Thresholds Logic ──────────────────────────────────
// Body threshold (~50% mass): ≈ 0.658σ
body_threshold     = math.sqrt((5.0 - math.sqrt(17.0)) / 2.0)      // ≈ 0.658σ
// Tail threshold (~1.6% each tail): ≈ 2.135σ
tail_threshold     = math.sqrt((5.0 + math.sqrt(17.0)) / 2.0)      // ≈ 2.135σ
// Breakout points (2nd derivative extrema): ±1.000σ
breakout_threshold = 1.0                                           // = 1.000σ
// Reversal points (3rd derivative extrema): ±√3σ
reversal_threshold = math.sqrt(3.0)                                // ≈ 1.732σ


// ─── Helpers: custom TANH + z→RSI distance mapping ───────────
// Pine lacks tanh; implement via exponentials:
// tanh(x) = (e^(2x) - 1) / (e^(2x) + 1)
TANH(x) =>
    float ex = math.exp(2.0 * x)
    (ex - 1.0) / (ex + 1.0)

// Map z-score to RSI distance from 50: 50 * tanh(z / sqrt(Length - 1))
RSI_distance_from50(z) =>
    half_range * TANH(z * inv_sqrt_lenm1)
// end tanh(x) function


// Support/Resistance zone (body ↔ breakout)
Z_ins  = RSI_distance_from50(body_threshold)
Z_out  = RSI_distance_from50(breakout_threshold)

// Overbought/Oversold zone (reversal ↔ tail)
OO_ins = RSI_distance_from50(reversal_threshold)
OO_out = RSI_distance_from50(tail_threshold)


// ─── Colors & Opacity ────────────────────────────────────────
color_1        = SupRes ? color_SR : color.new(color_RSI, 100)
color_RM       = OO ? color.new(color_R, 15) : color.new(color.red, 100)
color_GM       = OO ? color.new(color_G, 15) : color.new(color.green, 100)
opacity_OO     = OO ? 90 : 100

// up and down candles
isCandleUp     = myRSI > RSI_O
opacity_candle = isCandleUp ? 90 : 10

//overbought and oversold colors
isOB_line = myRSI > (50 + OO_ins)
isOS_line = myRSI < (50 - OO_ins)

isOB_OHLC = bar_index >= Length and RSI_H > 50 + OO_ins
isOS_OHLC = bar_index >= Length and RSI_L < 50 - OO_ins

BC_color =
     not OO_coloring ? color.new(color_RSI, opacity_candle) :
     isOB_OHLC       ? color.new(color_RI, opacity_candle) :
     isOS_OHLC       ? color.new(color_GI, opacity_candle) :
                       color.new(color_RSI, opacity_candle)

border_color =
     not OO_coloring ? color.new(color_RSI, 0) :
     isOB_OHLC       ? color_RI :
     isOS_OHLC       ? color_GI :
                       color.new(color_RSI, 0)
// ─── End of Colors & Opacity ──────────────────────────────────


// ─── RSI Type Plots ──────────────────────────────────────────
plot(RSIType == 'line'   ? myRSI : na, 'RSI Line',   color.new(color_RSI, 25), linewidth = 2, editable = true)
plotcandle(RSIType == 'candle' ? RSI_O : na,
           RSIType == 'candle' ? RSI_H : na,
           RSIType == 'candle' ? RSI_L : na,
           RSIType == 'candle' ? myRSI : na,
           'RSI Candle', color = BC_color, wickcolor = border_color, bordercolor = border_color)

plotbar(   RSIType == 'bar'    ? RSI_O : na,
           RSIType == 'bar'    ? RSI_H : na,
           RSIType == 'bar'    ? RSI_L : na,
           RSIType == 'bar'    ? myRSI : na,
           'RSI Bar', color = border_color)

// Dots on overbought/oversold when in line mode
plot(OO_coloring and RSIType == 'line' and (isOB_line or isOS_line) ? myRSI : na,
     'Dots', color = border_color, linewidth = 2, style = plot.style_circles, editable = true)
// ─── End of RSI Type Plots ───────────────────────────────────


// Middle line
rsiMiddle = plot(50,     'Middle', color.new(color_RSI, 50), style = plot.style_linebr, display = display.all, editable = true)

// ─── Overbought Zone Fill ────────────────────────────────────
overbought  = plot(50 + OO_out, 'Top of Overbought',    color = color_GM,                       editable = true, display = display.none)
overbought1 = plot(50 + OO_ins, 'Bottom of Overbought', color = color.new(color_G, opacity_OO), editable = true, display = display.none)
fill(overbought, overbought1, 50 + OO_out, 50 + OO_ins,
     top_color = color_GM, bottom_color = color.new(color_G, opacity_OO),
     title = 'Overbought Zone')
// ─── End of Overbought Zone Fill ─────────────────────────────


// ─── Oversold Zone Fill ──────────────────────────────────────
oversold  = plot(50 - OO_out, 'Bottom of Oversold', color = color_RM,                       editable = true, display = display.none)
oversold1 = plot(50 - OO_ins, 'Top of Oversold',    color = color.new(color_R, opacity_OO), editable = true, display = display.none)
fill(oversold1, oversold, 50 - OO_ins, 50 - OO_out,
     top_color = color.new(color_R, opacity_OO), bottom_color = color_RM,
     title = 'Oversold Zone')
// ─── End of Oversold Zone Fill ───────────────────────────────


// ─── Support / Resistance Fills ──────────────────────────────
R1 = plot(50 + Z_out, 'Top of Resistance',    color_1, editable = true, display = display.none)
R2 = plot(50 + Z_ins, 'Bottom of Resistance', color_1, editable = true, display = display.none)
fill(R1, R2, color_1, title = 'Resistance Zone')

S1 = plot(50 - Z_ins, 'Top of Support',    color_1, editable = true, display = display.none)
S2 = plot(50 - Z_out, 'Bottom of Support', color_1, editable = true, display = display.none)
fill(S2, S1, color_1, title = 'Support Zone')


// ─────────────────────────────────────────────────────────────
// ─── DIVERGENCE DETECTION ────────────────────────────────────
// ─────────────────────────────────────────────────────────────

if showDivergence
    // Detect pivots on RSI
    float pivotHighRSI = ta.pivothigh(myRSI, pivotLookback, pivotLookback)
    float pivotLowRSI  = ta.pivotlow(myRSI, pivotLookback, pivotLookback)
    
    // Store pivot values and positions
    var float lastPivotHighRSI = na
    var int lastPivotHighBar = na
    var float lastPivotHighPrice = na
    
    var float lastPivotLowRSI = na
    var int lastPivotLowBar = na
    var float lastPivotLowPrice = na
    
    // Update when new RSI pivot high is found
    if not na(pivotHighRSI)
        currentBar = bar_index - pivotLookback
        currentPrice = high[pivotLookback]
        
        // Check for bearish divergences
        if not na(lastPivotHighRSI) and not na(lastPivotHighBar)
            barsBack = currentBar - lastPivotHighBar
            
            if barsBack > 0 and barsBack <= maxLookback
                // Regular Bearish: Price makes higher high, RSI makes lower high
                regBear = currentPrice > lastPivotHighPrice and pivotHighRSI < lastPivotHighRSI
                
                // Hidden Bearish: Price makes lower high, RSI makes higher high
                hidBear = currentPrice < lastPivotHighPrice and pivotHighRSI > lastPivotHighRSI
                
                if showRegularBear and regBear
                    line.new(lastPivotHighBar, lastPivotHighRSI, currentBar, pivotHighRSI, 
                             color = colorRegBear, width = 2, style = line.style_solid)
                    label.new(currentBar, pivotHighRSI, 'Bear', 
                             color = color.new(colorRegBear, 80), 
                             textcolor = colorRegBear, 
                             style = label.style_label_down, size = size.tiny)
                
                if showHiddenBear and hidBear
                    line.new(lastPivotHighBar, lastPivotHighRSI, currentBar, pivotHighRSI, 
                             color = colorHidBear, width = 1, style = line.style_dashed)
                    label.new(currentBar, pivotHighRSI, 'H Bear', 
                             color = color.new(colorHidBear, 80), 
                             textcolor = colorHidBear, 
                             style = label.style_label_down, size = size.tiny)
        
        lastPivotHighRSI := pivotHighRSI
        lastPivotHighBar := currentBar
        lastPivotHighPrice := currentPrice
    
    // Update when new RSI pivot low is found
    if not na(pivotLowRSI)
        currentBarLow = bar_index - pivotLookback
        currentPriceLow = low[pivotLookback]
        
        // Check for bullish divergences
        if not na(lastPivotLowRSI) and not na(lastPivotLowBar)
            barsBackLow = currentBarLow - lastPivotLowBar
            
            if barsBackLow > 0 and barsBackLow <= maxLookback
                // Regular Bullish: Price makes lower low, RSI makes higher low
                regBull = currentPriceLow < lastPivotLowPrice and pivotLowRSI > lastPivotLowRSI
                
                // Hidden Bullish: Price makes higher low, RSI makes lower low
                hidBull = currentPriceLow > lastPivotLowPrice and pivotLowRSI < lastPivotLowRSI
                
                if showRegularBull and regBull
                    line.new(lastPivotLowBar, lastPivotLowRSI, currentBarLow, pivotLowRSI, 
                             color = colorRegBull, width = 2, style = line.style_solid)
                    label.new(currentBarLow, pivotLowRSI, 'Bull', 
                             color = color.new(colorRegBull, 80), 
                             textcolor = colorRegBull, 
                             style = label.style_label_up, size = size.tiny)
                
                if showHiddenBull and hidBull
                    line.new(lastPivotLowBar, lastPivotLowRSI, currentBarLow, pivotLowRSI, 
                             color = colorHidBull, width = 1, style = line.style_dashed)
                    label.new(currentBarLow, pivotLowRSI, 'H Bull', 
                             color = color.new(colorHidBull, 80), 
                             textcolor = colorHidBull, 
                             style = label.style_label_up, size = size.tiny)
        
        lastPivotLowRSI := pivotLowRSI
        lastPivotLowBar := currentBarLow
        lastPivotLowPrice := currentPriceLow

// ─── End of Divergence Detection ─────────────────────────────

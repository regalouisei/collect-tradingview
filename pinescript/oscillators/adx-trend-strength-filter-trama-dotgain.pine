// @ DotGain

//@version=6
indicator('ADX Trend Strength Filter + TRAMA [DotGain]', shorttitle = 'ADX TSF', overlay = true)

// === INPUTS ===
tramaLength = input.int(99, title = 'TRAMA Length')
tramaSrc = input.source(close, title = 'TRAMA Source')
rsiLength = input.int(7, title = 'RSI Length')
rsiSmaLength = input.int(50, title = 'RSI SMA Length')
rsiEmaLength = input.int(35, title = 'RSI EMA Length')
adxLength = input.int(14, title = 'ADX Length')
adxThreshold = input.float(30, title = 'ADX Threshold')
showSma = input.bool(true, title = 'Show SMA')
smaLength = input.int(200, title = 'SMA Length')

// === TRAMA CALCULATION ===
var float ama = tramaSrc
hh = math.max(math.sign(ta.change(ta.highest(tramaLength))), 0)
ll = math.max(math.sign(-ta.change(ta.lowest(tramaLength))), 0)
tc = math.pow(ta.sma(hh != 0 or ll != 0 ? 1.0 : 0.0, tramaLength), 2)
ama := nz(ama[1] + tc * (tramaSrc - ama[1]), tramaSrc)

// === RSI & MA CALCULATIONS ===
rsi = ta.rsi(close, rsiLength)
rsiSma = ta.sma(rsi, rsiSmaLength)
rsiEma = ta.ema(rsi, rsiEmaLength)

// === ADX MANUAL CALCULATION ===
upMove = high - high[1]
downMove = low[1] - low
plusDM = upMove > downMove and upMove > 0 ? upMove : 0
minusDM = downMove > upMove and downMove > 0 ? downMove : 0
trur = ta.rma(ta.tr(true), adxLength)
plusDI = 100 * ta.rma(plusDM, adxLength) / trur
minusDI = 100 * ta.rma(minusDM, adxLength) / trur
dx = 100 * math.abs(plusDI - minusDI) / (plusDI + minusDI)
adx = ta.rma(dx, adxLength)
adxCondition = adx > adxThreshold

// === SMA CALCULATION ===
sma = ta.sma(close, smaLength)

// === NEUTRAL CONDITION ===
priceBetweenSmaAndTrama = close > math.min(ama, sma) and close < math.max(ama, sma)

// === SIGNAL CONDITIONS ===
bullCondition = close > ama and rsiEma > rsiSma and adxCondition
bearCondition = close < ama and rsiEma < rsiSma and adxCondition

// === SIGNAL STATE LOGIC ===
var int signalState = 0 // 1 = bull, -1 = bear, 0 = neutral

if priceBetweenSmaAndTrama
    signalState := 0
    signalState
else
    if signalState == 1
        if bullCondition
            signalState := 1
            signalState
        else if bearCondition
            signalState := -1
            signalState
    else if signalState == -1
        if bearCondition
            signalState := -1
            signalState
        else if bullCondition
            signalState := 1
            signalState
    else
        if bullCondition
            signalState := 1
            signalState
        else if bearCondition
            signalState := -1
            signalState

// === BAR COLORING ===
barcolor(signalState == 1 ? color.green : signalState == -1 ? color.red : color.orange)

// === PLOTS ===
plotTrama = plot(ama, title = 'TRAMA', color = color.new (color.orange, 33), linewidth = 1)
plotSma = plot(showSma ? sma : na, title = 'SMA', color = color.white, linewidth = 2)

// === BACKGROUND FILL BETWEEN TRAMA & SMA ===
fill(plotTrama, plotSma, color = color.new(color.orange, 80))

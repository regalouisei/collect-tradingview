// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © RWCS_LTD

// Fear & Greed Oscillator – Risk Sentiment
// Visualizes macro risk-on/risk-off sentiment using Z-score of expansion vs contraction assets.
// Defaults: Copper/Gold (CAPITALCOM:COPPER / TVC:GOLD)
// Green zone = Risk-On (Greed), Red zone = Risk-Off (Fear)

//@version=6
indicator("Fear & Greed Oscillator - Risk Sentiment", overlay=false)

// Input period for z-score
length = input.int(200, title="Z-Score Lookback Period")
sym1 = input.symbol("CAPITALCOM:COPPER", "Macro Expansion Ticker", tooltip = "What does well when the economy is expanding?")
sym2 = input.symbol("TVC:GOLD", "Macro Contraction Ticker", tooltip = "What does well when the economy is contracting?")

// Copper/Gold ratio (unsmoothed)
copper = request.security(sym1, timeframe.period, close)
gold   = request.security(sym2, timeframe.period, close)
ratio  = copper / gold

// Z-score calculation
mean = ta.sma(ratio, length)
std  = ta.stdev(ratio, length)
z    = std != 0 ? (ratio - mean) / std : na

// Gradient from -3 (red) to +3 (green)
z_min = -3.0
z_max = 3.0
line_color = color.from_gradient(z, z_min, z_max, color.fuchsia, color.aqua)

// Plot Z-score with dynamic gradient color
plot(z, title="Z-Score", color=line_color, linewidth=2)

// Z-score zone guides
hline(3,  "OB Max", color=color.new(color.green, 40))
hline(2,  "OB Start", color=color.new(color.lime, 50))
hline(1,  "Neutral +1",  color=color.new(color.gray, 60))
hline(0,  "Neutral",  color=color.new(color.gray, 70))
hline(-1,  "Neutral -1",  color=color.new(color.gray, 60))
hline(-2, "OS Start", color=color.new(color.orange, 50))
hline(-3, "OS Max",   color=color.new(color.red, 40))

// Dynamic Alert for Fear and Greed Zones
alertcondition(z > 2, title="Entering Greed Zone", message="Z-Score has entered the Greed Zone")
alertcondition(z < -2, title="Entering Fear Zone", message="Z-Score has entered the Fear Zone")

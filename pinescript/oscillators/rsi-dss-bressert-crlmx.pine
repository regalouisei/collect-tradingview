// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 | https://mozilla.org/MPL/2.0/
// [  crlmx.RSI-DSS v1.1  ]
// [    ••• © crlmx •••     ]
//
//@version=6
indicator(title="[crlmx] RSI & DSS Bressert", shorttitle="RSI & DSS Bressert [crlmx]", format=format.price, precision=2, timeframe="", timeframe_gaps=true)

// ══════════════════════════════════════════════════════════════════════════════
// INPUTS
// ══════════════════════════════════════════════════════════════════════════════

showRSI = input.bool(true, "Show RSI", group="__________ RSI __________")
rsiLengthInput = input.int(14, minval=1, title="RSI Length", group="__________ RSI __________")
rsiSourceInput = input.source(close, "Source", group="__________ RSI __________")

showDSS = input.bool(true, "Show DSS Bressert", group="__________ DSS BRESSERT __________")
periodK = input.int(10, title="%K Length", minval=1, group="__________ DSS BRESSERT __________")
smoothK = input.int(9, title="%K Smoothing", minval=1, group="__________ DSS BRESSERT __________")
periodD = input.int(5, title="%D Smoothing", minval=1, group="__________ DSS BRESSERT __________")

// ══════════════════════════════════════════════════════════════════════════════
// RSI
// ══════════════════════════════════════════════════════════════════════════════

change = ta.change(rsiSourceInput)
up = ta.rma(math.max(change, 0), rsiLengthInput)
down = ta.rma(-math.min(change, 0), rsiLengthInput)
rsi = down == 0 ? 100 : up == 0 ? 0 : 100 - (100 / (1 + up / down))

// ══════════════════════════════════════════════════════════════════════════════
// DSS BRESSERT
// ══════════════════════════════════════════════════════════════════════════════

k = ta.ema(ta.stoch(close, high, low, periodK), smoothK)
d = ta.ema(k, periodD)

DSS_k = ta.ema(ta.stoch(k, k, k, periodK), smoothK)
DSS_d = ta.ema(DSS_k, periodD)

// ══════════════════════════════════════════════════════════════════════════════
// PLOTS - order controls Style tab listing
// ══════════════════════════════════════════════════════════════════════════════

midLinePlot = plot(50, color = na, editable = false, display = display.none)

// RSI
rsiPlot = plot(showRSI ? rsi : na, "RSI", color=#f0709b)
fill(rsiPlot, midLinePlot, 120, 70, top_color = color.new(#ee7472, 50), bottom_color = color.new(#ee7472, 100), title = "RSI Overbought")
fill(rsiPlot, midLinePlot, 30, -20, top_color = color.new(#089981, 100), bottom_color = color.new(#089981, 50), title = "RSI Oversold")

// DSS Bressert
dssKPlot = plot(showDSS ? DSS_k : na, title="DSS %K", color=#4e698b)
plot(showDSS ? DSS_d : na, title="DSS %D", color=#ac8b5b)
fill(dssKPlot, midLinePlot, 120, 80, top_color = color.new(#ee7472, 50), bottom_color = color.new(#ee7472, 100), title = "DSS Overbought")
fill(dssKPlot, midLinePlot, 20, -20, top_color = color.new(#089981, 100), bottom_color = color.new(#089981, 50), title = "DSS Oversold")

// Bands
rsiUpperBand = hline(70, "RSI Band Upper", color=#263140, linestyle=hline.style_dotted, display = showRSI ? display.all : display.none)
rsiLowerBand = hline(30, "RSI Band Lower", color=#263140, linestyle=hline.style_dotted, display = showRSI ? display.all : display.none)
dssUpperBand = hline(80, "DSS Band Upper", color=#263140, linestyle=hline.style_dotted, display = showDSS ? display.all : display.none)
dssLowerBand = hline(20, "DSS Band Lower", color=#263140, linestyle=hline.style_dotted, display = showDSS ? display.all : display.none)
midline = hline(50, "Band Middle", color=#40526c, linestyle=hline.style_dotted)
fill(rsiUpperBand, rsiLowerBand, color = color.new(#263f6b, 90), title="RSI Background", display = showRSI ? display.all : display.none)
fill(dssUpperBand, dssLowerBand, color = color.new(#263f6b, 95), title="DSS Background", display=display.none)

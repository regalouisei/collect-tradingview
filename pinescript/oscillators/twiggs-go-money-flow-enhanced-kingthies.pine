// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © kingthies

//@version=6
indicator("Twiggs Money Flow Enhanced", shorttitle="TMF_KT", timeframe="", timeframe_gaps=true)

// Inputs
length = input.int(21, "Period", minval=1)

// Wilders Moving Average
wilders_ma(src, len) =>
    var float ma = na
    ma := na(ma) ? src : (ma * (len - 1) + src) / len

// True Range boundaries
tr_high = math.max(close[1], high)
tr_low = math.min(close[1], low)
tr_range = tr_high - tr_low

// Average Daily Value
adv = tr_range == 0 ? 0.0 : volume * ((close - tr_low) - (tr_high - close)) / tr_range

// Wilders MA of volume and ADV
wma_volume = wilders_ma(volume, length)
wma_adv = wilders_ma(adv, length)

// Twiggs Money Flow
tmf = wma_volume == 0 ? 0.0 : wma_adv / wma_volume

// Plot
hline(0, "Zero", color.gray)
plot(tmf, "TMF", color.new(tmf > 0 ? color.teal : color.red, 0), 2)

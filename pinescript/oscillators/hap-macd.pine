// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © agahakanaga

//@version=6
indicator("HaP MACD", shorttitle="HaP MACD", timeframe = "", timeframe_gaps = true)

// --- GİRDİLER ---
float  sourceInput  = input.source(close, "Kaynak")
int    fastLenInput = input.int(12, "Hızlı Periyot",   1)
int    slowLenInput = input.int(26, "Yavaş Periyot",   1)
int    sigLenInput  = input.int(9,  "Sinyal Periyodu", 1)
string oscTypeInput = input.string("EMA", "Oscillator MA Tipi", ["EMA", "SMA"])
string sigTypeInput = input.string("EMA", "Sinyal MA Tipi",     ["EMA", "SMA"])

// --- FONKSİYONLAR ---
f_dema(source, length) =>
    ema1 = ta.ema(source, length)
    ema2 = ta.ema(ema1, length)
    2 * ema1 - ema2

ma(float source, int length, simple string maType) =>
    switch maType
        "EMA" => ta.ema(source, length)
        "SMA" => ta.sma(source, length)

// --- 1. STANDART MACD HESAPLAMASI ---
float maFast = ma(sourceInput, fastLenInput, oscTypeInput)
float maSlow = ma(sourceInput, slowLenInput, oscTypeInput)
float macd   = maFast - maSlow
float signal = ma(macd, sigLenInput, sigTypeInput)
float hist   = macd - signal

// --- 2. DEMA MACD HESAPLAMASI (Sinyal Noktaları İçin) ---
fast_dema = f_dema(sourceInput, fastLenInput)
slow_dema = f_dema(sourceInput, slowLenInput)
dema_macd_line = fast_dema - slow_dema
dema_signal_line = f_dema(dema_macd_line, sigLenInput)

// --- ŞARTLAR VE KESİŞİMLER ---
dema_ustunde = dema_macd_line > dema_signal_line
dema_signal_egim_pozitif = dema_signal_line > dema_signal_line[1]
buy_condition = dema_ustunde and dema_signal_egim_pozitif
is_first_signal = buy_condition and not buy_condition[1]

// Çıkış nokta mantığı
// Eğer bir önceki barda nokta vardıysa (buy_condition) ve bu barda yoksa kırmızı koy
orange_dot_condition = buy_condition[1] and not buy_condition

bull_cross = ta.crossover(macd, signal)
bear_cross = ta.crossunder(macd, signal)

// --- RENK MANTIĞI (Noktalar İçin) ---
color dot_color = if is_first_signal
    color.blue
else if buy_condition
    macd > macd[1] ? color.green : color.orange
else
    na

// --- ÇİZİMLER ---
hline(0, "Sıfır Çizgisi", color=color.new(color.gray, 50))

hColor = hist >= 0 ? hist > hist[1] ? #26a69a : #b2dfdb : hist > hist[1] ? #ffcdd2 : #ff5252
plot(hist, "Histogram", hColor, style = plot.style_columns, display = display.none)

plot(macd, "MACD", color=color.black, linewidth=1)
plot(signal, "Sinyal", color=#ff6d00, linewidth=1)

// 1. DEMA'DAN GELEN NOKTALAR (Mavi, Yeşil, Kırmızı)
plotshape(buy_condition ? macd : na, 
     title="HaP Sinyal Noktası", 
     style=shape.circle, 
     location=location.absolute, 
     color=dot_color, 
     size=size.tiny)

// Çıkış noktası
plotshape(orange_dot_condition ? macd : na, 
     title="Hap Çıkış ", 
     style=shape.circle, 
     location=location.absolute, 
     color=color.red, 
     size=size.tiny)

// 3. KESİŞİM İŞARETLERİ 
// Yukarı Kesişim (Al)
plotshape(bull_cross ? macd : na, 
     title="MACD Yukarı Kesişim", 
     style=shape.triangleup, 
     location=location.absolute, 
     color=color.lime, 
     size=size.small, display = display.none)

// Aşağı Kesişim (Sat)
plotshape(bear_cross ? macd : na, 
     title="MACD Aşağı Kesişim", 
     style=shape.triangledown, 
     location=location.absolute, 
     color=color.red, 
     size=size.small, display = display.none)

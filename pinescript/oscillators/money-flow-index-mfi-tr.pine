// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Tiagorocha1989
//                              ████████╗ ████████╗  ████████╗  ███████╗   ██████╗        ██████╗   ██████╗   ██████╗ ██╗   ██╗ ████████╗
//                              ╚══██╔══╝ ╚══██╔══╝  ██╔═══██║  ██╔════╝  ██╔═══██╗       ██╔══██╗ ██╔═══██╗ ██╔════╝ ██║   ██║ ██╔═══██║
//                                 ██║       ██║     ████████║  ██║ ███║  ██║   ██║       ██████╔╝ ██║   ██║ ██║      ████████║ ████████║
//                                 ██║       ██║     ██╔═══██║  ██║  ██║  ██║   ██║       ██╔══██╗ ██║   ██║ ██║      ██║   ██║ ██╔═══██║
//                                 ██║    ████████╗  ██║   ██║  ███████║  ╚██████╔╝       ██║  ██║ ╚██████╔╝ ╚██████╗ ██║   ██║ ██║   ██║
//@version=6
indicator('Money Flow Index - MFI | TR', 'MFI | TR →', false)
//╔════════════════════════╗
//║ Choose Moving Averages ║
//╚════════════════════════╝
ma(source, length, type) =>
     type == 'EMA' ? ta.ema(source, length) :
     type == 'SMA' ? ta.sma(source, length) :
     type == 'RMA' ? ta.rma(source, length) :
     type == 'WMA' ? ta.wma(source, length) :
     type == 'VWMA' ? ta.vwma(source, length) :
     type == 'HMA' ? ta.hma(source, length) :
     na
//╔════════════════════════╗
//║     INPUT SETTINGS     ║
//╚════════════════════════╝
Color_Mode =        input.string('Classic', 'Color Choice', group = 'Color', options = ['Classic', 'Modern', 'Robust', 'Accented', 'Monochrome', 'Moderate', 'Aqua', 'Cosmic'])
Length_MFI =        input.int(14, title = 'Length MFI:', group='MFI Setting', inline = 'MFI')
Source_MFI =        input(close, title = '', group='MFI Setting', inline = 'MFI')
Signal_MFI =        input.string('50', 'Entry/Exit Signal', group='MFI Setting', options = ['50', 'MFI MA'],tooltip = 'Choose Entry/Exit Point')
Length_MA_MFI =     input.int(365, title = 'Length MA', group='MFI MA', inline = 'MFI MA')
Source_MA_MFI =     input.string('EMA', '', group='MFI MA', inline = 'MFI MA', options=['EMA', 'SMA', 'RMA', 'WMA', 'VWMA', 'HMA'],tooltip = 'Moving Averages')
//╔════════════════════════╗
//║      Calculation       ║
//╚════════════════════════╝
MFI =     ta.mfi(Source_MFI,Length_MFI)
MFI_MA =  ma(MFI, Length_MA_MFI, Source_MA_MFI)
float Entry_MFI = Signal_MFI == 'MFI MA' ? MFI_MA : 50.0
isBull_MFI = MFI > Entry_MFI
isBear_MFI = MFI < Entry_MFI
Bull_MFI = ta.crossover(MFI, Entry_MFI)
Bear_MFI = ta.crossunder(MFI, Entry_MFI)
//╔════════════════════════╗
//║         Color          ║
//╚════════════════════════╝
[UpC, DnC] = switch Color_Mode
    'Classic' =>    [#008800, #ff0000]
    'Modern' =>     [#ffffff, #b721ff]
    'Robust' =>     [#ffbb00, #770737]
    'Accented' =>   [#8c5cf7, #e83e8c]
    'Monochrome' => [#e9ecef, #495057]
    'Moderate' =>   [#43a047, #e53935]
    'Aqua' =>       [#00a8e8, #f18f01]
    'Cosmic' =>     [#e83e8c, #6f2da8]
//╔════════════════════════╗
//║          Plot          ║
//╚════════════════════════╝
Plot_MFI =     plot(MFI, color = MFI > Entry_MFI ? color.new(UpC, 50):color.new(DnC, 50), linewidth = 3)
Plot_Entry_MFI =  plot(Entry_MFI,color = color.yellow)
fill(Plot_MFI, Plot_Entry_MFI, 80, Entry_MFI, top_color = color.new(UpC, 0),    bottom_color = color.new(UpC, 100), title = 'Overbought Fill')
fill(Plot_MFI, Plot_Entry_MFI, Entry_MFI, 20, top_color = color.new(DnC, 100), bottom_color = color.new(DnC, 0), title = 'Oversold Fill')
fill(Plot_MFI, Plot_Entry_MFI, MFI < Entry_MFI ? MFI : Entry_MFI, MFI < Entry_MFI ? Entry_MFI : MFI, MFI < Entry_MFI ? DnC : #00000000, MFI < Entry_MFI ? #00000000 : UpC)
plotcandle(open, high, low, close, 'Bar Color', isBull_MFI ? UpC : isBear_MFI ? DnC : na, isBull_MFI ? UpC : isBear_MFI ? DnC : na, bordercolor = isBull_MFI ? UpC : isBear_MFI ? DnC : na, force_overlay = true)
plotshape(Bull_MFI, 'LONG' , shape.triangleup  , location.belowbar, UpC,    0, size = size.small, force_overlay = true)
plotshape(Bear_MFI, 'SHORT', shape.triangledown, location.abovebar, DnC,    0, size = size.small, force_overlay = true)
//╔════════════════════════╗
//║          VIEW          ║
//╚════════════════════════╝
var table Table_MFI = table.new(position.middle_right, 1, 1, border_width = 1)
var label MFILabel = na
if barstate.islast
    table.cell(Table_MFI, 0, 0, text = isBull_MFI ? '⬆️ Bullish' : '⬇️ Bearish', text_color = isBull_MFI ? UpC : DnC,text_size = size.huge)
    if not na(MFILabel)
        label.delete(MFILabel)
    MFILabel := label.new(bar_index + 5, MFI, text =  str.tostring(MFI, '#.##'), color = MFI > Entry_MFI ? color.new(UpC, 20) : MFI < Entry_MFI ? color.new(DnC, 20) : color.gray, style = label.style_label_left, size = size.large, textcolor = #000000)
//╔════════════════════════╗
//║         Alerts         ║
//╚════════════════════════╝
alertcondition(Bull_MFI, title= 'LONG', message= '{{ticker}} LONG')
alertcondition(Bear_MFI, title= 'SHORT', message= '{{ticker}} SHORT')

// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Tiagorocha1989
//                              ████████╗ ████████╗  ████████╗  ███████╗   ██████╗        ██████╗   ██████╗   ██████╗ ██╗   ██╗ ████████╗
//                              ╚══██╔══╝ ╚══██╔══╝  ██╔═══██║  ██╔════╝  ██╔═══██╗       ██╔══██╗ ██╔═══██╗ ██╔════╝ ██║   ██║ ██╔═══██║
//                                 ██║       ██║     ████████║  ██║ ███║  ██║   ██║       ██████╔╝ ██║   ██║ ██║      ████████║ ████████║
//                                 ██║       ██║     ██╔═══██║  ██║  ██║  ██║   ██║       ██╔══██╗ ██║   ██║ ██║      ██║   ██║ ██╔═══██║
//                                 ██║    ████████╗  ██║   ██║  ███████║  ╚██████╔╝       ██║  ██║ ╚██████╔╝ ╚██████╗ ██║   ██║ ██║   ██║
//@version=6
indicator('Commodity Channel Index - CCI | TR', 'CCI | TR →',false)
//╔════════════════════════╗
//║ Choose Moving Averages ║
//╚════════════════════════╝
ma(source, length, type) =>
     type == 'EMA' ? ta.ema(source, length) :
     type == 'SMA' ? ta.sma(source, length) :
     type == 'RMA' ? ta.rma(source, length) :
     type == 'WMA' ? ta.wma(source, length) :
     type == 'VWMA' ? ta.vwma(source, length) :
     type == 'HMA' ? ta.hma(source, length) :
     na
//╔════════════════════════╗
//║     INPUT SETTINGS     ║
//╚════════════════════════╝
Color_Mode =        input.string('Classic', 'Color Choice', group = 'Color', options = ['Classic', 'Modern', 'Robust', 'Accented', 'Monochrome', 'Moderate', 'Aqua', 'Cosmic'])
Length_CCI =        input.int(55, title = 'Length CCI', inline = 'CCI', group='CCI Setting')
Source_CCI =        input.source(close, '', inline = 'CCI', group = 'CCI Setting')
Signal_CCI =        input.string('0', 'Entry/Exit Signal', group='CCI Setting', options = ['0', 'CCI MA'],tooltip = 'Choose Entry/Exit Point')
Length_MA_CCI =     input.int(365, title = 'Length MA', group='CCI MA', inline = 'MA')
Source_MA_CCI =     input.string('EMA', '', group='CCI MA', inline = 'MA', options=['EMA', 'SMA', 'RMA', 'WMA', 'VWMA', 'HMA'],tooltip = 'Moving Averages')
//╔════════════════════════╗
//║      Calculation       ║
//╚════════════════════════╝
CCI = ta.cci(Source_CCI,Length_CCI)
CCI_MA = ma(CCI, Length_MA_CCI, Source_MA_CCI)
float Entry_CCI = Signal_CCI == 'CCI MA' ? CCI_MA : 0.0
isBull_CCI = CCI > Entry_CCI
isBear_CCI = CCI < Entry_CCI
Bull_CCI = ta.crossover(CCI, Entry_CCI)
Bear_CCI = ta.crossunder(CCI, Entry_CCI)
//╔════════════════════════╗
//║         Color          ║
//╚════════════════════════╝
[UpC, DnC] = switch Color_Mode
    'Classic' =>    [#008800, #ff0000]
    'Modern' =>     [#ffffff, #b721ff]
    'Robust' =>     [#ffbb00, #770737]
    'Accented' =>   [#8c5cf7, #e83e8c]
    'Monochrome' => [#e9ecef, #495057]
    'Moderate' =>   [#43a047, #e53935]
    'Aqua' =>       [#00a8e8, #f18f01]
    'Cosmic' =>     [#e83e8c, #6f2da8]
//╔════════════════════════╗
//║          Plot          ║
//╚════════════════════════╝
Plot_Entry_CCI =    plot(Entry_CCI, color = color.yellow)
Plot_CCI =          plot(CCI, color = CCI > Entry_CCI ? color.new(UpC, 50):color.new(DnC, 50), linewidth = 3)
fill(Plot_CCI, Plot_Entry_CCI, 300, 0, top_color = color.new(UpC, 0),    bottom_color = color.new(UpC, 100), title = 'Overbought Fill')
fill(Plot_CCI, Plot_Entry_CCI, 0, -300, top_color = color.new(DnC, 100), bottom_color = color.new(DnC, 0),    title = 'Oversold Fill')
fill(Plot_CCI, Plot_Entry_CCI, CCI < Entry_CCI ? CCI : Entry_CCI, CCI < Entry_CCI ? Entry_CCI : CCI, CCI < Entry_CCI ? DnC : #00000000, CCI < Entry_CCI ? #00000000 : UpC)
plotcandle(open, high, low, close, 'Bar Color', isBull_CCI ? UpC : isBear_CCI ? DnC : na, isBull_CCI ? UpC : isBear_CCI ? DnC : na, bordercolor = isBull_CCI ? UpC : isBear_CCI ? DnC : na, force_overlay = true)
plotshape(Bull_CCI, 'LONG' , shape.triangleup  , location.belowbar, UpC,    0, size = size.small, force_overlay = true)
plotshape(Bear_CCI, 'SHORT', shape.triangledown, location.abovebar, DnC,    0, size = size.small, force_overlay = true)
//╔════════════════════════╗
//║          VIEW          ║
//╚════════════════════════╝
var table Table_CCI = table.new(position.middle_right, 1, 1, border_width = 1)
var label CCILabel = na
if barstate.islast
    table.cell(Table_CCI, 0, 0, text = isBull_CCI ? '⬆️ Bullish' : '⬇️ Bearish', text_color = isBull_CCI ? UpC : DnC,text_size = size.huge)
    if not na(CCILabel)
        label.delete(CCILabel)
    CCILabel := label.new(bar_index + 5, CCI, text =  str.tostring(CCI, '#.##'), color = CCI > Entry_CCI ? color.new(UpC, 20) : CCI < Entry_CCI ? color.new(DnC, 20) : color.gray, style = label.style_label_left, size = size.large, textcolor = #000000)
//╔════════════════════════╗
//║         Alerts         ║
//╚════════════════════════╝
alertcondition(isBull_CCI, title= 'LONG', message= '{{ticker}} LONG')
alertcondition(isBear_CCI, title= 'SHORT', message= '{{ticker}} SHORT')

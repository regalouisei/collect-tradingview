//@version=6
//Reverse Relative Strength Indicator

indicator('Reverse RSI Levels', overlay = true)


float inp = input.source(title = 'Source', defval = close)
string res = input.timeframe(title = 'Resolution', defval = '')
float src = request.security(syminfo.tickerid, res, inp)

//If you want a gradient ebtween your own RSI points
bool gradient = input.bool(false, title = 'Show gradient', tooltip="This will give a gradient between points you make yourself if you want that. It only works with connected points in the settings, for instance the first and second point, but not first and third/fourth.", group = 'RSI levels')

//Top restance level, preferably above 70
bool bTop = input.bool(false, title = '', inline = 'b', group = 'RSI levels')
float rsiValTop = input.float(70.0, 'RSI OBO', minval = 0.0, maxval = 100.0, step = 0.1, tooltip = 'This is the RSI value that indicates an overbought instrument. I suggest 70/75 for the stock market and 80/85 for Crypto', inline = 'b', group = 'RSI levels')
color rsiColTop = input.color(color.orange, title = '', inline = 'b', group = 'RSI levels')

//Mid restance level, preferably between 50 and 70
bool bRes = input.bool(false, title = '', inline = 'c', group = 'RSI levels')
float rsiValRes = input.float(55.0, 'RSI RES', minval = 0.0, maxval = 100.0, step = 0.1, tooltip = 'This is the current RSI resistance value. You can find thiw by drawing a declining trend line from HH RSI to LH RSI', inline = 'c', group = 'RSI levels')
color rsiColRes = input.color(color.yellow, title = '', inline = 'c', group = 'RSI levels')

//Mid support level, preferably between 30 and 50
bool bSup = input.bool(false, title = '', inline = 'd', group = 'RSI levels')
float rsiValSup = input.float(45.0, 'RSI SUP', minval = 0.0, maxval = 100.0, step = 0.1, tooltip = 'This is the current RSI support value. You can find thiw by drawing an inclining trend line from LL RSI to HL RSI', inline = 'd', group = 'RSI levels')
color rsiColSup = input.color(color.rgb(0, 255, 255), title = '', inline = 'd', group = 'RSI levels')

//Bottom support level, preferably below 30
bool bBot = input.bool(false, title = '', inline = 'e', group = 'RSI levels')
float rsiValBot = input.float(30.0, 'RSI OSO', minval = 0.0, maxval = 100.0, step = 0.1, tooltip = 'This is the RSI value that indicates an oversold instrument. 70 Is suggested for the stock market and 75 or 80 for Crypto', inline = 'e', group = 'RSI levels')
color rsiColBot = input.color(color.white, title = '', inline = 'e', group = 'RSI levels')

int rsiLength = input.int(title = 'RSI Length', defval = 14, minval = 1)
//float rsiLevel = input.float(title = "RSI Level", defval = 50.0, minval = 0.0, maxval = 100.0)

int emaLength = 2 * rsiLength - 1
float up = math.max(src - nz(src[1]), 0)
float dn = math.max(nz(src[1]) - src, 0)
float upEma = ta.ema(up, emaLength)
float dnEma = ta.ema(dn, emaLength)

float x70 = (rsiLength - 1) * (dnEma * 70 / (100 - 70) - upEma)
float rev70 = x70 >= 0 ? src + x70 : nz(src + x70 * ((100 - 70) / 70))

float xTop = (rsiLength - 1) * (dnEma * rsiValTop / (100 - rsiValTop) - upEma)
float revTop = xTop >= 0 ? src + xTop : nz(src + xTop * ((100 - rsiValTop) / rsiValTop))

float xRes = (rsiLength - 1) * (dnEma * rsiValRes / (100 - rsiValRes) - upEma)
float revRes = xRes >= 0 ? src + xRes : nz(src + xRes * ((100 - rsiValRes) / rsiValRes))

float x50 = (rsiLength - 1) * (dnEma * 50 / (100 - 50) - upEma)
float rev50 = x50 >= 0 ? src + x50 : nz(src + x50 * ((100 - 50) / 50))

float xSup = (rsiLength - 1) * (dnEma * rsiValSup / (100 - rsiValSup) - upEma)
float revSup = xSup >= 0 ? src + xSup : nz(src + xSup * ((100 - rsiValSup) / rsiValSup))

float xBot = (rsiLength - 1) * (dnEma * rsiValBot / (100 - rsiValBot) - upEma)
float revBot = xBot >= 0 ? src + xBot : nz(src + xBot * ((100 - rsiValBot) / rsiValBot))

float x30 = (rsiLength - 1) * (dnEma * 30 / (100 - 30) - upEma)
float rev30 = x30 >= 0 ? src + x30 : nz(src + x30 * ((100 - 30) / 30))



topPlot = plot(bTop ? revTop : na, title = 'TOP RSI Resistance', color = rsiColTop, linewidth = 1)
resPlot = plot(bRes ? revRes : na, title = 'RSI Resistance', color = rsiColRes, linewidth = 1)
supPlot = plot(bSup ? revSup : na, title = 'RSI Support', color = rsiColSup, linewidth = 1)
botPlot = plot(bBot ? revBot : na, title = 'Bottom RSI Support', color = rsiColBot, linewidth = 1)
plot(rev70, title = 'RSI 70', color = color.red, linewidth = 2, style = plot.style_linebr)
plot(rev50, title = 'RSI 50', color = color.blue, linewidth = 2, style = plot.style_linebr)
plot(rev30, title = 'RSI 30', color = color.green, linewidth = 2, style = plot.style_linebr)

color topFill = color.new(rsiColTop, 80)
color resFill = color.new(rsiColRes, 80)
color supFill = color.new(rsiColSup, 80)
color botFill = color.new(rsiColBot, 80)

fill(topPlot, resPlot, revTop, revRes, gradient and bTop ? topFill : na, gradient and bRes ? resFill : na)
fill(resPlot, supPlot, revRes, revSup, gradient and bRes ? resFill : na, gradient and bSup ? supFill : na)
fill(supPlot, botPlot, revSup, revBot, gradient and bSup ? supFill : na, gradient and bBot ? botFill : na)

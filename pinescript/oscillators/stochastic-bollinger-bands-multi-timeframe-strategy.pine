// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © RWCS_LTD

//@version=6
strategy("Stochastic + Bollinger Bands Multi-Timeframe Strategy", 
     overlay = false, 
     pyramiding = 0, 
     initial_capital = 1000, 
     default_qty_type = strategy.percent_of_equity, 
     default_qty_value = 100, 
     slippage = 3, 
     commission_value = 0.045
     )

// === INPUTS ===
group1 = "Plotting Options"
group2 = "Stochastic Settings"
group3 = "Bollinger Bands Settings"

plotChoice = input.string("Stochastic", title="Plot Selector", options=["Stochastic", "Bollinger Bands"], group = group1)

STOCH_TIME = input.timeframe("240", "Stochastic Timeframe", group = group2)
stochKLen = input.int(17, title="%K Length", group = group2)
stochSmoothK = input.int(1, title="%K Smoothing", group = group2)
stochDLen = input.int(3, title="%D Smoothing", group = group2)

BB_TIME = input.timeframe("60", "Bollinger Bands Timeframe", group = group3)
bbLen = input.int(20, title="BB Length", group = group3)
bbMult = input.float(2.0, title="BB Multiplier", group = group3)


// === STOCHASTIC on 1D ===
[k, d] = request.security(syminfo.tickerid, STOCH_TIME, 
     [ta.sma(ta.stoch(close, high, low, stochKLen), stochSmoothK),
     ta.sma(ta.sma(ta.stoch(close, high, low, stochKLen), stochSmoothK), stochDLen)]
     )

stochLong = ta.crossover(k, d) and k < 20 and d < 20
stochExit = ta.crossunder(k, d) and k > 80 and d > 80

// === BOLLINGER BANDS on 4H ===
[bbBasis, bbUpper, bbLower] = request.security(syminfo.tickerid, BB_TIME,
     ta.bb(close, bbLen, bbMult)
     )

bbLong = ta.crossover(close, bbLower)
bbExit = close >= bbUpper

// === ENTRY & EXIT CONDITIONS ===
longCondition = stochLong and bbLong
if longCondition
    strategy.entry("Long", strategy.long)

exitCondition = stochExit or bbExit
if exitCondition
    strategy.close("Long")

// === PLOTTING ===
showStoch = plotChoice == "Stochastic"
showBB = plotChoice == "Bollinger Bands"

// Stochastic %K and %D
plot(showStoch ? k : na, title="%K (1D)", color=color.blue)
plot(showStoch ? d : na, title="%D (1D)", color=color.orange)
hline(showStoch ? 80 : na, "Stoch OB", color=color.red)
hline(showStoch ? 20 : na, "Stoch OS", color=color.green)

// Bollinger Bands
plot(showBB ? bbUpper : na, title="BB Upper (4H)", color=color.red)
plot(showBB ? bbBasis : na, title="BB Basis (4H)", color=color.gray)
plot(showBB ? bbLower : na, title="BB Lower (4H)", color=color.green)

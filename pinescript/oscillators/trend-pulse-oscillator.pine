// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Uncle_the_shooter

//@version=6
indicator('Trend Pulse Oscillator', overlay = false, precision = 2)

// INPUTS

// OSCILLATOR SETTINGS
period              = input.int(100, 'Osc Average Body Periods', minval = 1, group="OSCILLATOR SETTINGS")
bandMultiplier      = input.float(2.5, 'Osc Band Multiplier', step = 0.1, group="OSCILLATOR SETTINGS")
toleranceMultiplier = input.float(1.0, 'Osc Tolerance Multiplier', step = 0.1, group="OSCILLATOR SETTINGS")
smoothType          = input.string('EMA', 'Smoothing Type', options = ['EMA', 'SMA', 'RMA', 'WMA'], group="OSCILLATOR SETTINGS")
smoothLen           = input.int(5, 'Osc Smoothing Length', group="OSCILLATOR SETTINGS")

// HIGHER TIMEFRAME TREND
period_trend              = input.int(100, 'Trend Average Body Periods', minval = 1, group="HIGHER TIMEFRAME TREND")
bandMultiplier_trend      = input.float(2.5, 'Trend Band Multiplier', step = 0.1, group="HIGHER TIMEFRAME TREND")
toleranceMultiplier_trend = input.float(2.5, 'Trend Tolerance Multiplier', step = 0.1, group="HIGHER TIMEFRAME TREND")

// OB / OS ZONES
obLevel = input.float(45, 'Overbought Level', minval = 1, maxval = 99, group="OB / OS ZONES")
osLevel = input.float(-45, 'Oversold Level', minval = -99, maxval = -1, group="OB / OS ZONES")

// VISUAL SETTINGS
bullColor = input.color(color.rgb(22, 193, 67), "Bullish Color", group="VISUAL SETTINGS")
bearColor = input.color(color.rgb(229, 11, 11), "Bearish Color", group="VISUAL SETTINGS")
osColor   = input.color(color.gray, "OS Line Color", group="VISUAL SETTINGS")
midColor  = input.color(color.gray, "Mid Line Color", group="VISUAL SETTINGS")

enableCandleColoring = input.bool(true, 'Enable Candle Coloring', group="VISUAL SETTINGS")
showOverboughtGradient = input.bool(true, "Show OB Gradient", group="VISUAL SETTINGS")
showOversoldGradient   = input.bool(true, "Show OS Gradient", group="VISUAL SETTINGS")
showOscillatorGradient = input.bool(true, "Show Oscillator Gradient", group="VISUAL SETTINGS")
fillEnabled            = input.bool(true, "Enable Fill", group="VISUAL SETTINGS")
fillTransparency       = input.int(70, "Fill Transparency", group="VISUAL SETTINGS")
bandTransparency       = input.int(50, "Band Transparency", group="VISUAL SETTINGS")

// SMOOTH FUNCTION
smoothFunc(src, len, type) =>
    switch type
        'EMA' => ta.ema(src, len)
        'SMA' => ta.sma(src, len)
        'RMA' => ta.rma(src, len)
        'WMA' => ta.wma(src, len)

// SHARED CALCULATIONS
body = math.abs(open - close)
bodyMid = (open + close) / 2

// SWIFT TREND ENGINE FOR OSCILLATOR
avgBody_osc = ta.sma(body, period)
basicUpper_osc = bodyMid[1] + bandMultiplier * avgBody_osc[1]
basicLower_osc = bodyMid[1] - bandMultiplier * avgBody_osc[1]
tolerance_osc = toleranceMultiplier * avgBody_osc

var bool isUptrend_osc = true
var float avgLine_osc = na

if na(avgLine_osc[1])
    avgLine_osc := basicLower_osc
    isUptrend_osc := true
else
    avgLine_osc := isUptrend_osc ? math.max(basicLower_osc, nz(avgLine_osc[1])) : math.min(basicUpper_osc, nz(avgLine_osc[1]))

marginLineBase_osc = isUptrend_osc ? avgLine_osc - tolerance_osc : avgLine_osc + tolerance_osc

if isUptrend_osc and close < marginLineBase_osc
    isUptrend_osc := false
else if not isUptrend_osc and close > marginLineBase_osc
    isUptrend_osc := true

// SWIFT TREND ENGINE FOR HIGHER TREND
avgBody_trend = ta.sma(body, period_trend)
basicUpper_trend = bodyMid[1] + bandMultiplier_trend * avgBody_trend[1]
basicLower_trend = bodyMid[1] - bandMultiplier_trend * avgBody_trend[1]
tolerance_trend = toleranceMultiplier_trend * avgBody_trend

var bool isUptrend_trend = true
var float avgLine_trend = na
prevTrend = isUptrend_trend

if na(avgLine_trend[1])
    avgLine_trend := basicLower_trend
else
    avgLine_trend := isUptrend_trend ? math.max(basicLower_trend, nz(avgLine_trend[1])) : math.min(basicUpper_trend, nz(avgLine_trend[1]))

marginLineBase_trend = isUptrend_trend ? avgLine_trend - tolerance_trend : avgLine_trend + tolerance_trend

if isUptrend_trend and close < marginLineBase_trend
    isUptrend_trend := false
else if not isUptrend_trend and close > marginLineBase_trend
    isUptrend_trend := true

trendChange = isUptrend_trend != prevTrend

// OSCILLATOR
osc_raw = close - marginLineBase_osc
scale = math.max(avgBody_osc * 1.5, syminfo.mintick * 10)
osc = osc_raw / scale * 25
oscSmooth = smoothFunc(osc, smoothLen, smoothType)

// COLORS
lineColor = oscSmooth > 0 ? bullColor : bearColor
obTrendColor = isUptrend_trend ? bullColor : bearColor

// LEVELS
plot(obLevel, 'OB', color = color.new(obTrendColor, bandTransparency), linewidth = 1, style = plot.style_linebr)
hline(osLevel, 'OS', osColor, hline.style_dashed)
hline(0, 'MID', midColor, hline.style_dashed)

// GRADIENTS
plot(showOverboughtGradient ? obLevel : na, '', color.new(obTrendColor, bandTransparency), 8)
plot(showOversoldGradient ? osLevel : na, '', color.new(osColor, bandTransparency), 8)

// OSC PLOT
pOsc = plot(oscSmooth, 'OSC', lineColor, 2)
plot(showOscillatorGradient ? oscSmooth : na, '', color.new(lineColor, 85), 10)

// FILL
p1 = plot(oscSmooth * 0.8, color = na, display = display.none)
p2 = plot(oscSmooth * 0.6, color = na, display = display.none)
p3 = plot(oscSmooth * 0.4, color = na, display = display.none)
p4 = plot(oscSmooth * 0.2, color = na, display = display.none)
p5 = plot(oscSmooth * 0.0, color = na, display = display.none)

fill(pOsc, p1, fillEnabled ? color.new(lineColor, fillTransparency) : na)
fill(p1, p2, fillEnabled ? color.new(lineColor, fillTransparency + 5) : na)
fill(p2, p3, fillEnabled ? color.new(lineColor, fillTransparency + 10) : na)
fill(p3, p4, fillEnabled ? color.new(lineColor, fillTransparency + 15) : na)
fill(p4, p5, fillEnabled ? color.new(lineColor, fillTransparency + 20) : na)

// OSCILLATOR SIGNALS
var bool longGivenInWave = false
var bool shortGivenInWave = false
var color prevOscColor = na

if not na(prevOscColor) and prevOscColor != lineColor
    longGivenInWave := false
    shortGivenInWave := false

prevOscColor := lineColor

longSignal = ta.crossover(oscSmooth, osLevel) and not longGivenInWave
shortSignal = ta.crossunder(oscSmooth, obLevel) and not shortGivenInWave

if longSignal
    longGivenInWave := true
if shortSignal
    shortGivenInWave := true

plotshape(longSignal, location = location.bottom, style = shape.triangleup, color = bullColor, size = size.tiny)
plotshape(shortSignal, location = location.top, style = shape.triangledown, color = bearColor, size = size.tiny)

// ZERO LINE SIGNALS
var bool waitForRedAfterZero = false
var bool waitForGreenAfterZero = false

isGreenCandle = close > open
isRedCandle = close < open

if ta.crossunder(oscSmooth, 0)
    waitForRedAfterZero := true
    waitForGreenAfterZero := false

if ta.crossover(oscSmooth, 0)
    waitForGreenAfterZero := true
    waitForRedAfterZero := false

zeroShortSignal = waitForRedAfterZero and isRedCandle and oscSmooth < 0
zeroLongSignal = waitForGreenAfterZero and isGreenCandle and oscSmooth > 0

if zeroShortSignal
    waitForRedAfterZero := false
if zeroLongSignal
    waitForGreenAfterZero := false

plotshape(zeroShortSignal, style = shape.circle, location = location.top, color = bearColor, size = size.tiny)
plotshape(zeroLongSignal, style = shape.circle, location = location.bottom, color = bullColor, size = size.tiny)

// CANDLE COLORING
trendDistance = math.abs(close - marginLineBase_trend)
maxDistance = ta.highest(trendDistance, period_trend)
strength = maxDistance != 0 ? trendDistance / maxDistance : 0

lightBull = color.from_gradient(strength, 0, 1, color.new(bullColor, 40), color.new(bullColor, 0))
lightBear = color.from_gradient(strength, 0, 1, color.new(bearColor, 40), color.new(bearColor, 0))

candleColor = isUptrend_trend ? lightBull : lightBear

plotcandle(open, high, low, close, title = 'SwiftTrend Candles', color = enableCandleColoring ? candleColor : na, wickcolor = enableCandleColoring ? candleColor : na, bordercolor = enableCandleColoring ? candleColor : na, force_overlay = true)

// ALERTS
alertcondition(longSignal, "Long Signal", "Oscillator Long Signal")
alertcondition(shortSignal, "Short Signal", "Oscillator Short Signal")
alertcondition(zeroLongSignal, "Zero Line Long", "Zero Line Long Signal")
alertcondition(zeroShortSignal, "Zero Line Short", "Zero Line Short Signal")
alertcondition(trendChange, "Higher Trend Change", "Higher Timeframe Trend Direction Changed")

// Â© OmegaTools

//@version=6
strategy("Retracement Strategy [OmegaTools]", overlay = true)

tp = input.string('Extension', 'Take Profit', ['None', 'Trend Change', 'Extension'])
sl = input.string('Extension', 'Stop Loss', ['None', 'Trend Change', 'Extension'])
lnt = input.int(10, 'Length')
obs = input.int(70, 'Threshold')

rsi = ta.sma(ta.rsi(close, lnt), 2)
var trend = 0
var lasttrend = 0
if ta.crossover(rsi, 100 - obs)
    trend := 1
    lasttrend := 1
if ta.crossunder(rsi, obs)
    trend := -1
    lasttrend := -1
else if rsi > obs or rsi < 100 - obs
    trend := 0

secondcol = trend > 0 ? #2962ff : trend < 0 ? #e91e63 : chart.fg_color
maincol = trend > 0 ? (close < open ? na : color.new(#2962ff, 75)) : trend < 0 ? (close > open ? na : color.new(#e91e63, 75)) : (close > open ? chart.fg_color : na)
ll = ta.lowest(low[1], lnt - 1)
hh = ta.highest(high[1], lnt - 1)
long = trend > 0 and low < ll
short = trend < 0 and high > hh
plotcandle(open, high, low, close, 'Trend Candles', maincol, secondcol, bordercolor = secondcol)
atr = ta.sma(high - low, 34) / 8
var last = 0
if long and last <= 0
    label.new(bar_index, low - atr, 'UP', xloc = xloc.bar_index, color = #2962ff, style = label.style_label_up, textcolor = chart.bg_color)
    last := 1
    strategy.entry('B', strategy.long)
if short and last >= 0
    label.new(bar_index, high + atr, 'DN', xloc = xloc.bar_index, color = #e91e63, textcolor = chart.bg_color)
    last := -1
    strategy.entry('S', strategy.short)

longprofit = strategy.position_size > 0 and close > strategy.position_avg_price
shortprofit = strategy.position_size < 0  and close < strategy.position_avg_price
if tp == 'Trend Change'
    if longprofit and trend == -1 and lasttrend[1] == 1
        strategy.close('B')
    if shortprofit and trend == 1 and lasttrend[1] == -1
        strategy.close('S')
if tp == 'Extension'
    if longprofit and rsi > obs
        strategy.close('B')
    if shortprofit and rsi < 100 - obs
        strategy.close('S')

if sl == 'Trend Change'
    if trend == -1 and lasttrend[1] == 1
        strategy.close('B')
    if trend == 1 and lasttrend[1] == -1
        strategy.close('S')
if sl == 'Extension'
    if rsi < 100 - obs
        strategy.close('B')
    if rsi > obs
        strategy.close('S')

// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © tejaspanarafire

//@version=6
indicator("ADX FAST and NOICE FREE DI", overlay=false, max_lines_count=500, max_labels_count=500)

// === Inputs
adxLength = input.int(14, "ADX Length")
threshold = input.float(20.0, "Trend Threshold")
slopeMultiplier = input.float(10.0, "Slope Projection Multiplier") 
hullLength = input.int(16, "Length")
highlightTrend = input.bool(true, "Highlight Trend Zones")
showDI = input.bool(true, "Show Noice-Free +DI/-DI")

// === TR & DM
upMove = high - high[1]
downMove = low[1] - low
plusDM = (upMove > downMove and upMove > 0) ? upMove : 0.0
minusDM = (downMove > upMove and downMove > 0) ? downMove : 0.0

tr1 = high - low
tr2 = math.abs(high - close[1])
tr3 = math.abs(low - close[1])
trueRange = math.max(tr1, math.max(tr2, tr3))

smoothTR = ta.rma(trueRange, adxLength)
smoothPlusDM = ta.rma(plusDM, adxLength)
smoothMinusDM = ta.rma(minusDM, adxLength)

plusDI = 100 * (smoothPlusDM / smoothTR)
minusDI = 100 * (smoothMinusDM / smoothTR)
dx = 100 * math.abs(plusDI - minusDI) / (plusDI + minusDI)
adx = ta.rma(dx, adxLength)

// === Fast ADX Projection
adxSlope = adx - adx[1]
adxProjected = adx + adxSlope * slopeMultiplier
fastADX = math.max(adx, adxProjected)

// === Hull Smoothed ADX
wma1 = ta.wma(fastADX, hullLength)
wma2 = ta.wma(fastADX, math.round(hullLength / 2))
hullADX = ta.wma(2 * wma2 - wma1, math.round(math.sqrt(hullLength)))

// === Hull +DI
wma1_p = ta.wma(plusDI, hullLength)
wma2_p = ta.wma(plusDI, math.round(hullLength / 2))
hullPlusDI = ta.wma(2 * wma2_p - wma1_p, math.round(math.sqrt(hullLength)))

// === Hull -DI
wma1_m = ta.wma(minusDI, hullLength)
wma2_m = ta.wma(minusDI, math.round(hullLength / 2))
hullMinusDI = ta.wma(2 * wma2_m - wma1_m, math.round(math.sqrt(hullLength)))

// === Trend Logic
upTrend = hullADX > threshold and hullPlusDI > hullMinusDI
downTrend = hullADX > threshold and hullMinusDI > hullPlusDI

// === Plotting
plot(adx, title="Original ADX", color=color.new(color.gray, 0))
plot(hullADX, title="Fast ADX", color=color.new(color.green, 0), linewidth=2)
hline(threshold, "Trend Threshold", color=color.new(color.red, 0))

plot(showDI ? hullPlusDI : na, title="Noice-Free +DI", color=color.new(color.blue, 0))
plot(showDI ? hullMinusDI : na, title="Noice-Free -DI", color=color.new(color.orange, 0))

bgColor = highlightTrend ? (upTrend ? color.new(color.green, 85) : downTrend ? color.new(color.red, 85) : na) : na
bgcolor(bgColor)

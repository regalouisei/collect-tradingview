// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © BackQuant
// Aggregated OI logic adapted from "Crypto OI Agregated" by NoveltyTrade, © NoveltyTrade
// https://www.tradingview.com/script/1z6RXBA9-crypto-oi-agregated/

import TradingView/ta/11 as ta 

//@version=6
indicator("Aggregated Open Interest Z-Score [BackQuant]","Agg. OI Z [BackQuant]",overlay=false)

// Groups
const string grpZS  = "Z-Score"
const string grpMA  = "Moving Average"
const string grpThr = "Thresholds & Reference Lines"
const string grpUI  = "Extra Plotting and UI"
const string grpDiv = "Divergences"
const string grpOI  = "Open Interest Source"

// Inputs

// Z-Score
string plotting = input.string("Oscillator","Plotting Type",options=["None","Line","Colored Line","Oscillator"],group=grpZS,tooltip="Select how to render the OI Z-Score on the panel.")
int z_len = input.int(100,"Z-Score Lookback Period",group=grpZS,tooltip="Lookback period used to compute mean and standard deviation of aggregated OI.")
string maType = input.string("EMA", "Smoothing Type",options = ["SMA", "HMA", "EMA", "WMA", "DEMA", "RMA", "LINREG", "ALMA", "TEMA", "T3"], group =grpZS,inline="smooth")
simple int smooth_len = input.int(5, "Smoothing Period", group = grpZS, inline="smooth")

// Moving Average
bool show_ma = input.bool(true,"Show EMA",group=grpMA,tooltip="Toggle the EMA overlay on the OI Z-Score.",inline="ema")
int ma_p = input.int(21,"EMA Period",group=grpMA,tooltip="EMA length applied to the OI Z-Score.",inline="ema")
color ma_col = input.color(#ffffff,"EMA Color",group=grpMA,inline="ema")

// Thresholds & Reference Lines
string show_refs = input.string("Extreme Bands","Select Reference Lines",options=["None","Standard Deviation Levels","Extreme Bands"],group=grpThr,tooltip="Control whether to show standard deviation levels or extreme bands.")

// Extra Plotting and UI
color zs_col = input.color(#ffffff,"Base Line Color",group=grpUI)
int lw = input.int(1,"Line Width",minval=1,maxval=5,group=grpUI)
color long_col = input.color(#00ff00,"Positive Color",group=grpUI)
color short_col = input.color(#ff0000,"Negative Color",group=grpUI)

// Divergences
bool showdivergences = input.bool(true,"Show Detected Divergences?",group=grpDiv)
int lbR = input.int(30,"Pivot Lookback Right",group=grpDiv,inline="div")
int lbL = input.int(30,"Pivot Lookback Left",group=grpDiv,inline="div")

// Open Interest Source
string typ2 = input.string("COIN","OI Units",options=["USD","COIN"],group=grpOI,tooltip="Choose whether to aggregate OI in coin terms or converted to USD.")
bool ex1 = input.bool(true,"Binance",group=grpOI,inline="cexs")
bool ex2 = input.bool(true,"Bybit",group=grpOI,inline="cexs")
bool ex3 = input.bool(true,"OKX",group=grpOI,inline="cexs")
bool ex5 = input.bool(true,"Bitget",group=grpOI,inline="cexs")
bool ex6 = input.bool(true,"Kraken",group=grpOI,inline="cexs")
bool ex7 = input.bool(true,"HTX",group=grpOI,inline="cexs")
bool ex8 = input.bool(true,"Deribit",group=grpOI,inline="cexs")

// OI aggregation
type OI
    float o
    float h
    float l
    float c

const string[] exchanges = array.from(
 ex1 ? "Binance" : na,
 ex2 ? "Bybit"   : na,
 ex3 ? "OKX"     : na,
 ex5 ? "Bitget"  : na,
 ex6 ? "Kraken"  : na,
 ex7 ? "HTX"     : na,
 ex8 ? "Deribit" : na
 )

const string[] appendixes = array.from("USDT.P","USD.P","USDC.P","USD.PM")
const int bars = 10000

method sw(float _v,string _ex,string _app) =>
    float _tmp = (
     (_app == "USD.P" and (_ex == "Binance" or _ex == "Bybit" or _ex == "Kraken")) or
     _ex == "OKX" or _ex == "Deribit"
     ) ? _v * (_ex == "Binance" ? (syminfo.basecurrency == "BTC" ? 100 : 10) : 1) / close : _v
    typ2 == "USD" ? _tmp * close : _tmp

OI candle = OI.new(0.0,0.0,0.0,0.0)

for [i, ex] in exchanges
    if not na(ex)
        for [j, app] in appendixes
            OI oi_ex = request.security(ex + ":" + syminfo.basecurrency + app + "_OI","",OI.new(open,high,low,close),ignore_invalid_symbol=true,calc_bars_count=bars)
            if not na(oi_ex)
                float o_conv = oi_ex.o.sw(ex,app)
                float h_conv = oi_ex.h.sw(ex,app)
                float l_conv = oi_ex.l.sw(ex,app)
                float c_conv = oi_ex.c.sw(ex,app)
                candle.o += o_conv
                candle.h += h_conv
                candle.l += l_conv
                candle.c += c_conv

float oiClose = candle.c

if barstate.islastconfirmedhistory and na(oiClose)
    runtime.error(str.format("No aggregated Open Interest data for `{0}`.",syminfo.prefix + ":" + syminfo.ticker))

// Helper MA Func
ma(src, len, type) =>
    switch type
        "SMA" => ta.sma(src, len)
        "HMA" => ta.hma(src, len)
        "EMA" => ta.ema(src, len)
        "WMA" => ta.wma(src, len)
        "DEMA" => ta.dema(src, len)
        "RMA" => ta.rma(src, len)
        "LINREG"=> ta.linreg(src, len, 0)
        "TEMA" => ta.tema(src, len)
        "ALMA" => ta.alma(src, len, 0, 6)
        "T3" => ta.t3(src,len, 0.7)

// Z-Score calculations on aggregated OI
float subject = oiClose
float mean = ta.ema(subject,z_len)
float stdDev = ta.stdev(subject,z_len)
float oi_z = ma((subject - mean) / stdDev, smooth_len, maType)
float ma = ta.ema(oi_z,ma_p)

// Mode flags
bool isLine = plotting == "Line"
bool isColLine = plotting == "Colored Line"
bool isOsc = plotting == "Oscillator"
bool show_ma_ = show_ma and (isLine or isColLine or isOsc)

// Conditional color (same logic as LSMA Z-Score)
bool posroc = oi_z > oi_z[1]
bool negroc = oi_z < oi_z[1]
color col = zs_col
if oi_z > 0 and posroc
    col := #00ff08
if oi_z > 0 and negroc
    col := #00bbd4
if oi_z < 0 and negroc
    col := #ff0000
if oi_z < 0 and posroc
    col := #771515

// Base plots by plotting type
plot(isLine ? oi_z : na,"OI Z-Score Line",color=zs_col,linewidth=lw)
plot(isColLine ? oi_z : na,"OI Z-Score Colored Line",color=col,linewidth=lw)
plot(isOsc ? oi_z : na,"OI Z-Score Oscillator",style=plot.style_columns,color=col,histbase=0)
plot(show_ma_ ? ma : na,"OI Z-Score EMA",color=ma_col,linewidth=1)

// Reference lines: standard deviation levels
plotchar(show_refs == "Standard Deviation Levels" ? 0 : na,"0","•",location.absolute,size=size.tiny,color=#8080804d)
plot(show_refs == "Standard Deviation Levels" or show_refs == "Extreme Bands" ? 1 : na,"1",#8080804d,style=plot.style_linebr)
plot(show_refs == "Standard Deviation Levels" or show_refs == "Extreme Bands" ? 2 : na,"2",#8080804d,style=plot.style_linebr)
plot(show_refs == "Standard Deviation Levels" or show_refs == "Extreme Bands" ? -1 : na,"-1",#8080804d,style=plot.style_linebr)
plot(show_refs == "Standard Deviation Levels" or show_refs == "Extreme Bands" ? -2 : na,"-2",#8080804d,style=plot.style_linebr)

// Adaptive Trend Strength and extreme bands (reference line method)
float trendStrength = math.abs(oi_z)
float maxStrength = 4.5
float normalizedStrength = math.min(trendStrength / maxStrength * 100.0,100.0)
int alphaValue = int(100.0 - normalizedStrength)

plot(show_refs == "Extreme Bands" ? 1 : na,"1",#8080804d,style=plot.style_linebr)
plot(show_refs == "Extreme Bands" ? 2 : na,"2",#8080804d,style=plot.style_linebr)
plot(show_refs == "Extreme Bands" ? -1 : na,"-1",#8080804d,style=plot.style_linebr)
plot(show_refs == "Extreme Bands" ? -2 : na,"-2",#8080804d,style=plot.style_linebr)

// Upper Extreme Levels
u1 = plot(show_refs == "Extreme Bands" ? 4.0 : na,"Upper",color=#787b864d,editable=false)
u2 = plot(show_refs == "Extreme Bands" ? 3.0 : na,"Upper Inner",color=#787b864d,editable=false)
fill(u1,u2,show_refs == "Extreme Bands" ? (oi_z >= 0.0 ? color.new(color.rgb(255,0,0),alphaValue) : color.rgb(255,255,255,100)) : color.new(color.white,100),"Extreme Upper")

// Lower Extreme Levels
l1 = plot(show_refs == "Extreme Bands" ? -4.0 : na,"Lower",color=#787b864d,editable=false)
l2 = plot(show_refs == "Extreme Bands" ? -3.0 : na,"Lower Inner",color=#787b864d,editable=false)
fill(l1,l2,show_refs == "Extreme Bands" ? (oi_z < 0.0 ? color.new(color.rgb(0,255,0),alphaValue) : color.rgb(255,255,255,100)) : color.new(color.white,100),"Extreme Lower")


// Divergences (on OI Z-Score)
color obcol = #ff0000fc
color oscol = #00ff00fc
color bearColor = obcol
color bullColor = oscol
color hiddenBullColor = color.new(oscol,50)
color hiddenBearColor = color.new(obcol,50)
color textColor = color.white
color noneColor = color.new(color.white,100)

bool plFound = not na(ta.pivotlow(oi_z,lbL,lbR))
bool phFound = not na(ta.pivothigh(oi_z,lbL,lbR))

_inRange(bool cond) =>
    int barsBack = ta.barssince(cond)
    -80 <= barsBack and barsBack <= 80

// Regular Bullish: price LL, osc HL
bool oscHL = oi_z[lbR] > ta.valuewhen(plFound,oi_z[lbR],1) and _inRange(plFound[1])
bool priceLL = low[lbR] < ta.valuewhen(plFound,low[lbR],1)
bool bullCond = showdivergences and priceLL and oscHL and plFound

plot(plFound ? oi_z[lbR] : na,offset=-lbR,title="Regular Bullish",linewidth=2,color=bullCond ? bullColor : noneColor)
plotshape(bullCond ? oi_z[lbR] : na,offset=-lbR,title="Regular Bullish Label",text="ℝ",style=shape.labelup,location=location.absolute,color=bullColor,textcolor=textColor)

// Hidden Bullish: price HL, osc LL
bool oscLL = oi_z[lbR] < ta.valuewhen(plFound,oi_z[lbR],1) and _inRange(plFound[1])
bool priceHL = low[lbR] > ta.valuewhen(plFound,low[lbR],1)
bool hiddenBullCond = showdivergences and priceHL and oscLL and plFound

plot(plFound ? oi_z[lbR] : na,offset=-lbR,title="Hidden Bullish",linewidth=2,color=hiddenBullCond ? hiddenBullColor : noneColor)
plotshape(hiddenBullCond ? oi_z[lbR] : na,offset=-lbR,title="Hidden Bullish Label",text="ℍ",style=shape.labelup,location=location.absolute,color=bullColor,textcolor=textColor)

// Regular Bearish: price HH, osc LH
bool oscLH = oi_z[lbR] < ta.valuewhen(phFound,oi_z[lbR],1) and _inRange(phFound[1])
bool priceHH = high[lbR] > ta.valuewhen(phFound,high[lbR],1)
bool bearCond = showdivergences and priceHH and oscLH and phFound

plot(phFound ? oi_z[lbR] : na,offset=-lbR,title="Regular Bearish",linewidth=2,color=bearCond ? bearColor : noneColor)
plotshape(bearCond ? oi_z[lbR] : na,offset=-lbR,title="Regular Bearish Label",text="ℝ",style=shape.labeldown,location=location.absolute,color=bearColor,textcolor=textColor)

// Hidden Bearish: price LH, osc HH
bool oscHH = oi_z[lbR] > ta.valuewhen(phFound,oi_z[lbR],1) and _inRange(phFound[1])
bool priceLH = high[lbR] < ta.valuewhen(phFound,high[lbR],1)
bool hiddenBearCond = showdivergences and priceLH and oscHH and phFound

plot(phFound ? oi_z[lbR] : na,offset=-lbR,title="Hidden Bearish",linewidth=2,color=hiddenBearCond ? hiddenBearColor : noneColor)
plotshape(hiddenBearCond ? oi_z[lbR] : na,offset=-lbR,title="Hidden Bearish Label",text="ℍ",style=shape.labeldown,location=location.absolute,color=bearColor,textcolor=textColor)


// Alerts
alertcondition(ta.crossover(oi_z,0.0),"OI Z-Score Positive","Aggregated OI Z-Score crossed above 0 on {{exchange}}:{{ticker}}")
alertcondition(ta.crossunder(oi_z,0.0),"OI Z-Score Negative","Aggregated OI Z-Score crossed below 0 on {{exchange}}:{{ticker}}")
alertcondition(ta.crossover(oi_z,2.0),"OI Z-Score Enters +2σ","Aggregated OI Z-Score entered +2σ on {{exchange}}:{{ticker}}")
alertcondition(ta.crossunder(oi_z,-2.0),"OI Z-Score Enters -2σ","Aggregated OI Z-Score entered -2σ on {{exchange}}:{{ticker}}")

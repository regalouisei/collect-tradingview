// Free for everyone, free for use, even without reference to original
// @IkkeOmar
//@version=6
indicator('KAMA Oscillator | IkkeOmar', timeframe = '', timeframe_gaps = true)

// Source
src = input.source(close, 'Source', group = 'Source')

// Input parameters
fast_period = input.int(title = 'Fast Period', defval = 14, minval = 1)
slow_period = input.int(title = 'Slow Period', defval = 28, minval = 1)
er_period = input.int(title = 'Efficiency Ratio Period', defval = 8, minval = 1)
norm_period = input.int(title = 'Normalization lookback', defval = 50, minval = 1, group = 'Normalized Settings')

norm = input.bool(defval = false, title = 'Use normalization', group = 'Normalized Settings')

// Signal Line / SMA / RSI Toggle
sig_type = input.string('None', 'Signal Line Type', options = ['SMA', 'RSI', 'None'], group = 'Signal Settings')
sig_period = input.int(28, 'Signal Period', minval = 1, group = 'Signal Settings')

col_up = input.color(#22ab94, 'Up Color')
col_dn = input.color(#f7525f, 'Down Color')

// Calculate the efficiency ratio
change = math.abs(src - src[er_period])
volatility = math.sum(math.abs(src - src[1]), er_period)
er = volatility != 0 ? change / volatility : 0

// Calculate the smoothing constant
sc = er * (2 / (fast_period + 1) - 2 / (slow_period + 1)) + 2 / (slow_period + 1)

// Calculate the KAMA
kama = ta.ema(src, fast_period) + sc * (src - ta.ema(src, fast_period))

// Normalize the oscillator (-1 to +1)
lowest = ta.lowest(kama, norm_period)
highest = ta.highest(kama, norm_period)
range_val = highest - lowest
normalized = range_val != 0 ? (kama - lowest) / range_val * 2 - 1 : 0

// Select data to process for Signal Line
plot_data = norm ? normalized : kama

// Calculate Signal Line (SMA or RSI)
var float sig_line = na

if sig_type == 'SMA'
    sig_line := ta.sma(plot_data, sig_period)
    sig_line
else if sig_type == 'RSI'
    // RSI scaled to 0 to 1. (RSI / 100)
    sig_line := ta.rsi(plot_data, sig_period) / 100
    sig_line
else
    sig_line := na
    sig_line

// COLORING //----------------------------------------------------------------------------------------

// Normalized Coloring (Based on Zero Line)
col_wn = normalized < 0 ? normalized < normalized[1] ? color.new(col_dn, 50) : color.new(col_dn, 75) : normalized > normalized[1] ? color.new(col_up, 50) : color.new(col_up, 75)

// Non-Normalized Coloring (Based on Slope)
col_wnkama = kama < kama[1] ? color.new(col_dn, 50) : color.new(col_up, 50)

// Determine final color based on mode
final_col = norm ? col_wn : col_wnkama

// PLOTTING //----------------------------------------------------------------------------------------

plot(plot_data, style = plot.style_columns, color = final_col, title = 'KAMA Oscillator')
plot(sig_line, style = plot.style_line, color = color.white, title = 'Signal Line')

// Zero line (Only active if Normalized or RSI mode is on)
hline(norm or sig_type == 'RSI' ? 0 : na, 'Zero Line', color = color.gray, linestyle = hline.style_dotted)

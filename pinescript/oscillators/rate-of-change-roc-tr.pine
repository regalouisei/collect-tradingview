// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Tiagorocha1989
//                              ████████╗ ████████╗  ████████╗  ███████╗   ██████╗        ██████╗   ██████╗   ██████╗ ██╗   ██╗ ████████╗
//                              ╚══██╔══╝ ╚══██╔══╝  ██╔═══██║  ██╔════╝  ██╔═══██╗       ██╔══██╗ ██╔═══██╗ ██╔════╝ ██║   ██║ ██╔═══██║
//                                 ██║       ██║     ████████║  ██║ ███║  ██║   ██║       ██████╔╝ ██║   ██║ ██║      ████████║ ████████║
//                                 ██║       ██║     ██╔═══██║  ██║  ██║  ██║   ██║       ██╔══██╗ ██║   ██║ ██║      ██║   ██║ ██╔═══██║
//                                 ██║    ████████╗  ██║   ██║  ███████║  ╚██████╔╝       ██║  ██║ ╚██████╔╝ ╚██████╗ ██║   ██║ ██║   ██║
//@version=6
indicator('Rate of Change - ROC | TR', 'ROC | TR →', false)
//╔════════════════════════╗
//║ Choose Moving Averages ║
//╚════════════════════════╝
ma(source, length, type) =>
     type == 'EMA' ? ta.ema(source, length) :
     type == 'SMA' ? ta.sma(source, length) :
     type == 'RMA' ? ta.rma(source, length) :
     type == 'WMA' ? ta.wma(source, length) :
     type == 'VWMA' ? ta.vwma(source, length) :
     type == 'HMA' ? ta.hma(source, length) :
     na
//╔════════════════════════╗
//║     INPUT SETTINGS     ║
//╚════════════════════════╝
Color_Mode =        input.string('Classic', 'Color Choice', group = 'Color', options = ['Classic', 'Modern', 'Robust', 'Accented', 'Monochrome', 'Moderate', 'Aqua', 'Cosmic'])
Length_ROC =        input.int(35, title = 'Length ROC:', group='ROC Setting', inline = 'ROC')
Source_ROC =        input(close, title = '', group='ROC Setting', inline = 'ROC')
Signal_ROC =        input.string('0', 'Entry/Exit Signal', group='ROC Setting', options = ['0', 'ROC MA'],tooltip = 'Choose Entry/Exit Point')
Length_MA_ROC =     input.int(365, title = 'Length MA', group='ROC MA', inline = 'ROC MA')
Source_MA_ROC =     input.string('EMA', '', group='ROC MA', inline = 'ROC MA', options=['EMA', 'SMA', 'RMA', 'WMA', 'VWMA', 'HMA'],tooltip = 'Moving Averages')
//╔════════════════════════╗
//║      Calculation       ║
//╚════════════════════════╝
ROC =     ta.roc(Source_ROC,Length_ROC)
ROC_MA =  ma(ROC, Length_MA_ROC, Source_MA_ROC)
float Entry_ROC = Signal_ROC == 'ROC MA' ? ROC_MA : 0.0
isBull_ROC = ROC > Entry_ROC
isBear_ROC = ROC < Entry_ROC
Bull_ROC = ta.crossover(ROC, Entry_ROC)
Bear_ROC = ta.crossunder(ROC, Entry_ROC)
//╔════════════════════════╗
//║         Color          ║
//╚════════════════════════╝
[UpC, DnC] = switch Color_Mode
    'Classic' =>    [#008800, #ff0000]
    'Modern' =>     [#ffffff, #b721ff]
    'Robust' =>     [#ffbb00, #770737]
    'Accented' =>   [#8c5cf7, #e83e8c]
    'Monochrome' => [#e9ecef, #495057]
    'Moderate' =>   [#43a047, #e53935]
    'Aqua' =>       [#00a8e8, #f18f01]
    'Cosmic' =>     [#e83e8c, #6f2da8]
//╔════════════════════════╗
//║          Plot          ║
//╚════════════════════════╝
Plot_ROC =     plot(ROC, color = ROC > Entry_ROC ? color.new(UpC, 50):color.new(DnC, 50), linewidth = 3)
Plot_Entry_ROC =  plot(Entry_ROC,color = color.yellow)
fill(Plot_ROC, Plot_Entry_ROC, 40, Entry_ROC, top_color = color.new(UpC, 0),    bottom_color = color.new(UpC, 100), title = 'Overbought Fill')
fill(Plot_ROC, Plot_Entry_ROC, Entry_ROC, -40, top_color = color.new(DnC, 100), bottom_color = color.new(DnC, 0), title = 'Oversold Fill')
fill(Plot_ROC, Plot_Entry_ROC, ROC < Entry_ROC ? ROC : Entry_ROC, ROC < Entry_ROC ? Entry_ROC : ROC, ROC < Entry_ROC ? DnC : #00000000, ROC < Entry_ROC ? #00000000 : UpC)
plotcandle(open, high, low, close, "Bar Color", isBull_ROC ? UpC : isBear_ROC ? DnC : na, isBull_ROC ? UpC : isBear_ROC ? DnC : na, bordercolor = isBull_ROC ? UpC : isBear_ROC ? DnC : na, force_overlay = true)
plotshape(Bull_ROC, "LONG" , shape.triangleup  , location.belowbar, UpC,    0, size = size.small, force_overlay = true)
plotshape(Bear_ROC, "SHORT", shape.triangledown, location.abovebar, DnC,    0, size = size.small, force_overlay = true)
//╔════════════════════════╗
//║          VIEW          ║
//╚════════════════════════╝
var table Table_ROC = table.new(position.middle_right, 1, 1, border_width = 1)
var label ROCLabel = na
if barstate.islast
    table.cell(Table_ROC, 0, 0, text = isBull_ROC ? '⬆️ Bullish' : '⬇️ Bearish', text_color = isBull_ROC ? UpC : DnC,text_size = size.huge)
    if not na(ROCLabel)
        label.delete(ROCLabel)
    ROCLabel := label.new(bar_index + 5, ROC, text =  str.tostring(ROC, '#.##'), color = ROC > Entry_ROC ? color.new(UpC, 20) : ROC < Entry_ROC ? color.new(DnC, 20) : color.gray, style = label.style_label_left, size = size.large, textcolor = #000000)
//╔════════════════════════╗
//║         Alerts         ║
//╚════════════════════════╝
alertcondition(Bull_ROC, title= 'LONG', message= '{{ticker}} LONG')
alertcondition(Bear_ROC, title= 'SHORT', message= '{{ticker}} SHORT')

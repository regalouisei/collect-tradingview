// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © audrius_exe

//@version=6
indicator(title = "Buying/Selling Pressure", shorttitle = "Buying/Selling Pressure", overlay = false)

calculation_method = input.string("Money Flow", title="Calculation Method",
     options=["Simple Delta", "Money Flow", "Weighted Delta"],
     group = "Settings",
     tooltip="Simple Delta: volume based on close vs open\nMoney Flow: volume weighted by position in range\nWeighted Delta: volume weighted by body size")

ema_length = input.int(21, title="EMA Length", minval=1, group="Settings")

show_signal = input.bool(false, title="Show Signal Line", group="Display")
signal_length = input.int(9, title="Signal Length", minval=1, group="Display")

color COLOR_BUY = #26A69A
color COLOR_SELL = #FF5252

volumeATR(length) =>
    volChange = math.abs(volume - volume[1])
    ta.rma(volChange, length)

volATR = volumeATR(ema_length)

get_delta_volume() =>
    float buying_vol = 0.0
    float selling_vol = 0.0

    if calculation_method == "Simple Delta"
        // Method 1: Simple Delta
        buying_vol := close > open ? volume : 0
        selling_vol := close < open ? volume : 0

    else if calculation_method == "Money Flow"
        // Method 2: Money Flow
        bar_range = high - low
        if bar_range > 0
            mf_multiplier = ((close - low) - (high - close)) / bar_range
            if mf_multiplier > 0
                buying_vol := volume * mf_multiplier
                selling_vol := 0
            else
                buying_vol := 0
                selling_vol := volume * math.abs(mf_multiplier)

    else if calculation_method == "Weighted Delta"
        // Method 3: Weighted Delta
        bar_range = high - low
        body = math.abs(close - open)
        if bar_range > 0
            weight = body / bar_range
            if close > open
                buying_vol := volume * weight
            else if close < open
                selling_vol := volume * weight

    [buying_vol, selling_vol]

// Calculate delta
[buying_volume, selling_volume] = get_delta_volume()
delta = buying_volume - selling_volume

// Cumulative delta 
cumulative_delta = ta.cum(delta)

// Pressure oscillator normalized by Volume ATR
pressure_osc = ((cumulative_delta - ta.ema(cumulative_delta, ema_length)) / volATR) * 100.0

// Signal line
signal_line = show_signal ? ta.ema(pressure_osc, signal_length) : na

// Main oscillator color
osc_color = pressure_osc > 0 ? COLOR_BUY : COLOR_SELL

// Plot oscillator
plot(pressure_osc, color=osc_color, title="Pressure Oscillator", linewidth=2, precision=2)
plot(pressure_osc, color=color.new(osc_color, 85), title="Pressure Area", precision=2, style=plot.style_area)

// Plot signal line
plot(signal_line, color=color.orange, title="Signal Line", linewidth=1, precision=2)

// Zero line
hline(0, title="Zero Line", color=color.gray, linestyle=hline.style_dashed, linewidth=1)

// Fill between oscillator and signal
fill_color = pressure_osc > signal_line ? color.new(COLOR_BUY, 85) : color.new(COLOR_SELL, 85)
signal_plot = plot(show_signal ? signal_line : na, display=display.none)
osc_plot = plot(pressure_osc, display=display.none)
fill(osc_plot, signal_plot, color=show_signal ? fill_color : na, title="Osc/Signal Fill")

plot(buying_volume, title="Buying Volume", display=display.data_window, precision=2)
plot(selling_volume, title="Selling Volume", display=display.data_window, precision=2)

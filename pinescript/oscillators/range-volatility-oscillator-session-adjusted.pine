//@version=6
indicator(title="Range Volatility Oscillator [Session Adjusted]", shorttitle="Volat Range Osc", format=format.price, precision=2, timeframe="", timeframe_gaps=true)

session_duration_hours = input.float(6.5, "Session Duration in Hours", minval=0.1, step=0.1, group="Session Input", tooltip="Enter the trading session duration in hours (e.g., 8.5 for FTSEMIB).")

// ──────────────── Oscillator Settings ────────────────
fast_length = input.int(5, "Fast SMA Length (sessions - e.g. days)", minval=1, group="Oscillator Settings")
slow_length = input.int(15, "Slow SMA Length (sessions - e.g. days)", minval=1, group="Oscillator Settings")

// ──────────────── Signal Line Settings ────────────────
show_signal = input.bool(true, "Show Signal Line", group="Signal Line")
signal_length = input.int(3, "Signal Line Length (sessions - e.g. days)", minval=1, group="Signal Line")

// ──────────────── Colors ────────────────
osc_color = input.color(color.aqua, "Oscillator Color", group="Style")
signal_color = input.color(color.silver, "Signal Line Color", group="Style")

// Detect the timeframe and calculate the shift for the session
var float timeframe_seconds = timeframe.in_seconds(timeframe.period)
var float session_seconds = session_duration_hours * 3600 // Session duration in seconds
var int candles_per_day = int(math.ceil(session_seconds / timeframe_seconds)) // Number of candles in one day, rounded up

// Range calculation
sma_fast = ta.sma(high - low, fast_length * candles_per_day)
sma_slow = ta.sma(high - low, slow_length * candles_per_day)

// Volume Oscillator: percentage difference between fast and slow SMAs
osc = 100 * (sma_fast / sma_slow - 1)

// Optional signal line (SMA of the oscillator)
signal_line = show_signal ? ta.sma(osc, signal_length * candles_per_day) : na

// ──────────────── Plots ────────────────
plot(osc, "Range Volatility Osc", color=osc_color, linewidth=1)
plot(signal_line, "Signal Line", color=signal_color, style=plot.style_cross, linewidth=1)

hline(0, "Zero Line", color=color.black, linestyle=hline.style_dotted)

// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// @vaultfy
//@version=5

indicator("Vaultfy Sentiment Apex", shorttitle="Vaultfy SA", overlay=false)

tf        = input.timeframe("M", "Base Timeframe")
rsiLen    = input.int(14, "RSI Length", minval=2)
normLen   = input.int(42, "Normalization Window", minval=12)
smoothRSI = input.int(4, "RSI Smoothing (EMA)", minval=1)
smoothFin = input.int(5, "Final Smoothing (EMA)", minval=1)

mult      = input.float(1.10, "Amplitude Multiplier", step=0.05)
offset    = input.float(0.0, "Offset (-2 to +2)", step=0.5, minval=-2.0, maxval=2.0)

showFill         = input.bool(true,  "Show Fill Between Lines")
fillTransparency = input.int(70,     "Line Fill Transparency", minval=0, maxval=90)
zoneTransp       = input.int(85,     "Static Zone Transparency", minval=0, maxval=100)

rsiRaw = request.security(syminfo.tickerid, tf, ta.rsi(close, rsiLen))
rsiSm  = ta.ema(rsiRaw, smoothRSI)

lowR   = ta.lowest(rsiSm, normLen)
highR  = ta.highest(rsiSm, normLen)

val0   = 100 * (rsiSm - lowR) / math.max(highR - lowR, 1e-10)
val1   = ta.ema(val0, smoothFin)
val2   = 100 - val1

lineRed   = math.max(0, math.min(100, val1 * mult + offset))
lineGreen = math.max(0, math.min(100, val2 * mult + offset))

plotRed   = plot(lineRed,   title="Red Line",   color=color.new(color.red,   0), linewidth=2)
plotGreen = plot(lineGreen, title="Green Line", color=color.new(color.green, 0), linewidth=2)

h100 = plot(100, "Level 100", color=color.new(color.gray, 50), display=display.none)
h95  = plot(95,  "Level 95",  color=color.new(color.red, 50),  display=display.none)
h5   = plot(5,   "Level 5",   color=color.new(color.green, 50),display=display.none)
h0   = plot(0,   "Level 0",   color=color.new(color.gray, 50), display=display.none)

fill(h100, h95, color=color.new(color.red, zoneTransp),   title="Top Red Zone")
fill(h5, h0,    color=color.new(color.green, zoneTransp), title="Bottom Green Zone")

hline(50, "Middle", color=color.new(color.gray, 50), linestyle=hline.style_dotted)

isExtreme = (lineRed >= 95 or lineRed <= 5 or lineGreen >= 95 or lineGreen <= 5)

fillBaseColor = lineRed > lineGreen ? color.red : color.green
dynamicColor  = (showFill and isExtreme) ? color.new(fillBaseColor, fillTransparency) : color.new(color.white, 100)

fill(plotRed, plotGreen, color=dynamicColor, title="Dynamic Fill")

showZeroHist = input.bool(false, "Show Net Histogram")
net = lineRed - lineGreen
plot(showZeroHist ? net : na, "Net Value", color = net > 0 ? color.new(color.red, 0) : color.new(color.green, 0), style=plot.style_histogram)

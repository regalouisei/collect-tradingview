// This Pine Scriptâ„¢ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© Skyrexio

//@version=6
//_______ <licence>
strategy(title = "Bullish Divergent Bar DCA Strategy [Skyrexio]", 
         shorttitle = "BDB DCA", 
         overlay = true, 
         format = format.inherit, 
         pyramiding = 4, 
         calc_on_order_fills = false, 
         calc_on_every_tick = false, 
         default_qty_type = strategy.cash, 
         default_qty_value = 0, 
         initial_capital = 10000, 
         currency = currency.USDT,  
         commission_type = strategy.commission.percent, 
         commission_value = 0.1,
         slippage = 5,
         use_bar_magnifier = true)

//_______ <constant_declarations>
var const color skyrexGreen               = color.new(#2ECD99, 0)
var const color skyrexGray                = color.new(#F2F2F2, 0)
var const color skyrexWhite               = color.new(#FFFFFF, 0)

//________<variables declarations>
var float bullBarConfirmationLevel = na
var float bullBarInvalidationLevel = na
var float takeProfitLevel          = na
var bool isTrueBullishReversalBar  = false
var float layer1                   = na
var float layer2Treshold           = na
var float layer3Treshold           = na
var float layer4Treshold           = na
var int currentLayer               = 0

//_______ <inputs>
// COMMON SETTINGS
// Trading bot settings
sourceUuid                        = input.string(title = "sourceUuid:", defval = "yourBotSourceUuid", group = "ğŸ¤–Trading Bot SettingsğŸ¤–")
secretToken                       = input.string(title = "secretToken:", defval = "yourBotSecretToken", group = "ğŸ¤–Trading Bot SettingsğŸ¤–")

// Trading period settings
lookBackPeriodStart               = input.time(title = "Trade Start Date/Time", defval = timestamp('2025-01-01T00:00:00'), group = "ğŸ•Trading Period SettingsğŸ•")
lookBackPeriodStop                = input.time(title = "Trade Stop Date/Time", defval = timestamp('2026-01-01T00:00:00'), group = "ğŸ•Trading Period SettingsğŸ•")

showDcaLevels                = input.bool(false, title = "Show DCA Levels", group = "ğŸ§ªStrategy SettingsğŸ§ª")
enable_MFI                   = input.bool(false, title = 'Enable MFI', group = "ğŸ§ªStrategy SettingsğŸ§ª")
enable_AO                    = input.bool(false, title = 'Enable AO', group = "ğŸ§ªStrategy SettingsğŸ§ª")
lowestBars                   = input.int(defval=7, step=1, minval=0, maxval=20, title="Number Of Bar For Lowest Bar", group = "ğŸ§ªStrategy SettingsğŸ§ª")

layer2TresholdPercent        = input.float(defval=4.0, step=0.5, maxval=100.0, minval=0.0, title="Layer 2 Treshold Percent",  group = "ğŸ§ªStrategy SettingsğŸ§ª")
layer3TresholdPercent        = input.float(defval=10.0, step=0.5, maxval=100.0, minval=0.0, title="Layer 3 Treshold Percent",  group = "ğŸ§ªStrategy SettingsğŸ§ª")
layer4TresholdPercent        = input.float(defval=22.0, step=0.5, maxval=100.0, minval=0.0, title="Layer 4 Treshold Percent", group = "ğŸ§ªStrategy SettingsğŸ§ª")
positionsSizeMultiplier      = input.float(defval=2.0, step=0.5, minval=1.0, maxval=4.0, title="Position Size Multiplier",  group = "ğŸ§ªStrategy SettingsğŸ§ª" )
takeprofitNumAtr             = input.float(defval=2.0, step=0.5, minval=0.5, maxval=10.0, title="Number Of ATR For Take Profit",  group = "ğŸ§ªStrategy SettingsğŸ§ª")



isLowestBar = ta.lowest(lowestBars) == low

//_______ <function_declarations>
//@function       Used to calculate Simple moving average for Alligator
smma(src, length) =>
    var float smma = na
    sma_value = ta.sma(src, length)
    smma := na(smma) ? sma_value : (smma * (length - 1) + src) / length
    smma

//@function       Used to define if the current bar is bullish reversal
isBullishReversalBar() =>
    result = close > hl2 and isLowestBar
    result 

//@function       Used to define the trade amount 
//@returns        Percent of equity to enter position at given layer
getLayerEquityQty(mult, layer, price) =>
    float sumW = 1.0 + mult + math.pow(mult, 2) + math.pow(mult, 3)
    float wCur = math.pow(mult, layer)
    float pct  = wCur / sumW                     
    float cap  = strategy.equity * pct           
    float qty  = cap / price                     
    qty

//_______ <calculations>
//Calculating ATR 
atr = ta.atr(14)

//Calculating MFI
MFI      = 1000000000 * (high - low) / volume  
PreMFI   = 1000000000 * (high[1] - low [1]) / volume[1]
greenbar = (MFI > PreMFI) and (volume > volume[1])
fadebar  = (MFI < PreMFI) and (volume < volume[1])
fakebar  = (MFI > PreMFI) and (volume < volume[1])
squatbar = (MFI < PreMFI) and (volume > volume[1])

//Calculating Awesome Oscillator
ao   = ta.sma(hl2,5) - ta.sma(hl2,34)
diff = ao - ao[1]

//Calculating chart's time frame Alligator's lines (Jaw, Teeth, Lips)
jaw   = smma(hl2, 13)[8]
teeth = smma(hl2, 8)[5]
lips  = smma(hl2, 5)[3]

//Calculating the bullish and bearish reversal bars according to inputs
if enable_AO and enable_MFI
    isTrueBullishReversalBar := isBullishReversalBar() and high < jaw and high < teeth and high < lips and diff < 0 and (squatbar or squatbar[1] or squatbar[2])

if enable_AO and not enable_MFI
    isTrueBullishReversalBar := isBullishReversalBar() and high < jaw and high < teeth and high < lips and diff < 0

if not enable_AO and enable_MFI 
    isTrueBullishReversalBar := isBullishReversalBar() and high < jaw and high < teeth and high < lips and (squatbar or squatbar[1] or squatbar[2])

if not enable_AO and not enable_MFI
    isTrueBullishReversalBar := isBullishReversalBar() and high < jaw and high < teeth and high < lips 

//Plotting the bullish reversal bars labels
if isTrueBullishReversalBar
    bullBarConfirmationLevel := high 
    bullBarInvalidationLevel := low



isBullBarInvalidated = ta.crossunder(low, bullBarInvalidationLevel)

if ta.crossover(high, bullBarConfirmationLevel) or isBullBarInvalidated
    bullBarConfirmationLevel := na
    bullBarInvalidationLevel := na 

// Defining current DCA layer
if strategy.opentrades == 1 and strategy.opentrades[1] == 0
    layer1 := strategy.position_avg_price
    currentLayer := 1

if strategy.opentrades == 2 and strategy.opentrades[1] == 1
    currentLayer := 2

if strategy.opentrades == 3 and strategy.opentrades[1] == 2
    currentLayer := 3

if strategy.opentrades == 4 and strategy.opentrades[1] == 3
    currentLayer := 4

// Tresholds price from layer1
layer2Treshold := layer1 * (100 - layer2TresholdPercent)/100
layer3Treshold := layer1 * (100 - layer3TresholdPercent)/100
layer4Treshold := layer1 * (100 - layer4TresholdPercent)/100

//Calculating take profit level 
if strategy.opentrades > 0 
    takeProfitLevel := strategy.position_avg_price + atr * takeprofitNumAtr
else
    takeProfitLevel := na



// ------- Ğ’Ğ¥ĞĞ”Ğ« Ğ¡ Ğ£Ğ§ĞĞ¢ĞĞœ DCA-ĞšĞĞ›Ğ˜Ğ§Ğ•Ğ¡Ğ¢Ğ’Ğ -------
if currentLayer == 0 and time >= lookBackPeriodStart and time <= lookBackPeriodStop and isTrueBullishReversalBar
    float qty1 = getLayerEquityQty(positionsSizeMultiplier, 0, bullBarConfirmationLevel)
    strategy.entry(id = 'entry1', direction = strategy.long, stop = bullBarConfirmationLevel, qty = qty1, alert_message = '{\n"base": "' + syminfo.basecurrency + '",\n"quote": "' + syminfo.currency + '",\n"position": "entry1",\n"price": "' + str.tostring(close) + '",\n"sourceUuid": "' + sourceUuid + '",\n"secretToken": "' + secretToken + '",\n"timestamp": "' + str.tostring(timenow) + '"\n}')

if currentLayer == 1 and low < layer2Treshold and time >= lookBackPeriodStart and time <= lookBackPeriodStop and isTrueBullishReversalBar
    float qty2 = getLayerEquityQty(positionsSizeMultiplier, 1, bullBarConfirmationLevel)
    strategy.entry(id = 'entry2', direction = strategy.long, stop = bullBarConfirmationLevel, qty = qty2, alert_message = '{\n"base": "' + syminfo.basecurrency + '",\n"quote": "' + syminfo.currency + '",\n"position": "entry2",\n"price": "' + str.tostring(close) + '",\n"sourceUuid": "' + sourceUuid + '",\n"secretToken": "' + secretToken + '",\n"timestamp": "' + str.tostring(timenow) + '"\n}')

if currentLayer == 2 and low < layer3Treshold and time >= lookBackPeriodStart and time <= lookBackPeriodStop and isTrueBullishReversalBar
    float qty3 = getLayerEquityQty(positionsSizeMultiplier, 2, bullBarConfirmationLevel)
    strategy.entry(id = 'entry3', direction = strategy.long, stop = bullBarConfirmationLevel, qty = qty3, alert_message = '{\n"base": "' + syminfo.basecurrency + '",\n"quote": "' + syminfo.currency + '",\n"position": "entry3",\n"price": "' + str.tostring(close) + '",\n"sourceUuid": "' + sourceUuid + '",\n"secretToken": "' + secretToken + '",\n"timestamp": "' + str.tostring(timenow) + '"\n}')

if currentLayer == 3 and low < layer4Treshold and time >= lookBackPeriodStart and time <= lookBackPeriodStop and isTrueBullishReversalBar
    float qty4 = getLayerEquityQty(positionsSizeMultiplier, 3, bullBarConfirmationLevel)
    strategy.entry(id = 'entry4', direction = strategy.long, stop = bullBarConfirmationLevel, qty = qty4, alert_message = '{\n"base": "' + syminfo.basecurrency + '",\n"quote": "' + syminfo.currency + '",\n"position": "entry4",\n"price": "' + str.tostring(close) + '",\n"sourceUuid": "' + sourceUuid + '",\n"secretToken": "' + secretToken + '",\n"timestamp": "' + str.tostring(timenow) + '"\n}')

// Ğ’Ñ‹Ñ…Ğ¾Ğ´Ñ‹
strategy.exit(id = 'entry1', from_entry = 'entry1', limit = takeProfitLevel, alert_message = '{\n"base": "' + syminfo.basecurrency + '",\n"quote": "' + syminfo.currency + '",\n"position": "close",\n"price": "' + str.tostring(close) + '",\n"sourceUuid": "' + sourceUuid + '",\n"secretToken": "' + secretToken + '",\n"timestamp": "' + str.tostring(timenow) + '"\n}')
strategy.exit(id = 'entry2', from_entry = 'entry2', limit = takeProfitLevel, alert_message = '{\n"base": "' + syminfo.basecurrency + '",\n"quote": "' + syminfo.currency + '",\n"position": "close",\n"price": "' + str.tostring(close) + '",\n"sourceUuid": "' + sourceUuid + '",\n"secretToken": "' + secretToken + '",\n"timestamp": "' + str.tostring(timenow) + '"\n}')
strategy.exit(id = 'entry3', from_entry = 'entry3', limit = takeProfitLevel, alert_message = '{\n"base": "' + syminfo.basecurrency + '",\n"quote": "' + syminfo.currency + '",\n"position": "close",\n"price": "' + str.tostring(close) + '",\n"sourceUuid": "' + sourceUuid + '",\n"secretToken": "' + secretToken + '",\n"timestamp": "' + str.tostring(timenow) + '"\n}')
strategy.exit(id = 'entry4', from_entry = 'entry4', limit = takeProfitLevel, alert_message = '{\n"base": "' + syminfo.basecurrency + '",\n"quote": "' + syminfo.currency + '",\n"position": "close",\n"price": "' + str.tostring(close) + '",\n"sourceUuid": "' + sourceUuid + '",\n"secretToken": "' + secretToken + '",\n"timestamp": "' + str.tostring(timenow) + '"\n}')


plot(strategy.opentrades > 0 ? takeProfitLevel : na, color=skyrexGreen, style=plot.style_linebr, linewidth = 2)


plot(showDcaLevels ? layer1          : na, color = color.orange)
plot(showDcaLevels ? layer2Treshold  : na, color = color.orange)
plot(showDcaLevels ? layer3Treshold  : na, color = color.orange)
plot(showDcaLevels ? layer4Treshold  : na, color = color.orange)


if strategy.opentrades == 0
    currentLayer := 0

// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Tiagorocha1989
//                              ████████╗ ████████╗  ████████╗  ███████╗   ██████╗        ██████╗   ██████╗   ██████╗ ██╗   ██╗ ████████╗
//                              ╚══██╔══╝ ╚══██╔══╝  ██╔═══██║  ██╔════╝  ██╔═══██╗       ██╔══██╗ ██╔═══██╗ ██╔════╝ ██║   ██║ ██╔═══██║
//                                 ██║       ██║     ████████║  ██║ ███║  ██║   ██║       ██████╔╝ ██║   ██║ ██║      ████████║ ████████║
//                                 ██║       ██║     ██╔═══██║  ██║  ██║  ██║   ██║       ██╔══██╗ ██║   ██║ ██║      ██║   ██║ ██╔═══██║
//                                 ██║    ████████╗  ██║   ██║  ███████║  ╚██████╔╝       ██║  ██║ ╚██████╔╝ ╚██████╗ ██║   ██║ ██║   ██║
//@version=6
indicator('Simple Volume Zone Oscillator - SVZO | TR', 'SVZO | TR →', false)
//╔════════════════════════╗
//║ Choose Moving Averages ║
//╚════════════════════════╝
ma(source, length, type) =>
     type == 'EMA' ? ta.ema(source, length) :
     type == 'SMA' ? ta.sma(source, length) :
     type == 'RMA' ? ta.rma(source, length) :
     type == 'WMA' ? ta.wma(source, length) :
     type == 'VWMA' ? ta.vwma(source, length) :
     type == 'HMA' ? ta.hma(source, length) :
     na
//╔═════════════════════════╗
//║          Input          ║
//╚═════════════════════════╝
Color_Mode =        input.string('Classic', 'Color Choice', group = 'Color', options = ['Classic', 'Modern', 'Robust', 'Accented', 'Monochrome', 'Moderate', 'Aqua', 'Cosmic'])
Length_SVZO =       input.int(14, 'Length SVZO', minval=1, group='SVZO Setting')
Signal_SVZO =       input.string('0', 'Entry/Exit Signal', group='SVZO Setting', options = ['0', 'SVZO MA'],tooltip = 'Choose Entry/Exit Point')
Length_MA_SVZO =    input.int(365, 'Length MA', minval=1, group='SVZO MA', inline = 'SVZO MA')
Source_MA_SVZO =    input.string('EMA', '', group='SVZO MA', inline = 'SVZO MA', options=['EMA', 'SMA', 'RMA', 'WMA', 'VWMA', 'HMA'], tooltip = 'Moving Averages')
//╔════════════════════════╗
//║      Calculation       ║
//╚════════════════════════╝
typicalPrice = (high + low + close) / 3
volUp_SVZO = volume * (typicalPrice > typicalPrice[1] ? 1 : 0)
volDown_SVZO = volume * (typicalPrice < typicalPrice[1] ? 1 : 0)
sumVolUp = math.sum(volUp_SVZO, Length_SVZO)
sumVolDown = math.sum(volDown_SVZO, Length_SVZO)
totalVol = sumVolUp + sumVolDown
SVZO = (sumVolUp - sumVolDown) / (totalVol != 0 ? totalVol : 1) * 100
SVZO_MA =  ma(SVZO, Length_MA_SVZO, Source_MA_SVZO)
float Entry_SVZO = Signal_SVZO == 'SVZO MA' ? SVZO_MA : 0.0
isBull_SVZO = SVZO > Entry_SVZO
isBear_SVZO = SVZO < Entry_SVZO
Bull_SVZO = ta.crossover(SVZO, Entry_SVZO)
Bear_SVZO = ta.crossunder(SVZO, Entry_SVZO)
//╔════════════════════════╗
//║         Color          ║
//╚════════════════════════╝
[UpC, DnC] = switch Color_Mode
    'Classic' =>    [#008800, #ff0000]
    'Modern' =>     [#ffffff, #b721ff]
    'Robust' =>     [#ffbb00, #770737]
    'Accented' =>   [#8c5cf7, #e83e8c]
    'Monochrome' => [#e9ecef, #495057]
    'Moderate' =>   [#43a047, #e53935]
    'Aqua' =>       [#00a8e8, #f18f01]
    'Cosmic' =>     [#e83e8c, #6f2da8]
//╔════════════════════════╗
//║          Plot          ║
//╚════════════════════════╝
Plot_Entry_SVZO =   plot(Entry_SVZO, color = color.yellow)
Plot_SVZO =         plot(SVZO, 'SVZO', color= SVZO > Entry_SVZO ? color.new(UpC, 50) : color.new(DnC, 50), linewidth=2)
fill(Plot_SVZO, Plot_Entry_SVZO, 100, Entry_SVZO, top_color = color.new(UpC, 0),    bottom_color = color.new(UpC, 100), title = 'Overbought Fill')
fill(Plot_SVZO, Plot_Entry_SVZO, Entry_SVZO, -100, top_color = color.new(DnC, 100), bottom_color = color.new(DnC, 0), title = 'Oversold Fill')
fill(Plot_SVZO, Plot_Entry_SVZO, SVZO > Entry_SVZO ? SVZO : (SVZO < Entry_SVZO ? SVZO : Entry_SVZO), SVZO > Entry_SVZO ? Entry_SVZO : (SVZO < Entry_SVZO ? Entry_SVZO : SVZO), SVZO > Entry_SVZO ? color.new(UpC, 50) : (SVZO < Entry_SVZO ? color.new(DnC, 50) : #00000000), SVZO > Entry_SVZO ? #00000000 : (SVZO < Entry_SVZO ? #00000000 : #00000000),title = 'Dynamic Fill')
plotcandle(open, high, low, close, 'Bar Color', isBull_SVZO ? UpC : isBear_SVZO ? DnC : color.gray, isBull_SVZO ? UpC : isBear_SVZO ? DnC : color.gray, bordercolor = isBull_SVZO ? UpC : isBear_SVZO ? DnC : color.gray, force_overlay = true)
plotshape(Bull_SVZO, 'LONG', shape.triangleup, location.belowbar, UpC, 0, size = size.small, force_overlay = true)
plotshape(Bear_SVZO, 'SHORT', shape.triangledown, location.abovebar, DnC, 0, size = size.small, force_overlay = true)
//╔════════════════════════╗
//║          VIEW          ║
//╚════════════════════════╝
var table Table_SVZO = table.new(position.middle_right, 1, 1, border_width = 1)
var label svzoLabel = na
if barstate.islast
    table.cell(Table_SVZO, 0, 0, text = isBull_SVZO ? '⬆️ Bullish' : '⬇️ Bearish', text_color = isBull_SVZO ? UpC : DnC,text_size = size.huge)
    if not na(svzoLabel)
        label.delete(svzoLabel)
    svzoLabel := label.new(bar_index + 5, SVZO, text = str.tostring(SVZO, '#.##'), color = SVZO > Entry_SVZO ? color.new(UpC, 20) : SVZO < Entry_SVZO ? color.new(DnC, 20) : color.gray, style = label.style_label_left, size = size.large, textcolor = #000000)
//╔════════════════════════╗
//║         Alerts         ║
//╚════════════════════════╝
alertcondition(Bull_SVZO, title= 'LONG', message= '{{ticker}} LONG')
alertcondition(Bear_SVZO, title= 'SHORT', message= '{{ticker}} SHORT')

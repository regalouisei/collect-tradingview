// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © DotGain

//@version=6
indicator('RSI & BB Oversold Scalper with MACD Confirmation', shorttitle = 'Oversold Scalper', overlay = true, max_bars_back = 500)

// ==========================================
// INPUTS
// ==========================================
useBB = input.bool(true, '1. BB%b Filter active?', group = 'Filter')
useRSI = input.bool(true, '2. RSI Filter active?', group = 'Filter')
useDSS = input.bool(true, '3. DSS Crossover filter active?', group = 'Filter')
lookbackBB = input.int(20, 'BB%b Lookback (candles)', group = 'Lookback Periods')
lookbackRSI = input.int(10, 'RSI Lookback (candles)', group = 'Lookback Periods')
validWindow = input.int(15, 'MACD Lookback(candles)', group = 'Lookback Periods')

rsiLen = input.int(14, 'RSI Length', group = 'Relative Strength Index')
bbLen = input.int(20, 'BB Length', group = 'Bollinger Bands')
bbMult = input.float(2.0, 'BB StdDev', group = 'Bollinger Bands')
dssLen = input.int(10, 'DSS P-Period', group = 'Double Smoothed Stochastic')
dssSmooth = input.int(5, 'DSS EMA-Smoothing', group = 'Double Smoothed Stochastic')
macdFast = input.int(12, 'MACD Fast', group = 'Moving Average Convergence Divergence')
macdSlow = input.int(26, 'MACD Slow', group = 'Moving Average Convergence Divergence')
macdSignal = input.int(9, 'MACD Signal', group = 'Moving Average Convergence Divergence')

// ==========================================
// CALCULATION
// ==========================================

// BB %b
[_, _, lower] = ta.bb(close, bbLen, bbMult)
[_, upper_b, lower_b] = ta.bb(close, bbLen, bbMult)
bbPercentB = (close - lower_b) / (upper_b - lower_b)

// RSI
rsiVal = ta.rsi(close, rsiLen)

// DSS Bressert
calc_dss(len, smooth) =>
    high_ = ta.highest(high, len)
    low_ = ta.lowest(low, len)
    delta = high_ - low_
    frac = delta > 0 ? (close - low_) / delta * 100 : 0
    ema1 = ta.ema(frac, smooth)
    high_2 = ta.highest(ema1, len)
    low_2 = ta.lowest(ema1, len)
    delta2 = high_2 - low_2
    frac2 = delta2 > 0 ? (ema1 - low_2) / delta2 * 100 : 0
    ta.ema(frac2, smooth)

dssLine = calc_dss(dssLen, dssSmooth)
dssSig = ta.ema(dssLine, dssSmooth)

// MACD
[macdLine, signalLine, _] = ta.macd(close, macdFast, macdSlow, macdSignal)

// ==========================================
// LOGIC WITH RESET-FUNCTION
// ==========================================

condBB = ta.lowest(bbPercentB, lookbackBB) < 0
condRSI = ta.lowest(rsiVal, lookbackRSI) < 30
dssCross = ta.crossover(dssLine, dssSig)
condDSS = dssCross and dssLine < 20

setupTrigger = (useBB ? condBB : true) and (useRSI ? condRSI : true) and (useDSS ? condDSS : true)

var bool setupIsActive = false
var int barsSinceSetup = 0

if setupTrigger
    setupIsActive := true
    barsSinceSetup := 0
    barsSinceSetup

if setupIsActive
    barsSinceSetup := barsSinceSetup + 1
    barsSinceSetup

if barsSinceSetup > validWindow
    setupIsActive := false
    setupIsActive

macdBullishCross = ta.crossover(macdLine, signalLine)
buySignal = setupIsActive and macdBullishCross

if buySignal
    setupIsActive := false
    barsSinceSetup := 0
    barsSinceSetup

// ==========================================
// VISUALS
// ==========================================
plotshape(buySignal, 'Buy Signal', shape.labelup, location.belowbar, color.new(color.lime, 60), size = size.small)

//@version=6
indicator("SuperRSI: Enhanced Momentum", overlay = false)

group_rsi = 'RSI Settings'
len_input   = input.int(14, 'Length', minval=2, group=group_rsi)
src_input   = input.source(close, 'Source', group=group_rsi)
smooth_type = input.string('RMA', 'Smoothing Method', options=['RMA', 'SMA', 'EMA'], group=group_rsi)

group_style = 'Visual Settings'
ob_level    = input.float(80, 'Overbought Level', group=group_style)
os_level    = input.float(20, 'Oversold Level', group=group_style)
ob_fill_col = input.color(color.new(#089981, 60), 'Bullish Fill Color', group=group_style)
os_fill_col = input.color(color.new(#f23645, 60), 'Bearish Fill Color', group=group_style)

group_sig   = 'Signal Lines'
show_fast    = input.bool(false, 'Show Fast Signal', group=group_sig) 
sig_fast_len = input.int(7, 'Fast Signal Length', group=group_sig)
sig_fast_col = input.color(#2962ff, 'Fast Signal Color', group=group_sig)

sig_slow_len = input.int(14, 'Slow Signal Length', group=group_sig)
sig_slow_col = input.color(#ff5d00, 'Slow Signal Color', group=group_sig)

get_ma(src, len, type) =>
    switch type
        'SMA' => ta.sma(src, len)
        'EMA' => ta.ema(src, len)
        => ta.rma(src, len)

highest_high = ta.highest(src_input, len_input)
lowest_low   = ta.lowest(src_input, len_input)
price_range  = highest_high - lowest_low
price_change = src_input - src_input[1]

momentum_raw = if highest_high > highest_high[1]
    price_range
else if lowest_low < lowest_low[1]
    -price_range
else
    price_change

momentum_smoothed = get_ma(momentum_raw, len_input, smooth_type)
range_smoothed    = get_ma(math.abs(momentum_raw), len_input, smooth_type)

titan_rsi = (momentum_smoothed / nz(range_smoothed, 1)) * 50 + 50

sig_fast = get_ma(titan_rsi, sig_fast_len, 'EMA')
sig_slow = get_ma(titan_rsi, sig_slow_len, 'EMA')

rsi_dynamic_color = titan_rsi > sig_slow ? color.new(#089981, 0) : color.new(#f23645, 0)

p_rsi = plot(titan_rsi, 'RSI', color=rsi_dynamic_color, linewidth=1)

p_fast = plot(show_fast ? sig_fast : na, 'Fast Signal', color=sig_fast_col)
p_slow = plot(sig_slow, 'Slow Signal', color=sig_slow_col, linewidth=1)


p_ob  = plot(ob_level, 'OB Level', color=na, editable=false)
p_mid = plot(50, 'Mid Level', color=na, editable=false)
p_os  = plot(os_level, 'OS Level', color=na, editable=false)

hline(ob_level, 'Overbought Line', color=color.gray, linestyle=hline.style_dotted)
hline(50, 'Midline', color=color.gray, linestyle=hline.style_dotted)
hline(os_level, 'Oversold Line', color=color.gray, linestyle=hline.style_dotted)

fill(p_rsi, p_ob, titan_rsi > ob_level ? ob_fill_col : na, title="Overbought Fill")
fill(p_os, p_rsi, titan_rsi < os_level ? os_fill_col : na, title="Oversold Fill")

fill(p_rsi, p_mid, ob_level, 50, ob_fill_col, color.new(chart.bg_color, 100), title="Bullish Gradient")
fill(p_mid, p_rsi, 50, os_level, color.new(chart.bg_color, 100), os_fill_col, title="Bearish Gradient")

cross_bull = ta.crossover(titan_rsi, sig_slow)
cross_bear = ta.crossunder(titan_rsi, sig_slow)

alertcondition(ta.crossover(sig_fast, sig_slow), "Fast x Slow (Bullish)", "Fast Signal crossed ABOVE Slow Signal")
alertcondition(ta.crossunder(sig_fast, sig_slow), "Fast x Slow (Bearish)", "Fast Signal crossed BELOW Slow Signal")

alertcondition(ta.crossover(titan_rsi, sig_fast), "RSI x Fast (Bullish)", "RSI crossed ABOVE Fast Signal")
alertcondition(ta.crossunder(titan_rsi, sig_fast), "RSI x Fast (Bearish)", "RSI crossed BELOW Fast Signal")

alertcondition(cross_bull, "RSI x Slow (Bullish)", "RSI crossed ABOVE Slow Signal")
alertcondition(cross_bear, "RSI x Slow (Bearish)", "RSI crossed BELOW Slow Signal")

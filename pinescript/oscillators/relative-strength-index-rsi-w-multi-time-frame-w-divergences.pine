//@version=6
indicator("RSI + MTF Background Zone + Gradient (MTF Main)", shorttitle="RSI MTF Pro v2", overlay=false)

// --- Girdiler ---
group_main = " RSI Settings"
tfMain = input.timeframe("", "RSI TimeFrame", group=group_main)
length = input.int(14, title="Uzunluk", minval=1, group=group_main)
osLevel = input.int(30, minval=0, maxval=50, title="(Oversold)", group=group_main, inline = 'ab')
obLevel = input.int(70, minval=50, maxval=100, title="(Overbought)", group=group_main, inline = 'ab')

group_mtf = "MTF RSI Settings"
tfInput = input.timeframe("1D", "MTF Timeframe", group=group_mtf)
mtfLength = input.int(14, title="MTF Length", minval=1, group=group_mtf)
// MTF sinyalleri için ayrı seviyeler
mtfObLevel = input.int(60, title="MTF (Oversold)", group=group_mtf, inline = 'ab2')
mtfOsLevel = input.int(40, title="MTF (Overbought)", group=group_mtf, inline = 'ab2')

group_div = "(Divergence)"
showDivergences = input.bool(true, 'Show Divergence', group=group_div, inline = 'y')
plotBull = input.bool(true, '(Bullish)', group=group_div, inline = 'y')
plotBear = input.bool(true, '(Bearish)', group=group_div, inline = 'y')
lbR = input.int(5, title = 'Pivot Right', group=group_div, inline = 'u')
lbL = input.int(5, title = 'Pivot Left', group=group_div, inline = 'u')
rangeUpper = input.int(60, title = 'Max LookBack', group=group_div, inline = 'u')
rangeLower = input.int(5, title = 'Min LookBack', group=group_div, inline = 'u')

// --- RSI Hesaplamaları ---

// 1. Ana RSI (Kullanıcı tfMain seçerse oradan, seçmezse grafikten gelir)
rsiCurrent = request.security(syminfo.tickerid, tfMain, ta.rsi(close, length))

// 2. MTF RSI (Arka plan rengi için bağımsız hesaplama)
rsiMTF = request.security(syminfo.tickerid, tfInput, ta.rsi(close, mtfLength))


// --- Görünüm ve Çizimler ---

// Renk Mantığı: OB üstü Kırmızı, OS altı Yeşil, Arası Gri
rsiColor = rsiCurrent > obLevel ? color.red : rsiCurrent < osLevel ? color.green : color.rgb(184, 182, 180)

// Ana Çizgi (Standart ince çizgi)
rsiPlot = plot(rsiCurrent, title="RSI Çizgisi", linewidth=1, color=rsiColor)


// --- Referans Çizgileri ---
// Gradient dolgu için gizli referans
midLinePlot = plot(50, color = na, editable = false, display = display.none)

h_max = hline(100, "Max", linestyle=hline.style_dotted, color=color.new(color.white, 100), display = display.none)
h_min = hline(0, "Min", linestyle=hline.style_dotted, color=color.new(color.white, 100), display = display.none)
h_ob  = hline(obLevel, "Aşırı Alım Seviyesi", linestyle=hline.style_dotted, color=color.gray)
h_os  = hline(osLevel, "Aşırı Satım Seviyesi", linestyle=hline.style_dotted, color=color.gray)
h_mid = hline(50, "Orta Seviye (50)", linestyle=hline.style_dotted, color=color.gray)


// --- GRADIENT (DEYRADELİ) DOLGU ---

// 1. ÜST BÖLGE (OB - 100 arası): KIRMIZI Gradient
fill(rsiPlot, midLinePlot, 100, obLevel, top_color = color.new(color.red, 0), bottom_color = color.new(color.red, 100), title = "Aşırı Alım Gradient")

// 2. ALT BÖLGE (0 - OS arası): YEŞİL Gradient
fill(rsiPlot, midLinePlot, osLevel, 0, top_color = color.new(color.green, 100), bottom_color = color.new(color.green, 0), title = "Aşırı Satım Gradient")


// --- MTF Renk Kararı (Arka Plan) ---
// Arka plan rengi, MTF RSI'ın kullanıcının girdiği MTF seviyelerine göre belirlenir.
zoneColor = rsiMTF > mtfObLevel ? color.new(color.red, 77) : rsiMTF < mtfOsLevel ? color.new(color.green, 77) : color.new(color.white, 100)

fill(h_ob, h_os, color=zoneColor, title="MTF Arka Plan Rengi")


// ===== Uyumsuzluk (Divergence) Bölümü =====
bearColor = #f80909
bullColor = #08fc10
divergenceSmartColor = color.silver
noneColor = color.new(color.white, 100)

osc = rsiCurrent

plFound = not na(ta.pivotlow(osc, lbL, lbR))
phFound = not na(ta.pivothigh(osc, lbL, lbR))

_inRange(cond) =>
    bars = ta.barssince(cond == true)
    rangeLower <= bars and bars <= rangeUpper

//------------------------------------------------------------------------------
// Regular Bullish
oscHL = osc[lbR] > ta.valuewhen(plFound, osc[lbR], 1) and _inRange(plFound[1])
priceLL = low[lbR] < ta.valuewhen(plFound, low[lbR], 1) 
bullCond = showDivergences and plotBull and priceLL and oscHL and plFound

plot(showDivergences and plFound and showDivergences ? osc[lbR] : na, offset = -lbR, title = 'Regular Bullish Line', linewidth = 1, color = bullCond ? divergenceSmartColor : noneColor, display = display.pane)
plotshape(bullCond ? osc[lbR] : na, 'Bullish Circle', shape.circle, location.absolute, bullColor, offset = -lbR, size = size.tiny, display = display.pane)

//------------------------------------------------------------------------------
// Regular Bearish
oscLH = osc[lbR] < ta.valuewhen(phFound, osc[lbR], 1) and _inRange(phFound[1])
priceHH = high[lbR] > ta.valuewhen(phFound, high[lbR], 1) 
bearCond = showDivergences and plotBear and priceHH and oscLH and phFound

plot(showDivergences and phFound ? osc[lbR] : na, offset = -lbR, title = 'Regular Bearish Line', linewidth = 1, color = bearCond ? divergenceSmartColor : noneColor, display = display.pane)
plotshape(bearCond ? osc[lbR] : na, 'Bearish Circle', shape.circle, location.absolute, bearColor, offset = -lbR, size = size.tiny, display = display.pane)

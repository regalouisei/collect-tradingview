// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Tiagorocha1989
//                              ████████╗ ████████╗  ████████╗  ███████╗   ██████╗        ██████╗   ██████╗   ██████╗ ██╗   ██╗ ████████╗
//                              ╚══██╔══╝ ╚══██╔══╝  ██╔═══██║  ██╔════╝  ██╔═══██╗       ██╔══██╗ ██╔═══██╗ ██╔════╝ ██║   ██║ ██╔═══██║
//                                 ██║       ██║     ████████║  ██║ ███║  ██║   ██║       ██████╔╝ ██║   ██║ ██║      ████████║ ████████║
//                                 ██║       ██║     ██╔═══██║  ██║  ██║  ██║   ██║       ██╔══██╗ ██║   ██║ ██║      ██║   ██║ ██╔═══██║
//                                 ██║    ████████╗  ██║   ██║  ███████║  ╚██████╔╝       ██║  ██║ ╚██████╔╝ ╚██████╗ ██║   ██║ ██║   ██║
//@version=6
indicator('TRIX | TR' , 'TRIX | TR →', format=format.price, precision=2)
//╔════════════════════════╗
//║ Choose Moving Averages ║
//╚════════════════════════╝
ma(source, length, type) =>
     type == 'EMA' ? ta.ema(source, length) :
     type == 'SMA' ? ta.sma(source, length) :
     type == 'RMA' ? ta.rma(source, length) :
     type == 'WMA' ? ta.wma(source, length) :
     type == 'VWMA' ? ta.vwma(source, length) :
     type == 'HMA' ? ta.hma(source, length) :
     na
//╔════════════════════════╗
//║     INPUT SETTINGS     ║
//╚════════════════════════╝
Color_Mode =    input.string('Classic', 'Color Choice', group = 'Color', options = ['Classic', 'Modern', 'Robust', 'Accented', 'Monochrome', 'Moderate', 'Aqua', 'Cosmic'])
Length_TRIX =   input.int(18,'Length TRIX', inline='TRIX', group='TRIX Setting', minval=1)
Source_TRIX =   input(close,'', 'TRIX', group='TRIX Setting')
Signal_TRIX =   input.string('0', 'Entry/Exit Signal', group='TRIX Setting', options = ['0', 'TRIX MA'],tooltip = 'Choose Entry/Exit Point')
Length_MA_TRIX =input.int(365,'Length MA', group='TRIX MA', inline = 'TRIX MA')
MA_Type_TRIX =  input.string('EMA', '', group='TRIX MA', inline = 'TRIX MA', options=['EMA', 'SMA', 'RMA', 'WMA', 'VWMA', 'HMA'], tooltip = 'Moving Average Type for smoothing')
//╔════════════════════════╗
//║      Calculation       ║
//╚════════════════════════╝
logSource = math.log(Source_TRIX)
ma1 = ma(logSource, Length_TRIX, MA_Type_TRIX)
ma2 = ma(ma1, Length_TRIX, MA_Type_TRIX)
ma3 = ma(ma2, Length_TRIX, MA_Type_TRIX)
TRIX = 10000 * (ma3 - ma3[1])
TRIX_MA =  ma(TRIX, Length_MA_TRIX, MA_Type_TRIX)
float Entry_TRIX = Signal_TRIX == 'TRIX MA' ? TRIX_MA : 0.0
isBull_TRIX = TRIX > Entry_TRIX
isBear_TRIX = TRIX < Entry_TRIX
Bull_TRIX = ta.crossover(TRIX,Entry_TRIX)
Bear_TRIX = ta.crossunder(TRIX,Entry_TRIX)
//╔════════════════════════╗
//║         Color          ║
//╚════════════════════════╝
[UpC, DnC] = switch Color_Mode
    'Classic' =>    [#008800, #ff0000]
    'Modern' =>     [#ffffff, #b721ff]
    'Robust' =>     [#ffbb00, #770737]
    'Accented' =>   [#8c5cf7, #e83e8c]
    'Monochrome' => [#e9ecef, #495057]
    'Moderate' =>   [#43a047, #e53935]
    'Aqua' =>       [#00a8e8, #f18f01]
    'Cosmic' =>     [#e83e8c, #6f2da8]
//╔════════════════════════╗
//║          Plot          ║
//╚════════════════════════╝
Plot_TRIX =     plot(TRIX, color = TRIX > Entry_TRIX ? color.new(UpC, 20):color.new(DnC, 20), linewidth = 2)
Plot_Entry_TRIX =  plot(Entry_TRIX, color = color.yellow)
fill(Plot_TRIX, Plot_Entry_TRIX, 100, Entry_TRIX, top_color = color.new(UpC, 0),    bottom_color = color.new(UpC, 100), title = 'Overbought Fill')
fill(Plot_TRIX, Plot_Entry_TRIX, Entry_TRIX, -100, top_color = color.new(DnC, 100), bottom_color = color.new(DnC, 0),    title = 'Oversold Fill')
fill(Plot_TRIX, Plot_Entry_TRIX, TRIX < Entry_TRIX ? TRIX : Entry_TRIX, TRIX < Entry_TRIX ? Entry_TRIX : TRIX, TRIX < Entry_TRIX ? DnC : #00000000, TRIX < Entry_TRIX ? #00000000 : UpC)
plotcandle(open, high, low, close, 'Bar Color', isBull_TRIX ? UpC : isBear_TRIX ? DnC : na, isBull_TRIX ? UpC : isBear_TRIX ? DnC : na, bordercolor = isBull_TRIX ? UpC : isBear_TRIX ? DnC : na, force_overlay = true)
plotshape(Bull_TRIX, 'LONG' , shape.triangleup  , location.belowbar, UpC,    0, size = size.small, force_overlay = true)
plotshape(Bear_TRIX, 'SHORT', shape.triangledown, location.abovebar, DnC,    0, size = size.small, force_overlay = true)
//╔════════════════════════╗
//║          VIEW          ║
//╚════════════════════════╝
var table Table_TRIX = table.new(position.middle_right, 1, 1, border_width = 1)
var label TRIXLabel = na
if barstate.islast
    table.cell(Table_TRIX, 0, 0, text = isBull_TRIX ? '⬆️ Bullish' : '⬇️ Bearish', text_color = isBull_TRIX ? UpC : DnC,text_size = size.huge)
    if not na(TRIXLabel)
        label.delete(TRIXLabel)
    TRIXLabel := label.new(bar_index + 5, TRIX, text = str.tostring(TRIX, '#.##'), color = TRIX > Entry_TRIX ? color.new(UpC, 20) : TRIX < Entry_TRIX ? color.new(DnC, 20) : color.gray, style = label.style_label_left, size = size.large, textcolor = #000000)
//╔════════════════════════╗
//║         Alerts         ║
//╚════════════════════════╝
alertcondition(Bull_TRIX, title= 'LONG', message= '{{ticker}} LONG')
alertcondition(Bear_TRIX, title= 'SHORT', message= '{{ticker}} SHORT')

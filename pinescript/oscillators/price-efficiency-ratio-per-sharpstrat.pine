// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © SharpStrat

//@version=6
indicator("Price Efficiency Ratio (PER) [SharpStrat]", overlay=false)

// INPUTS
length = input.int(30, "Lookback Period", minval=2, group="Core Settings")
enableSmoothing = input.bool(true, "Enable Smoothing", group="Core Settings")
smoothing = input.int(20, "Smoothing Period", minval=1, group="Core Settings")
useVolumeWeight = input.bool(false, "Volume-Weighted PER", tooltip="Weight price movements by volume", group="Core Settings")

// Zone
zoneType = input.string("Dynamic", "Threshold Type", options=["Fixed", "Dynamic"], tooltip="Fixed: Manual threshold | Dynamic: Auto-calculated", group="Threshold Settings")
threshold = input.float(25.0, "Efficiency Threshold (Fixed)", minval=0, maxval=100, tooltip="Above = Trending | Below = Ranging", group="Threshold Settings")
thresholdLookback = input.int(252, "Dynamic Threshold Lookback", minval=20, group="Threshold Settings")
dynamicPercentile = input.int(40, "Dynamic Threshold Percentile", minval=10, maxval=90, group="Threshold Settings")

showInfoBox = input.bool(true, "Show Info Box", group="Display Settings")

// calc
netDistance = math.abs(close - close[length])

// volume weighting
totalDistance = 0.0
for i = 1 to length
    priceMove = math.abs(close[i-1] - close[i])
    weight = useVolumeWeight ? volume[i] : 1.0
    totalDistance := totalDistance + (priceMove * weight)

weightedNetDistance = netDistance
if useVolumeWeight
    totalVolume = math.sum(volume, length)
    avgVolume = totalVolume / length
    weightedNetDistance := netDistance * avgVolume

per = totalDistance > 0 ? ((useVolumeWeight ? weightedNetDistance : netDistance) / totalDistance) * 100 : 0

perFinal = enableSmoothing ? ta.sma(per, smoothing) : per

// Dynamic Threshold
thresholdDynamic = ta.percentile_linear_interpolation(perFinal, thresholdLookback, dynamicPercentile)

thresholdFinal = zoneType == "Dynamic" ? thresholdDynamic : threshold

// visual
getGradientColor(value, thresh) =>
    if value < thresh
        intensity = (value / thresh) * 100
        redAmt = 255
        greenAmt = int(intensity * 1.5)
        color.rgb(redAmt, greenAmt, 0)
    else
        intensity = math.min(100, ((value - thresh) / (100 - thresh)) * 100)
        redAmt = int(255 - (intensity * 2.55))
        greenAmt = 255
        color.rgb(redAmt, greenAmt, 0)

lineColor = getGradientColor(perFinal, thresholdFinal)

lowerZoneFill = plot(thresholdFinal, "Threshold", color=color.new(color.gray, 100), display=display.none)
bottomBound = plot(0, "Bottom", color=color.new(color.red, 100), display=display.none)
fill(lowerZoneFill, bottomBound, color=color.new(color.red, 93), title="Ranging Zone")

upperZoneFill = plot(thresholdFinal, "Threshold Upper", color=color.new(color.gray, 100), display=display.none)
topBound = plot(100, "Top", color=color.new(color.green, 100), display=display.none)
fill(upperZoneFill, topBound, color=color.new(color.green, 93), title="Trending Zone")

plot(thresholdFinal, "Efficiency Threshold", color=color.new(color.white, 20), linewidth=2, style=plot.style_line)

hline(50, "Reference", color=color.new(color.gray, 80), linestyle=hline.style_dotted)

plot(perFinal, "PER", color=lineColor, linewidth=3)

// INFO 
if showInfoBox and barstate.islast
    var table infoTable = table.new(position.top_right, 1, 4, 
         border_width=2, 
         border_color=color.new(color.gray, 30),
         frame_color=color.new(color.gray, 30),
         frame_width=1)
    
    marketState = perFinal >= thresholdFinal ? "Trending" : "Ranging"
    
    stateColor = perFinal >= thresholdFinal ? color.new(color.green, 0) : color.new(color.red, 0)
    
    strategy = perFinal >= thresholdFinal ? "Trend Following" : "Mean Reversion"
    
    table.cell(infoTable, 0, 0, "Price Efficiency Ratio", 
               text_color=color.white, 
               bgcolor=color.new(color.blue, 60))
    
    table.cell(infoTable, 0, 1, str.tostring(perFinal, "#.##") + "%", 
               text_color=lineColor, 
               bgcolor=color.new(#829de9, 40))
    
    table.cell(infoTable, 0, 2, marketState + " | " + strategy, 
               text_color=color.white, 
               bgcolor=stateColor)
    
    thresholdInfo = zoneType + " Threshold: " + str.tostring(thresholdFinal, "#.#")
    if useVolumeWeight
        thresholdInfo := thresholdInfo + " | Vol-Weighted"
    
    table.cell(infoTable, 0, 3, thresholdInfo, 
               text_color=color.new(color.white, 30), 
               bgcolor=color.new(color.black, 40),
               text_size=size.small)

// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Uncle_the_shooter

//@version=6
indicator('Smart WaveTrend Crossover', overlay = true, max_boxes_count = 500, max_labels_count = 500)

// Colors
col_up = input.color(color.new(#26A69A, 0), 'Bull Color', group = 'Colors')
col_dn = input.color(color.new(#EF5350, 0), 'Bear Color', group = 'Colors')

// WaveTrend Settings for Signals 
wt_channel_len = input.int(5, 'Signal Channel Length', group = 'WaveTrend Settings for Signals')
wt_average_len = input.int(10, 'Signal Average Length', group = 'WaveTrend Settings for Signals')
wt_ma_len = input.int(4, 'Signal MA Length', group = 'WaveTrend Settings for Signals')
src_wt = input.source(hlc3, 'Signal Source', group = 'WaveTrend Settings for Signals')

esa = ta.ema(src_wt, wt_channel_len)
d = ta.ema(math.abs(src_wt - esa), wt_channel_len)
ci = (src_wt - esa) / (0.015 * d)
wt1 = ta.ema(ci, wt_average_len)
wt2 = ta.sma(wt1, wt_ma_len)

signal_hist = wt1 - wt2
signal_bull_cross = ta.crossover(wt1, wt2)
signal_bear_cross = ta.crossunder(wt1, wt2)

// WaveTrend Settings for Trend 
wt_trend_channel_len = input.int(10, 'Trend Channel Length', group = 'WaveTrend Settings for Trend')
wt_trend_average_len = input.int(100, 'Trend Average Length', group = 'WaveTrend Settings for Trend')
wt_trend_ma_len = input.int(10, 'Trend MA Length', group = 'WaveTrend Settings for Trend')
src_wt_trend = input.source(hlc3, 'Trend Source', group = 'WaveTrend Settings for Trend')

esa_trend = ta.ema(src_wt_trend, wt_trend_channel_len)
d_trend = ta.ema(math.abs(src_wt_trend - esa_trend), wt_trend_channel_len)
ci_trend = (src_wt_trend - esa_trend) / (0.015 * d_trend)
wt1_trend = ta.ema(ci_trend, wt_trend_average_len)
wt2_trend = ta.sma(wt1_trend, wt_trend_ma_len)

trend_hist = wt1_trend - wt2_trend
trend_is_bull = wt1_trend > wt2_trend
trend_is_bear = wt1_trend < wt2_trend

// Signal & Box Settings
use_trend_filter = input.bool(true, 'Use Trend Filter for Boxes?', group = 'Signal Settings')
signal_type = input.string('Labels (Buy/Sell)', 'Signal Type', options = ['Triangles', 'Labels (Buy/Sell)'], group = 'Signal Settings')
show_only_matching = input.bool(true, 'Show Only Matching Signals?', group = 'Signal Settings')
use_box_multiplier = input.bool(false, 'Extend Box by Average Candle Size?', group = 'Signal Settings')
box_multiplier = input.float(1.0, 'Box Extension Multiplier', minval = 0.1, step = 0.1, group = 'Signal Settings')

// Visual Settings 
color_candles = input.bool(true, 'Color Candles by Trend?', group = 'Visual Settings')

// Fog Settings 
show_fog = input.bool(true, 'Fog', group = 'Fog')
offset_mult = input.float(0.7, 'Fog Height × Avg Candle', minval = 0.2, step = 0.1, group = 'Fog')
base_transp = input.int(80, 'Base Transparency', minval = 0, maxval = 100, group = 'Fog')
transp_inc = input.int(4, 'Transparency Increment', minval = 0, group = 'Fog')

// Calculations 
avg_candle_size = ta.sma(math.abs(close - open), 50)
offset = avg_candle_size * offset_mult

barcolor(color_candles ? trend_is_bull ? col_up : col_dn : na)

// Fog 
pu0 = plot(show_fog and trend_hist >= 0 ? hl2 : na, display = display.none)
pu1 = plot(show_fog and trend_hist >= 0 ? hl2 - offset : na, display = display.none)
pu2 = plot(show_fog and trend_hist >= 0 ? hl2 - 2 * offset : na, display = display.none)
pu3 = plot(show_fog and trend_hist >= 0 ? hl2 - 3 * offset : na, display = display.none)
pu4 = plot(show_fog and trend_hist >= 0 ? hl2 - 4 * offset : na, display = display.none)
pu5 = plot(show_fog and trend_hist >= 0 ? hl2 - 5 * offset : na, display = display.none)
pu6 = plot(show_fog and trend_hist >= 0 ? hl2 - 6 * offset : na, display = display.none)

pd0 = plot(show_fog and trend_hist < 0 ? hl2 : na, display = display.none)
pd1 = plot(show_fog and trend_hist < 0 ? hl2 + offset : na, display = display.none)
pd2 = plot(show_fog and trend_hist < 0 ? hl2 + 2 * offset : na, display = display.none)
pd3 = plot(show_fog and trend_hist < 0 ? hl2 + 3 * offset : na, display = display.none)
pd4 = plot(show_fog and trend_hist < 0 ? hl2 + 4 * offset : na, display = display.none)
pd5 = plot(show_fog and trend_hist < 0 ? hl2 + 5 * offset : na, display = display.none)
pd6 = plot(show_fog and trend_hist < 0 ? hl2 + 6 * offset : na, display = display.none)

fill(pu0, pu1, color.new(col_up, base_transp))
fill(pu1, pu2, color.new(col_up, base_transp + transp_inc))
fill(pu2, pu3, color.new(col_up, base_transp + 2 * transp_inc))
fill(pu3, pu4, color.new(col_up, base_transp + 3 * transp_inc))
fill(pu4, pu5, color.new(col_up, base_transp + 4 * transp_inc))
fill(pu5, pu6, color.new(col_up, base_transp + 5 * transp_inc))

fill(pd0, pd1, color.new(col_dn, base_transp))
fill(pd1, pd2, color.new(col_dn, base_transp + transp_inc))
fill(pd2, pd3, color.new(col_dn, base_transp + 2 * transp_inc))
fill(pd3, pd4, color.new(col_dn, base_transp + 3 * transp_inc))
fill(pd4, pd5, color.new(col_dn, base_transp + 4 * transp_inc))
fill(pd5, pd6, color.new(col_dn, base_transp + 5 * transp_inc))

// Boxes & Signals
var array<box> boxes = array.new<box>()
var array<int> directions = array.new<int>()

float box_top = use_box_multiplier ? high + avg_candle_size * box_multiplier : high
float box_bottom = use_box_multiplier ? low - avg_candle_size * box_multiplier : low

if signal_bull_cross and (not use_trend_filter or trend_is_bull)
    b = box.new(bar_index, box_top, bar_index + 1, box_bottom, bgcolor = color.new(col_up, 85), border_color = col_up, border_width = 2, extend = extend.right)
    array.push(boxes, b)
    array.push(directions, 1)

if signal_bear_cross and (not use_trend_filter or trend_is_bear)
    b = box.new(bar_index, box_top, bar_index + 1, box_bottom, bgcolor = color.new(col_dn, 85), border_color = col_dn, border_width = 2, extend = extend.right)
    array.push(boxes, b)
    array.push(directions, -1)

var bool buy_event = false
var bool sell_event = false
buy_event := false
sell_event := false

if array.size(boxes) > 0
    for i = array.size(boxes) - 1 to 0 by 1
        b = array.get(boxes, i)
        dir = array.get(directions, i)

        top = box.get_top(b)
        bot = box.get_bottom(b)

        if close > top
            box.set_extend(b, extend.none)
            box.set_right(b, bar_index)
            if not show_only_matching or dir == 1
                buy_event := true
                buy_event
            array.remove(boxes, i)
            array.remove(directions, i)

        if close < bot
            box.set_extend(b, extend.none)
            box.set_right(b, bar_index)
            if not show_only_matching or dir == -1
                sell_event := true
                sell_event
            array.remove(boxes, i)
            array.remove(directions, i)

// Visual Signals 
plotshape(buy_event and signal_type == 'Triangles', '', shape.triangleup, location.belowbar, col_up, size = size.tiny)
plotshape(sell_event and signal_type == 'Triangles', '', shape.triangledown, location.abovebar, col_dn, size = size.tiny)

if buy_event and signal_type == 'Labels (Buy/Sell)'
    label.new(bar_index, low, 'BUY', style = label.style_label_up, color = color.new(col_up, 40), textcolor = color.white, yloc = yloc.belowbar, size = size.small)

if sell_event and signal_type == 'Labels (Buy/Sell)'
    label.new(bar_index, high, 'SELL', style = label.style_label_down, color = color.new(col_dn, 40), textcolor = color.white, yloc = yloc.abovebar, size = size.small)

// TP/SL 
showTPSL = input.bool(true, 'Show TP/SL Levels', group = 'Risk Management')
tpSlMode = input.string('Candle Multiplier', 'TP/SL Calculation Mode', options = ['Candle Multiplier', 'Percentage'], group = 'Risk Management')
tp_sl_length = input.int(50, 'Average Candle Length Period', group = 'Risk Management')

tp1Multiplier = input.float(2.0, 'TP1×', minval = 0.1, step = 0.1, group = 'Risk Management')
tp2Multiplier = input.float(3.0, 'TP2×', minval = 0.1, step = 0.1, group = 'Risk Management')
tp3Multiplier = input.float(4.0, 'TP3×', minval = 0.1, step = 0.1, group = 'Risk Management')
slMultiplier = input.float(2.0, 'SL×', minval = 0.1, step = 0.1, group = 'Risk Management')

tp1Percent = input.float(2.0, 'TP1 %', minval = 0.01, step = 0.01, group = 'Risk Management')
tp2Percent = input.float(3.0, 'TP2 %', minval = 0.01, step = 0.01, group = 'Risk Management')
tp3Percent = input.float(4.0, 'TP3 %', minval = 0.01, step = 0.01, group = 'Risk Management')
slPercent = input.float(1.5, 'SL %', minval = 0.01, step = 0.01, group = 'Risk Management')

var int lastSignal = 0
var float lastClose = na
var float sl = na
var float tp1 = na
var float tp2 = na
var float tp3 = na

var line slLine = na
var line tp1Line = na
var line tp2Line = na
var line tp3Line = na

var table tpSlTable = table.new(position.top_right, 2, 5, frame_color = color.gray, frame_width = 1, border_width = 1)

if showTPSL and (buy_event or sell_event)
    lastSignal := buy_event ? 1 : -1
    lastClose := close

    float avgRange = ta.sma(high - low, tp_sl_length)

    sl := lastSignal == 1 ? tpSlMode == 'Candle Multiplier' ? lastClose - avgRange * slMultiplier : lastClose * (1 - slPercent / 100) : tpSlMode == 'Candle Multiplier' ? lastClose + avgRange * slMultiplier : lastClose * (1 + slPercent / 100)

    tp1 := lastSignal == 1 ? tpSlMode == 'Candle Multiplier' ? lastClose + avgRange * tp1Multiplier : lastClose * (1 + tp1Percent / 100) : tpSlMode == 'Candle Multiplier' ? lastClose - avgRange * tp1Multiplier : lastClose * (1 - tp1Percent / 100)

    tp2 := lastSignal == 1 ? tpSlMode == 'Candle Multiplier' ? lastClose + avgRange * tp2Multiplier : lastClose * (1 + tp2Percent / 100) : tpSlMode == 'Candle Multiplier' ? lastClose - avgRange * tp2Multiplier : lastClose * (1 - tp2Percent / 100)

    tp3 := lastSignal == 1 ? tpSlMode == 'Candle Multiplier' ? lastClose + avgRange * tp3Multiplier : lastClose * (1 + tp3Percent / 100) : tpSlMode == 'Candle Multiplier' ? lastClose - avgRange * tp3Multiplier : lastClose * (1 - tp3Percent / 100)

    line.delete(slLine)
    line.delete(tp1Line)
    line.delete(tp2Line)
    line.delete(tp3Line)

    color tp_color = lastSignal == 1 ? col_up : col_dn
    color sl_color = lastSignal == 1 ? col_dn : col_up

    slLine := line.new(bar_index, sl, bar_index + 5, sl, color = sl_color, width = 2)
    tp1Line := line.new(bar_index, tp1, bar_index + 5, tp1, color = tp_color, width = 1)
    tp2Line := line.new(bar_index, tp2, bar_index + 5, tp2, color = tp_color, width = 1)
    tp3Line := line.new(bar_index, tp3, bar_index + 5, tp3, color = tp_color, width = 1)

    table.cell(tpSlTable, 0, 0, 'Level', text_color = color.white, bgcolor = color.gray)
    table.cell(tpSlTable, 1, 0, 'Price', text_color = color.white, bgcolor = color.gray)
    table.cell(tpSlTable, 0, 1, 'TP1', text_color = tp_color)
    table.cell(tpSlTable, 1, 1, str.tostring(tp1, '#.####'), text_color = tp_color)
    table.cell(tpSlTable, 0, 2, 'TP2', text_color = tp_color)
    table.cell(tpSlTable, 1, 2, str.tostring(tp2, '#.####'), text_color = tp_color)
    table.cell(tpSlTable, 0, 3, 'TP3', text_color = tp_color)
    table.cell(tpSlTable, 1, 3, str.tostring(tp3, '#.####'), text_color = tp_color)
    table.cell(tpSlTable, 0, 4, 'SL', text_color = sl_color)
    table.cell(tpSlTable, 1, 4, str.tostring(sl, '#.####'), text_color = sl_color)

// Alerts 
alertcondition(buy_event, title = 'BUY Signal', message = 'WaveTrend → BUY {{ticker}} @{{close}}')
alertcondition(sell_event, title = 'SELL Signal', message = 'WaveTrend → SELL {{ticker}} @{{close}}')

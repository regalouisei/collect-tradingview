// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Uncle_the_shooter

//@version=6
indicator('Directional Strength and Momentum Index', shorttitle='DSMI', overlay=false)

// DSMI PARAMETERS
length = input.int(20, 'DSMI Period', group='DSMI Parameters')
showDSMILine = input.bool(true, 'Show DSMI Line', group='DSMI Parameters')
dsmiLineColor = input.color(color.rgb(236, 213, 7), 'DSMI Line Color', group='DSMI Parameters')

// EXTREME ZONES (previously OB/OS)
lowerExtreme = input.int(20, 'Lower Extreme Level', minval=1, maxval=99, group='Extreme Zones')
upperExtreme = input.int(80, 'Upper Extreme Level', minval=1, maxval=99, group='Extreme Zones')
bandColor = input.color(color.new(#545559, 0), 'Extreme Zone Line Color', group='Extreme Zones')
outsideGradientColor = input.color(color.new(#545559, 15), 'Gradient Color Outside Zones', group='Extreme Zones')

// TREND STRENGTH LEVELS (DSMI)
weak_thr     = input.int(10, 'Weak Trend – below = neutral', minval=1, maxval=100, group='Trend Strength Levels')
neutral_thr  = input.int(35, 'Moderate Trend – up to', minval=1, maxval=100, group='Trend Strength Levels')
strong_thr   = input.int(45, 'Strong Trend – up to', minval=1, maxval=100, group='Trend Strength Levels')
overheat_thr = input.int(55, 'Overheated Trend – up to', minval=1, maxval=100, group='Trend Strength Levels')

// TREND COLORS (DS, Gradient, Candles, Table)
bullColor    = input.color(color.rgb(6, 162, 47), 'BULLISH Color', group='Trend Colors')
bearColor    = input.color(color.rgb(207, 23, 23), 'BEARISH Color', group='Trend Colors')
neutralColor = input.color(color.new(color.gray, 60), 'NEUTRAL Color', group='Trend Colors')

// GRADIENT FOR DS (30 steps)
gradient_steps = 30
var array<color> grad_bull    = array.new_color(gradient_steps)
var array<color> grad_bear    = array.new_color(gradient_steps)
var array<color> grad_neutral = array.new_color(gradient_steps)

if barstate.isfirst
    for i = 0 to gradient_steps - 1
        float ratio = i / (gradient_steps - 1.0)
        array.set(grad_bull,    i, color.new(bullColor,    80 - ratio * 80))
        array.set(grad_bear,    i, color.new(bearColor,    80 - ratio * 80))
        array.set(grad_neutral, i, color.new(neutralColor, 80 - ratio * 80))

// DSMI CALCULATIONS
candleSize = high - low
direction  = close > open ? 1 : close < open ? -1 : 0
plusDM     = direction > 0 ? candleSize : 0
minusDM    = direction < 0 ? candleSize : 0

plusDM_EMA  = ta.ema(plusDM,  length)
minusDM_EMA = ta.ema(minusDM, length)
candle_EMA  = ta.ema(candleSize, length)

candle_EMA_safe = candle_EMA == 0 ? 1e-10 : candle_EMA

plusDS  = 100 * plusDM_EMA  / candle_EMA_safe
minusDS = 100 * minusDM_EMA / candle_EMA_safe

sumDS = plusDS + minusDS
DX    = sumDS == 0 ? 0.0 : 100 * math.abs(plusDS - minusDS) / sumDS
DSMI  = ta.ema(DX, length)
isBull = plusDS > minusDS

// COLOR BASED ON DSMI STRENGTH
normDSMI = math.max(0, math.min(100, DSMI))
idx_raw  = math.round(normDSMI / 100 * (gradient_steps - 1))
idx      = math.min(math.max(idx_raw, 0), gradient_steps - 1)

trendColor = DSMI < weak_thr ? neutralColor : isBull ? bullColor : bearColor
gradArray  = DSMI < weak_thr ? grad_neutral : isBull ? grad_bull : grad_bear
fillColor  = array.get(gradArray, idx)

// PLOT DSMI & DS LINES
plot(series=showDSMILine ? DSMI : na, title='DSMI', color=dsmiLineColor, linewidth=2)
plotPlus  = plot(plusDS,  '+DS', color=trendColor, linewidth=1)
plotMinus = plot(minusDS, '-DS', color=trendColor, linewidth=1)
fill(plotPlus, plotMinus, color=fillColor)

// EXTREME ZONE LINES + SHADE
hlineUpper = hline(upperExtreme, 'Upper Extreme', color=bandColor, linestyle=hline.style_dotted)
hlineLower = hline(lowerExtreme, 'Lower Extreme', color=bandColor, linestyle=hline.style_dotted)
hlineTop = hline(100, '', color=color.new(color.gray, 100))
hlineBot = hline(0,   '', color=color.new(color.gray, 100))
fill(hlineTop, hlineUpper, color=color.new(outsideGradientColor, 85))
fill(hlineBot, hlineLower, color=color.new(outsideGradientColor, 85))

// ENTRY SIGNALS + ALERTS
showEntrySignal = input.bool(true, 'Show Entry Signal Highlight', group='Entry Signals')
entryLevel      = input.int(20, 'DSMI Entry Level', minval=1, maxval=100, group='Entry Signals')

bool crossAbove = ta.crossover(DSMI, entryLevel)
bool trendChanged = ta.change(isBull ? 1 : 0) != 0
bool alreadyHighAndTrendChange = (DSMI >= entryLevel) and (DSMI[1] >= entryLevel) and trendChanged
bool signalTrigger = (crossAbove or alreadyHighAndTrendChange) and DSMI >= weak_thr  // <--- KLUCZOWY WARUNEK

bool bullEntry = showEntrySignal and signalTrigger and isBull
bool bearEntry = showEntrySignal and signalTrigger and not isBull

bgcolor(bullEntry ? color.new(bullColor, 70) : bearEntry ? color.new(bearColor, 70) : na, title='Entry Signal')

alertcondition(bullEntry, title='DSMI Bullish Entry', message='DSMI >= entry level + BULLISH TREND START!')
alertcondition(bearEntry, title='DSMI Bearish Entry', message='DSMI >= entry level + BEARISH TREND START!')

// CANDLE COLORING BASED ON TREND
showCandleColor = input.bool(true, 'Color Candles by Trend', group='Candle Coloring')

var color barColorTrend = na
if showCandleColor
    barColorTrend := DSMI < weak_thr ? neutralColor : isBull ? bullColor : bearColor
else
    barColorTrend := na

barcolor(barColorTrend)

// TREND STRENGTH TABLE
var table t = table.new(position.top_right, 2, 1, bgcolor=color.new(color.black, 80), border_width=1, border_color=color.gray)

if barstate.islast
    strength = DSMI <= weak_thr ? 'WEAK' : DSMI <= neutral_thr ? 'MODERATE' : DSMI <= strong_thr ? 'STRONG' : DSMI <= overheat_thr ? 'OVERHEATED' : 'EXTREME'
    dsmi_text = str.tostring(math.round(DSMI))
    table.cell(t, 0, 0, '', bgcolor=trendColor, text_color=na)
    table.cell(t, 1, 0, strength + ' ' + dsmi_text, text_color=color.white, bgcolor=color.new(color.black, 70))

//@version=6
//#region PREAMBLE
// â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’
//  Copyright Â© 2022-present Iason Nikolas | jason5480
//  The "Signal Adaptor" Pine Scriptâ„¢ code may be freely distributed under the MIT license.
//  
//  Permission is hereby granted, free of charge,
//  to any person obtaining a copy of this software and associated documentation files (the "Software"),
//  to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge,
//  publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software
//  is furnished to do so, subject to the following conditions:
//  
//  The above copyright notice and this permission notice shall be included in ALL COPIES or substantial portions of
//  the Software.
//  
//  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE
//  WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS
//  OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT
//  OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
//  
//  Description
//  --------------------------------------------------------------------------------------------------------------------
//  This Signal Adapter script can compose a signal based on inputs from other simple (non-signal) indicators and can
//  forward it to the "Template Trailing Strategy (Backtester)". It allows the user to combine up to eight external
//  inputs and define the conditions that will trigger the start, end, cancel start and cancel end deals. A signal will
//  be composed from those user-defined conditions. The "indicator on indicator" feature is needed so you can forward the
//  resulted signal to the "Template Trailing Strategy (Backtester)". Thus you should be Plus or Premium user to get it's
//  full potential. It is very convenient for those who want to create a strategy without coding their own signal indicator
//  and for those who want to fast prototype various ideas based on simple conditions.
//  
//  --------------------------------------------------------------------------------------------------------------------
//  Disclaimer:
//    1. I am not licensed financial advisors or broker-dealer. I do not tell you when or what to buy or sell.
//       I developed this software which enables you execute manual or automated trades using TradingView.
//       The software allows you to set the criteria you want for entering and exiting trades.
//    2. Do not trade with money you cannot afford to lose.
//    3. I do not guarantee consistent profits or that anyone can make money with no effort.
//       And I am not selling the holy grail.
//    4. Every system can have winning and losing streaks.
//    5. Money management plays a large role in the results of your trading. For example: lot size, account size,
//       broker leverage, and broker margin call rules all affect on results. Also, your Take Profit and Stop Loss
//       settings for individual pair trades and for overall account equity have a major impact on results.
//       If you are new to trading and do not understand these items, then I recommend you seek educational materials
//       to further your knowledge.
//    
//  YOU NEED TO FIND AND USE THE TRADING SYSTEM THAT WORKS BEST FOR YOU AND YOUR TRADING TOLERANCE.
//  I HAVE PROVIDED NOTHING MORE THAN A TOOL WITH OPTIONS FOR YOU TO TRADE WITH THIS PROGRAM ON TRADINGVIEW.
//  
//  I accept suggestions to improve the script.
//  If you encounter any problems I will be happy to share with me.
//  --------------------------------------------------------------------------------------------------------------------
//#endregion ===========================================================================================================
//#region SETUP
// â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’
indicator(title = 'Signal Adapter',
          shorttitle = 'SA',
          overlay = true)

//#endregion ===========================================================================================================
//#region ðŸ’¡ LOGIC
// â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’

import jason5480/external_input_utils/11 as exiu

// INPUT ===============================================================================================================
const string TT_EXTERNAL_INPUT_INFO = ' INFORMATION! The indicator should be added to the same chart with this strategy and the desired value that will take part in the conditions below should be plotted in the chart.'
input1 = input.source(defval = close, title = 'External Input1 ðŸ›ˆ', tooltip = 'Select the first input from an external indicator.' + TT_EXTERNAL_INPUT_INFO, group = 'Variables')
input2 = input.source(defval = close, title = 'External Input2 ðŸ›ˆ', tooltip = 'Select the second input from an external indicator.' + TT_EXTERNAL_INPUT_INFO, group = 'Variables')
input3 = input.source(defval = close, title = 'External Input3 ðŸ›ˆ', tooltip = 'Select the third input from an external indicator.' + TT_EXTERNAL_INPUT_INFO, group = 'Variables')
input4 = input.source(defval = close, title = 'External Input4 ðŸ›ˆ', tooltip = 'Select the fourth input from an external indicator.' + TT_EXTERNAL_INPUT_INFO, group = 'Variables')
input5 = input.source(defval = close, title = 'External Input5 ðŸ›ˆ', tooltip = 'Select the fifth input from an external indicator.' + TT_EXTERNAL_INPUT_INFO, group = 'Variables')
input6 = input.source(defval = close, title = 'External Input6 ðŸ›ˆ', tooltip = 'Select the sixth input from an external indicator.' + TT_EXTERNAL_INPUT_INFO, group = 'Variables')
input7 = input.source(defval = close, title = 'External Input7 ðŸ›ˆ', tooltip = 'Select the seventh input from an external indicator.' + TT_EXTERNAL_INPUT_INFO, group = 'Variables')
input8 = input.source(defval = close, title = 'External Input8 ðŸ›ˆ', tooltip = 'Select the eighth input from an external indicator.' + TT_EXTERNAL_INPUT_INFO, group = 'Variables')

value1 = input.float(defval = 0.0, title = 'Value1', tooltip = 'Type the first value and select it below to be used in the conditions.', group = 'Variables')
value2 = input.float(defval = 0.0, title = 'Value2', tooltip = 'Type the second value and select it below to be used in the conditions.', group = 'Variables')
value3 = input.float(defval = 0.0, title = 'Value3', tooltip = 'Type the third value and select it below to be used in the conditions.', group = 'Variables')
value4 = input.float(defval = 0.0, title = 'Value4', tooltip = 'Type the fourth value and select it below to be used in the conditions.', group = 'Variables')
value5 = input.float(defval = 0.0, title = 'Value5', tooltip = 'Type the fifth value and select it below to be used in the conditions.', group = 'Variables')
value6 = input.float(defval = 0.0, title = 'Value6', tooltip = 'Type the sixth value and select it below to be used in the conditions.', group = 'Variables')
value7 = input.float(defval = 0.0, title = 'Value7', tooltip = 'Type the seventh value and select it below to be used in the conditions.', group = 'Variables')
value8 = input.float(defval = 0.0, title = 'Value8', tooltip = 'Type the eighth value and select it below to be used in the conditions.', group = 'Variables')

enum VariableId
    in1 = 'Input1'
    in2 = 'Input2'
    in3 = 'Input3'
    in4 = 'Input4'
    in5 = 'Input5'
    in6 = 'Input6'
    in7 = 'Input7'
    in8 = 'Input8'
    val1 = 'Value1'
    val2 = 'Value2'
    val3 = 'Value3'
    val4 = 'Value4'
    val5 = 'Value5'
    val6 = 'Value6'
    val7 = 'Value7'
    val8 = 'Value8'
    sl = 'StartLong'
    ss = 'StartShort'
    el = 'EndLong'
    es = 'EndShort'

startLongDealInputId = input.enum(defval = VariableId.in1, title = 'Start Long whenâ€ƒâ€ƒâ€†', options = [VariableId.in1, VariableId.in2, VariableId.in3, VariableId.in4, VariableId.in5, VariableId.in6, VariableId.in7, VariableId.in8], inline = 'Start Long Deal', group = 'Conditions')
startLongDealOperator = input.enum(defval = exiu.Operator.crossover, title = '', options = [exiu.Operator.eq, exiu.Operator.l, exiu.Operator.g, exiu.Operator.le, exiu.Operator.ge, exiu.Operator.ne, exiu.Operator.crossover, exiu.Operator.crossunder, exiu.Operator.rising, exiu.Operator.falling, exiu.Operator.na, exiu.Operator.nna, exiu.Operator.noop], inline = 'Start Long Deal', group = 'Conditions')
startLongDealVariableId = input.enum(defval = VariableId.val1, title = '', options = [VariableId.in1, VariableId.in2, VariableId.in3, VariableId.in4, VariableId.in5, VariableId.in6, VariableId.in7, VariableId.in8, VariableId.val1, VariableId.val2, VariableId.val3, VariableId.val4, VariableId.val5, VariableId.val6, VariableId.val7, VariableId.val8], tooltip = 'Condition to start a Long Deal based on external input.', inline = 'Start Long Deal', group = 'Conditions')

endLongDealInputId = input.enum(defval = VariableId.in1, title = 'End Long whenâ€ƒâ€ƒâ€„â€„', options = [VariableId.in1, VariableId.in2, VariableId.in3, VariableId.in4, VariableId.in5, VariableId.in6, VariableId.in7, VariableId.in8], inline = 'End Long Deal', group = 'Conditions')
endLongDealOperator = input.enum(defval = exiu.Operator.crossunder, title = '', options = [exiu.Operator.eq, exiu.Operator.l, exiu.Operator.g, exiu.Operator.le, exiu.Operator.ge, exiu.Operator.ne, exiu.Operator.crossover, exiu.Operator.crossunder, exiu.Operator.rising, exiu.Operator.falling, exiu.Operator.na, exiu.Operator.nna, exiu.Operator.noop], inline = 'End Long Deal', group = 'Conditions')
endLongDealVariableId = input.enum(defval = VariableId.val1, title = '', options = [VariableId.in1, VariableId.in2, VariableId.in3, VariableId.in4, VariableId.in5, VariableId.in6, VariableId.in7, VariableId.in8, VariableId.val1, VariableId.val2, VariableId.val3, VariableId.val4, VariableId.val5, VariableId.val6, VariableId.val7, VariableId.val8], tooltip = 'Condition to end a Long Deal based on external input.', inline = 'End Long Deal', group = 'Conditions')

cnlStartLongDealInputId = input.enum(defval = VariableId.in1, title = 'Cnl Start Long whenâ€„', options = [VariableId.in1, VariableId.in2, VariableId.in3, VariableId.in4, VariableId.in5, VariableId.in6, VariableId.in7, VariableId.in8, VariableId.ss], inline = 'Cnl Start Long Deal', group = 'Conditions')
cnlStartLongDealOperator = input.enum(defval = exiu.Operator.noop, title = '', inline = 'Cnl Start Long Deal', group = 'Conditions')
cnlStartLongDealVariableId = input.enum(defval = VariableId.val1, title = '', options = [VariableId.in1, VariableId.in2, VariableId.in3, VariableId.in4, VariableId.in5, VariableId.in6, VariableId.in7, VariableId.in8, VariableId.val1, VariableId.val2, VariableId.val3, VariableId.val4, VariableId.val5, VariableId.val6, VariableId.val7, VariableId.val8], tooltip = 'Condition to cancel the start Long Deal if not already entered the position based on external input.', inline = 'Cnl Start Long Deal', group = 'Conditions')

cnlEndLongDealInputId = input.enum(defval = VariableId.in1, title = 'Cnl End Long whenâ€‡â€„', options = [VariableId.in1, VariableId.in2, VariableId.in3, VariableId.in4, VariableId.in5, VariableId.in6, VariableId.in7, VariableId.in8, VariableId.es], inline = 'Cnl End Long Deal', group = 'Conditions')
cnlEndLongDealOperator = input.enum(defval = exiu.Operator.noop, title = '', inline = 'Cnl End Long Deal', group = 'Conditions')
cnlEndLongDealVariableId = input.enum(defval = VariableId.val1, title = '', options = [VariableId.in1, VariableId.in2, VariableId.in3, VariableId.in4, VariableId.in5, VariableId.in6, VariableId.in7, VariableId.in8, VariableId.val1, VariableId.val2, VariableId.val3, VariableId.val4, VariableId.val5, VariableId.val6, VariableId.val7, VariableId.val8], tooltip = 'Condition to cancel the end Long Deal if not already exited the position based on external input.', inline = 'Cnl End Long Deal', group = 'Conditions')

startShortDealInputId = input.enum(defval = VariableId.in1, title = 'Start Short whenâ€ƒâ€‚â€…', options = [VariableId.in1, VariableId.in2, VariableId.in3, VariableId.in4, VariableId.in5, VariableId.in6, VariableId.in7, VariableId.in8], inline = 'Start Short Deal', group = 'Conditions')
startShortDealOperator = input.enum(defval = exiu.Operator.crossunder, title = '', options = [exiu.Operator.eq, exiu.Operator.l, exiu.Operator.g, exiu.Operator.le, exiu.Operator.ge, exiu.Operator.ne, exiu.Operator.crossover, exiu.Operator.crossunder, exiu.Operator.rising, exiu.Operator.falling, exiu.Operator.na, exiu.Operator.nna, exiu.Operator.noop], inline = 'Start Short Deal', group = 'Conditions')
startShortDealVariableId = input.enum(defval = VariableId.val1, title = '', options = [VariableId.in1, VariableId.in2, VariableId.in3, VariableId.in4, VariableId.in5, VariableId.in6, VariableId.in7, VariableId.in8, VariableId.val1, VariableId.val2, VariableId.val3, VariableId.val4, VariableId.val5, VariableId.val6, VariableId.val7, VariableId.val8], tooltip = 'Condition to start a Short Deal based on external input.', inline = 'Start Short Deal', group = 'Conditions')

endShortDealInputId = input.enum(defval = VariableId.in1, title = 'End Short whenâ€ƒâ€ƒâ€„', options = [VariableId.in1, VariableId.in2, VariableId.in3, VariableId.in4, VariableId.in5, VariableId.in6, VariableId.in7, VariableId.in8], inline = 'End Short Deal', group = 'Conditions')
endShortDealOperator = input.enum(defval = exiu.Operator.crossover, title = '', options = [exiu.Operator.eq, exiu.Operator.l, exiu.Operator.g, exiu.Operator.le, exiu.Operator.ge, exiu.Operator.ne, exiu.Operator.crossover, exiu.Operator.crossunder, exiu.Operator.rising, exiu.Operator.falling, exiu.Operator.na, exiu.Operator.nna, exiu.Operator.noop], inline = 'End Short Deal', group = 'Conditions')
endShortDealVariableId = input.enum(defval = VariableId.val1, title = '', options = [VariableId.in1, VariableId.in2, VariableId.in3, VariableId.in4, VariableId.in5, VariableId.in6, VariableId.in7, VariableId.in8, VariableId.val1, VariableId.val2, VariableId.val3, VariableId.val4, VariableId.val5, VariableId.val6, VariableId.val7, VariableId.val8], tooltip = 'Condition to end a Short Deal based on external input.', inline = 'End Short Deal', group = 'Conditions')

cnlStartShortDealInputId = input.enum(defval = VariableId.in1, title = 'Cnl Start Short when', options = [VariableId.in1, VariableId.in2, VariableId.in3, VariableId.in4, VariableId.in5, VariableId.in6, VariableId.in7, VariableId.in8, VariableId.sl], inline = 'Cnl Start Short Deal', group = 'Conditions')
cnlStartShortDealOperator = input.enum(defval = exiu.Operator.noop, title = '', inline = 'Cnl Start Short Deal', group = 'Conditions')
cnlStartShortDealVariableId = input.enum(defval = VariableId.val1, title = '', options = [VariableId.in1, VariableId.in2, VariableId.in3, VariableId.in4, VariableId.in5, VariableId.in6, VariableId.in7, VariableId.in8, VariableId.val1, VariableId.val2, VariableId.val3, VariableId.val4, VariableId.val5, VariableId.val6, VariableId.val7, VariableId.val8], tooltip = 'Condition to cancel the start Short Deal if not already entered the position based on external input.', inline = 'Cnl Start Short Deal', group = 'Conditions')

cnlEndShortDealInputId = input.enum(defval = VariableId.in1, title = 'Cnl End Short whenâ€‚', options = [VariableId.in1, VariableId.in2, VariableId.in3, VariableId.in4, VariableId.in5, VariableId.in6, VariableId.in7, VariableId.in8, VariableId.el], inline = 'Cnl End Short Deal', group = 'Conditions')
cnlEndShortDealOperator = input.enum(defval = exiu.Operator.noop, title = '', inline = 'Cnl End Short Deal', group = 'Conditions')
cnlEndShortDealVariableId = input.enum(defval = VariableId.val1, title = '', options = [VariableId.in1, VariableId.in2, VariableId.in3, VariableId.in4, VariableId.in5, VariableId.in6, VariableId.in7, VariableId.in8, VariableId.val1, VariableId.val2, VariableId.val3, VariableId.val4, VariableId.val5, VariableId.val6, VariableId.val7, VariableId.val8], tooltip = 'Condition to cancel the end Short Deal if not already exited the position based on external input.', inline = 'Cnl End Short Deal', group = 'Conditions')

// LOGIC ===============================================================================================================
getVariable(simple VariableId id) =>
    switch id
        VariableId.in1 => input1
        VariableId.in2 => input2
        VariableId.in3 => input3
        VariableId.in4 => input4
        VariableId.in5 => input5
        VariableId.in6 => input6
        VariableId.in7 => input7
        VariableId.in8 => input8
        VariableId.val1 => value1
        VariableId.val2 => value2
        VariableId.val3 => value3
        VariableId.val4 => value4
        VariableId.val5 => value5
        VariableId.val6 => value6
        VariableId.val7 => value7
        VariableId.val8 => value8
        => na

// Compute the start, end conditions first so we can use them as inputs to the cancel conditions later on when barsince is used
bool startLong = exiu.evalCond(getVariable(startLongDealInputId), startLongDealOperator, getVariable(startLongDealVariableId))
bool startShort = exiu.evalCond(getVariable(startShortDealInputId), startShortDealOperator, getVariable(startShortDealVariableId))
bool endLong = exiu.evalCond(getVariable(endLongDealInputId), endLongDealOperator, getVariable(endLongDealVariableId))
bool endShort = exiu.evalCond(getVariable(endShortDealInputId), endShortDealOperator, getVariable(endShortDealVariableId))

// Extend the options for the cancel conditions
getVariableOrCond(simple VariableId id) =>
    ret = getVariable(id)
    if na(ret)
        ret := switch id
            VariableId.sl => startLong ? 1.0 : 0.0
            VariableId.ss => startShort ? 1.0 : 0.0
            VariableId.el => endLong ? 1.0 : 0.0
            VariableId.es => endShort ? 1.0 : 0.0
            => na
    ret

//#endregion ===========================================================================================================
//#region ðŸ”Œ SIGNAL
// â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’

import jason5480/tts_convention/7 as conv

// LOGIC ===============================================================================================================
// Calculate the start, end, cancel start, cancel end conditions according to the user-defined logic
dealConditions = conv.DealConditions.new(
  startLongDeal = startLong,
  startShortDeal = startShort,
  endLongDeal = endLong,
  endShortDeal = endShort,
  cnlStartLongDeal = exiu.evalCond(getVariableOrCond(cnlStartLongDealInputId), cnlStartLongDealOperator, getVariable(cnlStartLongDealVariableId)),
  cnlStartShortDeal = exiu.evalCond(getVariableOrCond(cnlStartShortDealInputId), cnlStartShortDealOperator, getVariable(cnlStartShortDealVariableId)),
  cnlEndLongDeal = exiu.evalCond(getVariableOrCond(cnlEndLongDealInputId), cnlEndLongDealOperator, getVariable(cnlEndLongDealVariableId)),
  cnlEndShortDeal = exiu.evalCond(getVariableOrCond(cnlEndShortDealInputId), cnlEndShortDealOperator, getVariable(cnlEndShortDealVariableId)))

// Use this signal in scripts like "Template Trailing Strategy (Backtester)" and "Signal Composer" that can utilize its value
// Emit the current signal value according to the TTS framework convention
plot(series = conv.getSignal(dealConditions), title = 'ðŸ”ŒSignal to TTS', color = #808000, editable = false, display = display.data_window + display.status_line, precision = 0)

// PLOT ================================================================================================================
plotshape(series = dealConditions.startLongDeal, title = 'Start Long', style = shape.triangleup, location = location.abovebar, color = color.new(#0074BA, 70), offset = 1, size = size.small, display = display.pane + display.data_window)
plotshape(series = dealConditions.endLongDeal, title = 'End Long', style = shape.square, location = location.belowbar, color = color.new(#C790F1, 70), offset = 1, size = size.tiny, display = display.pane + display.data_window)
plotshape(series = dealConditions.cnlStartLongDeal, title = 'Cancel Start Long', style = shape.xcross, location = location.abovebar, color = color.new(#0074BA, 70), size = size.tiny, display = display.pane + display.data_window)
plotshape(series = dealConditions.cnlEndLongDeal, title = 'Cancel End Long', style = shape.xcross, location = location.belowbar, color = color.new(#C790F1, 70), size = size.tiny, display = display.pane + display.data_window)
plotshape(series = dealConditions.startShortDeal, title = 'Start Short', style = shape.triangledown, location = location.belowbar, color = color.new(#FF6723, 70), offset = 1, size = size.small, display = display.pane + display.data_window)
plotshape(series = dealConditions.endShortDeal, title = 'End Short', style = shape.square, location = location.abovebar, color = color.new(#C790F1, 70), offset = 1, size = size.tiny, display = display.pane + display.data_window)
plotshape(series = dealConditions.cnlStartShortDeal, title = 'Cancel Start Short', style = shape.xcross, location = location.belowbar, color = color.new(#FF6723, 70), size = size.tiny, display = display.pane + display.data_window)
plotshape(series = dealConditions.cnlEndShortDeal, title = 'Cancel End Short', style = shape.xcross, location = location.abovebar, color = color.new(#C790F1, 70), size = size.tiny, display = display.pane + display.data_window)

//#endregion ===========================================================================================================
//#region ðŸ”” ALERT MESSAGES
// â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’â–’

// LOGIC ===============================================================================================================
if dealConditions.startLongDeal and barstate.isconfirmed
    alert(message = 'Start Long')

if dealConditions.endLongDeal and barstate.isconfirmed
    alert(message = 'End Long')

if dealConditions.cnlStartLongDeal and barstate.isconfirmed
    alert(message = 'Cancel Start Long')

if dealConditions.cnlEndLongDeal and barstate.isconfirmed
    alert(message = 'Cancel End Long')

if dealConditions.startShortDeal and barstate.isconfirmed
    alert(message = 'Start Short')

if dealConditions.endShortDeal and barstate.isconfirmed
    alert(message = 'End Short')

if dealConditions.cnlStartShortDeal and barstate.isconfirmed
    alert(message = 'Cancel Start Short')

if dealConditions.cnlEndShortDeal and barstate.isconfirmed
    alert(message = 'Cancel End Short')

//#endregion ===========================================================================================================

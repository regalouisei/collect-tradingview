// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © MUQWISHI

//@version=5
indicator("Annual Returns %", overlay = true)

// |++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++|
// |                                   INPUT                                    |
// |++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++|
// +++++++++++++++ Plot Settings
var G1 = "Plot Settings"
// Location 
tablePos = input.string("Bottom Center", "Plot Location              ", 
             ["Top Right" , "Middle Right"  , "Bottom Right" , 
              "Top Center", "Middle Center" , "Bottom Center", 
              "Top Left"  , "Middle Left"   , "Bottom Left" ], group = G1, inline = "0")

// Plot Color
pltCol  = input.color(color.new(#101888, 50),  "", group = G1, inline = "0")

// Bar Size
brHt = input.float(0.3, "Bar Height | Width ", 0.2, step = 0.01, group = G1, inline = "1")
brWh = input.float(1.5, "" , 1.0,                   step = 0.01, group = G1, inline = "1")

// +++++++++++++++ Technical Settings
var G2 = "Technical Settings"
sDate  = input.int(1999, "Start | End Year      ", 1900, group = G2, inline = "0")
eDate  = input.int(2050, ""  , 1900,                     group = G2, inline = "0")

// Symbol
s01r = input.color(color.orange,  "Symbol 1    ", inline = "3", group = G2)
s01c = input.string("Chart Symbol", "", ["Chart Symbol" , "Custom"], inline = "3", group = G2)
s01v = input.symbol("AMEX:SPY",     "", inline = "3", tooltip = "Only Vaild With {Symbol 1 = 'Custom'}", group = G2)

s02r = input.color(color.blue,  "Symbol 2    ", inline = "5", group = G2)
s02c = input.string("Custom",     "", ["Chart Symbol" , "Custom"], inline = "5", group = G2)
s02v = input.symbol("NASDAQ:QQQ", "", inline = "5", tooltip = "Only Vaild With {Symbol 2 = 'Custom'}", group = G2)

// Adjustment
adjst  = input.bool(true, "Adjust Data for Dividends?", group = G2)

// |++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++|
// |                                 CALCULATION                                |
// |++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++|
main() =>
    var chg = array.new<float>(na)
    var tim = array.new<float>(na)

    yr = year(time, syminfo.timezone)
    if yr >= sDate and yr <= eDate
        if not na((close-close[1])/close[1])
            chg.push(math.round((close-close[1])/close[1] * 100, 2)) 
            tim.push(yr)

    if chg.size() > 40 
        chg.shift()
        tim.shift()
   
    [chg, tim]


// Importing Data
adjut(c, s0) =>
    s   = c == "Custom" ? s0 : syminfo.ticker
    out = adjst ? '={"adjustment":"dividends", "symbol":"' + s + '"}' : s

[chg1, tim1] = request.security(adjut(s01c, s01v), "12M", main())
[chg2, tim2] = request.security(adjut(s02c, s02v), "12M", main())

// Creating Array
chg01 = array.new<float>(na)
tim01 = array.new<float>(na)
if not na(chg1) and not na(tim1)
    chg01 := chg1, tim01 := tim1

chg02 = array.new<float>(na)
tim02 = array.new<float>(na)
if not na(chg2) and not na(tim2)
    chg02 := chg2, tim02 := tim2

//  Equalize size of arrays
if barstate.islast
    if chg01.size() == 0 and chg02.size() == 0 
        runtime.error("Data is not available. Please enter a valid symbol and date range")
    else
        if tim01.size() > 0
            for i = 0 to tim01.size() - 1
                if tim02.indexof(tim01.get(i)) == -1
                    tim02.insert(i, tim01.get(i))
                    chg02.insert(i, 0)

        if tim02.size() > 0
            for i = 0 to tim02.size() - 1
                if tim01.indexof(tim02.get(i)) == -1
                    tim01.insert(i, tim02.get(i))
                    chg01.insert(i, 0)


// Find the Maximum Number for Plot scale
coeff01  = math.max(math.abs(chg01.min()), math.abs(chg01.max()))
coeff02  = math.max(math.abs(chg02.min()), math.abs(chg02.max()))
scalCoef = 30/math.max(coeff01, coeff02)

// |++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++|
// |                                   TABLE                                    |
// |++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++|
// +++++++++++++++ Get Symbol Name
symbol(s) => 
    str.split(s, ":").get(1)

// +++++++++++++++ Get Tbale Location & Size
locNsze(x) => 
    y   = str.split(str.lower(x), " ")
    out = ""
    for i = 0 to y.size() - 1
        out := out + y.get(i)
        if i != y.size() - 1
            out := out + "_"
    out

// +++++++++++++++ Create Table Plot 
mxSz = math.max(tim01.size(), tim02.size())
nCol = mxSz * 3 + 2 < 17 ? 17 : mxSz * 3 + 2
nRow = 68
var table plt = na 
if mxSz > 0
    plt := table.new(locNsze(tablePos), nCol, nRow, pltCol, chart.fg_color, 1, color(na), 0)

// +++++++++++++++ Plot Cell
pltCell (x, y, w, h, col, tip) =>
    plt.cell(x, y, width = w, height = h, bgcolor = col, tooltip = tip)

// +++++++++++++++ Plot Columns
pltClmn (x, y, k, col) =>
    pltCell(x, y > 0 ? 33 - k : y < 0 ? 35 - k : 33, brWh, brHt, 
     color.new(col,   0), str.tostring(y, "#.##")+"%")
    pltCell(x, y > 0 ? 35 + k : y < 0 ? 33 + k : 33, brWh, brHt, 
     color.new(col, 100), "")

// +++++++++++++++ Label Y-Axis Increments
yInc(y, p, val) =>
    valign = p == 1 ? text.align_top : p == -1 ? text.align_bottom :  text.align_center
    plt.cell(0, y-2, str.tostring(val, "#") + "%", 0, 0.005, chart.fg_color,  text.align_center, 
     valign, size.tiny, text_font_family = font.family_monospace)
    plt.merge_cells(0, y-2, 0, y+2)

// +++++++++++++++ Add Legend
legend(xi, xf, txt, txtCol) =>
    plt.cell(xi, 0, txt, 0, 0, txtCol, text.align_left, text_size = size.small, bgcolor = chart.bg_color)
    table.merge_cells(plt, xi, 0, xf, 0)


// +++++++++++++++ Create Table
if not na(plt) and barstate.islast 
    table.clear(plt, 0, 0, nCol-1, nRow-1)

    // Reset Table. 
    for i = 0 to nCol - 1
        for j = 0 to 67
            pltCell(i, j, 0.001, 0.001, color.new(color.white, 100), "")

    // Plot Columns 
    w = 0
    for i = 2 to mxSz * 3 + 1 by 3
        if math.round(scalCoef * chg01.get(w)) == 0
            pltClmn(i, chg01.get(w), 0, color.new(s01r, 0))
        else 
            for k = 0 to math.round(scalCoef * chg01.get(w))
                pltClmn(i, chg01.get(w), k, s01r)
        
        if math.round(scalCoef * chg02.get(w)) == 0
            pltClmn( i+1, chg02.get(w), 0, s02r) 
        else
            for h = 0 to math.round(scalCoef * chg02.get(w))
                pltClmn( i+1, chg02.get(w), h, s02r)
        
        // X-Axis Increments
        plt.cell(i, 67, str.tostring(tim01.get(w)), brWh, 0, chart.fg_color, text_size = size.small)
        plt.merge_cells(i, 67, i+1, 67)
        
        w := w + 1

    // Plot Highlighted Color Between Interval
    for i = 1 to mxSz * 3 + 1 by 3
        for j = 0 to 67
            pltCell(i, j, 0.3, 0.001, color.new(chart.fg_color, 94), "")

    // Draw X Axis Line (X==0)
    for i = 0 to (mxSz * 3 + 1 < 16 ? 16 : mxSz * 3 + 1)
        if i > 1 
            pltCell(i, 34, 0.4, 0.1, chart.fg_color, "")
        pltCell(i, 01, 0.1, 0.1, color.new(chart.fg_color, 0), "")
    
    // Y-Axis Increments
    yInc( 4,  1, math.ceil(  29/scalCoef))
    yInc(18,  0, math.round( 15/scalCoef))
    yInc(34,  0, math.round(  0/scalCoef))
    yInc(50,  0, math.round(-15/scalCoef))
    yInc(64, -1, math.floor(-29/scalCoef))

    // Legends
    legend(00, 10, "Annual Returns %", chart.fg_color)
    legend(11, 13, s01c == "Custom" ? symbol(s01v) : syminfo.ticker, s01r)
    legend(14, 16, s02c == "Custom" ? symbol(s02v) : syminfo.ticker, s02r)

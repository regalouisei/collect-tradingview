// This work is licensed under a Attribution-NonCommercial-ShareAlike 4.0 International (CC BY-NC-SA 4.0) https://creativecommons.org/licenses/by-nc-sa/4.0/
// © LuxAlgo

//@version=6
indicator('Arbitrage Detector [LuxAlgo]','LuxAlgo - Arbitrage Detector', calc_bars_count = 10000)
//---------------------------------------------------------------------------------------------------------------------}
//CONSTANTS & STRINGS & INPUTS
//---------------------------------------------------------------------------------------------------------------------{
RED                     = #F23645
GREEN                   = #089981
BLUE                    = #297090
OCEAN                   = #1FA084
JUNGLE                  = #49C161
YELLOW                  = #F2E805

AUTO                    = 'Auto'
CRYPTO_EXCHANGES        = 'Crypto Exchanges'
FX_BROKERS              = 'Forex Brokers'

TOP_RIGHT               = 'Top Right'
BOTTOM_RIGHT            = 'Bottom Right'
BOTTOM_LEFT             = 'Bottom Left'

TINY                    = 'Tiny'
SMALL                   = 'Small'
NORMAL                  = 'Normal'
LARGE                   = 'Large'
HUGE                    = 'Huge'

PRICE_LAST              = 'Last Price'
VOLUME_LAST             = 'Last Volume'
PRICE_AVG               = 'Avg Price'
VOLUME_AVG              = 'Avg Volume'

NONE                    = 'None'
ASCENDING               = 'Ascending'
DESCENDING              = 'Descending'

ARROW_UP                = '↑'
ARROW_DOWN              = '↓'

PERCENTILES_GROUP       = 'Percentiles'
DASHBOARD_GROUP         = 'Dashboard'
STYLE_GROUP             = 'Style'

EM_SPACE                = ' '
EN_SPACE                = ' '
FOUR_PER_EM_SPACE       = ' '
HAIR_SPACE              = ' '

sortingSpacing          = EM_SPACE+EM_SPACE+EM_SPACE+EM_SPACE+EN_SPACE+HAIR_SPACE
unusualSpacing          = EM_SPACE+EN_SPACE+FOUR_PER_EM_SPACE+HAIR_SPACE+HAIR_SPACE
highSpacing             = EM_SPACE+EM_SPACE+EM_SPACE+FOUR_PER_EM_SPACE+HAIR_SPACE+HAIR_SPACE
typicalSpacing          = EM_SPACE+EM_SPACE+FOUR_PER_EM_SPACE
sortingTag              = 'Sorting'+sortingSpacing
spreadTag               = 'Percentile Length'

sourceTooltip           = 'Choose between crypto exchanges, forex brokers, or automatic selection based on the asset in the chart.'
averageLengthTooltip    = 'Select the length for the price and volume averages.'

spreadTooltip           = 'Select the length for the percentile calculation, or enable the use of the full dataset. Enabling this option may result in runtime errors due to exceeding the allotted resources.'
unusualTooltip          = 'Select the unusual percentile.'
highlTooltip            = 'Select the high percentile.'
typicalTooltip          = 'Select the typical percentile.'

dashboardTooltip        = 'Enable or disable the dashboard.'
dashboardSortingTooltip = 'Select the sorting column and direction.'
dashboardPositionTooltip= 'Select the dashboard location.'
dashboardSizeTooltip    = 'Select the dashboard size.'
priceDeltaTooltip       = 'Show the price difference between each exchange and the asset on the chart.'
volumeDeltaTooltip      = 'Show the volume difference between each exchange and the asset on the chart.'

unusualColorTooltip     = 'Enable the plot of the unusual percentile and select its color.'
highColorTooltip        = 'Enable the plot of the high percentile and select its color.'
typicalColorTooltip     = 'Enable the plot of the typical percentile and select its color.'
lowColorTooltip         = 'Select the color for the low percentile.'
autoColorTooltip        = 'Enable auto color for all plotted percentiles.'
gradientColorTooltip    = 'Enable the gradient color for the histogram.'
smoothingTooltip        = 'Select the length of the EMA smoothing for the histogram or enable the Auto feature. The Auto feature uses an adaptive moving average with the data percent rank as the efficiency ratio.'

sourceInput             = input.string( AUTO,       'Sources',          tooltip = sourceTooltip,        options = [AUTO,CRYPTO_EXCHANGES,FX_BROKERS])
averageLengthInput      = input.int(    20,         'Average Length',   tooltip = averageLengthTooltip)

spreadLengthInput       = input.int(    1000,       spreadTag,      group = PERCENTILES_GROUP, tooltip = '',            inline = 'spread')
spreadAllDataInput      = input.bool(   false,      'Full Dataset', group = PERCENTILES_GROUP, tooltip = spreadTooltip, inline = 'spread')
unusualPercentilInput   = input.int(    98,         'Unusual % >',  group = PERCENTILES_GROUP, tooltip = unusualTooltip)
highPercentilInput      = input.int(    75,         'High % >',     group = PERCENTILES_GROUP, tooltip = highlTooltip)
typicalPercentilInput   = input.int(    25,         'Typical % >',  group = PERCENTILES_GROUP, tooltip = typicalTooltip)

dashboardInput          = input.bool(   true,       'Dashboard',    group=DASHBOARD_GROUP, tooltip = dashboardTooltip)
orderColumnInput        = input.string( PRICE_LAST, sortingTag,     group=DASHBOARD_GROUP, tooltip = '',                        inline = 'sorting', options = [PRICE_LAST, PRICE_AVG, VOLUME_LAST, VOLUME_AVG])
orderBiasInput          = input.string( DESCENDING, '',             group=DASHBOARD_GROUP, tooltip = dashboardSortingTooltip,   inline = 'sorting', options = [NONE, ASCENDING, DESCENDING])
dashboardPositionInput  = input.string( TOP_RIGHT,  'Position',     group=DASHBOARD_GROUP, tooltip = dashboardPositionTooltip , options = [TOP_RIGHT,BOTTOM_RIGHT,BOTTOM_LEFT])
dashboardSizeInput      = input.string( SMALL,      'Size',         group=DASHBOARD_GROUP, tooltip = dashboardSizeTooltip,      options = [TINY,SMALL,NORMAL,LARGE,HUGE])
priceDeltaInput         = input.bool(   true,       'Price Delta',  group=DASHBOARD_GROUP, tooltip = priceDeltaTooltip)
volumeDeltaInput        = input.bool(   false,      'Volume Delta', group=DASHBOARD_GROUP, tooltip = volumeDeltaTooltip)

unusualInput            = input.bool(   true,       'Unusual',      group = STYLE_GROUP, tooltip = '',                  inline = 'showUnusual')
unusualColorInput       = input.color(  YELLOW,     unusualSpacing, group = STYLE_GROUP, tooltip = unusualColorTooltip, inline = 'showUnusual')
highInput               = input.bool(   true,       'High',         group = STYLE_GROUP, tooltip = '',                  inline = 'showHigh')
highColorInput          = input.color(  JUNGLE,     highSpacing,    group = STYLE_GROUP, tooltip = highColorTooltip,    inline = 'showHigh')
typicalInput            = input.bool(   true,       'Typical',      group = STYLE_GROUP, tooltip = '',                  inline = 'showTypical')
typicalColorInput       = input.color(  OCEAN,      typicalSpacing, group = STYLE_GROUP, tooltip = typicalColorTooltip, inline = 'showTypical')
lowColorInput           = input.color(  BLUE,       'Low',          group = STYLE_GROUP, tooltip = lowColorTooltip)
autoColorInput          = input.bool(   false,      'Percentiles Auto Color',   group = STYLE_GROUP, tooltip = autoColorTooltip)
gradientColorInput      = input.bool(   true,       'Histogram Gradient',       group = STYLE_GROUP, tooltip = gradientColorTooltip)
smoothingLengthInput    = input.int(    9,          'Histogram Smoothing',      group = STYLE_GROUP, tooltip = '',                  inline = 'smoothing')
autoSmoothingInput      = input.bool(   true,       'Auto',                     group = STYLE_GROUP, tooltip = smoothingTooltip,    inline = 'smoothing')

//---------------------------------------------------------------------------------------------------------------------}
//DATA STRUCTURES & VARIABLES
//---------------------------------------------------------------------------------------------------------------------{
type asset
    array<float> prices
    array<float> volumes

type columns
    array<float> lastPrices
    array<float> lastVolumes
    array<float> avgPrices
    array<float> avgVolumes
    float rows = 0

var array<string> crypto    = array.from('BITSTAMP','COINBASE','CRYPTO','BINANCE','KRAKEN','OKX','GEMINI','CRYPTOCOM','WEBULLPAY','BINANCEUS','BTSE','WHITEBIT','BYBIT','KUCOIN','MEXC','BITGET','COINEX','HTX','GATE')
var array<string> forex     = array.from('TICKMILL','FX','OANDA','FOREXCOM','PEPPERSTONE','CMCMARKETS','ICMARKETS','CAPITALCOM','VANTAGE','EIGHTCAP','SAXO','IBKR','IG')
var array<string> exchanges = sourceInput == AUTO ? (syminfo.type == 'crypto' ? crypto : forex) : sourceInput == CRYPTO_EXCHANGES ? crypto : forex

var array<float> spreads    = array.new<float>()
var columns currentColumns  = columns.new(array.new<float>(),array.new<float>(),array.new<float>(),array.new<float>())
var asset chartAsset        = asset.new(array.new<float>(),array.new<float>())
var array<asset> assets     = array.from(asset.new(array.new<float>(),array.new<float>()),asset.new(array.new<float>(),array.new<float>()),asset.new(array.new<float>(),array.new<float>()),asset.new(array.new<float>(),array.new<float>()),
     asset.new(array.new<float>(),array.new<float>()),asset.new(array.new<float>(),array.new<float>()),asset.new(array.new<float>(),array.new<float>()),asset.new(array.new<float>(),array.new<float>()),asset.new(array.new<float>(),array.new<float>()),
     asset.new(array.new<float>(),array.new<float>()),asset.new(array.new<float>(),array.new<float>()),asset.new(array.new<float>(),array.new<float>()),asset.new(array.new<float>(),array.new<float>()),asset.new(array.new<float>(),array.new<float>()),
     asset.new(array.new<float>(),array.new<float>()),asset.new(array.new<float>(),array.new<float>()),asset.new(array.new<float>(),array.new<float>()),asset.new(array.new<float>(),array.new<float>()),asset.new(array.new<float>(),array.new<float>()))

var float unusualPercentil  = na
var float highPercentil     = na
var float typicalPercentil  = na
var float averageSpread     = 0
var float ama               = 0

var parsedDashboardPosition = switch dashboardPositionInput
    TOP_RIGHT       => position.top_right
    BOTTOM_RIGHT    => position.bottom_right
    BOTTOM_LEFT     => position.bottom_left

var parsedDashboardSize     = switch dashboardSizeInput
    TINY            => size.tiny
    SMALL           => size.small
    NORMAL          => size.normal
    LARGE           => size.large
    HUGE            => size.huge

//---------------------------------------------------------------------------------------------------------------------}
//USER-DEFINED FUNCTIONS
//---------------------------------------------------------------------------------------------------------------------{
sortedIndex() =>
    if orderBiasInput != NONE
        switch orderColumnInput
            PRICE_LAST  => currentColumns.lastPrices.sort_indices(  orderBiasInput == ASCENDING ? order.ascending : order.descending)
            VOLUME_LAST => currentColumns.lastVolumes.sort_indices( orderBiasInput == ASCENDING ? order.ascending : order.descending)
            PRICE_AVG   => currentColumns.avgPrices.sort_indices(   orderBiasInput == ASCENDING ? order.ascending : order.descending)
            VOLUME_AVG  => currentColumns.avgVolumes.sort_indices(  orderBiasInput == ASCENDING ? order.ascending : order.descending)
    else
        na

clearColumns() =>
    currentColumns.lastPrices.clear()
    currentColumns.lastVolumes.clear()
    currentColumns.avgPrices.clear()
    currentColumns.avgVolumes.clear()

gatherData() =>
    string ticker       = syminfo.ticker
    array<float> prices = array.new<float>()
    clearColumns()
    
    chartAsset.prices.push(nz(close))
    chartAsset.volumes.push(nz(volume))
    if chartAsset.prices.size() > averageLengthInput
        chartAsset.prices.shift()
        chartAsset.volumes.shift()
    
    for [index,eachExchange] in exchanges
        string tickerid = eachExchange+':'+ticker        
        [assetPrice,assetVolume] = request.security(tickerid,'',[close,volume], ignore_invalid_symbol = true)              
        
        if not na(assetPrice)
            prices.push(assetPrice)

        assets.get(index).prices.push(nz(assetPrice))
        assets.get(index).volumes.push(nz(assetVolume))

        if assets.get(index).prices.size() > averageLengthInput
            assets.get(index).prices.shift()
            assets.get(index).volumes.shift()

        currentColumns.lastPrices.push(assets.get(index).prices.last())
        currentColumns.lastVolumes.push(assets.get(index).volumes.last())
        currentColumns.avgPrices.push(assets.get(index).prices.avg())
        currentColumns.avgVolumes.push(assets.get(index).volumes.avg())

    currentColumns.rows := prices.size()
    spreads.push(prices.range())
    
    if not spreadAllDataInput and spreads.size() > spreadLengthInput
        spreads.shift()

header() =>
    float spreadPercentRank = spreads.percentrank(spreads.size() - 1)
    string occurrences = switch
        spreadPercentRank > unusualPercentilInput   => 'Unusual'
        spreadPercentRank > highPercentilInput      => 'High'
        spreadPercentRank > typicalPercentilInput   => 'Typical'
        => 'Low'

    str.format('{0} | {1} Spread {2,number,#%} ',syminfo.ticker,occurrences,spreadPercentRank/100)

plotColorSolid() =>
    float spreadPercentRank = spreads.percentrank(spreads.size() - 1)
    switch
        spreadPercentRank > unusualPercentilInput   => unusualColorInput
        spreadPercentRank > highPercentilInput      => highColorInput
        spreadPercentRank > typicalPercentilInput   => typicalColorInput
        => lowColorInput

plotColorGradient() =>
    float spreadPercentRank = spreads.percentrank(spreads.size() - 1)
    
    switch
        spreadPercentRank > unusualPercentilInput   => color.new(color.from_gradient(spreadPercentRank,unusualPercentilInput,100,highColorInput,unusualColorInput),0)
        spreadPercentRank > highPercentilInput      => color.new(color.from_gradient(spreadPercentRank,highPercentilInput,unusualPercentilInput,typicalColorInput,highColorInput),25)
        spreadPercentRank > typicalPercentilInput   => color.new(color.from_gradient(spreadPercentRank,typicalPercentilInput,highPercentilInput,lowColorInput,typicalColorInput),50)
        => color.new(color.from_gradient(spreadPercentRank,1,typicalPercentilInput,color.new(lowColorInput,20),lowColorInput),75)

plotColor() => gradientColorInput ? plotColorGradient() : plotColorSolid()

cell(table t_able, int column, int row, string data, color = color.white, align = text.align_right, color background = na) => t_able.cell(column,row,data,text_color = color, text_size = parsedDashboardSize, text_halign = align, bgcolor = background)

row(table t_able, int row, int index) =>        
    if assets.get(index).prices.avg() != 0
        cell(t_able,0,row + 2,exchanges.get(index),align = text.align_left)
        cell(t_able,1,row + 2,str.tostring(assets.get(index).prices.last(),format.mintick) + (priceDeltaInput ? ' (' + str.tostring(assets.get(index).prices.last()-chartAsset.prices.last(),format.mintick) + ')' : ''), background = orderColumnInput == PRICE_LAST and orderBiasInput != NONE ? color.from_gradient(row,2,currentColumns.rows + 2, color.new(GREEN,orderBiasInput == ASCENDING ? 90 : 10),   color.new(GREEN,orderBiasInput == ASCENDING ? 10 : 90)) : na)
        cell(t_able,2,row + 2,str.tostring(assets.get(index).prices.avg(),format.mintick) + (priceDeltaInput ? ' (' + str.tostring(assets.get(index).prices.avg()-chartAsset.prices.avg(),format.mintick) + ')' : ''), background = orderColumnInput == PRICE_AVG and orderBiasInput != NONE ? color.from_gradient(row,2,currentColumns.rows + 2, color.new(GREEN,orderBiasInput == ASCENDING ? 90 : 10),   color.new(GREEN,orderBiasInput == ASCENDING ? 10 : 90)) : na)
        
        if assets.get(index).volumes.avg() != 0        
            cell(t_able,3,row + 2,str.tostring(assets.get(index).volumes.last(),format.volume) + (volumeDeltaInput ? ' (' +str.tostring(assets.get(index).volumes.last()-chartAsset.volumes.last(),format.volume) + ')' : ''), background = orderColumnInput == VOLUME_LAST and orderBiasInput != NONE ? color.from_gradient(row,2,currentColumns.rows + 2, color.new(GREEN,orderBiasInput == ASCENDING ? 90 : 10),   color.new(GREEN,orderBiasInput == ASCENDING ? 10 : 90)) : na)
            cell(t_able,4,row + 2,str.tostring(assets.get(index).volumes.avg(),format.volume) + (volumeDeltaInput ? ' (' +str.tostring(assets.get(index).volumes.avg()-chartAsset.volumes.avg(),format.volume) + ')' : ''), background = orderColumnInput == VOLUME_AVG and orderBiasInput != NONE ? color.from_gradient(row,2,currentColumns.rows + 2, color.new(GREEN,orderBiasInput == ASCENDING ? 90 : 10),   color.new(GREEN,orderBiasInput == ASCENDING ? 10 : 90)) : na)
        
drawDashboard() =>
    var table t_able    = table.new(parsedDashboardPosition,5,21
     , bgcolor          = #1e222d
     , border_color     = #373a46
     , border_width     = 1
     , frame_color      = #373a46
     , frame_width      = 1
     , force_overlay    = true)   
    
    t_able.merge_cells(0,0,4,0)
    cell(t_able,0,0,header(),   align = text.align_center, color = color.silver)
    cell(t_able,0,1,'Exchange', align = text.align_left)
    cell(t_able,1,1,'Last Price'    + (orderColumnInput == PRICE_LAST   and orderBiasInput != NONE ? ' ' + (orderBiasInput == ASCENDING ? ARROW_UP : ARROW_DOWN) : ''), align = text.align_center)
    cell(t_able,2,1,'Avg Price'     + (orderColumnInput == PRICE_AVG    and orderBiasInput != NONE ? ' ' + (orderBiasInput == ASCENDING ? ARROW_UP : ARROW_DOWN) : ''), align = text.align_center)
    cell(t_able,3,1,'Last Volume'   + (orderColumnInput == VOLUME_LAST  and orderBiasInput != NONE ? ' ' + (orderBiasInput == ASCENDING ? ARROW_UP : ARROW_DOWN) : ''), align = text.align_center)
    cell(t_able,4,1,'Avg Volume'    + (orderColumnInput == VOLUME_AVG   and orderBiasInput != NONE ? ' ' + (orderBiasInput == ASCENDING ? ARROW_UP : ARROW_DOWN) : ''), align = text.align_center)
    
    array<int> sortedIndex = sortedIndex()

    if not na(sortedIndex)
        for [index, eachIndex] in sortedIndex            
            row(t_able,index,eachIndex)
    else
        for [index,eachExchange] in exchanges
            row(t_able,index,index)

//---------------------------------------------------------------------------------------------------------------------}
//MUTABLE VARIABLES & EXECUTION
//---------------------------------------------------------------------------------------------------------------------{
if barstate.isconfirmed
    gatherData()

if barstate.islast
    if dashboardInput
        drawDashboard()

float currentSpread     = spreads.last()
float spreadPercentRank = spreads.percentrank(spreads.size() - 1)
color customColor       = plotColor()
ama                     += nz((0.01 * spreadPercentRank) * (currentSpread - ama))
averageSpread           := autoSmoothingInput ? ama : ta.ema(currentSpread,smoothingLengthInput)

plot(currentSpread,'Spread',customColor,style = plot.style_columns)
plot(averageSpread,'Average Spread',chart.fg_color)

if barstate.isconfirmed
    unusualPercentil  := unusualInput   ? spreads.percentile_nearest_rank(unusualPercentilInput)    : na
    highPercentil     := highInput      ? spreads.percentile_nearest_rank(highPercentilInput)       : na
    typicalPercentil  := typicalInput   ? spreads.percentile_nearest_rank(typicalPercentilInput)    : na

plot(unusualPercentil,'Unusual', autoColorInput ? chart.fg_color : unusualColorInput,   linestyle = plot.linestyle_dashed)
plot(highPercentil,   'High',    autoColorInput ? chart.fg_color : highColorInput,      linestyle = plot.linestyle_dashed)
plot(typicalPercentil,'Typical', autoColorInput ? chart.fg_color : typicalColorInput,   linestyle = plot.linestyle_dashed)

//---------------------------------------------------------------------------------------------------------------------}

// This work is licensed under a Attribution-NonCommercial-ShareAlike 4.0 International (CC BY-NC-SA 4.0) https://creativecommons.org/licenses/by-nc-sa/4.0/
// © LuxAlgo

//@version=5
indicator("Daily Chess Puzzles [LuxAlgo]", "LuxAlgo - Daily Chess Puzzles", overlay = true)

//---------------------------------------------------------------------------------------------------------------------}
//Puzzle Data
//---------------------------------------------------------------------------------------------------------------------{

// Single Script Token Limit is 71000, But tokens in the script from Libraries can be up to 1 Million
// Here we are importing 10 years of daily puzzle data is roughly 200k - 250k tokens, so with each script holding 2 years, we have 5 libraries.

import SamRecio/Chess_Data_1/1 as data1
import SamRecio/Chess_Data_2/1 as data2
import SamRecio/Chess_Data_3/1 as data3
import SamRecio/Chess_Data_4/1 as data4
import SamRecio/Chess_Data_5/1 as data5

//---------------------------------------------------------------------------------------------------------------------}
//Functions
//---------------------------------------------------------------------------------------------------------------------{

is_odd(_num) => (_num/2) - math.floor(_num/2) > 0

get_piece(_str) =>
    switch
        _str == "P" => "♙"
        _str == "N" => "♘"
        _str == "B" => "♗"
        _str == "R" => "♖"
        _str == "Q" => "♕"
        _str == "K" => "♔"
        _str == "p" => "♟"
        _str == "n" => "♞"
        _str == "b" => "♝"
        _str == "r" => "♜"
        _str == "q" => "♛"
        _str == "k" => "♚"
        _str == "E" => ""

num_to_let(_num) => 
    switch
        _num == 1 => "a"
        _num == 2 => "b"
        _num == 3 => "c"
        _num == 4 => "d"
        _num == 5 => "e"
        _num == 6 => "f"
        _num == 7 => "g"
        _num == 8 => "h"

let_to_num(_let) => 
    switch
        _let == "a" => 1
        _let == "b" => 2
        _let == "c" => 3
        _let == "d" => 4
        _let == "e" => 5
        _let == "f" => 6
        _let == "g" => 7
        _let == "h" => 8

pos_to_pos(_str,_iswhite) =>
    split = str.split(_str,"")
    switch
        split.size() == 4 => 
            int c1 = let_to_num(split.get(0))
            int r1 = int(9 - str.tonumber(split.get(1)))
            int c2 = let_to_num(split.get(2))
            int r2 = int(9 - str.tonumber(split.get(3)))
            [c1,r1,c2,r2,na]
        split.size() == 5 => 
            int c1 = let_to_num(split.get(0))
            int r1 = int(9 - str.tonumber(split.get(1)))
            int c2 = let_to_num(split.get(2))
            int r2 = int(9 - str.tonumber(split.get(3)))
            p = split.get(4)
            [c1,r1,c2,r2,(_iswhite?str.upper(p):str.lower(p))]

num_to_E(_str) =>
    str.replace_all(str.replace_all(str.replace_all(str.replace_all(str.replace_all(str.replace_all(str.replace_all(str.replace_all(_str,"8","EEEEEEEE"),"7","EEEEEEE"),"6","EEEEEE"),"5","EEEEE"),"4","EEEE"),"3","EEE"),"2","EE"),"1","E")

get_colors(_style) =>
    c1 = switch
        _style == "Classic" =>  #F0D9B5
        _style == "Modern" => #FFFFFF
        _style == "Red" => #FFEBE6  
        _style == "Green" => #eeeed2
        _style == "Blue" => #D9E2EC
        
    c2 = switch
        _style == "Classic" =>  #B58863
        _style == "Modern" =>#7B7B7B
        _style == "Red" => #993333       
        _style == "Green" =>#769656
        _style == "Blue" => #3C6E71
        
    c3 = switch
        _style == "Classic" =>  #5A3921
        _style == "Modern" => #3D3D3D
        _style == "Red" => #5e1f1f     
        _style == "Green" => #4A622A
        _style == "Blue" => #284B63
        
    [c1,c2,c3]

get_day(_yd) =>
    today = math.floor(timenow/86400000)-(_yd?1:0)
    full_day_num = today - math.floor(today/3650)*3650
    full_day_num


get_puzzle(_num) =>
    index_num = _num - math.floor(_num/730)*730
    ary_num = math.floor(_num/730)
    data = switch
        ary_num == 1 => data2.get()
        ary_num == 2 => data3.get()
        ary_num == 3 => data4.get()
        ary_num == 4 => data5.get()
        => data1.get()
    puzzle = str.split(array.get(data,index_num),",")
    puzzle

//---------------------------------------------------------------------------------------------------------------------}
//Inputs
//---------------------------------------------------------------------------------------------------------------------{
answer_input = input.string("", title = "Answer:", group = "Actions", inline = "answer", tooltip = "Move Notation: [Starting Square][Ending Square]\nExample: e2e4\n\nFor Pawn Promotion, add piece to the end.\nPromotion Pieces: [n,b,r,q]\nExample: e7e8q")
submit_answer = input.bool(false, title = "⬅ Submit Answer", inline = "answer", group = "Actions")
y_day = input.bool(false, title = "View Yesterday's Puzzle", group = "Generation")
tile_style = input.string("Classic", title = "Theme", options = ["Classic","Modern","Red","Green","Blue"], confirm = true, group = "Style")

puzzle_num = get_day(y_day)
//puzzle_num = input.int(0,title = "Puzzle Number", maxval = 3649, minval = 0)            //For Manual Control [Un-Comment This Line] and [Comment out the Line Above]

//---------------------------------------------------------------------------------------------------------------------}
//Colors
//---------------------------------------------------------------------------------------------------------------------{

[c1,c2,c3] = get_colors(tile_style)
black = color.rgb(0,0,0)
white = color.rgb(255,255,255)
move_color = color.yellow

//---------------------------------------------------------------------------------------------------------------------}
//Table (Board) Actions
//---------------------------------------------------------------------------------------------------------------------{

board = table.new("bottom_right",10,11, frame_width = 2, frame_color = c3)
board.merge_cells(0,10,4,10)
board.merge_cells(5,10,9,10)

if barstate.islast
    //Data Setup
    puzzle = get_puzzle(puzzle_num)
    start = num_to_E(puzzle.get(1))
    positions = str.split(start,"/")
    amc = positions.last() == "w" ? black : positions.last() == "b" ? white : na // Active Move Color
    anti_amc = positions.last() == "b" ? black : positions.last() == "w" ? white : na 
    puzzle_code = puzzle.get(0) //Lichess Puzzle Code
    //Board Setup
    board.cell(0,10, text = "Puzzle Code: " + puzzle_code + "", tooltip = "Case Sensitive", text_color = anti_amc, bgcolor = amc, height = 3, text_font_family = font.family_monospace)
    board.cell(5,10, text = "Rating: " + puzzle.get(3), text_color = anti_amc, bgcolor = amc, height = 3, text_font_family = font.family_monospace)
    board.cell(0,0, text = "◲", text_color = amc, bgcolor = c3, text_font_family = font.family_monospace, text_halign = text.align_right, text_valign = text.align_bottom)
    board.cell(9,0, text = "◱", text_color = amc, bgcolor = c3, text_font_family = font.family_monospace, text_halign = text.align_left, text_valign = text.align_bottom)
    board.cell(0,9, text = "◳", text_color = amc, bgcolor = c3, text_font_family = font.family_monospace, text_halign = text.align_right, text_valign = text.align_top)
    board.cell(9,9, text = "◰", text_color = amc, bgcolor = c3, text_font_family = font.family_monospace, text_halign = text.align_left, text_valign = text.align_top)
    //Populating board with starting positions
    for i = 1 to 8
        board.cell(9,i, text = str.tostring(9-i), text_color = c1, bgcolor = c3, text_font_family = font.family_monospace, text_halign = text.align_left,text_valign = text.align_center)
        board.cell(i,9, text = num_to_let(i), text_color = c1, bgcolor = c3, text_font_family = font.family_monospace, text_halign = text.align_center, text_valign = text.align_top)
        board.cell(0,i, text = str.tostring(9-i), text_color = c1, bgcolor = c3, text_font_family = font.family_monospace, text_halign = text.align_right, text_valign = text.align_center)
        board.cell(i,0, text = num_to_let(i), text_color = c1, bgcolor = c3, text_font_family = font.family_monospace, text_halign = text.align_center, text_valign = text.align_bottom)
        for e = 1 to 8
            i_odd = is_odd(i)
            e_odd = is_odd(e)
            col = switch
                i_odd and e_odd => c1
                i_odd and not e_odd => c2
                not i_odd and not e_odd => c1
                not i_odd and e_odd => c2
            p = get_piece(str.split(positions.get(i-1),"").get(e-1))
            board.cell(e,i, text = p, text_color = black, bgcolor = col,width = 3,  height = 6, text_size = size.huge, text_font_family = font.family_monospace, tooltip = num_to_let(e) + str.tostring(9-i))

    //Moves
    m_split = str.split(puzzle.get(2)," ")
    //Setting First Move
    [m1_c1,m1_r1,m1_c2,m1_r2,m1_p] = pos_to_pos(m_split.get(0),positions.last() == "w")
    p = get_piece(str.split(positions.get(m1_r1-1),"").get(m1_c1-1))
    board.cell_set_text(m1_c1,m1_r1,"")
    board.cell_set_text(m1_c2,m1_r2, text = na(m1_p)?p:get_piece(m1_p))
    //Display where move colors, to show what piece moved to where, only show this if there is no answer. Once an answer is submitted, this piece is no longer active.
    if not submit_answer
        board.cell_set_bgcolor(m1_c1,m1_r1,move_color)
        board.cell_set_bgcolor(m1_c2,m1_r2,move_color)
    //User's Move from Answer Input
    if str.length(answer_input) >= 4 and submit_answer
        [m2_c1,m2_r1,m2_c2,m2_r2,m2_p] = pos_to_pos(answer_input,positions.last() == "b")
        moved_p = get_piece(str.split(positions.get(m2_r1-1),"").get(m2_c1-1))
        board.cell_set_text(m2_c1,m2_r1,"")
        board.cell_set_text(m2_c2,m2_r2, text = na(m2_p)?moved_p:get_piece(m2_p))
        board.cell_set_bgcolor(m2_c1,m2_r1,move_color)
        board.cell_set_bgcolor(m2_c2,m2_r2,move_color)
        //Green if Right
        if answer_input == m_split.get(1)
            board.set_frame_color(color.rgb(0,255,0))
            board.set_frame_width(5)
        //Red if Wrong
        if answer_input != m_split.get(1)
            board.set_frame_color(color.rgb(255,0,0))
            board.set_frame_width(5)

//---------------------------------------------------------------------------------------------------------------------}

//@version=5
// ══════════════════════════════════════════════════════════════════════════════════════════════════ //
//# * ══════════════════════════════════════════════════════════════════════════════════════════════
//# *
//# * Study       : Auto PitchFan, Fib Extension / Retracement and Zig Zag
//# *                - Auto PitchFan
//# *                - Auto Fib Extension / Retracement
//# *                - Zig Zag
//# *               derived from build-in Auto Fib Retracement
//# * Author      : © dgtrd
//# *
//# * Revision History
//# *  Release    : Apr 26, 2021
//# *  Update     : Feb 12, 2022 : Thanks to New Pine Feature, aadded backgroud coloring option
//# *                              Additionally, imporved label customizations and converted to Pine v5
//# *
//# * ══════════════════════════════════════════════════════════════════════════════════════════════
// ══════════════════════════════════════════════════════════════════════════════════════════════════ //

indicator('Auto PitchFan, Fib Extension/Retracement and ZigZag by DGT', 'PitchFan ʙʏ DGT ☼☾', true, max_lines_count = 500)

// -Inputs ══════════════════════════════════════════════════════════════════════════════════════ //

tooltip_threshold = 'Deviation is a multiplier that affects how much the price should deviate from the previous pivot in order for the bar to become a new pivot' + 
                     '\n\nDepth affects the minimum number of bars that will be taken into account when building'
tooltip_pitch     = 'A Pitchfan is a set of rays spreading out of the point of a trend\'s beginning. These rays inclined with the coefficients formed by a Fibonacci number sequence' + 
                     '\nIt is recommended to apply a Pitchfan after the first wave of the trend has passed and the correction has clearly begun'

i_dev_thresh = ta.atr(10) / close * 100 * input.float(3, 'Deviation', minval=0, inline='Pivots', tooltip=tooltip_threshold)
i_depth = input.int(11, '         Depth', minval=1, inline='Pivots')
i_histPivot = input.int(0, 'Historical PitchFan/Fibonacci Levels   ', minval=0)

group_pitch = 'PitchFan Settings'

i_pPitch  = input.bool(true, 'PitchFan Rays      |     Background', inline = 'pfan', group=group_pitch, tooltip=tooltip_pitch)
i_bgPitch = input.bool(true, '', inline = 'pfan', group=group_pitch)

i_pLabels = input.string('Levels (Prices)', 'PitchFan Level Labels', options=['Levels', 'Prices', 'Levels (Prices)', 'None'], inline='pLines', group=group_pitch)
i_levelsS = input.string('Small', '', options=['Small', 'Normal'], inline='pLines', group=group_pitch)

i_pcolor_m = input.color(#f44336, 'Median Line          ', inline='PLM', group=group_pitch)
i_pwidth_m = input.int(3, '', minval=1, inline='PLM', group=group_pitch)
i_pstyle_m = input.string('Solid', '', options=['Dashed', 'Dotted', 'Solid'], inline='PLM', group=group_pitch)

group_zz  = 'Zig Zag Settings'
i_zigZag  = input.bool(false, 'Zig Zag   ', inline='ZZ', group=group_zz)
i_zzColor = input.color(color.orange, '', inline='ZZ', group=group_zz)
i_zzWidth = input.int(1, '', minval=1, inline='ZZ', group=group_zz)
i_zzStyle = input.string('Solid', '', options=['Dashed', 'Dotted', 'Solid'], inline='ZZ', group=group_zz)

group_fib = 'Fibonacci Retracement/Extension Settings'

i_isFib   = input.bool(false, 'Pick a Fibonacci Tool', inline='FIB', group=group_fib)
i_fibTool = input.string('Retracements', '             ', options=['Retracements', 'Extentions'], inline='FIB', group=group_fib)

i_reverse = input.bool(false, 'Reverse Levels   ', inline='fLines', group=group_fib)
i_extend  = input.bool(false, 'Extend Lines', inline='fLines', group=group_fib)

i_levels  = input.string('Levels', 'Level Labels', options=['Levels', 'Prices', 'Levels (Prices)', 'None'], inline='fLines1', group=group_fib)
i_levelsP = input.string('Pivot Start', '', options=['Last Bar', 'Pivot Start'], inline='fLines1', group=group_fib)
i_levelsF = input.string('Small', '', options=['Small', 'Normal'], inline='fLines1', group=group_fib)

// -Calculations ════════════════════════════════════════════════════════════════════════════════ //

var line lineLast = na
var int iLast   = 0
var int iPrev   = 0
var float pLast = 0
var isHighLast  = false  // otherwise the last pivot is a low pivot

var iPrevPivotRef = 0
var pPrevPivotRef = 0.
var iLastPivotRef = 0
var pLastPivotRef = 0.

pivots(src, length, isHigh) =>
    l2 = length * 2
    c = nz(src[length])
    ok = true

    for i = 0 to l2 by 1
        if isHigh and src[i] > c
            ok := false
            ok

        if not isHigh and src[i] < c
            ok := false
            ok
    if ok
        [bar_index[length], c]
    else
        [int(na), float(na)]

[iH, pH] = pivots(high, i_depth / 2, true )
[iL, pL] = pivots(low , i_depth / 2, false)

calc_dev(base_price, price) =>
    100 * (price - base_price) / price

pivotFound(dev, isHigh, index, price) =>
    if isHighLast == isHigh and not na(lineLast)
        // same direction
        if isHighLast ? price > pLast : price < pLast
            line.set_xy2(lineLast, index, price)
            [lineLast, isHighLast]
        else
            [line(na), bool(na)]
    else
        // reverse the direction (or create the very first line)
        if math.abs(dev) > i_dev_thresh
            // price move is significant

            // ---------------------------------------------------------------------------------------- //
            [zzCol, zzWid, zzSty] = if not i_zigZag
                [na, 1, line.style_dashed]
            else
                [i_zzColor, i_zzWidth, i_zzStyle == 'Solid' ? line.style_solid : i_zzStyle == 'Dotted' ? line.style_dotted : line.style_dashed]
            // ---------------------------------------------------------------------------------------- //

            id = line.new(iLast, pLast, index, price, color=zzCol, width=zzWid, style=zzSty)
            [id, isHigh]
        else
            [line(na), bool(na)]

if not na(iH)
    dev = calc_dev(pLast, pH)
    [id, isHigh] = pivotFound(dev, true, iH, pH)

    if not na(id)
        if id != lineLast
            // ---------------------------------------------------------------------------------------- //

            iPrevPivotRef := line.get_x1(lineLast)
            pPrevPivotRef := line.get_y1(lineLast)
            iLastPivotRef := line.get_x2(lineLast)
            pLastPivotRef := line.get_y2(lineLast)

            if not i_zigZag
            // ---------------------------------------------------------------------------------------- //

                line.delete(lineLast)

        lineLast := id
        isHighLast := isHigh
        iPrev := iLast
        iLast := iH
        pLast := pH
        pLast
else
    if not na(iL)
        dev = calc_dev(pLast, pL)
        [id, isHigh] = pivotFound(dev, false, iL, pL)

        if not na(id)
            if id != lineLast
                // ---------------------------------------------------------------------------------------- //

                iPrevPivotRef := line.get_x1(lineLast)
                pPrevPivotRef := line.get_y1(lineLast)
                iLastPivotRef := line.get_x2(lineLast)
                pLastPivotRef := line.get_y2(lineLast)

                if not i_zigZag
                // ---------------------------------------------------------------------------------------- //

                    line.delete(lineLast)

            lineLast := id
            isHighLast := isHigh
            iPrev := iLast
            iLast := iL
            pLast := pL
            pLast


var a_lnr  = array.new_line()
if ta.change(time) and array.size(a_lnr) > 0
    for i = 1 to array.size(a_lnr)
        line.delete(array.shift(a_lnr))

var a_lns  = array.new_line()
if ta.change(time) and array.size(a_lns) > 0
    for i = 1 to array.size(a_lns)
        line.delete(array.shift(a_lns))

var a_lf  = array.new_linefill()
if ta.change(time) and array.size(a_lf)   > 0
    for i = 1 to array.size(a_lf)
        linefill.delete(array.shift(a_lf))

f_drawPitchLine(_iStart, _pStart, _iEnd, _pEnd, _color, _width, _style, hg) =>
    style = _style == 'Solid' ? line.style_solid : _style == 'Dotted' ? line.style_dotted : line.style_dashed
    if _iEnd < bar_index
        if hg
            array.push(a_lnr, line.new(_iStart, _pStart, _iEnd, _pEnd, xloc.bar_index, extend.right, _color, style, _width))
        else
            array.push(a_lns, line.new(_iStart, _pStart, _iEnd, _pEnd, xloc.bar_index, extend.right, _color, style, _width))

f_drawLineX(_x1, _y1, _x2, _y2, _xloc, _extend, _color, _style, _width) =>
    var id = line.new(_x1, _y1, _x2, _y2, _xloc, _extend, _color, _style, _width)
    line.set_xy1(id, _x1, _y1)
    line.set_xy2(id, _x2, _y2)

f_drawLabelX(_x, _y, _text, _xloc, _yloc, _color, _style, _textcolor, _size, _textalign, _tooltip) =>
    var id = label.new(_x, _y, _text, _xloc, _yloc, _color, _style, _textcolor, _size, _textalign, _tooltip)
    label.set_xy(id, _x, _y)
    label.set_text(id, _text + '\n\n')
    label.set_tooltip(id, _tooltip)

f_crossingLevel(_curret, _level) =>
    _level > _curret and _level < _curret[1] or _level < _curret and _level > _curret[1]

iStartMedian = i_histPivot > 0 ? ta.valuewhen(ta.change(iPrevPivotRef), iPrevPivotRef, i_histPivot + 1) : ta.valuewhen(ta.change(iPrevPivotRef), iPrevPivotRef, 1)
pStartMedian = i_histPivot > 0 ? ta.valuewhen(ta.change(pPrevPivotRef), pPrevPivotRef, i_histPivot + 1) : ta.valuewhen(ta.change(pPrevPivotRef), pPrevPivotRef, 1)
iLastPivot   = i_histPivot > 0 ? ta.valuewhen(ta.change(iLastPivotRef), iLastPivotRef, i_histPivot)     : ta.valuewhen(ta.change(iLastPivotRef), iLastPivotRef, 0)
pLastPivot   = i_histPivot > 0 ? ta.valuewhen(ta.change(pLastPivotRef), pLastPivotRef, i_histPivot)     : ta.valuewhen(ta.change(pLastPivotRef), pLastPivotRef, 0)
iPrevPivot   = i_histPivot > 0 ? ta.valuewhen(ta.change(iPrevPivotRef), iPrevPivotRef, i_histPivot)     : ta.valuewhen(ta.change(iPrevPivotRef), iPrevPivotRef, 0)
pPrevPivot   = i_histPivot > 0 ? ta.valuewhen(ta.change(pPrevPivotRef), pPrevPivotRef, i_histPivot)     : ta.valuewhen(ta.change(pPrevPivotRef), pPrevPivotRef, 0)

iEndMedian   = int(math.avg(iPrevPivot, iLastPivot))
pEndMedian   = math.avg(pPrevPivot, pLastPivot)

if i_pPitch
    f_drawPitchLine(iStartMedian, pStartMedian, iEndMedian, pEndMedian, i_pcolor_m, i_pwidth_m, i_pstyle_m, true )
    f_drawPitchLine(iStartMedian, pStartMedian, iEndMedian, pEndMedian, i_pcolor_m, i_pwidth_m, i_pstyle_m, false)
    f_drawLineX(iPrevPivot, pPrevPivot, iLastPivot, pLastPivot, xloc.bar_index, extend.none, i_pcolor_m, line.style_solid, i_pwidth_m)

    slopeMedianP = (pEndMedian - pStartMedian) / (iEndMedian - iStartMedian)
    mPrice = slopeMedianP * bar_index + pStartMedian - slopeMedianP * iStartMedian

    if i_pLabels != 'None'
        mTip = str.tostring(mPrice, format.mintick)
        mTxt = i_pLabels == 'Prices' ?  mTip : i_pLabels == 'Levels (Prices)' ? 'PF M (' + mTip + ')' : 'PF M'
        size = i_levelsS == 'Small' ? size.small : size.normal
        f_drawLabelX(bar_index, mPrice, mTxt, xloc.bar_index, yloc.price, #00000000 , label.style_label_left, i_pcolor_m, size     , text.align_left, mTip)
        f_drawLabelX(bar_index, mPrice, ''  , xloc.bar_index, yloc.price, i_pcolor_m, label.style_circle    , #00000000 , size.auto, text.align_left, ''  )

    if f_crossingLevel(close, mPrice)
        alert('AutoPitchFan: ' + syminfo.ticker + ' crossing median level')

processPitchLevel(show, level, colorL, widthL, styleL) =>
    if i_pPitch
        iPvtDiff = math.abs(iPrevPivot - iLastPivot) / 2
        pPvtDiff = math.abs(pPrevPivot - pLastPivot) / 2

        iEndX = pPrevPivot > pLastPivot ? int(math.round(iEndMedian - iPvtDiff * level)) : int(math.round(iEndMedian + iPvtDiff * level))
        pEndX = pEndMedian + pPvtDiff * level

        iEndY = pPrevPivot > pLastPivot ? int(math.round(iEndMedian + iPvtDiff * level)) : int(math.round(iEndMedian - iPvtDiff * level))
        pEndY = pEndMedian - pPvtDiff * level

        if show
            f_drawPitchLine(iStartMedian, pStartMedian, iEndX, pEndX, colorL, widthL, styleL, true )
            f_drawPitchLine(iStartMedian, pStartMedian, iEndY, pEndY, colorL, widthL, styleL, false)

            slopeX = (pEndX - pStartMedian) / (iEndX - iStartMedian)
            slopeY = (pEndY - pStartMedian) / (iEndY - iStartMedian)

            rPrice = slopeX * bar_index + pStartMedian - slopeX * iStartMedian
            sPrice = slopeY * bar_index + pStartMedian - slopeY * iStartMedian
            
            if i_pLabels != 'None'
                rTip = str.tostring(rPrice, format.mintick)
                sTip = str.tostring(sPrice, format.mintick)
                rTxt = i_pLabels == 'Prices' ?  rTip : i_pLabels == 'Levels (Prices)' ? 'PF ' + str.tostring(level) + ' R (' + rTip + ')' : 'PF ' + str.tostring(level) + ' R'
                sTxt = i_pLabels == 'Prices' ?  sTip : i_pLabels == 'Levels (Prices)' ? 'PF ' + str.tostring(level) + ' S (' + sTip + ')' : 'PF ' + str.tostring(level) + ' S'
                size = i_levelsS == 'Small' ? size.small : size.normal
        
                f_drawLabelX(bar_index, rPrice, rTxt, xloc.bar_index, yloc.price, #00000000, label.style_label_left, colorL   , size     , text.align_left, rTip)
                f_drawLabelX(bar_index, sPrice, sTxt, xloc.bar_index, yloc.price, #00000000, label.style_label_left, colorL   , size     , text.align_left, sTip)
                f_drawLabelX(bar_index, rPrice, ''  , xloc.bar_index, yloc.price, colorL   , label.style_circle    , #00000000, size.auto, text.align_left, ''  )
                f_drawLabelX(bar_index, sPrice, ''  , xloc.bar_index, yloc.price, colorL   , label.style_circle    , #00000000, size.auto, text.align_left, ''  )

            if f_crossingLevel(close, rPrice)
                alert('AutoPitchFan: ' + syminfo.ticker + ' crossing resistance level ' + str.tostring(level))

            if f_crossingLevel(close, sPrice)
                alert('AutoPitchFan: ' + syminfo.ticker + ' crossing support level '    + str.tostring(level))
                
            if i_bgPitch
                rLines = array.size(a_lnr)
                if rLines > 1
                    array.push(a_lf, linefill.new(array.get(a_lnr, rLines - 2), array.get(a_lnr, rLines - 1), color.new(colorL, 89)))
                sLines = array.size(a_lns)
                if sLines > 1
                    array.push(a_lf, linefill.new(array.get(a_lns, sLines - 2), array.get(a_lns, sLines - 1), color.new(colorL, 89)))

processFibLevel(_show, _level, _color) =>
    if i_isFib
        pPivotDiff = math.abs(pPrevPivot - pLastPivot)
        price = 0.

        if i_fibTool == 'Extentions'
            f_drawLineX(iStartMedian, pStartMedian, iPrevPivot, pPrevPivot, xloc.bar_index, extend.none, color.gray, line.style_dashed, 1)
            f_drawLineX(iPrevPivot  , pPrevPivot  , iLastPivot, pLastPivot, xloc.bar_index, extend.none, color.gray, line.style_dashed, 1)

            offset = math.abs(pPrevPivot - pStartMedian)
            price := pLastPivot < pPrevPivot ? pPrevPivot - pPivotDiff + (i_reverse ? -1 : 1) * offset * _level : pPrevPivot + pPivotDiff - (i_reverse ? -1 : 1) * offset * _level
            price

        else if i_fibTool == 'Retracements'
            f_drawLineX(iPrevPivot, pPrevPivot, iLastPivot, pLastPivot, xloc.bar_index, extend.none, color.gray, line.style_dashed, 1)

            price := pLastPivot < pPrevPivot ? (i_reverse ? pLastPivot : pPrevPivot) - (i_reverse ? -1 : 1) * pPivotDiff * _level : (i_reverse ? pLastPivot : pPrevPivot) + (i_reverse ? -1 : 1) * pPivotDiff * _level
            price

        if _show
            f_drawLineX(iPrevPivot, price, bar_index, price, xloc.bar_index, i_extend ? extend.both : extend.right, _color, line.style_solid, 1)

            if i_levels != 'None'
                bar = i_levelsP == "Last Bar" ? bar_index : iPrevPivot
                style = i_levelsP == "Last Bar" ? label.style_label_left : label.style_label_right
                size = i_levelsF == 'Small' ? size.small : size.normal
                txt  = i_levels =='Levels' ? str.tostring(_level) :  i_levels == 'Prices' ? str.tostring(price, format.mintick) :  str.tostring(_level) + ' (' + str.tostring(price, format.mintick) + ')'
                f_drawLabelX(bar, price, (i_fibTool == 'Extentions' ? 'EXT ' : 'RET ') + txt, xloc.bar_index, yloc.price, #00000000, style, _color, size, text.align_left, str.tostring(price, format.mintick))

            if f_crossingLevel(close, price)
                alert('AutoFib ' + (i_fibTool == 'Extentions' ? 'Extention : ' : 'Retracment : ') + syminfo.ticker + ' crossing level ' + str.tostring(_level))

pshow_0_25 = input.bool(false, '', inline='pLevel_0_25', group=group_pitch)
pvalue_0_25 = input.float(.25, '', minval=.0, inline='pLevel_0_25', group=group_pitch)
pcolor_0_25 = input.color(#ffb74d, '', inline='pLevel_0_25', group=group_pitch)
pwidth_0_25 = input.int(1, '', minval=1, inline='pLevel_0_25', group=group_pitch)
pstyle_0_25 = input.string('Dashed', '', options=['Dashed', 'Dotted', 'Solid'], inline='pLevel_0_25', group=group_pitch)
processPitchLevel(pshow_0_25, pvalue_0_25, pcolor_0_25, pwidth_0_25, pstyle_0_25)

pshow_0_382 = input.bool(false, '', inline='pLevel_0_382', group=group_pitch)
pvalue_0_382 = input.float(.382, '', minval=.0, inline='pLevel_0_382', group=group_pitch)
pcolor_0_382 = input.color(#81c784, '', inline='pLevel_0_382', group=group_pitch)
pwidth_0_382 = input.int(1, '', minval=1, inline='pLevel_0_382', group=group_pitch)
pstyle_0_382 = input.string('Dotted', '', options=['Dashed', 'Dotted', 'Solid'], inline='pLevel_0_382', group=group_pitch)
processPitchLevel(pshow_0_382, pvalue_0_382, pcolor_0_382, pwidth_0_382, pstyle_0_382)

pshow_0_50 = input.bool(true, '', inline='pLevel_0_50', group=group_pitch)
pvalue_0_50 = input.float(.5, '', minval=.0, inline='pLevel_0_50', group=group_pitch)
pcolor_0_50 = input.color(#4caf50, '', inline='pLevel_0_50', group=group_pitch)
pwidth_0_50 = input.int(1, '', minval=1, inline='pLevel_0_50', group=group_pitch)
pstyle_0_50 = input.string('Solid', '', options=['Dashed', 'Dotted', 'Solid'], inline='pLevel_0_50', group=group_pitch)
processPitchLevel(pshow_0_50, pvalue_0_50, pcolor_0_50, pwidth_0_50, pstyle_0_50)

pshow_0_618 = input.bool(false, '', inline='pLevel_0_618', group=group_pitch)
pvalue_0_618 = input.float(.618, '', minval=.0, inline='pLevel_0_618', group=group_pitch)
pcolor_0_618 = input.color(#009688, '', inline='pLevel_0_618', group=group_pitch)
pwidth_0_618 = input.int(1, '', minval=1, inline='pLevel_0_618', group=group_pitch)
pstyle_0_618 = input.string('Dotted', '', options=['Dashed', 'Dotted', 'Solid'], inline='pLevel_0_618', group=group_pitch)
processPitchLevel(pshow_0_618, pvalue_0_618, pcolor_0_618, pwidth_0_618, pstyle_0_618)

pshow_0_75 = input.bool(false, '', inline='pLevel_0_75', group=group_pitch)
pvalue_0_75 = input.float(.75, '', minval=.0, inline='pLevel_0_75', group=group_pitch)
pcolor_0_75 = input.color(#64b5f6, '', inline='pLevel_0_75', group=group_pitch)
pwidth_0_75 = input.int(1, '', minval=1, inline='pLevel_0_75', group=group_pitch)
pstyle_0_75 = input.string('Dashed', '', options=['Dashed', 'Dotted', 'Solid'], inline='pLevel_0_75', group=group_pitch)
processPitchLevel(pshow_0_75, pvalue_0_75, pcolor_0_75, pwidth_0_75, pstyle_0_75)

pshow_1 = input.bool(true, '', inline='pLevel_1', group=group_pitch)
pvalue_1 = input.float(1, '', minval=.0, inline='pLevel_1', group=group_pitch)
pcolor_1 = input.color(#2196f3, '', inline='pLevel_1', group=group_pitch)
pwidth_1 = input.int(2, '', minval=1, inline='pLevel_1', group=group_pitch)
pstyle_1 = input.string('Solid', '', options=['Dashed', 'Dotted', 'Solid'], inline='pLevel_1', group=group_pitch)
processPitchLevel(pshow_1, pvalue_1, pcolor_1, pwidth_1, pstyle_1)

pshow_1_50 = input.bool(false, '', inline='pLevel_1_50', group=group_pitch)
pvalue_1_50 = input.float(1.5, '', minval=.0, inline='pLevel_1_50', group=group_pitch)
pcolor_1_50 = input.color(#9c27b0, '', inline='pLevel_1_50', group=group_pitch)
pwidth_1_50 = input.int(1, '', minval=1, inline='pLevel_1_50', group=group_pitch)
pstyle_1_50 = input.string('Solid', '', options=['Dashed', 'Dotted', 'Solid'], inline='pLevel_1_50', group=group_pitch)
processPitchLevel(pshow_1_50, pvalue_1_50, pcolor_1_50, pwidth_1_50, pstyle_1_50)

pshow_1_75 = input.bool(false, '', inline='pLevel_1_75', group=group_pitch)
pvalue_1_75 = input.float(1.75, '', minval=.0, inline='pLevel_1_75', group=group_pitch)
pcolor_1_75 = input.color(#e91e63, '', inline='pLevel_1_75', group=group_pitch)
pwidth_1_75 = input.int(1, '', minval=1, inline='pLevel_1_75', group=group_pitch)
pstyle_1_75 = input.string('Dashed', '', options=['Dashed', 'Dotted', 'Solid'], inline='pLevel_1_75', group=group_pitch)
processPitchLevel(pshow_1_75, pvalue_1_75, pcolor_1_75, pwidth_1_75, pstyle_1_75)

pshow_2 = input.bool(false, '', inline='pLevel_2', group=group_pitch)
pvalue_2 = input.float(2, '', minval=.0, inline='pLevel_2', group=group_pitch)
pcolor_2 = input.color(#e57373, '', inline='pLevel_2', group=group_pitch)
pwidth_2 = input.int(2, '', minval=1, inline='pLevel_2', group=group_pitch)
pstyle_2 = input.string('Solid', '', options=['Dashed', 'Dotted', 'Solid'], inline='pLevel_2', group=group_pitch)
processPitchLevel(pshow_2, pvalue_2, pcolor_2, pwidth_2, pstyle_2)


show_0 = input.bool(true, '', inline='Level0', group=group_fib)
value_0 = input.int(0, '', inline='Level0', group=group_fib)
color_0 = input.color(#787b86, '', inline='Level0', group=group_fib)
processFibLevel(show_0, value_0, color_0)

show_0_236 = input.bool(true, '', inline='Level0', group=group_fib)
value_0_236 = input.float(0.236, '', inline='Level0', group=group_fib)
color_0_236 = input.color(#f44336, '', inline='Level0', group=group_fib)
processFibLevel(show_0_236, value_0_236, color_0_236)

show_0_382 = input.bool(true, '', inline='Level1', group=group_fib)
value_0_382 = input.float(0.382, '', inline='Level1', group=group_fib)
color_0_382 = input.color(#81c784, '', inline='Level1', group=group_fib)
processFibLevel(show_0_382, value_0_382, color_0_382)

show_0_5 = input.bool(true, '', inline='Level1', group=group_fib)
value_0_5 = input.float(0.5, '', inline='Level1', group=group_fib)
color_0_5 = input.color(#4caf50, '', inline='Level1', group=group_fib)
processFibLevel(show_0_5, value_0_5, color_0_5)

show_0_618 = input.bool(true, '', inline='Level2', group=group_fib)
value_0_618 = input.float(0.618, '', inline='Level2', group=group_fib)
color_0_618 = input.color(#009688, '', inline='Level2', group=group_fib)
processFibLevel(show_0_618, value_0_618, color_0_618)

show_0_65 = input.bool(false, '', inline='Level2', group=group_fib)
value_0_65 = input.float(0.65, '', inline='Level2', group=group_fib)
color_0_65 = input.color(#009688, '', inline='Level2', group=group_fib)
processFibLevel(show_0_65, value_0_65, color_0_65)

show_0_786 = input.bool(true, '', inline='Level3', group=group_fib)
value_0_786 = input.float(0.786, '', inline='Level3', group=group_fib)
color_0_786 = input.color(#64b5f6, '', inline='Level3', group=group_fib)
processFibLevel(show_0_786, value_0_786, color_0_786)

show_1 = input.bool(true, '', inline='Level3', group=group_fib)
value_1 = input.int(1, '', inline='Level3', group=group_fib)
color_1 = input.color(#787b86, '', inline='Level3', group=group_fib)
processFibLevel(show_1, value_1, color_1)

show_1_272 = input.bool(false, '', inline='Level4', group=group_fib)
value_1_272 = input.float(1.272, '', inline='Level4', group=group_fib)
color_1_272 = input.color(#81c784, '', inline='Level4', group=group_fib)
processFibLevel(show_1_272, value_1_272, color_1_272)

show_1_414 = input.bool(false, '', inline='Level4', group=group_fib)
value_1_414 = input.float(1.414, '', inline='Level4', group=group_fib)
color_1_414 = input.color(#f44336, '', inline='Level4', group=group_fib)
processFibLevel(show_1_414, value_1_414, color_1_414)

show_1_618 = input.bool(true, '', inline='Level5', group=group_fib)
value_1_618 = input.float(1.618, '', inline='Level5', group=group_fib)
color_1_618 = input.color(#2196f3, '', inline='Level5', group=group_fib)
processFibLevel(show_1_618, value_1_618, color_1_618)

show_1_65 = input.bool(false, '', inline='Level5', group=group_fib)
value_1_65 = input.float(1.65, '', inline='Level5', group=group_fib)
color_1_65 = input.color(#2196f3, '', inline='Level5', group=group_fib)
processFibLevel(show_1_65, value_1_65, color_1_65)

show_2_618 = input.bool(false, '', inline='Level6', group=group_fib)
value_2_618 = input.float(2.618, '', inline='Level6', group=group_fib)
color_2_618 = input.color(#f44336, '', inline='Level6', group=group_fib)
processFibLevel(show_2_618, value_2_618, color_2_618)

show_2_65 = input.bool(false, '', inline='Level6', group=group_fib)
value_2_65 = input.float(2.65, '', inline='Level6', group=group_fib)
color_2_65 = input.color(#f44336, '', inline='Level6', group=group_fib)
processFibLevel(show_2_65, value_2_65, color_2_65)

show_3_618 = input.bool(false, '', inline='Level7', group=group_fib)
value_3_618 = input.float(3.618, '', inline='Level7', group=group_fib)
color_3_618 = input.color(#9c27b0, '', inline='Level7', group=group_fib)
processFibLevel(show_3_618, value_3_618, color_3_618)

show_3_65 = input.bool(false, '', inline='Level7', group=group_fib)
value_3_65 = input.float(3.65, '', inline='Level7', group=group_fib)
color_3_65 = input.color(#9c27b0, '', inline='Level7', group=group_fib)
processFibLevel(show_3_65, value_3_65, color_3_65)

show_4_236 = input.bool(false, '', inline='Level8', group=group_fib)
value_4_236 = input.float(4.236, '', inline='Level8', group=group_fib)
color_4_236 = input.color(#e91e63, '', inline='Level8', group=group_fib)
processFibLevel(show_4_236, value_4_236, color_4_236)

show_4_618 = input.bool(false, '', inline='Level8', group=group_fib)
value_4_618 = input.float(4.618, '', inline='Level8', group=group_fib)
color_4_618 = input.color(#81c784, '', inline='Level8', group=group_fib)
processFibLevel(show_4_618, value_4_618, color_4_618)

show_neg_0_236 = input.bool(false, '', inline='Level9', group=group_fib)
value_neg_0_236 = input.float(-0.236, '', inline='Level9', group=group_fib)
color_neg_0_236 = input.color(#f44336, '', inline='Level9', group=group_fib)
processFibLevel(show_neg_0_236, value_neg_0_236, color_neg_0_236)

show_neg_0_382 = input.bool(false, '', inline='Level9', group=group_fib)
value_neg_0_382 = input.float(-0.382, '', inline='Level9', group=group_fib)
color_neg_0_382 = input.color(#81c784, '', inline='Level9', group=group_fib)
processFibLevel(show_neg_0_382, value_neg_0_382, color_neg_0_382)

show_neg_0_618 = input.bool(false, '', inline='Level10', group=group_fib)
value_neg_0_618 = input.float(-0.618, '', inline='Level10', group=group_fib)
color_neg_0_618 = input.color(#009688, '', inline='Level10', group=group_fib)
processFibLevel(show_neg_0_618, value_neg_0_618, color_neg_0_618)

show_neg_0_65 = input.bool(false, '', inline='Level10', group=group_fib)
value_neg_0_65 = input.float(-0.65, '', inline='Level10', group=group_fib)
color_neg_0_65 = input.color(#009688, '', inline='Level10', group=group_fib)
processFibLevel(show_neg_0_65, value_neg_0_65, color_neg_0_65)

var table logo = table.new(position.bottom_right, 1, 1)
table.cell(logo, 0, 0, '☼☾  ', text_size=size.normal, text_color=color.teal)

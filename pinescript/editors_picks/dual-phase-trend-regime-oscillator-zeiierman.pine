// This work is licensed under a Attribution-NonCommercial-ShareAlike 4.0 International (CC BY-NC-SA 4.0) https://creativecommons.org/licenses/by-nc-sa/4.0/
// Â© Zeiierman {
//@version=6
indicator('Dual-Phase Trend Regime Oscillator (Zeiierman)', overlay = false, precision = 1)
//~~~~}

//~~ Tooltips for Inputs {
var string tt1 = "Lookback period (number of bars) for the Slow Trend Oscillator. This oscillator measures trend strength using price correlation with time and is primarily used when the indicator detects a 'Low Volatility' internal state. Longer values provide smoother but potentially lagging trend signals."
var string tt2 = "Lookback period (number of bars) for the Fast Trend Oscillator. This oscillator measures trend strength using price correlation with time and is primarily used when the indicator detects a 'High Volatility' internal state. Shorter values provide more reactive but potentially noisier trend signals."
var string tt3 = "Lookback period (number of bars) used to collect historical volatility data. This data is analyzed (using a median split) to determine the typical 'Low' and 'High' volatility levels for the current market context."
var string tt4 = "Frequency (in bars) for recalculating the 'Low' and 'High' volatility levels based on the data within the 'Volatility Window Size'. A lower value makes the levels adapt faster but can cause more frequent internal regime shifts."
var string tt5 = "Lookback period (number of bars) for calculating the 'current' volatility value (standard deviation of returns). This immediate volatility value is then compared against the calculated Low/High levels to determine the current internal volatility state."
var string tt6 = "Applies a Simple Moving Average (SMA) to the 'current' volatility value before it's compared to the Low/High levels. Higher values smooth the volatility input, reducing noise and potentially delaying internal volatility regime shifts."
var string tt7 = "Exponential Moving Average (EMA) smoothing factor applied to the calculated 'Low' and 'High' volatility levels after each refit. Lower values (e.g., 0.1) provide more smoothing (slower adaptation), reducing jumps in the levels. Higher values (e.g., 0.5) mean less smoothing (faster adaptation). Alpha relates to EMA Length (~(2/Alpha)-1)."
var string tt8 = "Color used for the background fill and top marker when the final Trend Regime is determined to be Bullish (based on the relevant oscillator being > 0.5)."
var string tt9 = "Color used for the background fill and bottom marker when the final Trend Regime is determined to be Bearish (based on the relevant oscillator being < 0.5)."
var string tt10 = "Color used for the marker (default: circle near midline) indicating a shift *into* the 'High Volatility' internal state."
var string tt11 = "Color used for the marker (default: circle near midline) indicating a shift *into* the 'Low Volatility' internal state."
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~}

//~~  Inputs {
// Oscillator settings
length_low = input.int(36, "Length (Slow Trend Oscillator)", tooltip = tt1, minval=2, group = "Oscillator settings")
length_high = input.int(18, "Length (Fast Trend Oscillator)", tooltip = tt2, minval=2, group = "Oscillator settings")

// Volatility Detection Settings
refit_interval = input.int(50, "Volatility Refit Interval", tooltip = tt4, step = 1, minval = 1, group = "Volatility Detection Settings")
vol_period = input.int(20, "Current Volatility Period", tooltip = tt5, minval=1, group="Volatility Detection Settings")

// Smoothing Settings
vol_smooth_len = input.int(5, "Volatility Smoothing Length", tooltip = tt6, minval=1, group="Smoothing Settings")

// Style settings - Renamed for Trend Regime
bull_col = input.color(color.rgb(33, 150, 243, 0), title = "Bullish Color", tooltip=tt8, group = "Style", inline = "Style_1")
bear_col = input.color(color.rgb(255, 82, 82, 0), title = "Bearish Color", tooltip=tt9, group = "Style", inline = "Style_1") 
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~}

//~~  Var {
var array<float> vola = array.new<float>()
var float last_refit_bar = 0
var series float cluster_1 = na 
var series float cluster_2 = na 
var series int internal_vol_regime = na 
var series int trend_regime = 0 
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~}

//~~  Volatility Regime function {
cluster(data_window) =>
    size = array.size(data_window)
    if size < 10
        [na, na]
    else
        median_vol = array.median(data_window)
        if na(median_vol)
            [na, na]
        else
            sum_low = 0.0
            count_low = 0
            sum_high = 0.0
            count_high = 0
            for i = 0 to size - 1 by 1
                el = array.get(data_window, i)
                if not na(el)
                    if el < median_vol
                        sum_low := sum_low + el
                        count_low := count_low + 1
                        count_low
                    else
                        sum_high := sum_high + el
                        count_high := count_high + 1
                        count_high
            center_low = count_low > 0 ? sum_low / count_low : na
            center_high = count_high > 0 ? sum_high / count_high : na
            [center_low, center_high]

//~~  Oscillator function {
oscillator(src, length) =>
    r_value = ta.correlation(src, bar_index, length)
    osc = not na(r_value) ? (r_value + 1) / 2.0 : na
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~}

//~~  Main {
//~~  Calculate returns {
returns = close / close[1] - 1
//~~~~}

//~~  Calculate and smooth current volatility {
vol_current_raw      = ta.stdev(returns, vol_period)
vol_current_smoothed = ta.sma(vol_current_raw, vol_smooth_len)
//~~~~}

//~~  Manage volatility history array {
if not na(vol_current_raw)
    array.push(vola, vol_current_raw)
if array.size(vola) > 150
    array.shift(vola)
//~~~~}

//~~  Refit internal volatility levels periodically {
if barstate.isconfirmed and array.size(vola) >= 150 and (bar_index - last_refit_bar >= refit_interval or na(cluster_1[1]))
    [cluster_1_tmp, cluster_2_tmp] = cluster(vola)
    if not na(cluster_1_tmp) and not na(cluster_2_tmp)
        cluster_1 := na(cluster_1[1]) ? cluster_1_tmp : cluster_1[1] + 0.1 * (cluster_1_tmp - cluster_1[1])
        cluster_2 := na(cluster_2[1]) ? cluster_2_tmp : cluster_2[1] + 0.1 * (cluster_2_tmp - cluster_2[1])
        last_refit_bar := bar_index
    // Ensure clusters don't cross
    if not na(cluster_1) and not na(cluster_2) and cluster_1 > cluster_2
        temp_c1 = cluster_1
        cluster_1 := cluster_2
        cluster_2 := temp_c1
//~~~~}

//~~  Determine INTERNAL Volatility Regime {
int vol_regime_calc = na
if not na(vol_current_smoothed) and not na(cluster_1) and not na(cluster_2)
    threshold = (cluster_1 + cluster_2) / 2.0
    vol_regime_calc := vol_current_smoothed < threshold ? 0 : 1
internal_vol_regime := vol_regime_calc 
//~~~~}

//~~  Calculate Trend Oscillators {
osc_low  = oscillator(close, length_low) 
osc_high = oscillator(close, length_high)
//~~~~}

//~~  Trend Regime based on Volatility {
float relevant_osc = na
if not na(internal_vol_regime)
    relevant_osc := internal_vol_regime == 1 ? osc_high : osc_low 

trend_regime_calc = 0 
if not na(relevant_osc)
    if relevant_osc > 0.5
        trend_regime_calc := 1 
    else if relevant_osc < 0.5
        trend_regime_calc := -1   
else
    trend_regime_calc := na 

trend_regime := na(trend_regime_calc) ? na : trend_regime_calc
//~~~~}
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~}

//~~  Plots {
zero_line = plot(-0.2, display = display.none)
top_line  = plot(1.2, display = display.none)
mid_line  = hline(0.5, 'Mid Line', color = color.new(color.gray, 50), linestyle = hline.style_dashed)

osc_low_plot  = plot(osc_low, title = 'Slow Oscillator', color = osc_low >= 0.5 ? bull_col : bear_col, linewidth = 1, style = plot.style_line) // Color based on its own direction
osc_high_plot = plot(osc_high, title = 'Fast Oscillator', color = osc_high >= 0.5 ? bull_col : bear_col, linewidth = 1, style = plot.style_line) // Color based on its own direction
fill(osc_low_plot, osc_high_plot, color = osc_low >= 0.5 ? color.new(bull_col, 85) : color.new(bear_col, 85), title = 'Oscillator Fill')

bullish_shift = not na(trend_regime) and trend_regime == 1 and (na(trend_regime[1]) or trend_regime[1] != 1)
bearish_shift = not na(trend_regime) and trend_regime == -1 and (na(trend_regime[1]) or trend_regime[1] != -1)

plotshape(bullish_shift?1.2:na, title = 'Bullish Regime Shift', location = location.absolute, color = color.new(bull_col, 0), style = shape.triangleup, size = size.tiny, offset = -1)
plotshape(bearish_shift?-0.2:na, title = 'Bearish Regime Shift', location = location.absolute, color = color.new(bear_col, 0), style = shape.triangledown, size = size.tiny, offset = -1)

fill_color = color.new(color.gray, 95) 
if not na(trend_regime)
    fill_color := trend_regime == 1 ? color.new(bull_col, 90) : color.new(bear_col, 90)
    fill_color
fill(top_line, zero_line, color = fill_color, title = 'Trend Regime Background')
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~}

//~~  Regime Table {
var tbl = table.new(position.top_right, 1, 1, bgcolor = color.new(color.gray, 90))
if barstate.islast
    regime_text = 'Calculating...'
    regime_bgcolor = color.new(color.gray, 20)
    if not na(trend_regime)
        regime_text := trend_regime == 1 ? 'Bullish Trend' : 'Bearish Trend'
        if trend_regime == 0
            regime_text := 'Neutral Trend'
            regime_bgcolor := color.new(color.gray, 20)
            regime_bgcolor
        else
            regime_bgcolor := trend_regime == 1 ? color.new(bull_col, 20) : color.new(bear_col, 20)
            regime_bgcolor
    table.cell(tbl, 0, 0, text = regime_text, bgcolor = regime_bgcolor, text_color = color.white, text_size = size.small)
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~}

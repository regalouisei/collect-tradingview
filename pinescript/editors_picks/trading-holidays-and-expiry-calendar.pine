// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Sharad_Gaikwad

var tab = table.new(position=position.top_right, columns=7, rows=30,frame_color = na, frame_width = 1)
msg(int row, int col, string msg_str, clr=color.blue) =>
    // if(barstate.islast)
    table.cell(table_id=tab, column=col, row=row, text=msg_str, text_color=clr)
//can round to closest 1, 5, 10, 20 ...
round_up(pp, round_to = 0) => round_to <= 0 ? pp : math.ceil(pp/round_to) * round_to
round_down(pp, round_to = 0) => round_to <= 0 ? pp : math.floor(pp/round_to) * round_to

t(val) => str.tostring(val)
tr(val) => str.tostring(math.round(val, 2))
r(val) => math.round_to_mintick(val)
method f(float f) => str.tostring(f)
method f(int i) => str.tostring(i)
method f(bool b) => str.tostring(b)
method f(string s) => str.tostring(s)
method fd(int t, string format = "dd-MM-yy HH:mm") => str.format_time(t, format, syminfo.timezone)

dt(val, format = "dd-MM-yy HH:mm:ss", tz = syminfo.timezone) => str.format_time(val, format, tz)
var debug = array.new<string>(1, "")
method clear_log(string [] s) =>
    s.clear()
    s.unshift("")
method log(string [] s, string msg, bool on_last_bar = false) =>
    if((not on_last_bar) or (on_last_bar and barstate.islast))
        if(str.length(s.get(0)) + str.length(msg) < 4095)
            s.set(0, s.get(0) + msg + "\n")

method display(string [] s, where = 'B', clr = color.blue) =>
    if(not na(s.get(0)))
        if(where == 'B')
            label.new(bar_index, low, yloc = yloc.belowbar, style = label.style_diamond, tooltip = s.get(0), size = size.tiny, color = clr, text_font_family = font.family_monospace)
        else
            label.new(bar_index, high, yloc = yloc.abovebar, style = label.style_diamond, tooltip = s.get(0), size = size.tiny, color = clr, text_font_family = font.family_monospace)
        debug.clear_log()
method lastr(string [] s, where = 'B') =>
    var lbl = label.new(na, na, style = label.style_diamond, text_font_family = font.family_monospace)
    if(not na(s.get(0)))
        lbl.set_xy(bar_index + 10, close)
        lbl.set_tooltip(s.get(0))
        debug.clear_log()        

var CTF_SEC = timeframe.in_seconds(timeframe.period),
var CTF_MSEC = timeframe.in_seconds(timeframe.period) * 1000
var int DAY_MSEC = 1 * 60 * 60 * 24 * 1000
// //

//@version=6
// indicator(title = 'Trading Calendar', overlay = true)
indicator(title = 'Trading Holidays and Expiry Calendar', overlay = true)

g10 = "Calrendar Setup"
exp_day = input.string(title = "Expiry Day", defval = "THU", options = ["MON", "TUE", "WED", "THU", "FRI"], group = g10)
plot_exp = input.bool(title = "Plot Vertical Line on Expiry Day", defval = true, inline = "10", group = g10)
exp_line_color = input.color(title = "", defval = color.blue, inline  = "10", group = g10)
exp_day_color = input.color(title = "Expiry Day Color", defval = color.red, group = g10)
trade_day_color = input.color(title = "Trading Day Color", defval = color.green, group = g10)
weekend_color = input.color(title = "Weekend Color", defval = color.gray, group = g10)
holiday_color = input.color(title = "Trading Holiday Color", defval = color.black, group = g10)
holiday_bg_color = input.color(title = "Trading Holiday Background Color", defval = color.new(color.white, 0), group = g10)



//--------------------------------------------
g20 = "Calendar Visuals"
table_frame_color=input.color(color.aqua, "Table Frame Color", group = g20)
table_border_color = input.color(defval = color.gray, title = "Table Border Color", group = g20)
title_text_color = input.color(defval = color.green, title = "Title Text Color", group = g20)
title_text_size = input.string(defval = "normal", title = "Title Text Size", options = ["tiny", "small", "normal", "large", "huge"], group = g20)
title_bg_color = input.color(defval = color.new(color.black, 0), title = "Title Background Color", group = g20)
data_text_color = input.color(defval = color.white, title = "Data Text Color", group = g20)
data_text_size = input.string(defval = "small", title = "Data Text Size", options = ["tiny", "small", "normal", "large", "huge"], group = g20)
data_bg_color = input.color(defval = color.new(color.black, 80), title = "Data Background Color", group = g20)

var tab1 = table.new(position=position.top_right, columns=8, rows=30, frame_color = table_frame_color, frame_width = 1, 
     border_color = table_border_color, border_width = 1)

var tab2 = table.new(position=position.middle_right, columns=8, rows=30, frame_color = table_frame_color, frame_width = 1, 
     border_color = table_border_color, border_width = 1)

var tab3 = table.new(position=position.bottom_right, columns=8, rows=30, frame_color = table_frame_color, frame_width = 1, 
     border_color = table_border_color, border_width = 1)

method show_title(table t, int row, int col, string msg_str) =>
    table.cell(table_id=t, column=col, row=row, text=msg_str, text_color=title_text_color, text_size=title_text_size, bgcolor=title_bg_color)


method show_data(table t, int row, int col, string msg_str, color text_color = na, color bg_color = na ) =>
    clr = na(text_color) ? data_text_color : text_color 
    bg = na(bg_color) ? data_bg_color : bg_color
    table.cell(table_id=t, column=col, row=row, text=msg_str, text_color=clr, text_size=data_text_size, bgcolor=bg)

method show_header(table t, int row, int col, string msg_str) =>
    table.merge_cells(table_id=t, start_column = 0,start_row = 0, end_column = 6, end_row = 0)
    table.cell(table_id=t, column=col, row=row, text=msg_str, text_color=title_text_color, text_size=title_text_size, bgcolor=title_bg_color)


method clear_tab_data(table t) => t.clear(0, 0, 7, 29)

//----------------------------------------------

exp_day_no = exp_day == "MON" ? 0
     : exp_day == "TUE" ? 1
     : exp_day == "WED" ? 2
     : exp_day == "THU" ? 3
     : exp_day == "FRI" ? 4
     : 99
     
const int MON = 0
const int TUE = 1
const int WED = 2
const int THU = 3
const int FRI = 4
const int SAT = 5
const int SUN = 6

type CALENDER 
    int id
    int [] week

//
rs() =>
    tdays1 = array.new<int>()
    tdays2 = array.new<int>()
    tdays3 = array.new<int>()
    arr = array.new<int>()
    
    start = timeframe.in_seconds(timeframe.main_period) == timeframe.in_seconds("D") ? 0 : 1
    start_time = time("D", -start)

    month_cnt = 0
    this_month = 0
    // mon = 0
    for i = start to 90 
        // day = time("D", -i)
        day = time(timeframe.period, -i)
        if(day == start_time and month_cnt == 0)
            this_month := month(day)
            arr := tdays1
            month_cnt += 1
        else if(month(day) != this_month and month_cnt == 1)
            this_month := month(day)
            arr := tdays2
            month_cnt += 1
        else if(month(day) != this_month and month_cnt == 2)
            this_month := month(day)
            month_cnt += 1
            arr := tdays3
        else if(month(day) != this_month and month_cnt == 3)
            break
        arr.push(day)
    [tdays1, tdays2, tdays3] 
//

day_num(day) =>
    result = switch 
        dayofweek(day) == dayofweek.monday => MON 
        dayofweek(day) == dayofweek.tuesday => TUE 
        dayofweek(day) == dayofweek.wednesday => WED
        dayofweek(day) == dayofweek.thursday => THU 
        dayofweek(day) == dayofweek.friday => FRI 
        dayofweek(day) == dayofweek.saturday => SAT 
        dayofweek(day) == dayofweek.sunday => SUN 
    result
//

get_exp_days(array<CALENDER> cal, array<int> tdays) =>
    x_day = 0
    exp_days = array.new<int>()
    for i = 0  to cal.size() - 1 
        rec = cal.get(i)
        if(rec.week.get(exp_day_no) != 0)
            x_day := rec.week.get(exp_day_no)
            if(x_day != 0)
                for [j, day] in tdays 
                    if(day == x_day)
                        exp_days.push(tdays.get(j))
                        break 
                    else if(day > x_day and j > 0)
                        exp_days.push(tdays.get(j-1))
                        break
    exp_days

//
new_cal_rec() => CALENDER.new(0, array.from(0, 0, 0, 0, 0, 0, 0))

//
calender(array<int> trading_days, tab) =>
    cal = array.new<CALENDER>()
    
    first_trading_day = trading_days.get(0) 
    first_cal_day = first_trading_day  - ((dayofmonth(first_trading_day) - 1)*DAY_MSEC)
    week_no = 0 
    cal_day = first_cal_day
    id = 0
    next_id = 6
    this_cal_rec = new_cal_rec()

    for i = 0 to 32
        day_no = day_num(cal_day)        
        this_cal_rec.week.set(day_no, cal_day) 

        if(day_no == SUN)
            id += 0
            this_cal_rec.id := id
            cal.push(this_cal_rec)
            this_cal_rec := new_cal_rec()


        cal_day += DAY_MSEC
        if(month(cal_day) != month(first_cal_day))
            if(day_no != SUN)
                id += 0
                this_cal_rec.id := id
                cal.push(this_cal_rec)
            
            break

    r = 0
    header = "Month : " + dt(first_trading_day, "MMM-yy")
    tab.show_header(r, 0, header)

    r+=1
    tab.show_title(r, 0, "Mo")
    tab.show_title(r, 1, "Tu")
    tab.show_title(r, 2, "We")
    tab.show_title(r, 3, "Th")
    tab.show_title(r, 4, "Fr")
    tab.show_title(r, 5, "Sa")
    tab.show_title(r, 6, "Su")
    
    r+=1

    expiry_days = get_exp_days(cal, trading_days)
    

    for rec in cal 
        for i = 0 to 6
            time_val = rec.week.get(i)
            fake_day = time_val == 0 
            val1 =  fake_day ? "." : t(dayofmonth(time_val))
            exp = expiry_days.includes(time_val)
            if(exp and plot_exp)
                line.new(time_val, 1000*1000, time_val, -1000*1000, xloc = xloc.bar_time, color = exp_line_color)

            day_no = day_num(time_val)
            is_week_end = day_no == SAT or day_no == SUN

            color clr = trading_days.includes(time_val) ? exp ? exp_day_color : trade_day_color : is_week_end ? weekend_color : holiday_color

            color bg_clr = trading_days.includes(time_val) ? exp ? data_bg_color : data_bg_color : is_week_end ? data_bg_color : fake_day ? data_bg_color :  holiday_bg_color

            tab.show_data(r, i, val1, text_color = clr, bg_color = bg_clr)
        r += 1


new_month = timeframe.change("M") 
new_day = timeframe.change("D")
latest_month = month(time) == month(last_bar_time) and year(time) == year(last_bar_time) 
if(new_month and new_day and latest_month)
    [trading_days1, trading_days2, trading_days3] = request.security("", "D", rs())
    
    calender(trading_days1, tab1)
    calender(trading_days2, tab2)
    calender(trading_days3, tab3)

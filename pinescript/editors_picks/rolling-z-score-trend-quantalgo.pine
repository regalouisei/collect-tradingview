// This script is licensed under Creative Commons Attribution-NonCommercial-ShareAlike 4.0 International
// https://creativecommons.org/licenses/by-nc-sa/4.0/
// © QuantAlgo

//@version=6
indicator("Rolling Z-Score Trend [QuantAlgo]", overlay=false)

//              ╔═══════════════════════════╗              //
//              ║    USER DEFINED INPUTS    ║              //
//              ╚═══════════════════════════╝              //

// Input Groups
var string z_score_settings        = "════════ Z-Score Settings ════════"
var string color_settings          = "════════ Color Options ════════"
var string table_settings          = "════════ Info Table ════════"

// Tooltips
tooltip_lookback_period            = "Number of bars used to calculate rolling mean and standard deviation for Z-Score. Lower values (10-15) make the indicator more sensitive to recent price changes but may produce more false signals. Higher values (25-50) provide smoother, more stable readings but with increased lag in signal detection."
tooltip_entry_threshold            = "Z-Score level that defines overbought/oversold zones for trading signals. Lower values (0.5-1.0) generate more frequent signals but with higher risk of false entries. Higher values (2.0-3.0) produce fewer but potentially more reliable signals at extreme market conditions."
tooltip_preset                     = "Select a predefined configuration optimized for different trading approaches. Each preset adjusts both lookback period and entry threshold to match the selected strategy."
var string tooltip_preset_details  = "Default (20, 1.5): Universally applicable configuration that works well across multiple timeframes and assets. Balanced for medium-term investing and general trading.\n\n" +
                                     "Scalping (10, 1.0): Highly responsive for ultra-short-term trades. More sensitive to quick price movements. Best for 1-15min charts.\n\n" +
                                     "Swing Trading (25, 1.8): Designed for multi-day positions with stronger noise filtering. Focuses on larger price swings. Works best on 1-4h and daily charts.\n\n" +
                                     "Trend Following (40, 2.2): Maximum smoothing for established trends only. Generates fewer but more reliable signals. Best for daily/weekly charts and long-term investing."
tooltip_color_preset               = "Pre-configured color schemes for different visual preferences. Classic uses traditional bright green/red. Aqua provides a modern blue/orange combination. Cosmic offers a vibrant pink/purple theme. Custom allows full color customization."
tooltip_color_up                   = "Color used for bullish/positive conditions including positive Z-Scores, upward momentum, and rising trends. Choose colors that provide good contrast against your chart background for optimal visibility."
tooltip_color_down                 = "Color used for bearish/negative conditions including negative Z-Scores, downward momentum, and falling trends. Choose colors that provide good contrast against your chart background for optimal visibility."
tooltip_column_transparency        = "Transparency level for momentum background columns. Lower values (0-50) make columns more visible but may obstruct the main Z-Score line. Higher values (80-100) provide subtle background context without overwhelming the primary signal."
tooltip_show_table                 = "Enable/disable the information table that displays current Z-Score and momentum values with trend indicators. Useful for quick reference of exact numerical values and rate of change between bars."
tooltip_table_preset               = "Pre-configured table color schemes. Mono provides black background with white text only (no colors). Light offers white background with dark text. Blue uses a modern blue theme. Custom allows full table color customization and is the recommended default."
tooltip_table_position             = "Screen location where the information table will be displayed. Choose a position that doesn't interfere with your chart analysis. Top positions are generally preferred to avoid blocking price action."
tooltip_table_background           = "Background color for the information table. Darker colors (black, dark gray) typically provide better contrast for text readability. Ensure sufficient contrast with your chosen text colors."
tooltip_header_background          = "Background color for table column headers. Should be different from the main table background to clearly distinguish headers from data rows. Slightly lighter shades often work well."
tooltip_header_text                = "Text color for table column headers. White or light colors typically work best with dark header backgrounds. Ensure high contrast for optimal readability."
tooltip_normal_text                = "Text color for static table labels like 'Z-Score' and 'Momentum'. Should provide good contrast against the table background while being distinct from value colors."
tooltip_border_color               = "Color of the table border and frame. Subtle colors work best to define table boundaries without being distracting. Gray tones typically complement most color schemes."
tooltip_table_text_size            = "Size of text displayed in the info table. Larger sizes improve readability but make the table bigger. Smaller sizes save space but may be harder to read."

// Z-Score Configuration
lookback_period = input.int(20, "Lookback Period", 
     minval = 10, maxval = 100,
     group = z_score_settings,
     tooltip = tooltip_lookback_period)

entry_threshold = input.float(1.5, "Entry Threshold", 
     minval = 0.5, maxval = 3.0, step = 0.1,
     group = z_score_settings,
     tooltip = tooltip_entry_threshold)

// Preconfigured Input Preset
input_preset = input.string("Default", "Preconfigured Input Preset", 
     options = ["Default", "Scalping", "Swing Trading", "Trend Following"],
     group = z_score_settings,
     tooltip = tooltip_preset + "\n\n" + tooltip_preset_details)

// Apply input presets
if input_preset == "Scalping"
    lookback_period := 10
    entry_threshold := 1.0
else if input_preset == "Swing Trading"
    lookback_period := 25
    entry_threshold := 1.8
else if input_preset == "Trend Following"
    lookback_period := 40
    entry_threshold := 2.2

// Color Options
color_preset = input.string("Custom", "Color Preset", 
     options = ["Classic", "Aqua", "Cosmic", "Custom"],
     group = color_settings,
     tooltip = tooltip_color_preset)

bullish_color = input.color(#00ffaa, "Bullish Color", 
     group = color_settings,
     tooltip = tooltip_color_up)

bearish_color = input.color(#ff0000, "Bearish Color", 
     group = color_settings,
     tooltip = tooltip_color_down)

momentum_column_transparency = input.int(65, "Momentum Column Transparency", 
     minval = 0, maxval = 100,
     group = color_settings,
     tooltip = tooltip_column_transparency)

// Info Table Settings
show_info_table = input.bool(true, "Show Info Table", 
     group = table_settings,
     tooltip = tooltip_show_table)

table_preset = input.string("Custom", "Table Color Preset", 
     options = ["Mono", "Light", "Blue", "Custom"],
     group = table_settings,
     tooltip = tooltip_table_preset)

info_table_position = input.string("Top Right", "Table Position", 
     options = ["Top Left", "Top Center", "Top Right", "Middle Left", "Middle Center", "Middle Right", "Bottom Left", "Bottom Center", "Bottom Right"],
     group = table_settings,
     tooltip = tooltip_table_position)

table_background_color = input.color(color.new(#1a1a1a, 0), "Table Background Color", 
     group = table_settings,
     tooltip = tooltip_table_background)

header_background_color = input.color(color.new(#2d2d2d, 0), "Header Background Color", 
     group = table_settings,
     tooltip = tooltip_header_background)

header_text_color = input.color(color.white, "Header Text Color", 
     group = table_settings,
     tooltip = tooltip_header_text)

normal_text_color = input.color(color.white, "Normal Text Color", 
     group = table_settings,
     tooltip = tooltip_normal_text)

border_color = input.color(color.new(#333333, 0), "Border Color", 
     group = table_settings,
     tooltip = tooltip_border_color)

table_text_size = input.string("Normal", "Table Text Size", 
     options = ["Tiny", "Small", "Normal", "Large"],
     group = table_settings,
     tooltip = tooltip_table_text_size)

//              ╔════════════════════════╗              //
//              ║    CORE CALCULATIONS   ║              //
//              ╚════════════════════════╝              //

// Apply color presets
[final_bullish_color, final_bearish_color] = switch color_preset
    "Classic" => [#00ff00, #ff0000]
    "Aqua" => [#00bfff, #ff8c00]
    "Cosmic" => [#ff1493, #9932cc]
    "Custom" => [bullish_color, bearish_color]

// Apply table presets
[final_table_bg, final_header_bg, final_header_text, final_normal_text, final_border, use_preset_colors] = switch table_preset
    "Mono" => [color.new(#1a1a1a, 0), color.new(#2d2d2d, 0), color.white, color.white, color.new(#333333, 0), false]
    "Light" => [color.new(#ffffff, 0), color.new(#f0f0f0, 0), color.black, color.black, color.new(#cccccc, 0), true]
    "Blue" => [color.new(#1e3a5f, 0), color.new(#2e4a6f, 0), color.white, color.white, color.new(#4a6a8f, 0), false]
    "Custom" => [table_background_color, header_background_color, header_text_color, normal_text_color, border_color, false]

light_bullish_color = #006600
light_bearish_color = #cc0000

// Text size conversion
text_size = switch table_text_size
    "Tiny" => size.tiny
    "Small" => size.small
    "Normal" => size.normal
    "Large" => size.large

// Calculate rolling mean and standard deviation
mean = ta.sma(close, lookback_period)
stdDev = ta.stdev(close, lookback_period)

// Z-Score calculation
zScore = stdDev > 0 ? (close - mean) / stdDev : 0

// Smooth Z-Score for cleaner signals
smoothZ = ta.ema(zScore, 3)

// Rate of change of Z-Score (momentum)
zMomentum = smoothZ - smoothZ[1]

// Simplified position logic
position = smoothZ > 0 ? 1 : -1

//              ╔══════════════════════════════╗              //
//              ║         VISUALIZATION        ║              //
//              ╚══════════════════════════════╝              //

// Plot momentum
plot(barstate.isconfirmed ? zMomentum * 3 : na, "Momentum Background", style=plot.style_columns,
     color=zMomentum > 0 ? color.new(final_bullish_color, momentum_column_transparency) : color.new(final_bearish_color, momentum_column_transparency))

// Plot Z-Score
zScoreColor = smoothZ > 0 ? final_bullish_color : final_bearish_color
plot(barstate.isconfirmed ? smoothZ : na, "Z-Score Signal", color=zScoreColor, linewidth=3)
plot(barstate.isconfirmed ? smoothZ : na, "", color=color.new(zScoreColor, 85), linewidth=10, display=display.pane)
plot(barstate.isconfirmed ? smoothZ : na, "", color=color.new(zScoreColor, 98), linewidth=25, display=display.pane)

// Thresholds
p1 = plot(entry_threshold, display = display.none)
p3 = plot(-entry_threshold, display = display.none)
p4 = plot(-4, display = display.none)
p2 = plot(4, display = display.none)

fill(p1, p2, entry_threshold, 4, color.new(final_bullish_color, 60), na)
fill(p3, p4, -entry_threshold, -4, color.new(final_bearish_color, 60), na)

hline(0, "Mean", color=color.gray, linewidth=2)

// Table position
tablePos = switch info_table_position
    "Top Left" => position.top_left
    "Top Center" => position.top_center
    "Top Right" => position.top_right
    "Middle Left" => position.middle_left
    "Middle Center" => position.middle_center
    "Middle Right" => position.middle_right
    "Bottom Left" => position.bottom_left
    "Bottom Center" => position.bottom_center
    "Bottom Right" => position.bottom_right
    => position.top_right

// Info table
var table infoTable = show_info_table ? table.new(tablePos, 4, 3, bgcolor=final_table_bg, border_width=1, frame_color=final_border, frame_width=1) : na

if barstate.isconfirmed and show_info_table
    table.clear(infoTable, 0, 0, 3, 2)
    
    table.cell(infoTable, 0, 0, "Metric", bgcolor=final_header_bg, 
               text_color=final_header_text, text_size=text_size)
    table.cell(infoTable, 1, 0, "Value", bgcolor=final_header_bg, 
               text_color=final_header_text, text_size=text_size)
    table.cell(infoTable, 2, 0, "Trend", bgcolor=final_header_bg, 
               text_color=final_header_text, text_size=text_size)
    table.cell(infoTable, 3, 0, "Change", bgcolor=final_header_bg, 
               text_color=final_header_text, text_size=text_size)
    
    zScoreChange = smoothZ - smoothZ[1]
    zMomentumChange = zMomentum - zMomentum[1]
    
    table.cell(infoTable, 0, 1, "Z-Score", bgcolor=final_table_bg,
               text_color=final_normal_text, text_size=text_size)
    table.cell(infoTable, 1, 1, str.tostring(smoothZ, "0.00"), bgcolor=final_table_bg,
               text_color=use_preset_colors ? (smoothZ > 0 ? light_bullish_color : light_bearish_color) : (table_preset == "Mono" ? final_normal_text : (smoothZ > 0 ? final_bullish_color : final_bearish_color)), text_size=text_size)
    table.cell(infoTable, 2, 1, smoothZ > 0 ? "↑" : "↓", bgcolor=final_table_bg,
               text_color=use_preset_colors ? (smoothZ > 0 ? light_bullish_color : light_bearish_color) : (table_preset == "Mono" ? final_normal_text : (smoothZ > 0 ? final_bullish_color : final_bearish_color)), text_size=text_size)
    table.cell(infoTable, 3, 1, (zScoreChange >= 0 ? "+" : "") + str.tostring(zScoreChange, "0.00"), bgcolor=final_table_bg,
               text_color=use_preset_colors ? (zScoreChange >= 0 ? light_bullish_color : light_bearish_color) : (table_preset == "Mono" ? final_normal_text : (zScoreChange >= 0 ? final_bullish_color : final_bearish_color)), text_size=text_size)
    
    table.cell(infoTable, 0, 2, "Momentum", bgcolor=final_table_bg,
               text_color=final_normal_text, text_size=text_size)
    table.cell(infoTable, 1, 2, str.tostring(zMomentum, "0.00"), bgcolor=final_table_bg,
               text_color=use_preset_colors ? (zMomentum > 0 ? light_bullish_color : light_bearish_color) : (table_preset == "Mono" ? final_normal_text : (zMomentum > 0 ? final_bullish_color : final_bearish_color)), text_size=text_size)
    table.cell(infoTable, 2, 2, zMomentum > 0 ? "↑" : "↓", bgcolor=final_table_bg,
               text_color=use_preset_colors ? (zMomentum > 0 ? light_bullish_color : light_bearish_color) : (table_preset == "Mono" ? final_normal_text : (zMomentum > 0 ? final_bullish_color : final_bearish_color)), text_size=text_size)
    table.cell(infoTable, 3, 2, (zMomentumChange >= 0 ? "+" : "") + str.tostring(zMomentumChange, "0.00"), bgcolor=final_table_bg,
               text_color=use_preset_colors ? (zMomentumChange >= 0 ? light_bullish_color : light_bearish_color) : (table_preset == "Mono" ? final_normal_text : (zMomentumChange >= 0 ? final_bullish_color : final_bearish_color)), text_size=text_size)

//              ╔══════════════════════════════╗              //
//              ║            ALERTS            ║              //
//              ╚══════════════════════════════╝              //

alertcondition(ta.crossover(smoothZ, 0), "Long Signal", "Z-Score Turned Bullish - Go Long")
alertcondition(ta.crossunder(smoothZ, 0), "Short Signal", "Z-Score Turned Bearish - Go Short")
alertcondition(ta.crossover(smoothZ, entry_threshold), "Overbought Zone Entry", "Z-Score Entered Overbought Zone")
alertcondition(ta.crossunder(smoothZ, entry_threshold), "Overbought Zone Exit", "Z-Score Exited Overbought Zone")
alertcondition(ta.crossunder(smoothZ, -entry_threshold), "Oversold Zone Entry", "Z-Score Entered Oversold Zone")
alertcondition(ta.crossover(smoothZ, -entry_threshold), "Oversold Zone Exit", "Z-Score Exited Oversold Zone")

//              ╔════════════════════════════════╗              //
//              ║           CREATED BY           ║              //
//              ╚════════════════════════════════╝              //

// ██████╗ ██╗   ██╗ █████╗ ███╗   ██╗████████╗     █████╗ ██╗      ██████╗  ██████╗ 
//██╔═══██╗██║   ██║██╔══██╗████╗  ██║╚══██╔══╝    ██╔══██╗██║     ██╔════╝ ██╔═══██╗
//██║   ██║██║   ██║███████║██╔██╗ ██║   ██║       ███████║██║     ██║  ███╗██║   ██║
//██║▄▄ ██║██║   ██║██╔══██║██║╚██╗██║   ██║       ██╔══██║██║     ██║   ██║██║   ██║
//╚██████╔╝╚██████╔╝██║  ██║██║ ╚████║   ██║       ██║  ██║███████╗╚██████╔╝╚██████╔╝
// ╚══▀▀═╝  ╚═════╝ ╚═╝  ╚═╝╚═╝  ╚═══╝   ╚═╝       ╚═╝  ╚═╝╚══════╝ ╚═════╝  ╚═════╝

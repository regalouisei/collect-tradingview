//@version=5
indicator(title='Yield Curve Percent Inverted', shorttitle='Yield Curve', overlay=false)

//**************************************************************************************************
// Yield Curve Percent Inverted Indicator
// 
// This indicator checks all seventy-eight Treasury Bond Yield spreads - every combination from
// 1-month up to 30-year - and then graph the percentage of spreads which are inverted.
// 
// Yield curve inversion occurs when the longer-duration bond pays a lower yield than the shorter-
// duration bond. Longer-dated bonds normally pay a higher yield because the investor's money is
// committed for a longer period of time. Inversion occurs when investors have little confidence
// in the near-term economy and demand higher rates for short-term investments.
// 
// Historically, a few months ahead of a recession this percent-inverted value will spike up into
// the 60%-70% range - you can see this behavior throughout the 1960's and 1970's, as well as in
// 1989, 2000, 2007, and 2019/2020. As of 2023-2024 the Yield Curve has been very inverted for
// quite a while but no recession yet.
// 
// Jan 2024 update
// - Convert to PineScript v5
// - Include 4-month and 20-year bonds
// - Replace tedious expressions with array and nested for loop
//**************************************************************************************************

warn_level = input.int(title='Warning Level', defval=60, minval=0, maxval=100)

arr = array.new_float()
arr.push(request.security('TVC:US01MY', timeframe.period, close, barmerge.gaps_off, barmerge.lookahead_on))
arr.push(request.security('TVC:US02MY', timeframe.period, close, barmerge.gaps_off, barmerge.lookahead_on))
arr.push(request.security('TVC:US03MY', timeframe.period, close, barmerge.gaps_off, barmerge.lookahead_on))
arr.push(request.security('TVC:US04MY', timeframe.period, close, barmerge.gaps_off, barmerge.lookahead_on))
arr.push(request.security('TVC:US06MY', timeframe.period, close, barmerge.gaps_off, barmerge.lookahead_on))
arr.push(request.security('TVC:US01Y', timeframe.period, close, barmerge.gaps_off, barmerge.lookahead_on))
arr.push(request.security('TVC:US02Y', timeframe.period, close, barmerge.gaps_off, barmerge.lookahead_on))
arr.push(request.security('TVC:US03Y', timeframe.period, close, barmerge.gaps_off, barmerge.lookahead_on))
arr.push(request.security('TVC:US05Y', timeframe.period, close, barmerge.gaps_off, barmerge.lookahead_on))
arr.push(request.security('TVC:US07Y', timeframe.period, close, barmerge.gaps_off, barmerge.lookahead_on))
arr.push(request.security('TVC:US10Y', timeframe.period, close, barmerge.gaps_off, barmerge.lookahead_on))
arr.push(request.security('TVC:US20Y', timeframe.period, close, barmerge.gaps_off, barmerge.lookahead_on))
arr.push(request.security('TVC:US30Y', timeframe.period, close, barmerge.gaps_off, barmerge.lookahead_on))

int count = 0
int total = 0

for [i, ival] in arr
    for [j, jval] in arr
        if (j > i and not na(ival) and not na(jval))
            total += 1
            count += (ival > jval) ? 1 : 0

pct = (count/total) * 100
plot(pct, title='Pct. Inverted', color=color.black)

hline(100, title='Max. Level', color=color.gray, linestyle=hline.style_solid, linewidth=1)
hline(warn_level, title='Warning Level', linestyle=hline.style_solid, color=color.red, linewidth=1)
hline(0, title='Min. Level', color=color.gray, linestyle=hline.style_solid, linewidth=1)

bgcolor((pct >= warn_level) ? color.new(color.red, 70) : color.new(color.white, 70))

//EOF

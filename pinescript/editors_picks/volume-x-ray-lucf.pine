//@version=5
//@author=LucF
 
// Volume X-ray [LucF]
//  v4, 2021.11.01 12:20 — LucF

// Compares daily volume from the intraday and EOD feeds.

// This code was written using:
//  • The recommendations in Pine's Style Guide: https://www.tradingview.com/pine-script-docs/en/v5/writing/Style_guide.html

indicator("Volume X-ray [LucF]", "Volume X-ray", format = format.volume)

color BULL_COLOR  = #008000
color BEAR_COLOR  = #FF0000
color ERROR_COLOR = color.yellow
color EOD_COLOR   = #008080
color INT_COLOR   = #FF8000

printTable(txt, txtColor, bgColor) => 
    var table t = table.new(position.top_right, 1, 1)
    table.cell(t, 0, 0, txt, text_color = txtColor, text_size = size.large, bgcolor = bgColor)

// Doesn't make sense to run the script on >1D TFs, as `request.security()` calls would be fetching incomplete intrabar data.
if (timeframe.isdaily and timeframe.multiplier != 1) or timeframe.isweekly or timeframe.ismonthly
    runtime.error("Use at timeframes of 1D or less.")

// Volume from EOD feed.
float volumeEod = request.security(syminfo.tickerid, "D",    volume, gaps = barmerge.gaps_on)
// Create a regular session ticker for intraday volume because EOD feed does not contain extended session volume.
regularSessionInt = ticker.modify(syminfo.tickerid, session.regular)
// Volume from intraday feed (sum of 1min intraday bars).
float volumeInt = request.security(regularSessionInt, "1440", volume, gaps = barmerge.gaps_on)
// Cumul of daily values only when volume data exists in both feeds.
float volumeEodCum = fixnan(ta.cum(not na(volumeInt) and barstate.ishistory? volumeEod : 0))
float volumeIntCum = fixnan(ta.cum(not na(volumeEod) and barstate.ishistory? volumeInt : 0))
// Percentage that daily intraday volume represents over daily EOD volume.
float volumeIntradayPct = volumeIntCum / volumeEodCum


// ————— Plots

// Daily area and line. Plot darker if the other feed supplies no data.
plot(barstate.ishistory ? volumeEod : na, "EOD Volume",      nz(volumeInt) != 0 ? EOD_COLOR : color.new(EOD_COLOR, 50), style = plot.style_area)
plot(barstate.ishistory ? volumeInt : na, "Intraday Volume", nz(volumeEod) != 0 ? INT_COLOR : color.new(INT_COLOR, 50), 2)

// Colored table cell with cumulative %
bool noEodVolumeCum = nz(volumeEodCum) == 0
bool noIntVolumeCum = nz(volumeIntCum) == 0
color msgBgColor = 
  switch
    noEodVolumeCum or noIntVolumeCum => ERROR_COLOR
    volumeIntradayPct <  0.50        => color.from_gradient(volumeIntradayPct, 0.0, 0.5, BEAR_COLOR, color.new(BEAR_COLOR, 70))
    volumeIntradayPct >= 0.50        => color.from_gradient(volumeIntradayPct, 0.5, 1.0, color.new(BULL_COLOR, 70), BULL_COLOR)
string msg = 
  switch
    noEodVolumeCum and noIntVolumeCum => "No volume"
    noEodVolumeCum => "No EOD volume"
    noIntVolumeCum => "No Intraday volume"
    => "Intraday volume: " + (not na(volumeIntradayPct) ? str.tostring(volumeIntradayPct, "0  %") : "—")
printTable(msg, msgBgColor == ERROR_COLOR ? color.black : color.white, msgBgColor)

// Data Window plots.
plotchar(volumeEodCum, "Cumulative EOD volume", "", location.top, msgBgColor)
plotchar(volumeIntCum, "Cumulative Intraday volume", "", location.top, msgBgColor)
plotchar(volumeIntradayPct * 100, "volumeIntradayPct", "", location.top, msgBgColor)

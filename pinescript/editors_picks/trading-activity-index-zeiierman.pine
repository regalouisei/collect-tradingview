// This work is licensed under Creative Commons Attribution-NonCommercial-ShareAlike 4.0 International  
// https://creativecommons.org/licenses/by-nc-sa/4.0/

// © Zeiierman {
//@version=6
indicator("Trading Activity Index (Zeiierman)", overlay=false, max_bars_back=2000, precision = 1)
//~~}

// ~~ Tooltips {
t1 = "Formation window (bars) for averaging dollar volume before taking the log."
t2 = "History window (bars) used to compute rolling percentiles and bands."
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~}

// ~~ Inputs {
len_form   = input.int(20,  minval=2,  step=1, title="Formation Window (bars)", group="", inline="", tooltip=t1)
len_hist   = input.int(252, minval=50, step=5, title="History Window (bars)",   group="", inline="", tooltip=t2)
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~}

// ~~ Core series {
var float na_f = na
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~}

// Dollar Volume: price * volume {
float dlrVol  = close * volume
float dlrVolAvg = ta.sma(dlrVol, len_form)
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~}

// VOLSCALE proxy = log(average(dollar volume)) {
float vscale = math.log(math.max(dlrVolAvg, 1e-10))
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~}

// Rolling percentiles of VOLSCALE proxy over len_hist {
float p20 = ta.percentile_linear_interpolation(vscale, len_hist, 20)
float p40 = ta.percentile_linear_interpolation(vscale, len_hist, 40)
float p60 = ta.percentile_linear_interpolation(vscale, len_hist, 60)
float p80 = ta.percentile_linear_interpolation(vscale, len_hist, 80)
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~}

// Percentile rank proxy for display only (normalize by rolling min/max) {
float vMin = ta.lowest(vscale, len_hist)
float vMax = ta.highest(vscale, len_hist)
float rank01 = (vMax == vMin) ? 0.5 : math.max(0.0, math.min(1.0, (vscale - vMin) / (vMax - vMin)))
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~}

// ~~ Gradient color function {
gradientColor(_val) =>
    // Light blue = rgb(173, 216, 230), Orange/Red = rgb(255, 69, 0)
    r1 = 173.0, g1 = 216.0, b1 = 230.0
    r2 = 255.0, g2 = 69.0,  b2 = 0.0
    r = r1 + (r2 - r1) * _val
    g = g1 + (g2 - g1) * _val
    b = b1 + (b2 - b1) * _val
    color.rgb(math.round(r), math.round(g), math.round(b))

blueGradient(_val) =>
    // Light blue → Deep blue for percentile bands
    r1 = 173.0, g1 = 216.0, b1 = 230.0  
    r2 = 0.0,   g2 = 0.0,   b2 = 255.0 
    r = r1 + (r2 - r1) * _val
    g = g1 + (g2 - g1) * _val
    b = b1 + (b2 - b1) * _val
    color.rgb(math.round(r), math.round(g), math.round(b))
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~}

// ~~ Plots {
plot(vscale, title="Trading Activity", color=gradientColor(rank01), linewidth=2)
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~}

// Quintile bands {
plot(p20, title="P20", style=plot.style_line, linewidth=1, color=blueGradient(0.2))
plot(p40, title="P40", style=plot.style_line, linewidth=1, color=blueGradient(0.4))
plot(p60, title="P60", style=plot.style_line, linewidth=1, color=blueGradient(0.6))
plot(p80, title="P80", style=plot.style_line, linewidth=1, color=blueGradient(0.8))
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~}

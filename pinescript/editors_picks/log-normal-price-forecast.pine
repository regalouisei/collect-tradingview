// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © nasu_is_gaji

//@version=6
indicator("Log-Normal Price Forecast", overlay=true)

import loxx/norminv/1 as norminv

len = input.int(100, "Model Length", minval=1)
forecastLen = input.int(100, "Forecast Length", minval=1, maxval=500)
smoothLen = input.int(20, "Volatility SMA Length", minval=1)

dev1 = input.float(95, "Confidence 1")
dev2 = input.float(99, "Confidence 2")
dev3 = input.float(99.9, "Confidence 3")

logRet = math.log(close/close[1])

mu = ta.sma(logRet, len)
sigma = ta.sma(ta.stdev(logRet, len), smoothLen)

var polyline l3 = na
var polyline l2 = na
var polyline l1 = na
var polyline m = na
var polyline h1 = na
var polyline h2 = na
var polyline h3 = na
if barstate.islast
    l3.delete()
    l2.delete()
    l1.delete()
    m.delete()
    h1.delete()
    h2.delete()
    h3.delete()

    vl3 = array.new<chart.point>(0)
    vl2 = array.new<chart.point>(0)
    vl1 = array.new<chart.point>(0)
    vm = array.new<chart.point>(0)
    vh1 = array.new<chart.point>(0)
    vh2 = array.new<chart.point>(0)
    vh3 = array.new<chart.point>(0)

    for i = 0 to forecastLen
        drift = (mu-0.5*sigma*sigma) * i
        vol = sigma * math.sqrt(i)
        low3 = close*math.exp(drift + vol * norminv.norminv(0.5 - dev3/200, 0, 1))
        low2 = close*math.exp(drift + vol * norminv.norminv(0.5 - dev2/200, 0, 1))
        low1 = close*math.exp(drift + vol * norminv.norminv(0.5 - dev1/200, 0, 1))
        med = close*math.exp(drift + vol * norminv.norminv(0.5, 0, 1))
        high1 = close*math.exp(drift + vol * norminv.norminv(0.5 + dev1/200, 0, 1))
        high2 = close*math.exp(drift + vol * norminv.norminv(0.5 + dev2/200, 0, 1))
        high3 = close*math.exp(drift + vol * norminv.norminv(0.5 + dev3/200, 0, 1))
        vl3.push(chart.point.from_index(bar_index+i, low3))
        vl2.push(chart.point.from_index(bar_index+i, low2))
        vl1.push(chart.point.from_index(bar_index+i, low1))
        vm.push(chart.point.from_index(bar_index+i, med))
        vh1.push(chart.point.from_index(bar_index+i, high1))
        vh2.push(chart.point.from_index(bar_index+i, high2))
        vh3.push(chart.point.from_index(bar_index+i, high3))

    l3 := polyline.new(vl3, line_color=color.red)
    l2 := polyline.new(vl2, line_color=color.yellow)
    l1 := polyline.new(vl1, line_color=color.green)
    m := polyline.new(vm, line_color=color.gray, line_style=line.style_dashed)
    h1 := polyline.new(vh1, line_color=color.green)
    h2 := polyline.new(vh2, line_color=color.yellow)
    h3 := polyline.new(vh3, line_color=color.red)

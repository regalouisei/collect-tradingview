// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © MUQWISHI

//@version=6
indicator("Volume Speed [MUQWISHI]", overlay = true)
import MUQWISHI/colorLab/1 as colLab
import n00btraders/Timezone/1 as tz

// |++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++|
// |                                   INPUT                                    |
// |++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++|
// +++++++++++++++ Table Settings
var G1 = "Widget Settings"
// Timezone
var timzon = input.enum(tz.TimezoneDST.NEW_YORK, "Timezone                          ", 
                         inline = "0", group = G1, display = display.none).tostring()

// Table Colors
tBgCol = input.color(#000000, "", group = G1, inline = "0", display = display.none)
tTxCol = input.color(#ffffff, "", group = G1, inline = "0", display = display.none)

// Location 
pltPos = input.string("Bottom Center", "Widget Location|Size", 
             ["Top Right" , "Middle Right"  , "Bottom Right" , 
              "Top Center", "Middle Center" , "Bottom Center", 
              "Top Left"  , "Middle Left"   , "Bottom Left" ], inline = "1", group = G1, display = display.none)

// Size
pltSiz = input.int(10, "", 0, inline = "1", group = G1, display = display.none)

// Cell Colors
upXCC = input.color(#0c5a99, "Up | Down Volume       ",        group = G1, inline = "4")
dnXCC = input.color(#E65100, "",                               group = G1, inline = "4")
clrMd = input.string("Gradient", "     ", ["Solid", "Gradient"], group = G1, inline = "4", display = display.none)

// Space
space = input.bool(false, "Make The Volume Scale Bar Spaced",    group = G1)


// +++++++++++++++ Technical Settings
var G2 = "Technical Settings"
// Timeframe
tFrm = input.timeframe("1S", "Lower Timeframe", group = G2, display = display.none,
         tooltip = "Note:\nIf the selected timeframe is invalid (higher than chart), " + 
                   "the indicator will automatically switch to 1 second.")

// Length
rowLen = input.int(60, "Length", 1, 100, group = G2, display = display.none) + 2

// Volume OR Price Volume
volTyp = input.string("Volume", "Size Type", ["Volume", "Price Volume"], group = G2, display = display.none)

// Flash
cFlsh = input.bool(true, "Enable a 3-Second Flash When Volume Is")
vFlsh = input.float(8,   "          Greater Than Average by X Times", 1, 100, inline = "5", display = display.none,
         tooltip = "For Example:\nFlashes when selected timeframe volume is...\n   2 (Double the average volume)." +
                   "\n   3 (Triple the average volume).\n   8 (8 Times the average volume).\n   and so...")


// |++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++|
// |                                 CALCULATION                                |
// |++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++|
// +++++++++++++++ Define Object
type tape
    int    tim  = na 
    int    chg  = na 
    float  last = na 
    float  vol  = na 

method volIndexof(array<tape> a, x) => 
    out = -1
    for [index, value] in a
        if value.vol == x
            out := index 
            break 
    out 


// +++++++++++++++ Set-Up Strings Format Based on Historical Values 
varip strFormat = array.new<string>(2, "0")

// +++++ Max Digits 
maxDig(x1, x2) => 
    str.length(x1) > str.length(x2) ? x1 : x2

// +++++ Get Price Format 
strTick(x) => 
    str.tostring(x, str.replace(str.tostring(syminfo.mintick), "1", "0"))

// +++++ Volume Format
// Number of Digits 
rDigits(x1, x2) =>
    var lft = 0, var out = "" 
    if str.contains(x2, ".")
        out := "0." 
        nLft = str.length(str.split(x2, ".").get(1))
        if nLft > lft
            lft := nLft
            for i = 1 to lft
                out += "0"

    str.length(x1) < str.length(out) ? out : x1

// Get Volume Format 
strVol(x) =>
    if volTyp == "Volume" 
        if x > 999999 
            str.tostring(x, format.volume) 
        else 
            volFormat = strFormat.get(1)
            str.tostring(x, volFormat)
    else 
        str.tostring(x, "$#0.00")


// +++++++++++++++ Get Timeframe Format
strTF(y) =>
    x   = y
    out = string(na)
    if timeframe.in_seconds(x) == timeframe.in_seconds("")
        x := timeframe.period

    if str.contains(x, "D") or str.contains(x, "W") or str.contains(x, "M") or str.contains(x, "S")
        out := str.length(x) == 1 ? "1" + x : x
    else
        out := str.tonumber(x) % 60 == 0 ? str.tostring(str.tonumber(x) / 60) + "H" : x + "min"
    
    out
  

// +++++++++++++++ Import Data
var timfram =  timeframe.in_seconds(tFrm) > timeframe.in_seconds(timeframe.period) ? "1S" : tFrm
[cls, tim, vol] = request.security_lower_tf(syminfo.tickerid, timfram, 
                  [close, time, volTyp == "Volume" ? volume : hlc3 * volume], true, 
                  ignore_invalid_timeframe = true, calc_bars_count = rowLen * 2)
 
// Stack Lower Timeframe Data
var tapeData = array.new<tape>(na)
if  vol.size() > 0 and vol.size() == cls.size()

    // Slice Variables
    sIdx = cls.size() - rowLen < 0 ? 0 : cls.size() - rowLen
    eIdx = cls.size()

    tim0 = tim.slice(sIdx, eIdx) // Time
    vol0 = vol.slice(sIdx, eIdx) // Volume
    cls0 = cls.slice(sIdx, eIdx) // Close

    // Process LTF Data 
    for i = 0 to cls0.size() - 1
        cTim = int(tim0.get(i))
        cVol = vol0.get(i)
        cCls = cls0.get(i)

        // Price Change 
        var int chg = 0
        if tapeData.size() > 0
            pCls = tapeData.get(0).last
            if pCls != cCls
                chg := (cCls - pCls)/pCls > 0 ? 1 : -1
        
        if not na(cVol) and cVol > 0
            // Add Element To "tapeData" Array
            tapeData.unshift(tape.new(cTim, chg, cCls, cVol))

            if barstate.islast
                strFormat.set(1, rDigits(strFormat.get(1), str.tostring(cVol)))


        // Remove Exceeded Elements
        while tapeData.size() > rowLen
            tapeData.pop()


// +++++++++++++++ Calculate Speed
speed(x1, x2) =>
    var ltf  = strTF(timfram)
    var unit = if volTyp == "Price Volume"
        "/" 
    else 
        switch syminfo.type
            "stock"  => " Shares/"
            "dr"     => " Shares/"
            "fund"   => " Shares/"
            "crypto" => " Coins/"
            => " Units/"

    // Speed 
    varip spMin = 0.0, varip spMax = 0.0, varip spTim = timenow

    speed = barstate.isrealtime ? x1/x2 : 0

    spMin := spMin == 0 ? speed : math.min(speed, spMin)
    spMax := spMax == 0 ? speed : math.max(speed, spMax)

    // Return 
    [strVol(math.round(speed, 6)) + unit + ltf, 
     strVol(math.round(spMin, 6)) + unit + ltf,
     strVol(math.round(spMax, 6)) + unit + ltf,
     str.format_time(spTim, "HH:mm:ss  yyyy-MM-dd", timzon)]



// |++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++|
// |                                   TABLE                                    |
// |++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++|
// +++++++++++++++ Get Tbale Location & Size
locNsze(x) => 
    y   = str.split(str.lower(x), " ")
    out = ""
    for i = 0 to y.size() - 1
        out += y.get(i)
        if i != y.size() - 1
            out += "_"
    out

// +++++++++++++++ Create Table
var table plt = na

// +++++++++++++++ Get Cell Function
cell(col, row, txt, w, h, color, txtCol, tip) =>
    plt.cell(col, row, txt, w, h, txtCol, bgcolor = color, text_size = pltSiz, text_formatting = text.format_bold)
    if tip != ""
        plt.cell_set_tooltip(col, row, tip)

// +++++++++++++++ Draw Table
if barstate.islast
    if tapeData.size() > 0
        plt.delete()
        plt := table.new(locNsze(pltPos), 102, 4, tBgCol, chart.fg_color, 1, color.new(tBgCol, 100), space ? 1 : 0)

        // Coollect Up & Down Volume 
        upVol = .0
        dnVol = .0
        for i = 0 to tapeData.size() - 3
            element = tapeData.get(i)
            if element.chg > 0
                upVol += element.vol 
            else
                dnVol += element.vol 
        
        span  = (timenow - tapeData.get(-3).tim)/(1000 * timeframe.in_seconds(timfram))
        smVol = upVol + dnVol
        avVol = smVol/(tapeData.size()-2)


        // Up & Down Volumes Columns
        strFormat.set(0, maxDig(strFormat.get(0), maxDig(strVol(upVol), strVol(dnVol))))

        cell(0,   3, "+" + strFormat.get(0) + "+", 0, 0.0001, color(na), color(na),   "Total Up Volume")
        cell(101, 3, "+" + strFormat.get(0) + "+", 0, 0.0001, color(na), color(na), "Total Down Volume")

        cell(0,   1, strVol(upVol), 0, 0, upXCC, tTxCol, "Total Up Volume")
        cell(101, 1, strVol(dnVol), 0, 0, dnXCC, tTxCol, "Total Down Volume")

        cell(  0, 0, "UP",   0, 0,  color(na), tTxCol, "Total Up Volume")
        cell(101, 0, "DOWN", 0, 0,  color(na), tTxCol, "Total Down Volume")

        
        // Volume Dynamic Scale Bar
        upStng = math.round(upVol/smVol * 100)
        dnStng = 100 - upStng

        var upCol = clrMd == "Gradient" ? colLab.lsh_scheme(upXCC, 100, "lightness", 85, 40) : array.new<color>(100, upXCC)
        var dnCol = clrMd == "Gradient" ? colLab.lsh_scheme(dnXCC, 100, "lightness", 85, 40) : array.new<color>(100, dnXCC)
        for i = 0 to 99
            cell(1 + i, 0, "", 0.3, 0, color(na), color(na), "")
            cell(1 + i, 2, "", 0.3, 0, color(na), color(na), "")
            cell(1 + i, 3, "", 0.3, 0.0001, color(na), color(na), "")
            if i < upStng 
                cell(1   + i, 1, "", 0.3, 0, upCol.get(i), tTxCol,  "")

            if i < dnStng
                cell(100 - i, 1, "", 0.3, 0, dnCol.get(i), tTxCol,  "")

        
        // Average Speed
        [avgSp, minSp, maxSp, timSp] = speed(smVol, span)
        plt.merge_cells(1, 0, 100, 0)
        plt.cell(1, 0, "AVG Speed " + avgSp, 0, 0, tTxCol, text_size = pltSiz,
         tooltip = "MAX Speed " + maxSp + "\nMIN  Speed " + minSp + "\n\n\nsince " + timSp)


        // Bottom Info
        plt.merge_cells(1, 2, 100, 2)
        if barstate.isrealtime
            cell(1, 2, "Total Vol " + strVol(smVol) + 
             "                    Period = "  + str.tostring(rowLen-2) + " @ " + strTF(timfram), 0, 0, color(na), tTxCol, "")

            // Flash
            if cFlsh
                varip spkTim = 0.0
                varip spkDic = 0.0
                varip spkVol = 0.0
                lstVol = tapeData.get(0).vol
                if lstVol >= avVol * vFlsh
                    dir     = tapeData.get(0).chg
                    spkTim := timenow + 3000
                    spkDic := dir
                    spkVol := lstVol

                if tapeData.volIndexof(spkVol) != -1
                    if spkDic > 0
                        cell(0,   2, "+" + strVol(spkVol), 0, 0, color(na), tTxCol, 
                         "Recorded - Most Recent \nUp Volume Is Graeter Than " + str.tostring(vFlsh) + " Times of the Total Average Volume")
                    if spkDic < 0
                        cell(101, 2, "+" + strVol(spkVol), 0, 0, color(na), tTxCol,
                         "Recorded - Most Recent \nDown Volume Is Graeter Than " + str.tostring(vFlsh) + " Times of the Total Average Volume")

                if spkTim > timenow + 2000
                    plt.set_bgcolor(spkDic > 0 ? color.new(upXCC, 25) : color.new(dnXCC, 25))
                else if spkTim > timenow + 1000
                    plt.set_bgcolor(spkDic > 0 ? color.new(upXCC, 50) : color.new(dnXCC, 50))
                else if spkTim > timenow
                    plt.set_bgcolor(spkDic > 0 ? color.new(upXCC, 75) : color.new(dnXCC, 75))
                else 
                    plt.set_bgcolor(tBgCol)
        else
        // Message
            cell(1, 2, "⚠ This Tool Performs During Live Market", 0, 0, color(na), color.red, "")

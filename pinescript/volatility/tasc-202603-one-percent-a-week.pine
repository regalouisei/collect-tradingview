//  TASC Issue: March 2026
//     Article: Trading Snapbacks In A Leveraged ETF
//              One Percent A Week: A High-Probability 
//              Weekly Trading Strategy For TQQQ - Part 1
//  Article By: Dion Kurczek
//    Language: TradingView's Pine ScriptÂ® v6
// Provided By: PineCoders, for tradingview.com

//@version=6
strategy(
    "TASC 2026.03 One Percent A Week", "1%/W", overlay = true, 
    initial_capital = 1000, 
    default_qty_type = strategy.percent_of_equity, 
    default_qty_value = 10
)


//#region 1. Observe the weekly open price on Monday.

var float wOpen = na
var float pt    = na
bool newSession = dayofweek == dayofweek.monday and session.isfirstbar_regular

if newSession
    wOpen := open
//#endregion


//#region 2. Set limit buy at a 1% dip from Monday open.

float down1 = wOpen * 0.99

if newSession
    strategy.entry("Buy", strategy.long, limit = down1)
    pt := na
//#endregion


//#region 3. Once filled, set a 1% target on the next day.

bool newFill = ta.change(strategy.position_size) != 0
var bool sendPT = false
var bool beSent = false

if strategy.position_size > 0 and newFill
    sendPT := true
    beSent := false

if sendPT == true and session.isfirstbar_regular and not beSent
    pt := strategy.position_avg_price * 1.01
    strategy.exit(
        "profit", "Buy", limit = pt, 
        oca_name = "Order", comment = "Profit"
    )
    sendPT := false
//#endregion

//#region 4. Risk management: Exit at break-even level after a -0.5% change.

float bet = strategy.position_avg_price * 0.995

if low < bet and not beSent
    pt := strategy.position_avg_price
    strategy.exit(
        "profit", "Buy", limit = pt, 
        oca_name = "Order", comment = "Break-even"
    )
    beSent := true
//#endregion


//#region 5. Hard exit at Friday close.

if (
    dayofweek == dayofweek.friday and 
    strategy.position_size > 0 and 
    session.islastbar_regular and barstate.isconfirmed
)    
    strategy.close_all("End of week", immediately = true)
//#endregion


//#region Plots

plot(
    newSession or not na(pt) ? na : wOpen, 
    "Weekly Open", color.yellow, 
    style = plot.style_linebr
)

plot(
    newSession or not na(pt) ? na : down1,
    "Weekly 1% Dip", color.blue, 
    style = plot.style_linebr, 
    linestyle = plot.linestyle_dashed
)

plot(
    strategy.position_size > 0? bet : na,
    "0.5% Drawdown", color.rgb(255,0,0),
    style = plot.style_linebr, 
    linestyle = plot.linestyle_dashed
)

plot(
    strategy.position_size > 0 ? pt : na, 
    "Profit Target", color.rgb(0,255,0), 
    style = plot.style_linebr, 
    linestyle = plot.linestyle_solid
)
//#endregion

// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © AdarshShrma

//@version=6
indicator('Institutional absorption score', shorttitle = 'IAS', overlay = false)

lookback = input.int(20, 'Lookback Period', minval = 5, maxval = 100)
volMult = input.float(1.5, 'Volume Multiplier Threshold', minval = 1.0, maxval = 5.0, step = 0.1)
bodyRatio = input.float(0.35, 'Max Body/Range Ratio', minval = 0.05, maxval = 0.9, step = 0.05)
rangePerc = input.float(0.6, 'Max Range % of ATR', minval = 0.1, maxval = 2.0, step = 0.1)
hlLookback = input.int(5, 'Higher Lows Lookback', minval = 2, maxval = 20)
scoreSmooth = input.int(3, 'Score Smoothing', minval = 1, maxval = 10)

cNone = input.color(color.new(color.red, 60), 'None')
cLow = input.color(color.new(color.orange, 30), 'Low')
cMedium = input.color(color.new(color.yellow, 20), 'Medium')
cHigh = input.color(color.new(color.green, 20), 'High')
cVeryHigh = input.color(color.new(color.lime, 0), 'Very High')

atr = ta.atr(lookback)
avgVol = ta.sma(volume, lookback)
candleRange = high - low
bodySize = math.abs(close - open)
bodyToRange = candleRange > 0 ? bodySize / candleRange : 1.0

smallBody = bodyToRange < bodyRatio ? 1 : 0
highVol = volume > avgVol * volMult ? 1 : 0
tightRange = candleRange < atr * rangePerc ? 1 : 0

isHigherLow = low > ta.lowest(low, hlLookback)[1]

volStrength = math.min(volume / (avgVol * volMult) * 30, 30)
bodyScore = math.min((bodyRatio - bodyToRange) / bodyRatio * 20, 20)
rangeScore = math.min((atr * rangePerc - candleRange) / (atr * rangePerc) * 20, 20)
hlScore = isHigherLow ? 15 : 0
baseCondition = bool(smallBody) and bool(highVol) and bool(tightRange) ? 15 : 0

rawScore = baseCondition + volStrength + math.max(bodyScore, 0) + math.max(rangeScore, 0) + hlScore
absScore = ta.sma(rawScore, scoreSmooth)
absScoreNorm = math.min(absScore, 100)

momentum = ta.sma(absScoreNorm, 3) - ta.sma(absScoreNorm, 8)
scoreWithMom = math.min(absScoreNorm + math.max(momentum * 0.5, 0), 100)

scoreColor = scoreWithMom >= 80 ? cVeryHigh : scoreWithMom >= 60 ? cHigh : scoreWithMom >= 40 ? cMedium : scoreWithMom >= 20 ? cLow : cNone

plot(scoreWithMom, 'Absorption Score', color = scoreColor, linewidth = 2, style = plot.style_area)

// This work is licensed under Creative Commons Attribution-NonCommercial-ShareAlike 4.0 International  
// https://creativecommons.org/licenses/by-nc-sa/4.0/
// © BigBeluga

//@version=6
indicator("Liquidity Depth Indicator [BigBeluga]", overlay = true, max_labels_count = 500, max_boxes_count = 500)

// ＩＮＰＵＴＳ ――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――{
loockBack = input.int(300, "LoockBack Period")
pocD = input.bool(true, "PoC", inline = "h1")
colP = input.color(color.rgb(212, 26, 26), "", inline = "h1")

Heatmap = input.bool(true, "HeatMap", inline = "h")
colBuy = input.color(color.rgb(26, 128, 212), "", inline = "h")
colSell = input.color(color.rgb(212, 100, 26), "", inline = "h")



binsVol = array.new_float(100, 0)
// }


// ＣＡＬＣＵＬＡＴＩＯＮＳ / ＰＬＯＴ ―――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――{

top = ta.highest(loockBack)
bot = ta.lowest(loockBack)
mid = math.avg(top, bot)
step = (top-bot) / 100



if barstate.islast

    totalCumulative   = 0.
    totalCumulativeUp = 0.
    totalCumulativeDn = 0.

    cumulativeUp = 0.
    cumulativeDn = 0.
    volUp   = 0.
    volDn   = 0.
    cpSell  = array.new<chart.point>()
    cpSell1 = array.new<chart.point>()

    cpBuy  = array.new<chart.point>()
    cpBuy1 = array.new<chart.point>()

    var heatmap = array.new_box()

    for b in heatmap
        b.delete()

    heatmap.clear()


    offset = 100

    for i = 0 to loockBack
        c = close[i]
        v = volume[i]

        for k = 0 to 100-1
            m = (bot + step * k) + step/2
            if math.abs(m - c) <= step
                binsVol.set(k, binsVol.get(k) + v)


    for k = 0 to 100-1
        m   = bot + step * k + step/2
        val = binsVol.get(k) / binsVol.max() * 2
        totalCumulative += val

        if m > mid
            totalCumulativeUp += val
            volUp += binsVol.get(k)
        else 
            totalCumulativeDn += val
            volDn += binsVol.get(k)

    off = int(math.max(totalCumulativeUp, totalCumulativeDn)/totalCumulative * offset)

    // Sell Curve
    for k = 50 to 100-1

        m   = bot + step * k + step/2
        val = binsVol.get(k) / binsVol.max() * 2

        if m > mid

            cumulativeUp += val/totalCumulative*offset

            if k == 50
                cpSell.push(chart.point.from_index(bar_index+off, m))
                cpSell1.push(chart.point.from_index(bar_index+off, m))

            cpSell.push(chart.point.from_index(bar_index+off-int(cumulativeUp), m))
            cpSell1.push(chart.point.from_index(bar_index+off-int(cumulativeUp), m))

            if k == 100-1 
                cpSell.push(chart.point.from_index(bar_index+off, m))
                label.delete(label.new(bar_index-int(cumulativeUp)+off, m, style = label.style_label_down, text = str.tostring(volUp, format.volume), color = colSell)[1])

    // Buy curve
    for k = 99 to 0

        m   = bot + step * k + step/2
        val = binsVol.get(k) / binsVol.max() * 2

        if m <= mid
            cumulativeDn += val/totalCumulative*offset 
            volUp        += binsVol.get(k)

            if k == 49 
                cpBuy1.push(chart.point.from_index(bar_index+off, m))
                cpBuy.push(chart.point.from_index(bar_index+off, m))

            cpBuy.push(chart.point.from_index(bar_index-int(cumulativeDn)+off, m))
            cpBuy1.push(chart.point.from_index(bar_index-int(cumulativeDn)+off, m))

            if k == 0 
                cpBuy.push(chart.point.from_index(bar_index+off, m))
                label.delete(label.new(bar_index-int(cumulativeDn)+off, m, style = label.style_label_up, text = str.tostring(volDn, format.volume), color = colBuy)[1])



    // Heatmap
    if Heatmap
        for k = 0 to 100-1
            m       = bot + step * k + step/2
            bbot    = bot + step * k 
            ttop    = bbot + step
            val     = binsVol.get(k) / binsVol.max() 
            heatCol = val > 0.0 ? color.from_gradient(val, 0, 2, color(na), m > mid ? colSell : colBuy) : color(na)

            heatmap.push(box.new(bar_index-loockBack, ttop, bar_index, bbot, color(na), 0, bgcolor = heatCol))

    // PoC
    if pocD
        for k = 0 to 100-1
            m       = bot + step * k + step/2
            
            if binsVol.get(k) ==  binsVol.max() 
                line.delete(line.new(bar_index, m, bar_index-loockBack, m, color = colP, style = line.style_solid, width = 2)[1])
                label.delete(label.new(bar_index-loockBack, m, style = label.style_label_right, text = str.tostring(binsVol.get(k), format.volume), color = colP)[1])

    buyPerc = (totalCumulativeDn/totalCumulative)*100
    stepB   = (mid-bot) / 100
    sellPer = (totalCumulativeUp/totalCumulative)*100


    // Buy Sell % Boxes
    box.delete(box.new(bar_index+off, top, bar_index+off+10, mid, border_color = color.new(colSell, 40), bgcolor = color(na))[1])
    box.delete(box.new(bar_index+off, bot, bar_index+off+10, mid, border_color = color.new(colBuy, 40), bgcolor = color(na))[1])

    box.delete(box.new(bar_index+off, mid + stepB * sellPer, bar_index+off+10, mid, border_color = color(na), bgcolor = color.new(colSell, 80), text = "\n" + str.tostring(sellPer, format.percent), text_valign = text.align_top)[1])
    box.delete(box.new(bar_index+off, mid-stepB * buyPerc, bar_index+off+10, mid, border_color = color(na), bgcolor = color.new(colBuy, 80), text = str.tostring(buyPerc, format.percent) + "\n", text_valign = text.align_bottom)[1])

    // Range Frame
    line.delete(line.new(bar_index+off, mid, bar_index-loockBack, mid, color = color.new(chart.fg_color, 50), style = line.style_dotted)[1])
    line.delete(line.new(bar_index+off, top, bar_index-loockBack, top, color = colSell, style = line.style_dashed, width = 1)[1])
    line.delete(line.new(bar_index+off, bot, bar_index-loockBack, bot, color = colBuy, style = line.style_dashed, width = 1)[1])


    // Cumulative Volume curves
    polyline.delete(polyline.new(cpSell, closed = true, fill_color = color.new(colSell, 80), line_color = color(na))[1])
    polyline.delete(polyline.new(cpSell1, line_color = colSell, line_width = 4, line_style = line.style_dotted)[1])
    polyline.delete(polyline.new(cpSell1, line_color = colSell, line_width = 2)[1])

    polyline.delete(polyline.new(cpBuy, closed = true, fill_color = color.new(colBuy, 80), line_color = color(na))[1])
    polyline.delete(polyline.new(cpBuy1, line_color = colBuy, line_width = 4, line_style = line.style_dotted)[1])
    polyline.delete(polyline.new(cpBuy1, line_color = colBuy, line_width = 2)[1])
    
// }

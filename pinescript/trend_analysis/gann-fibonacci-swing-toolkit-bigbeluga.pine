// This work is licensed under Creative Commons Attribution-NonCommercial-ShareAlike 4.0 International
// https://creativecommons.org/licenses/by-nc-sa/4.0/
// © BigBeluga

//@version=6
indicator(
     "Gann-Fibonacci Swing Toolkit [BigBeluga]",
     overlay          = true,
     max_lines_count  = 500,
     max_labels_count = 500,
     max_bars_back    = 5000
)

// ─────────────────────────────────────────────────────────────────────────────
// INPUTS
// ─────────────────────────────────────────────────────────────────────────────

group1  = "ZigZag"

length  = input.int(
     60,
     "Swing Detection",
     group = group1,
     tooltip = "Number of bars used to detect swing highs and lows.\n\nHigher values → fewer but stronger swings.\nLower values → more reactive swing detection."
)

zigZag  = input.bool(
     true,
     "Swings",
     group = group1,
     inline = "sw"
)

colUp   = input.color(
     color.lime,
     "",
     group = group1,
     inline = "sw",
     tooltip = "Color used for  swing legs."
)

colDn   = input.color(
     color.red,
     "",
     group = group1,
     inline = "sw",
     tooltip = "Color used for bullish/bearish swing legs."
)

fibbType = input.string(
     "Fan",
     "Fibonacci Type:",
     ["Fan", "Box", "None"],
     tooltip = "Select Fibonacci visualization type:\n\nFan → Gann/Fibonacci fan projections.\nBox → Horizontal & vertical Fibonacci box levels.\nNone → Disable Fibonacci tools."
) 

// ─────────────────────────────────────────────────────────────────────────────
// TYPES
// ─────────────────────────────────────────────────────────────────────────────

type fibbs
    bool  enable
    float ratio
    color col
    bool  style

type levels
    line  level
    label labels

// ─────────────────────────────────────────────────────────────────────────────
// GANN FAN SETTINGS
// ─────────────────────────────────────────────────────────────────────────────

group3     = "Gann Fan"
displayFan = fibbType == "Fan"

fillOn     = input.bool(
     true,
     "Fill Color",
     group = group3,
     inline = "dis",
     tooltip = "Fills the area between active Gann Fan levels.\n\nImproves visual clarity of projection zones."
)

extend_    = input.bool(
     true,
     "Extend Levels",
     group = group3,
     inline = "",
     tooltip = "Extensds dibonacci levels to the right"
)


extend = 1

array<fibbs> fibRatios = array.from(
     fibbs.new(
         input.bool(true, "1", inline = "1", group = group3,
             tooltip = ""
         ),
         input.float(1, "", inline = "1", group = group3, minval = 1,
             tooltip = ""
         ),
         input.color(color.gray, "", inline = "1", group = group3,
             tooltip = ""
         ),
         input.string("Dashed", "", ["Dashed","Solid"], inline = "1", group = group3,
             tooltip = "Enable/disable this specific Gann ratio level.\nSlope ratio multiplier for this fan line.\nColor of this Gann fan level.\nLine style for this fan level."
         ) == "Solid"
     ),
     fibbs.new(
         input.bool(true, "2", inline = "2", group = group3,
             tooltip = "Enable/disable this specific Gann ratio level."
         ),
         input.float(2, "", inline = "2", group = group3, minval = 1,
             tooltip = "Slope ratio multiplier for this fan line."
         ),
         input.color(color.green, "", inline = "2", group = group3,
             tooltip = "Color of this Gann fan level."
         ),
         input.string("Solid", "", ["Dashed","Solid"], inline = "2", group = group3,
             tooltip = "Enable/disable this specific Gann ratio level.\nSlope ratio multiplier for this fan line.\nColor of this Gann fan level.\nLine style for this fan level."
         ) == "Solid"
     ),
     fibbs.new(
         input.bool(true, "3", inline = "3", group = group3,
             tooltip = "Enable/disable this specific Gann ratio level."
         ),
         input.float(3, "", inline = "3", group = group3, minval = 1,
             tooltip = "Slope ratio multiplier for this fan line."
         ),
         input.color(color.yellow, "", inline = "3", group = group3,
             tooltip = "Color of this Gann fan level."
         ),
         input.string("Solid", "", ["Dashed","Solid"], inline = "3", group = group3,
             tooltip = "Enable/disable this specific Gann ratio level.\nSlope ratio multiplier for this fan line.\nColor of this Gann fan level.\nLine style for this fan level."
         ) == "Solid"
     ),
     fibbs.new(
         input.bool(true, "4", inline = "4", group = group3,
             tooltip = "Enable/disable this specific Gann ratio level."
         ),
         input.float(4, "", inline = "4", group = group3, minval = 1,
             tooltip = "Slope ratio multiplier for this fan line."
         ),
         input.color(color.gray, "", inline = "4", group = group3,
             tooltip = "Color of this Gann fan level."
         ),
         input.string("Dashed", "", ["Dashed","Solid"], inline = "4", group = group3,
             tooltip = "Enable/disable this specific Gann ratio level.\nSlope ratio multiplier for this fan line.\nColor of this Gann fan level.\nLine style for this fan level."
         ) == "Solid"
     ),
     fibbs.new(
         input.bool(true, "5", inline = "5", group = group3,
             tooltip = "Enable/disable this specific Gann ratio level."
         ),
         input.float(8, "", inline = "5", group = group3, minval = 1,
             tooltip = "Slope ratio multiplier for this fan line."
         ),
         input.color(color.orange, "", inline = "5", group = group3,
             tooltip = "Color of this Gann fan level."
         ),
         input.string("Solid", "", ["Dashed","Solid"], inline = "5", group = group3,
             tooltip = "Enable/disable this specific Gann ratio level.\nSlope ratio multiplier for this fan line.\nColor of this Gann fan level.\nLine style for this fan level."
         ) == "Solid"
     )
)

// ─────────────────────────────────────────────────────────────────────────────
// GANN BOX SETTINGS
// ─────────────────────────────────────────────────────────────────────────────

group4     = "Gann Box"
displayBox = fibbType == "Box"

inverse = input.bool(
    false,
    "Inverse",
    group   = group4,
    tooltip = "Flips Fibonacci level calculations."
)

OTE = input.bool(
    false,
    "OTE",
    group = group4,
    tooltip = "Highlights Optimal Trade Entry zone.\n\nTypically between 0.618 – 0.786 retracement."
)

horizontal = input.bool(
    true,
    "Horizontal Levels",
    group = group4,
    inline = "dis1",
    tooltip = ""
)

vertical = input.bool(
    true,
    "Vertical Levels",
    group = group4,
    inline = "dis1",
    tooltip = "Display horizontal Fibonacci retracement levels.\nDisplay vertical Fibonacci time projections."
)

array<fibbs> fibRatios1 = array.from(
     fibbs.new(
         input.bool(true, "1", inline = "2", group = group4,
             tooltip = "Enable this Fibonacci level."
         ),
         input.float(0.236, "", inline = "2", group = group4, step = 0.001, minval = 0, maxval = 1,
             tooltip = "Fibonacci retracement ratio."
         ),
         input.color(color.green, "", inline = "2", group = group4,
             tooltip = "Color of this Fibonacci level."
         ),
         input.string("Solid", "", ["Dashed","Solid"], inline = "2", group = group4,
             tooltip = "Enable this Fibonacci level.\nFibonacci retracement ratio.\nColor of this Fibonacci level.\nLine style for this level."
         ) == "Solid"
     ),
     fibbs.new(
         input.bool(true, "2", inline = "3", group = group4,
             tooltip = ""
         ),
         input.float(0.382, "", inline = "3", group = group4, step = 0.001, minval = 0, maxval = 1,
             tooltip = ""
         ),
         input.color(color.yellow, "", inline = "3", group = group4,
             tooltip = ""
         ),
         input.string("Solid", "", ["Dashed","Solid"], inline = "3", group = group4,
             tooltip = "Enable this Fibonacci level.\nFibonacci retracement ratio.\nColor of this Fibonacci level.\nLine style for this level."
         ) == "Solid"
     ),
     fibbs.new(
         input.bool(true, "3", inline = "4", group = group4,
             tooltip = "Enable this Fibonacci level."
         ),
         input.float(0.5, "", inline = "4", group = group4, step = 0.001, minval = 0, maxval = 1,
             tooltip = "Midpoint retracement level."
         ),
         input.color(color.gray, "", inline = "4", group = group4,
             tooltip = "Color of this Fibonacci level."
         ),
         input.string("Dashed", "", ["Dashed","Solid"], inline = "4", group = group4,
             tooltip = "Enable this Fibonacci level.\nMidpoint retracement ratio.\nColor of this Fibonacci level.\nLine style for this level."
         ) == "Solid"
     ),
     fibbs.new(
         input.bool(true, "4", inline = "5", group = group4,
             tooltip = "Enable this Fibonacci level."
         ),
         input.float(0.618, "", inline = "5", group = group4, step = 0.001, minval = 0, maxval = 1,
             tooltip = "Golden ratio retracement level."
         ),
         input.color(color.orange, "", inline = "5", group = group4,
             tooltip = "Color of this Fibonacci level."
         ),
         input.string("Solid", "", ["Dashed","Solid"], inline = "5", group = group4,
             tooltip = "Enable this Fibonacci level.\nGolden retracement ratio.\nColor of this Fibonacci level.\nLine style for this level."
         ) == "Solid"
     ),
     fibbs.new(
         input.bool(true, "5", inline = "6", group = group4,
             tooltip = "Enable this Fibonacci level."
         ),
         input.float(0.786, "", inline = "6", group = group4, step = 0.001, minval = 0, maxval = 1,
             tooltip = "Deep retracement level often used for reversals."
         ),
         input.color(color.red, "", inline = "6", group = group4,
             tooltip = "Color of this Fibonacci level."
         ),
         input.string("Solid", "", ["Dashed","Solid"], inline = "6", group = group4,
             tooltip = "Enable this Fibonacci level.\nDeep retracement ratio.\nColor of this Fibonacci level.\nLine style for this level."
         ) == "Solid"
     )
)


var plotObj = array.new<levels>(fibRatios.size()*2-1, levels.new())




// ─────────────────────────────────────────────────────────────────────────────
// CALCULATIONS
// ─────────────────────────────────────────────────────────────────────────────

H = ta.highest(high, length)
L = ta.lowest(low,  length)

var int   StartH    = 0
var int   StartL    = 0
var int   direction = 0

var float upper     = 0.0
var float lower     = 0.0

if high == H
    direction := 1
    upper     := high

if low == L
    direction := -1
    lower     := low

if high[1] == H[1] and high < H
    StartH := bar_index[1]

if low[1] == L[1] and low > L
    StartL := bar_index[1]

startIndex = direction > 0 ? StartL : StartH
startY     = direction > 0 ? lower  : upper

if barstate.islast
    for i = 0 to bar_index - startIndex
        upper := math.max(upper, high[i])
        lower := math.min(lower, low[i])

size = upper - lower

// ─────────────────────────────────────────────────────────────────────────────
// DRAW GANN FAN
// ─────────────────────────────────────────────────────────────────────────────

if barstate.islast and displayFan

    var Lfills = array.new<linefill>()

    for f in Lfills
        f.delete()

    for obj in plotObj
        obj.level.delete()
        obj.labels.delete()

    for i = 0 to array.size(fibRatios) - 1

        ratio = fibRatios.get(i)

        if ratio.enable and direction != 0

            y1 = direction > 0 ? lower : upper
            y2 = direction > 0 ? lower + size / ratio.ratio
                               : upper - size / ratio.ratio

            slope = (y1 - y2) / (bar_index - startIndex)

            lvl = line.new(
                startIndex, y1,
                bar_index + extend, y2 - slope * extend,
                color = ratio.col,
                style = ratio.style ? line.style_solid : line.style_dashed, extend = extend_ ? extend.right : extend.none
            )

            lbl = label.new(
                bar_index + extend,
                y2 - slope * extend,
                "1/" + str.tostring(ratio.ratio),
                textcolor = ratio.col,
                color     = color.new(color.black, 100),
                style     = label.style_label_left
            )

            plotObj.set(i, levels.new(lvl, lbl))

        if ratio.enable and direction != 0

            len = bar_index - startIndex - extend

            y1  = direction > 0 ? lower : upper
            y2  = direction > 0 ? upper : lower
            rat = ratio.ratio

            if rat != 1
                x2 = bar_index[len - int(len / rat)]

                slope = (y1 - y2) / (bar_index - startIndex)

                lvl = line.new(
                    startIndex, y1,
                    x2, y2 - slope * extend,
                    color = ratio.col,
                    style = ratio.style ? line.style_solid : line.style_dashed, extend = extend_ ? extend.right : extend.none
                )

                lbl = label.new(
                    x2,
                    y2 - slope * extend,
                    "1/" + str.tostring(ratio.ratio),
                    textcolor = ratio.col,
                    color     = color.new(color.black, 100),
                    style     = direction > 0 ? label.style_label_down
                                              : label.style_label_up
                )

                plotObj.set(
                    i + fibRatios.size() - 1,
                    levels.new(lvl, lbl)
                )
    if fillOn

        for i = 2 to fibRatios.size() - 1
            r     = fibRatios.get(i)
            idx   = i + fibRatios.size() - 1
            r1    = plotObj.get(idx)
            r2    = plotObj.get(idx - 1)

            fill = linefill.new(
                r1.level,
                r2.level,
                color.new(r.col, 90)
            )

            Lfills.push(fill)

        for i = 0 to fibRatios.size() - 1
            r  = fibRatios.get(i)
            r1 = plotObj.get(i)
            r2 = plotObj.get(i - 1)

            fill = linefill.new(
                r1.level,
                r2.level,
                color.new(r.col, 90)
            )

            Lfills.push(fill)


// ─────────────────────────────────────────────────────────────────────────────
// DRAW GANN BOX
// ─────────────────────────────────────────────────────────────────────────────

if barstate.islast and displayBox

    var Lfills = array.new<linefill>()
    var boxes  = array.new<box>()

    for f in Lfills
        f.delete()

    for b in boxes
        b.delete()

    boxes.clear()

    for obj in plotObj
        obj.level.delete()
        obj.labels.delete()

    for i = 0 to array.size(fibRatios1) - 1

        ratio = fibRatios1.get(i)
        size1 = upper - lower

        if ratio.enable

            if horizontal

                if OTE

                    y1 = inverse ? (direction > 0 ? 0.786 : 0.214) : (direction > 0 ? 0.214 : 0.786)
                    y2 = inverse ? (direction > 0 ? 0.618 : 0.382) : (direction > 0 ? 0.382 : 0.618)

                    b = box.new(
                        startIndex,
                        lower + size1 * y1,
                        bar_index + extend,
                        lower + size1 * y2,
                        border_color = color.new(color.black, 100),
                        bgcolor      = color.new(color.orange, 95)
                    )
                    boxes.push(b)

                rat1 = direction > 0 ? ratio.ratio : 1 - ratio.ratio
                rat  = inverse
                     ? rat1
                     : direction > 0 ? 1 - ratio.ratio : ratio.ratio

                y = lower + size1 * rat

                lvl = line.new(
                    startIndex, y,
                    bar_index + extend, y,
                    color = ratio.col,
                    style = ratio.style ? line.style_solid : line.style_dashed
                )

                lbl = label.new(
                    bar_index + extend,
                    y,
                    str.tostring(ratio.ratio),
                    textcolor = ratio.col,
                    color     = color.new(color.black, 100),
                    style     = label.style_label_left
                )

                plotObj.push(levels.new(lvl, lbl))

            if vertical

                len = bar_index - startIndex - extend

                rat1 = direction > 0 ? ratio.ratio : 1 - ratio.ratio
                rat  = inverse
                     ? rat1
                     : direction > 0 ? 1 - ratio.ratio : ratio.ratio

                x = bar_index[len - int(len * rat)]

                lvl = line.new(
                    x, lower,
                    x, upper,
                    color = ratio.col,
                    style = ratio.style ? line.style_solid : line.style_dashed
                )

                lbl = label.new(
                    x,
                    upper,
                    str.tostring(ratio.ratio),
                    textcolor = ratio.col,
                    color     = color.new(color.black, 100),
                    style     = label.style_label_down
                )

                plotObj.push(levels.new(lvl, lbl))




// ─────────────────────────────────────────────────────────────────────────────
// MAIN FRAME
// ─────────────────────────────────────────────────────────────────────────────

if barstate.islast

    var frameBox = box(na)

    slope = (upper - lower) / (bar_index - startIndex)

    if direction > 0
        frameBox :=
             box.new(
                 bar_index + extend,
                 upper + slope * extend,
                 startIndex,
                 lower,
                 color.new(chart.fg_color, 70),
                 1,
                 line.style_dashed,
                 bgcolor = color.new(chart.fg_color, 100))
    else
        frameBox := 
             box.new(
                 bar_index + extend,
                 lower - slope * extend,
                 startIndex,
                 upper,
                 color.new(chart.fg_color, 70),
                 1,
                 line.style_dashed,
                 bgcolor = color.new(chart.fg_color, 100))

    box.delete(frameBox[1])
// ─────────────────────────────────────────────────────────────────────────────
// SWINGS
// ─────────────────────────────────────────────────────────────────────────────

if direction != direction[1]

    txt = direction > 0
         ? "●\n" + str.tostring(startY)
         : str.tostring(startY) + "\n●"

    lbl = label.new(
        startIndex,
        startY,
        txt,
        style     = direction > 0 ? "lup" : "ldn",
        color     = color.new(color.black, 100),
        textcolor = direction > 0 ? colUp : colDn,
        tooltip   = "Swing Anchor Point"
    )

    ln = line.new(
        startIndex[1],
        startY[1],
        startIndex,
        startY,
        color = startY[1] > startY ? colDn : colUp
    )

    if not zigZag
        (lbl[1]).delete()
        ln.delete()

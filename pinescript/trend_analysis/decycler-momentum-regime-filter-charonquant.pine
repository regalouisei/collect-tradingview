// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © CharonQuant

//@version=6
// ______________________
//  (CharonQuant;:.:. .. )
//  T""""""""""""""""""""T
//  |.;....,..........;..|
//  |;;:: .  .    .      |
//  l;;;:. :   .     ..  ;
//  `;;:::.: .    .     .'
//   l;;:. ..  .     .: ;
//   `;;::.. .    .  ; .'
//    l;;:: .  .    /  ;
//     \;;:. .   .,'  /
//      `\;:.. ..'  .'
//        `\;:.. ..'
//          \;:. /
//           l; f
//           `;f'
//            ||
//            ;l.
//           ;: l
//          / ;  \
//        ,/  :   `.
//      ./' . :     `.
//     /' ,'  :       \
//    f  /  . :        i
//   ,' ;  .  :        `.
//   f ;  .   :      .  i
//  .'    :   :       . `.
//  f ,  .    ;       :  i
//  |    :  ,/`.       : |
//  |    ;,/;:. `.     . |
//  |___,/;;:. . .`._____|
// (!;:.:. .  CharonQuant)
//  """"""""""""""""""""""`
indicator("Decycler Momentum Regime Filter ~ CharonQuant","DMRF ~ CharonQuant",overlay = true)

// ── Imports ────────────────────────────────────────────────────────────────
import TradingView/ta/7 as ta

// ── Groups ───────────────────────────────────────────────
group_decycler = "Decycler Core"
group_pmo      = "PMO Momentum Filter"

// ── Inputs ───────────────────────────────────────────────
highpassLength = input.int(8, "Highpass Period", minval=1,group=group_decycler,tooltip="Controls the Decycler smoothing strength.\nHigher values = smoother trend filter, fewer flips.\nLower values = faster reaction, more noise.")
src            = input.source(close, "Source", group=group_decycler, tooltip="Price source used for Decycler and PMO calculations.")
firstLength    = input.int(23, "PMO 1st Smoothing Length", minval=1,group=group_pmo,tooltip="First EMA smoothing stage of the PMO.\nHigher values reduce noise but slow momentum response.")
secondLength   = input.int(17, "PMO 2nd Smoothing Length", minval=1,group=group_pmo,tooltip="Second EMA smoothing stage of the PMO.\nHelps confirm sustained momentum shifts.")
signalLength   = input.int(10, "PMO Signal Length", minval=1,group=group_pmo,tooltip="Signal line length used for momentum confirmation.\nShorter = faster signals.\nLonger = more stable confirmation.")

// ── Ehlers Highpass Filter (Decycler Core) ───────────────────────────────────────────────
PI = 2 * math.asin(1)
alphaArg = 2 * PI / (highpassLength * math.sqrt(2))
alpha = 0.0
alpha := math.cos(alphaArg) != 0 ?
     (math.cos(alphaArg) + math.sin(alphaArg) - 1) / math.cos(alphaArg) :
     nz(alpha[1])
hp = 0.0
hp := math.pow(1 - (alpha / 2), 2) * (src - 2 * nz(src[1]) + nz(src[2])) +
     2 * (1 - alpha) * nz(hp[1]) -
     math.pow(1 - alpha, 2) * nz(hp[2])
decycler = src - hp

// ── PMO Calculation (Momentum Filter) ━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━
roc1 = ta.roc(src, 1)
pmo =
     ta.ema(
         10 * ta.ema(nz(roc1), firstLength),
         secondLength
     )
signal = ta.ema(pmo, signalLength)

// ── Signals ━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━
decyclerUp   = decycler > decycler[1]
decyclerDown = decycler < decycler[1]

pmoBull = (pmo > signal) and (pmo > 0)
pmoBear = (pmo < signal) and (pmo < 0)

longSignal  = decyclerUp   and pmoBull
shortSignal = decyclerDown and pmoBear

var int CharonQuant = 0

if longSignal and not shortSignal
    CharonQuant := 1
if shortSignal
    CharonQuant := -1

// ── Visualization ━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━
color BULL    = #D4A017
color BEAR    = #6B2A3A
color NEUTRAL = color.gray
trendColor =
     CharonQuant == 1  ? BULL :
     CharonQuant == -1 ? BEAR :
     NEUTRAL
plot(decycler, title="Decycler", linewidth=3, color=trendColor)
barcolor(CharonQuant == 1  ? color.new(BULL, 0) :
         CharonQuant == -1 ? color.new(BEAR, 0) :
         color.new(NEUTRAL, 0))
bgcolor(CharonQuant == 1  ? color.new(BULL, 92) :
       CharonQuant == -1 ? color.new(BEAR, 92) :
       color.new(NEUTRAL, 95))

// ── Alerts ────────────────────────────────────────────
alertcondition(longSignal, title="DMRF Long", message="DMRF Long {{exchange}}:{{ticker}}")
alertcondition(shortSignal, title="DMRF Short", message="DMRF Short {{exchange}}:{{ticker}}")

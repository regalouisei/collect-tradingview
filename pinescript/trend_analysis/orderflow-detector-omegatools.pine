// Â© OmegaTools

//@version=6
indicator("Orderflow Detector [OmegaTools]", max_boxes_count = 500, max_labels_count = 500)

// Calculations

ass = syminfo.tickerid
showbox = input.string('Least', 'Path of', ['None', 'Least', 'Greater', 'Both'], inline = 'box')
sens = input.int(3, 'Sensibility', minval = 1, maxval = 10, tooltip = 'The percentile to recognise candles of least resistance or maximum resistance from limit sellers', inline = 'box')
tf = input.timeframe('1', 'Resolution')
show = input.string('All', 'Show', ['All', 'Iceberg', 'Sweep', 'Absorb'])
upc = input.color(#2962ff, 'Colors', inline = 'col')
dnc = input.color(#e91e63, '', inline = 'col')

volma = ta.sma(volume, 21)
maxvol = ta.highest(volume, 21)
ll = ta.lowest(low[1], 11)
hh = ta.highest(high[1], 11)
highvol = volume > volma + ta.stdev(volume, 21) * 1.5
volspike = volume > volume[1] * 2 and volume == maxvol and volume > volma * 6
samehigh = high[1] == high[2] and high[1] == high[3]
samelow = low[1] == low[2] and low[1] == low[3]
newh = high > hh
newl = low < ll
rangefromlow = close > ll
rangefromhigh = close < hh
highwick = ta.sma(high - math.max(close, open), 7) > ta.sma(math.min(close, open) - low, 7) * 2
lowwick = ta.sma(math.min(close, open) - low, 7) > ta.sma(high - math.max(close, open), 7) * 2

longicebergx = samelow and highvol and newh and rangefromhigh
shorticebergx = samehigh and highvol and newl and rangefromlow

sweeplongx = newl and close > ll and volspike    // sweep sotto ll e rientro -> long
sweepshortx = newh and close < hh and volspike    // sweep sopra hh e rientro -> short

tr = high - low
trma = ta.sma(tr, 21)
absorbBase = highvol and tr < trma * 0.7
absorbshortx = absorbBase and highwick and close < open  // assorbimento su offer -> short
absorblongx  = absorbBase and lowwick  and close > open

[icebergx, sweepx, absorbx] = request.security_lower_tf(ass, tf, [longicebergx ? 1 : shorticebergx ? -1 : 0, sweeplongx ? 1 : sweepshortx ? -1 : 0, absorblongx ? 1 : absorbshortx ? -1 : 0])

iceberg = array.size(icebergx) > 0 ? (array.max(icebergx) + array.min(icebergx)) : 0
sweep = array.size(sweepx) > 0 ? (array.max(sweepx) + array.min(sweepx)) : 0
absorb = array.size(absorbx) > 0 ? (array.max(absorbx) + array.min(absorbx)) : 0

[upvol, dnvol] = request.security_lower_tf(ass, tf, [close > open ? volume : 0, close < open ? volume : 0])
delta = upvol.sum() - dnvol.sum()
deltaz = math.abs(delta) / ta.stdev(delta, 21)
rng = math.avg(math.abs(close - open) * 2 - (high - low), math.max(close - low, high - close), math.max(close - low, high - close))
avgrng = ta.sma(rng, 21)
rangex = rng / avgrng
resistance = deltaz / rangex

hightres = 3
lowthres = 0.5

col = delta > 0 ? upc : dnc
col2 = delta < 0 ? upc : dnc
ext = 11
leastres = resistance < ta.percentile_linear_interpolation(resistance, 100, sens)
greaterres = resistance > ta.percentile_linear_interpolation(resistance, 100, 100-sens)

// Plotting

atr = ta.sma(high - low, 21)

if show == 'All' or show == 'Iceberg'
    if iceberg == 1
        label.new(bar_index, low - atr * 0.2, 'IC', color = upc, style = label.style_diamond, size = 3, textcolor = upc, force_overlay = true)
    if iceberg == -1
        label.new(bar_index, high + atr * 0.2, 'IC', color = dnc, style = label.style_diamond, size = 3, textcolor = dnc, force_overlay = true)
if show == 'All' or show == 'Sweep'
    if sweep == 1
        label.new(bar_index, low - atr * 0.4, 'SW', color = upc, style = label.style_triangleup, size = 3, textcolor = upc, force_overlay = true)
    if sweep == -1
        label.new(bar_index, high + atr * 0.4, 'SW', color = dnc, style = label.style_triangledown, size = 3, textcolor = dnc, force_overlay = true)
if show == 'All' or show == 'Absorb'
    if absorb == 1
        label.new(bar_index, low - atr * 0.6, 'AB', color = upc, style = label.style_circle, size = 3, textcolor = upc, force_overlay = true)
    if absorb == -1
        label.new(bar_index, high + atr * 0.6, 'AB', color = dnc, style = label.style_circle, size = 3, textcolor = dnc, force_overlay = true)

if leastres and (showbox == 'Least' or showbox == 'Both')
    box.new(bar_index, high, bar_index + ext, low, color.new(col, 70), bgcolor = color.new(col, 80), force_overlay = true)
if greaterres and (showbox == 'Greater' or showbox == 'Both')
    box.new(bar_index, high, bar_index + ext, low, color.new(col2, 30), bgcolor = color.new(col2, 40), force_overlay = true)

plot(deltaz, 'VDelta', deltaz > 3 ? (delta >= 0 ? upc : dnc) : deltaz > 1 ? (delta >= 0 ? color.new(upc, 30) : color.new(dnc, 30)) : color.new(color.gray, 40), style = plot.style_columns)
hline(hightres, 'High Delta', color.new(color.gray, 50), hline.style_dotted)
hline(lowthres, 'Low Delta', color.new(color.gray, 50), hline.style_dotted)

// End of Script

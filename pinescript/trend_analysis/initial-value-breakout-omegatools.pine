// © OmegaTools

//@version=6
indicator("Initial Value Breakout [OmegaTools]", 'Ω IVB', overlay = true)

// Inputs

sess = input.session('0930-1000', 'Session', tooltip = 'NY Time (UTC-5)', group = 'Settings')
validity = input.int(16, 'Validity', minval = 1, maxval = 18, group = 'Settings')
showmean = input.bool(false, 'Zone', group = 'Visual', inline = 'add1')
fill1 = input.bool(false, 'Fill', group = 'Visual', inline = 'add1')
showdeviations = input.bool(false, 'Deviations', group = 'Visual', inline = 'add2')
fill2 = input.bool(false, 'Fill', group = 'Visual', inline = 'add2')
showtarget = input.bool(false, 'Target', group = 'Visual', inline = 'add3')
showvwap = input.bool(false, 'VWAP', group = 'Visual', inline = 'add4')
upc = input.color(#2962ff, '', group = 'Visual', inline = 'col')
dnc = input.color(#e91e63, '', group = 'Visual', inline = 'col')

// Calculations

t = time
issess = t == time(timeframe.period, sess, 'America/New_York')

var int bias = 0
var float hh = na
var float ll = na
var hlarr = array.new_float()
if issess
    if na(hh) or na(ll)
        hh := high
        ll := low
        hlarr.push(hl2)
    else
        hh := math.max(high, hh)
        ll := math.min(low, ll)
        hlarr.push(hl2)
if ta.crossover(close, hh) and not na(hh)
    bias := 1
if ta.crossunder(close, ll) and not na(ll)
    bias := -1
if hour(t, 'America/New_York') > validity
    hh := na
    ll := na
    hlarr.clear()
    bias := 0
hl = (hlarr.avg() + hlarr.median()) / 2

highdev = hh + (hh - ll)
lowdev = ll - (hh - ll)

var uparr = array.new_float()
var dnarr = array.new_float()
var totarr = array.new_float()
var float highest = na
var float lowest = na
if issess[1] and not issess
    highest := high
    lowest := low
if hour(t, 'America/New_York') <= validity
    highest := math.max(high, highest)
    lowest := math.min(low, lowest)
else
    highest := na
    lowest := na
if na(hh) and not na(hh[1])
    if bias[1] == 1
        uparr.push(highest[1] - hh[1])
    if bias[1] == -1
        dnarr.push(ll[1] - lowest[1])
    totarr.push(math.max(ll[1] - lowest[1], highest[1] - hh[1]))
if uparr.size() > 3
    uparr.remove(0)
if dnarr.size() > 3
    dnarr.remove(0)
if totarr.size() > 21
    totarr.remove(0)
hightarget = hh + math.avg(uparr.avg(), totarr.avg())
lowtarget = ll - math.avg(dnarr.avg(), totarr.avg())

vwap = ta.vwap(close, (not issess[1]) and issess)

var int firstbias = na
var float levelthatbroke = na
var biaspredarr = array.new_int()
if hour(t, 'America/New_York') > validity
    if firstbias[1] != 0
        if (close[1] > levelthatbroke and firstbias[1] == 1) or (close[1] < levelthatbroke and firstbias[1] == -1) // firstbias[1] == bias[1] //
            biaspredarr.push(1)
        else
            biaspredarr.push(0)
    firstbias := na
if na(firstbias) and bias != 0
    firstbias := bias
    levelthatbroke := close
if biaspredarr.size() > 100
    biaspredarr.remove(0)
biaspred = biaspredarr.sum() / biaspredarr.size() * 100

// Plotting

starttime = ta.valuewhen(issess and not issess[1], time, 0)

var line lineh = na
var line linel = na
var line linem = na

var line linehd = na
var line lineld = na

var line lineh2 = na
var line linel2 = na

var linefill lf_zone = na
var linefill lf_zone_up = na
var linefill lf_zone_dn = na
var linefill lf_dev_up = na
var linefill lf_dev_dn = na

sameday(_t1, _t2) => year(_t1, "America/New_York") == year(_t2, "America/New_York") and month(_t1, "America/New_York") == month(_t2, "America/New_York") and dayofmonth(_t1, "America/New_York") == dayofmonth(_t2, "America/New_York")

newline(_x1, _y, _col, _style, _width) => line.new(x1 = _x1, y1 = _y, x2 = time, y2 = _y, xloc = xloc.bar_time, extend = extend.none, color = _col, style = _style, width = _width)

extendlines(ln) =>
    if not na(ln)
        if dayofmonth(ln.get_x1(), "America/New_York") == dayofmonth(t, "America/New_York") and hour(time, "America/New_York") <= validity
            ln.set_x2(time)

if (not issess) and issess[1] and not na(hh) and not na(ll) and not na(starttime)
    lineh := newline(starttime, hh, upc, line.style_solid, 1)
    linel := newline(starttime, ll, dnc, line.style_solid, 1)

    if showmean and not na(hl)
        linem := newline(starttime, hl, chart.fg_color, line.style_dotted, 1)
    else
        linem := na

    if showdeviations and not na(highdev) and not na(lowdev)
        linehd := newline(starttime, highdev, upc, line.style_dashed, 1)
        lineld := newline(starttime, lowdev, dnc, line.style_dashed, 1)
    else
        linehd := na
        lineld := na

    if showtarget and not na(hightarget) and not na(lowtarget)
        lineh2 := newline(starttime, hightarget, upc, line.style_dotted, 1)
        linel2 := newline(starttime, lowtarget, dnc, line.style_dotted, 1)
    else
        lineh2 := na
        linel2 := na

    lf_zone := na
    lf_dev_up := na
    lf_dev_dn := na

    if fill1
        lf_zone := linefill.new(lineh, linel, color.new(chart.fg_color, 95))

    if fill2 and showdeviations and not na(linehd) and not na(lineld)
        lf_dev_up := linefill.new(lineh, linehd, color.new(upc, 95))
        lf_dev_dn := linefill.new(linel, lineld, color.new(dnc, 95))

extendlines(lineh)
extendlines(linel)
extendlines(linem)
extendlines(linehd)
extendlines(lineld)
extendlines(lineh2)
extendlines(linel2)

plot(showvwap and not na(hh) ? vwap : na, 'VWAP', bias == 1 ? upc : bias == -1 ? dnc : chart.fg_color, style = plot.style_linebr)

var tab = table.new(position.bottom_left, 10, 10)
tab.cell(0, 0, '1º Bias WR: ' + str.tostring(biaspred, '#.#') + '%', text_color = chart.fg_color)
tab.cell(0, 1, '', text_color = chart.bg_color)
tab.cell(0, 2, '', text_color = chart.bg_color)

// End of Script

// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © AdarshShrma

//@version=6
indicator('Flow Engine', shorttitle = 'Flow Engine', overlay = false, max_bars_back = 500)

mom_len = input.int(14, 'Momentum Length', minval = 1)
vol_len = input.int(20, 'Volume MA Length', minval = 1)
ob_level = input.float(70, 'Overbought Level')
os_level = input.float(-70, 'Oversold Level')
trend_len = input.int(50, 'Trend Length', minval = 1)
lbR = input.int(5, 'Pivot Lookback Right', minval = 1)
lbL = input.int(5, 'Pivot Lookback Left', minval = 1)
htf = input.timeframe('D', 'HTF Timeframe')

mom_raw = ta.roc(close, mom_len)
mom_max = ta.highest(math.abs(mom_raw), 100)
mom_norm = mom_max != 0 ? mom_raw / mom_max * 100 : 0

vol_ratio = volume / ta.sma(volume, vol_len)
vol_scaled = math.min(vol_ratio, 3.0) / 3.0
vol_transp = math.round((1 - vol_scaled) * 80)

rsi_val = ta.rsi(close, mom_len)
osc_line = (rsi_val - 50) * 1.4

ema_fast = ta.ema(close, 21)
ema_slow = ta.ema(close, trend_len)
trend_raw = (ema_fast - ema_slow) / ema_slow * 1000
trend_smooth = ta.ema(trend_raw, 8)

htf_close = request.security(syminfo.tickerid, htf, close)
htf_ema_fast = request.security(syminfo.tickerid, htf, ta.ema(close, 21))
htf_ema_slow = request.security(syminfo.tickerid, htf, ta.ema(close, 50))
htf_bull = htf_close > htf_ema_fast and htf_ema_fast > htf_ema_slow
htf_bear = htf_close < htf_ema_fast and htf_ema_fast < htf_ema_slow

bgcolor(htf_bull ? color.new(#00E5FF, 96) : htf_bear ? color.new(#FF1744, 96) : color.new(#FFFFFF, 98), title = 'HTF Bias')

hist_color = mom_norm >= 0 ? color.new(#00E5FF, vol_transp) : color.new(#FF1744, vol_transp)
plot(mom_norm, style = plot.style_columns, color = hist_color, linewidth = 1, title = 'Momentum Histogram')

osc_color = osc_line > ob_level ? color.new(#FF6D00, 0) : osc_line < os_level ? color.new(#76FF03, 0) : osc_line > 0 ? color.new(#00BCD4, 30) : color.new(#F44336, 30)
plot(osc_line, color = osc_color, linewidth = 2, title = 'OB/OS Line')

trend_color = trend_smooth > 0 ? color.new(#B2FF59, 60) : color.new(#FF8A80, 60)
plot(trend_smooth, color = trend_color, linewidth = 2, title = 'Trend Line')

hline(ob_level, 'OB', color = color.new(#FF6D00, 55), linestyle = hline.style_dashed, linewidth = 1)
hline(os_level, 'OS', color = color.new(#76FF03, 55), linestyle = hline.style_dashed, linewidth = 1)
hline(0, 'Zero', color = color.new(#FFFFFF, 65), linestyle = hline.style_solid, linewidth = 1)
bgcolor(osc_line > ob_level ? color.new(#FF6D00, 92) : osc_line < os_level ? color.new(#76FF03, 92) : na, title = 'OB/OS Zone')

ph_mom = ta.pivothigh(mom_norm, lbL, lbR)
pl_mom = ta.pivotlow(mom_norm, lbL, lbR)

var float ph_mom_y1 = na
var float ph_mom_y2 = na
var int ph_mom_x1 = na
var int ph_mom_x2 = na
var float pl_mom_y1 = na
var float pl_mom_y2 = na
var int pl_mom_x1 = na
var int pl_mom_x2 = na
var float ph_price_y1 = na
var float ph_price_y2 = na
var float pl_price_y1 = na
var float pl_price_y2 = na

var bool bear_div = false
var bool bull_div = false

bear_div := false
bull_div := false

if not na(ph_mom)
    if not na(ph_mom_x2) and high[lbR] > ph_price_y2 and ph_mom < ph_mom_y2
        bear_div := true
        bear_div
    ph_mom_x1 := ph_mom_x2
    ph_mom_y1 := ph_mom_y2
    ph_mom_x2 := bar_index - lbR
    ph_mom_y2 := ph_mom
    ph_price_y1 := ph_price_y2
    ph_price_y2 := high[lbR]
    ph_price_y2

if not na(pl_mom)
    if not na(pl_mom_x2) and low[lbR] < pl_price_y2 and pl_mom > pl_mom_y2
        bull_div := true
        bull_div
    pl_mom_x1 := pl_mom_x2
    pl_mom_y1 := pl_mom_y2
    pl_mom_x2 := bar_index - lbR
    pl_mom_y2 := pl_mom
    pl_price_y1 := pl_price_y2
    pl_price_y2 := low[lbR]
    pl_price_y2

if bear_div and not na(ph_mom_x1) and not na(ph_mom_x2)
    line.new(ph_mom_x1, ph_mom_y1, ph_mom_x2, ph_mom_y2, color = color.new(#FF6D00, 0), width = 2, style = line.style_dashed)

if bull_div and not na(pl_mom_x1) and not na(pl_mom_x2)
    line.new(pl_mom_x1, pl_mom_y1, pl_mom_x2, pl_mom_y2, color = color.new(#76FF03, 0), width = 2, style = line.style_dashed)

plotshape(bull_div, '', shape.triangleup, location.belowbar, color.new(#76FF03, 0), 0, 'Bull Div', color.new(#76FF03, 0), true, size.small, force_overlay = true)
plotshape(bear_div, '', shape.triangledown, location.abovebar, color.new(#FF6D00, 0), 0, 'Bear Div', color.new(#FF6D00, 0), true, size.small, force_overlay = true)
